Round Sage Mouse

medium

# Anyone can grief the `getRewardForDuration` to return an incorrect amount until a non-zero depositReward is made again for a particular token if the current reward period has gone by in both the ZivoeRewards and/or ZivoeRewardsVesting contracts.

## Summary
If the current reward period has [ended](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L232) for a certain `_rewardToken`, the if block will run and a `reward` input of 0 will make the *rewardRate* for a particular token to become zero even if the malicious user does not have any balance of *_rewardToken* or *stakingToken*. This will make the contract function `getRewardForDuration` to return 0 until a new update is made though the only function that updates the `rewardRate`, both in the ZivoeRewards and/or ZivoeRewardsVesting contracts: [depositReward] [function](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243) in ZivoeRewards and [function](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L352-L367) in ZivoeRewardsVesting contract. This is not a vulnerability per say since it does not affect the protocol in any way, but I consider it a bug since it can trick some honest user into believing they will receive no rewards for a certain token (or even all tokens if the 0 deposit is made for all reward tokens) - and this can be easily avoided by a simple check.

## Vulnerability Detail

The `rewardRate` variable can only be set when a call is made to the `depositReward` function in both contracts. If the current reward period has ended for a specific token, the if block in both contracts will run as shown below:
```solidity
    function depositReward(address _rewardsToken, uint256 reward) external updateReward(address(0)) nonReentrant {
        IERC20(_rewardsToken).safeTransferFrom(_msgSender(), address(this), reward);

        // Update vesting accounting for reward (if existing rewards being distributed, increase proportionally).
        if (block.timestamp >= rewardData[_rewardsToken].periodFinish) {
            // @auditor the below will be something like: 0 / nonZeroSomething => 0
            rewardData[_rewardsToken].rewardRate = reward.div(rewardData[_rewardsToken].rewardsDuration);
        } else {
            // @auditor not important for the issue in question.
        }

        rewardData[_rewardsToken].lastUpdateTime = block.timestamp;
        rewardData[_rewardsToken].periodFinish = block.timestamp.add(rewardData[_rewardsToken].rewardsDuration);
        emit RewardDeposited(_rewardsToken, reward, _msgSender());
    }
```

## Impact
If the `rewardRate` becomes 0 for a reward token, the following function, if called in between a zero deposit and a non-zero deposit, will return zero as a result:
```solidity
    /// @notice Returns the total amount of rewards being distributed to everyone for current rewardsDuration.
    /// @param  _rewardsToken The asset that's being distributed.
    /// @return amount The amount of rewards being distributed.
    function getRewardForDuration(address _rewardsToken) external view returns (uint256 amount) {
        // @auditor this will be like: return 0 * nonZeroSomething => amount is 0.
        return rewardData[_rewardsToken].rewardRate.mul(rewardData[_rewardsToken].rewardsDuration);
    }
```
Therefore, the function that should return the *@notice ... the total amount of rewards being distributed to everyone for current rewardsDuration* will return 0, tricking the user to think that if (s)he deposits, (s)he will receive no rewards for that - which is obviously deceptive! This might be true for all `_rewardToken`s - as there is a maximum of 10 tokens allowed to be rewardTokens. The attacker just needs to pay for fees - the worst scenario being if the protocol is launched in a very cheap blockchain futurely. The second issue is that, currently, the `getRewardForDuration` function seems not to be used anywhere else within the protocol. However, if there are other protocols that want to integrate to Zivoe, they will be susceptible to this bug, which may end up becoming a vulnerability for them, causing loss of reputation for the Zivoe protocol.

## Code Snippet

Without loss of generality, the proof of concept was made for the `ZivoeRewards` contract. The same proof of concept can be easily done for the `ZivoeRewardsVesting` as well. 
```solidity
pragma solidity ^0.8.0;

import {Test, console} from "../lib/forge-std/src/Test.sol";
import {ERC20} from "../lib/openzeppelin-contracts/contracts/token/ERC20/ERC20.sol";
import {ZivoeRewards} from "../src/ZivoeRewards.sol";
import {ZivoeGlobals} from "../src/ZivoeGlobals.sol";

contract StakingToken is ERC20("SToken", "STK") {
    constructor() {
        _mint(msg.sender, 1000 ether);
    }

    function mintMe() external {
        _mint(msg.sender, 1 ether);
    }
}

contract CounterTest is Test {
    ZivoeRewards rewards;
    ZivoeGlobals _GBL;
    StakingToken stToken;
    StakingToken rewardsToken;
    address[] globals;
    address[] stables;
    address attacker;
    address innocentUser;

    function setUp() public {
        attacker = makeAddr("attacker");
        innocentUser = makeAddr("innocentUser");
        stToken = new StakingToken();
        rewardsToken = new StakingToken(); // just an ERC20
        _GBL = new ZivoeGlobals();

        rewards = new ZivoeRewards(address(stToken), address(_GBL));

        for (uint i; i < 14; i++) {
            if (i == 10) globals.push(address(this));
            globals.push(address(999));
            if (i <= 2) stables.push(address(888));
        }

        _GBL.initializeGlobals(globals, stables); // initialize ZVL variable for calling addReward.

        rewards.addReward(address(rewardsToken), 1 days);

        // innocentUser mints and stakes
        vm.startPrank(innocentUser);
        stToken.mintMe();
        stToken.approve(address(rewards), 10 ether);
        rewards.stake(1 ether);

        rewardsToken.mintMe();
        rewardsToken.mintMe();
        rewardsToken.mintMe();
        rewardsToken.approve(address(rewards), 10 ether);
        rewards.depositReward(address(rewardsToken), 1 ether);
        vm.stopPrank();
    }

    function testManipulateRewardRateWithZeroDeposit() public {
        vm.warp(5 days); // move to a future block after the reward period has ended.

        vm.startPrank(innocentUser);
        uint256 earnedBeforeAttack = rewards.earned(
            innocentUser,
            address(rewardsToken)
        );
        assertGt(earnedBeforeAttack, 0); // innocentUser has earned some rewards.
        assertGt(rewards.getRewardForDuration(address(rewardsToken)), 0);

        vm.stopPrank();

        // attacker sets rewardRate to 0 with a 0 depositReward.
        vm.startPrank(attacker);
        rewards.depositReward(address(rewardsToken), 0);
        vm.stopPrank();

        vm.warp(6 days);

        vm.startPrank(innocentUser);
        rewards.depositReward(address(rewardsToken), 0); // innocentUser deposits 0 amount to manipulate rewardRate.
        assert(rewards.getRewardForDuration(address(rewardsToken)) == 0); // 0-ed rewardRate.

        rewardsToken.approve(address(rewards), 2 ether);
        rewards.depositReward(address(rewardsToken), 2 ether); // innocentUser deposits 0 amount to manipulate rewards.
        assertGt(rewards.getRewardForDuration(address(rewardsToken)), 0); // non-0 deposit needs to happen for rewardRate to increase again.
    }
}
```

## Tool used

Manual Review

## Recommendation

Simply add the check in both contracts:
```solidity
    function depositReward(address _rewardsToken, uint256 reward) external updateReward(address(0)) nonReentrant {
        require(reward > 0, ">0"); // @auditor: simple check for `reward`.
        IERC20(_rewardsToken).safeTransferFrom(_msgSender(), address(this), reward);
```
