Fresh White Barracuda

high

# `ZivoeYDL.sol#unlock`: `lastDistribution` is initialized mistakenly.

## Summary
`ZivoeYDL.sol#unlock` function initialize `lastDistribution` mistakenly: `lastDistribution = block.timestamp + 30 days`.
This causes the following two problems.
1. The first distribution of yield is possible only 60 days later after unlock.
2. The first distribution amount for tranches decreases by half for the first distribution of yield.

## Vulnerability Detail
The `ZivoeYDL.sol#unlock` function is the following.
```solidity
    function unlock() external {
        ......
328:    lastDistribution = block.timestamp + 30 days;
        ......
    }
```
And the `ZivoeYDL.sol#distributeYield` function is the following.
```solidity
    function distributeYield() external nonReentrant {
        require(unlocked, "ZivoeYDL::distributeYield() !unlocked"); 
        require(
216:        block.timestamp >= lastDistribution + daysBetweenDistributions * 86400, 
            "ZivoeYDL::distributeYield() block.timestamp < lastDistribution + daysBetweenDistributions * 86400"
        );

        // Calculate protocol earnings.
        uint256 earnings = IERC20(distributedAsset).balanceOf(address(this));
        uint256 protocolEarnings = protocolEarningsRateBIPS * earnings / BIPS;
        uint256 postFeeYield = earnings.floorSub(protocolEarnings);

        // Update timeline.
        distributionCounter += 1;
        lastDistribution = block.timestamp;

        // Calculate yield distribution (trancheuse = "slicer" in French).
        (
            uint256[] memory _protocol, uint256 _seniorTranche, uint256 _juniorTranche, uint256[] memory _residual
232:    ) = earningsTrancheuse(protocolEarnings, postFeeYield); 
        ......
    }
```
`L328` and `L216` shows that the first distribution of yield is possible only 60 days later after unlock.

On the other hand, `L232` calls the `ZivoeYDL.sol#earningsTrancheuse` function to calculate the amount of yield to distribute to senior and junior tranches.
```solidity
    function earningsTrancheuse(uint256 yP, uint256 yD) public view returns (
        uint256[] memory protocol, uint256 senior, uint256 junior, uint256[] memory residual
    ) {
        protocol = new uint256[](protocolRecipients.recipients.length);
        residual = new uint256[](residualRecipients.recipients.length);
        
        // Accounting for protocol earnings.
        for (uint256 i = 0; i < protocolRecipients.recipients.length; i++) {
            protocol[i] = protocolRecipients.proportion[i] * yP / BIPS;
        }

        // Accounting for senior and junior earnings.
        uint256 _seniorProportion = MATH.seniorProportion(
            IZivoeGlobals_YDL(GBL).standardize(yD, distributedAsset),
461:        MATH.yieldTarget(emaSTT, emaJTT, targetAPYBIPS, targetRatioBIPS, daysBetweenDistributions),
462:        emaSTT, emaJTT, targetAPYBIPS, targetRatioBIPS, daysBetweenDistributions
        );
        senior = (yD * _seniorProportion) / RAY;
        junior = (yD * MATH.juniorProportion(emaSTT, emaJTT, _seniorProportion, targetRatioBIPS)) / RAY;
        
        // Handle accounting for residual earnings.
        yD = yD.floorSub(senior + junior);
        for (uint256 i = 0; i < residualRecipients.recipients.length; i++) {
            residual[i] = residualRecipients.proportion[i] * yD / BIPS;
        }
    }
```
`L461`, `L462` uses `daysBetweenDistributions`(= 30 days) for the calculation of the interest of senior tranche.
Therefore, the first distribution after unlock is called by 60 days, but distribute interest for only 30 days.

## Impact
There are the following two problems.
1. The first distribution of yield is possible only 60 days (instead of 30 days) later after unlock.
2. The first distribution amount for tranches decreases by half for the first distribution of yield.

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeYDL.sol#L328

## Tool used
Manual Review

## Recommendation
Modify `ZivoeYDL.sol#unlock` function as follows.
```solidity
    function unlock() external {
        ......
--      lastDistribution = block.timestamp + 30 days;
++      lastDistribution = block.timestamp;
        ......
    }
```
If the first distribution of 60 days is intended implementation, we may modify `ZivoeYDL.sol#earningsTrancheuse` function.
