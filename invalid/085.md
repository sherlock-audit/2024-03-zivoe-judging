Clumsy Cobalt Lion

high

# First call of `ZivoeYDL.distributeYield()` will distribute yield incorrectly

## Summary
[ZivoeYDL.distributeYield()](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeYDL.sol#L213-L269) distributes a montly yield. Since `lastDistribution` is set to `block.timestamp + 30 days` when unlocking the contract, the first yield distribution will use the yield for 2 months, but the calculations will be made as if only 1 month has passed.

## Vulnerability Detail
When the contract is [unlocked](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeYDL.sol#L328), it sets 
```solidity
        lastDistribution = block.timestamp + 30 days;
```

Then [distributeYield](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeYDL.sol#L215-L218) can be called only if `daysBetweenDistribution` (which is set to 30 days) have passed since `lastDistribution`.

```solidity
        require(
            block.timestamp >= lastDistribution + daysBetweenDistributions * 86400, 
            "ZivoeYDL::distributeYield() block.timestamp < lastDistribution + daysBetweenDistributions * 86400"
        );
```

Therefore, the first distribution can be executed after at least 60 days since the unlocking have passed. However, the whole distribution process calculates its values for a single month, using `daysBetweenDistributions`.

```solidity
        uint256 _seniorProportion = MATH.seniorProportion(
            IZivoeGlobals_YDL(GBL).standardize(yD, distributedAsset),
            MATH.yieldTarget(emaSTT, emaJTT, targetAPYBIPS, targetRatioBIPS, daysBetweenDistributions),
            emaSTT, emaJTT, targetAPYBIPS, targetRatioBIPS, daysBetweenDistributions
        );
```

The `targetYield` will be calculated for only 1 month, which will completely mess up the whole distribution process. Let's look at the following example: 

|Senior supply| Junior Supply | target APY | junior multiplier | 
| ----------- | ----------- | ------------- | ---------- |
| 20 000 | 4 000 | 10% | 2.2 |

The senior yield per month should be 10% / 12 = 0.83%
The junior yield per month is $0.0083 * 2.2 = 1.83$%

The senior yield in stablecoin for one month is `166 DAI`.
The junior yield in stablecoin for one month is `73 DAI`.

When distributing for a first time, the yield should be around `332 DAI` and `146 DAI`, since 2 months has passed.  In the test below we can see that when the yield is `800 DAI` the contract distributes only `166 DAI` to the senior and `73 DAI` to the junior. The rest is send to the residual addresses.

## Impact
Wrong yield distribution depending on the yield accumulation. The contract will calculate the yield target as much smaller value than it has to be and will take wrong decisions based on that.

## Code Snippet
Paste the following test in [Test_ZivoeYDL.sol](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-testing/src/TESTS_Core/Test_ZivoeYDL.sol) and import `console`.

```solidity
   function test_first_distribution() public {
       simulateITO_byTranche_optionalStake(20_000 ether, true);

       uint256 YDLUnlockTimestamp = block.timestamp;
       hevm.warp(YDL.lastDistribution() + YDL.daysBetweenDistributions() * 86400);

       address[] memory recipients = new address[](1);
       uint256[] memory proportions = new uint256[](1);
       recipients[0] = address(0xb0b);
       proportions[0] = 10000;

       hevm.prank(GBL.TLC());
       YDL.updateRecipients(recipients, proportions, false);

       // 60 days have passed since unlocking
       assertEq((block.timestamp - YDLUnlockTimestamp) / 86400, YDL.daysBetweenDistributions() * 2);

       mint("DAI", address(YDL), 800 ether);

       uint256 seniorTrancheBefore = IERC20(DAI).balanceOf(address(stSTT));
       uint256 juniorTrancheBefore = IERC20(DAI).balanceOf(address(stJTT));
       uint256 residualBefore = IERC20(DAI).balanceOf(address(0xb0b));

       YDL.distributeYield();

       uint256 seniorTrancheAfter = IERC20(DAI).balanceOf(address(stSTT));
       uint256 juniorTrancheAfter = IERC20(DAI).balanceOf(address(stJTT));
       uint256 residualAfter = IERC20(DAI).balanceOf(address(0xb0b));

       console.log('senior difference: ', seniorTrancheAfter - seniorTrancheBefore);
       console.log('junior difference: ', juniorTrancheAfter - juniorTrancheBefore);
       console.log('residual difference: ', residualAfter - residualBefore);
    }
```
The logs: 

```solidity
  senior difference:  164383561643835616438
  junior difference:  72328767123287671232
  residual difference:  403287671232876712330
```
From the 800 DAI that accrued in the contract, the senior and junior staking contracts received only around 236 DAI. The residuals got around 403 DAI.

## Tool used

Foundry

## Recommendation
Do not add 30 days to the `lastDistribution` variable when unlocking.

```diff
    function unlock() external {
      ...
-     lastDistribution = block.timestamp + 30 days;
+     lastDistribution = block.timestamp;
      ...
      }
```