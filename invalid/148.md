Ancient Chartreuse Kangaroo

high

# Funds can remain stuck and undistributed if `distributedAsset` of `ZivoeYDL.sol` change

## Summary
Yield which is supposed to be distributed by `ZivoeYDL.sol`, will be stuck in `OCT_YDL.sol` contract if the `distributedAsset` is changed.

## Vulnerability Detail
The `ZivoeYDL.sol` contract is designed to gather yield from across the protocol and distribute it to recipients. The yield is distributed in a single asset, specified by the `distributedAsset` state variable. This variable can be modified by calling the `updateDistributedAsset()` function.

When funds are sent to the `ZivoeYDL.sol` contract, all sending functions check if the `asset` being sent matches the current `distributedAsset`. If it doesn't, the funds are instead sent to the `OCT_YDL.sol` contract. This contract collects all non-matching assets, converts them to the `distributedAsset`, and forwards them to the `ZivoeYDL.sol` contract using the `convertAndForward()` function.

However, a vulnerability arises if the `distributedAsset` is changed after funds have been sent to the `OCT_YDL.sol` contract but before they have been converted and forwarded. In this scenario, the `convertAndForward()` function will attempt to convert an asset into itself, which will cause the transaction to revert. As a result, the funds will remain stuck in the `OCT_YDL.sol` contract, unable to be forwarded to the `ZivoeYDL.sol` contract.

This issue is exacerbated by the fact that the `OCT_YDL.sol` contract's `convertAndForward()` function does not check if the asset being converted matches the `ZivoeYDL.sol` contract's `distributedAsset` before attempting the transfer. This lack of a check means that the function will always attempt to convert and forward the funds, even if the `distributedAsset` has changed, leading to the aforementioned transaction reversion and stuck funds.

## Impact
This vulnerability can result in funds becoming trapped in the `OCT_YDL.sol` contract. This could lead to financial loss for users and stakers, as they would be unable to access their expected yield rewards.

## Code Snippet
Here is the vulnerable function of `OCT_YDL.sol` which does not check if the `asset` is the `distributedAsset` of `ZivoeYDL.sol` contract before transferring it to `ZivoeYDL.sol` contract :
```solidity
    function convertAndForward(address asset, bytes calldata data) external nonReentrant {
        require(
            IZivoeGlobals_OCT_YDL(GBL).isKeeper(_msgSender()),
            "OCT_YDL::convertAndForward !isKeeper(_msgSender())"
        );
        // ...
@>        convertAsset(asset, distributedAsset, amountFrom, data);
        // ...
        IERC20(distributedAsset).safeTransfer(IZivoeGlobals_OCT_YDL(GBL).YDL(), balDistributedAsset);
    }
```
[Link to code](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCT/OCT_YDL.sol#L97)

Here is the function of `convertAsset()` of `ZivoeSwapper.sol` which will revert the transaction if the `asset` is the `distributedAsset` :
```solidity
    function convertAsset(
        address assetIn,
        address assetOut,
        uint256 amountIn,
        bytes calldata data
    ) internal {
        // Handle validation.
        bytes4 sig = bytes4(data[:4]);
        if (sig == bytes4(keccak256("swap(address,(address,address,address,address,uint256,uint256,uint256),bytes,bytes)"))) {
            handle_validation_12aa3caf(data, assetIn, assetOut, amountIn);
        }

        // ...

        // Execute swap.
        (bool succ,) = address(router1INCH_V5).call(data);
        require(succ, "ZivoeSwapper::convertAsset() !succ");
    }
```
[Link to code](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/Utility/ZivoeSwapper.sol#L166)

Also, this is the function of `ZivoeYDL.sol` contract which can change the `distributedAsset` :
```solidity
    function updateDistributedAsset(address _distributedAsset) external nonReentrant {
        require(
            _distributedAsset != distributedAsset, 
            "ZivoeYDL::updateDistributedAsset() _distributedAsset == distributedAsset"
        );

        // ...

        emit UpdatedDistributedAsset(distributedAsset, _distributedAsset);
        distributedAsset = _distributedAsset;
    }
```
[Link to code](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeYDL.sol#L356)

## Tool used
Manual Review

## Recommendation
Consider adding this if-statement in `convertAndForward()` function of `OCT_YDL.sol` :
```diff
    function convertAndForward(address asset, bytes calldata data) external nonReentrant {
        require(
            IZivoeGlobals_OCT_YDL(GBL).isKeeper(_msgSender()),
            "OCT_YDL::convertAndForward !isKeeper(_msgSender())"
        );
        
+        if(asset == IZivoeYDL_OCT_YDL(IZivoeGlobals_OCT_YDL(GBL).YDL()).distributedAsset()) {
+            IERC20(asset).safeTransfer(IZivoeGlobals_OCT_YDL(GBL).YDL(), IERC20(asset).balanceOf(address(this)));
+            return;
+        }

        // ...
    }
```