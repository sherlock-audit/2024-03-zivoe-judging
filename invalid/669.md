Best Lava Dragon

medium

# `makePayment()` charges interest on the intervals that have not yet passed.

## Summary
Users can overpay interest when calling `makePayment()` multiple times.
## Vulnerability Detail
`makePayment()` calculates interest via call to `amountOwed()`.
[OCC_Modular.sol#L575-L578](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L575-L578)
```solidity
function makePayment(uint256 id) external nonReentrant {
    require(
        loans[id].state == LoanState.Active,
        "OCC_Modular::makePayment() loans[id].state != LoanState.Active"
    );
    (
        uint256 principalOwed,
        uint256 interestOwed,
        uint256 lateFee,
    ) = amountOwed(id);
```
This is how interest is calculated in `amountOwed()`.
[OCC_Modular.sol#L440-L457](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L440-L457)
```solidity
interest =
    (loans[id].principalOwed *
        loans[id].paymentInterval *
        loans[id].APR) /
    (86400 * 365 * BIPS);
total = principal + interest + lateFee;
```
Calling `makePayment()` multiple times in a row will result in a borrower paying interest for every payment interval, even though that interval has not yet passed
When using `callLoan()` to close the loan in one payment, the interest paid will only be for one interval, but doing the same action with `makePayment()` will result in paying interest for the entire duration of the loan
## Impact
User overpays interest.
## Proof Of Concept
```solidity
function test_OCC_Modular_interest_overpaying() public {
    mint("DAI", address(tim), 100_000e18);
    tim.try_approveToken(DAI, address(OCC_Modular_DAI), 100_000e18);
    uint256 borrowAmount = 50_000e18;
    uint256 APR = 5000;
    uint256 APRLateFee = 1000;
    uint256 term = 5;
    uint256 paymentInterval = 7 days;
    uint256 gracePeriod = 21 days;
    int8 paymentSchedule = 0;
    // Create loan
    vm.startPrank(address(roy));
    OCC_Modular_DAI.createOffer(
        address(tim),
        borrowAmount,
        APR,
        APRLateFee,
        term,
        paymentInterval,
        gracePeriod,
        paymentSchedule
    );
    vm.stopPrank();
    // Accept loan
    vm.prank(address(tim));
    OCC_Modular_DAI.acceptOffer(0);
    // DAI balance before repaying a loan
    uint256 balanceBefore = ERC20(DAI).balanceOf(address(tim));
    uint256 snapshot = vm.snapshot();
    vm.warp(block.timestamp + 6 days);
    vm.prank(address(tim));
    OCC_Modular_DAI.callLoan(0);
    (, , uint[10] memory info) = OCC_Modular_DAI.loanInfo(0);
    // Assert loanState == Repaid
    assertEq(info[9], 3);
    uint256 balanceAfter = ERC20(DAI).balanceOf(address(tim));
    uint256 interestPaidOnce = balanceBefore - balanceAfter - 50000e18;
    console2.log("Interest paid via callLoan(): ", interestPaidOnce / 1e18);
    console2.log();
    vm.revertTo(snapshot);
    // Repay a loan in 5 payments instead of 1
    vm.startPrank(address(tim));
    OCC_Modular_DAI.makePayment(0);
    OCC_Modular_DAI.makePayment(0);
    OCC_Modular_DAI.makePayment(0);
    OCC_Modular_DAI.makePayment(0);
    OCC_Modular_DAI.makePayment(0);
    vm.stopPrank();
    (, , uint[10] memory infoNew) = OCC_Modular_DAI.loanInfo(0);
    // Assert loanState == Repaid
    assertEq(infoNew[9], 3);
    balanceAfter = ERC20(DAI).balanceOf(address(tim));
    uint256 interestPaidMultiple = balanceBefore - balanceAfter - 50000e18;
    console2.log(
        "Interest paid via makePayment(): ",
        interestPaidMultiple / 1e18
    );
    console2.log();
    console2.log(
        "Interest overpaid: ",
        (interestPaidMultiple - interestPaidOnce) / 1e18
    );
}
```
Console output:
```md
Logs:
  Interest paid via callLoan():  479
  
  Interest paid via makePayment():  2397
  
  Interest overpaid:  1917
```
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L440-L457
## Tool used

Manual Review

## Recommendation
Change the way `amountOwed()` calculates interest, so user only pays for intervals that have actually passed.