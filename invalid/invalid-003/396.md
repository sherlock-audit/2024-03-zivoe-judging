Great Metal Ram

medium

# Minting `zJTT` and `zSTT` inside `ZivoeTranches` can be broken if the adjusted senior supply is 0.

## Summary

When users mint `zJTT` or `zSTT` inside `ZivoeTranches`, it will calculates the ZVE reward that will be sent to the users. The reward amount sent depends on the deposit amount and the adjusted senior and junior supply. However, this calculation doesn't account for the case when the adjusted senior supply is 0, causing the reward calculation to always revert due to division by 0.

## Vulnerability Detail 

When users call `depositSenior` / `depositJunior`, it will calculate incentives that will be send to users by calling `rewardZVESeniorDeposit` / `rewardZVEJuniorDeposit`.

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeTranches.sol#L306
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeTranches.sol#L283

```solidity
    function depositSenior(uint256 amount, address asset) public notPaused nonReentrant {
        require(
            IZivoeGlobals_ZivoeTranches(GBL).stablecoinWhitelist(asset), 
            "ZivoeTranches::depositSenior() !IZivoeGlobals_ZivoeTranches(GBL).stablecoinWhitelist(asset)"
        );
        require(tranchesUnlocked, "ZivoeTranches::depositSenior() !tranchesUnlocked");

        address depositor = _msgSender();

        IERC20(asset).safeTransferFrom(depositor, IZivoeGlobals_ZivoeTranches(GBL).DAO(), amount);
        
        uint256 convertedAmount = IZivoeGlobals_ZivoeTranches(GBL).standardize(amount, asset);

>>>     uint256 incentives = rewardZVESeniorDeposit(convertedAmount);

        emit SeniorDeposit(depositor, asset, amount, incentives);

        // Ordering important, transfer ZVE rewards prior to minting zJTT() due to totalSupply() changes.
        IERC20(IZivoeGlobals_ZivoeTranches(GBL).ZVE()).safeTransfer(depositor, incentives);
        IERC20Mintable_ZivoeTranches(IZivoeGlobals_ZivoeTranches(GBL).zSTT()).mint(depositor, convertedAmount);
    }
```

Inside `rewardZVESeniorDeposit` and `rewardZVEJuniorDeposit`, it will calculates the amount based on the deposit, as well as the adjusted senior and junior supply, to determine the ratio that will determine the reward rate for the users.

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeTranches.sol#L236-L262
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeTranches.sol#L203-L229

```solidity
    function rewardZVESeniorDeposit(uint256 deposit) public view returns (uint256 reward) {

>>>     (uint256 seniorSupp, uint256 juniorSupp) = IZivoeGlobals_ZivoeTranches(GBL).adjustedSupplies();

        uint256 avgRate;    // The avg ZVE per stablecoin deposit reward, used for reward calculation.

        uint256 diffRate = maxZVEPerJTTMint - minZVEPerJTTMint;

>>>     uint256 startRatio = juniorSupp * BIPS / seniorSupp;
        uint256 finalRatio = juniorSupp * BIPS / (seniorSupp + deposit);
        uint256 avgRatio = (startRatio + finalRatio) / 2;

        if (avgRatio <= lowerRatioIncentiveBIPS) {
            avgRate = minZVEPerJTTMint;
        } else if (avgRatio >= upperRatioIncentiveBIPS) {
            avgRate = maxZVEPerJTTMint;
        } else {
            avgRate = minZVEPerJTTMint + diffRate * (avgRatio - lowerRatioIncentiveBIPS) / (upperRatioIncentiveBIPS - lowerRatioIncentiveBIPS);
        }

        reward = avgRate * deposit / 1 ether;

        // Reduce if ZVE balance < reward.
        if (IERC20(IZivoeGlobals_ZivoeTranches(GBL).ZVE()).balanceOf(address(this)) < reward) {
            reward = IERC20(IZivoeGlobals_ZivoeTranches(GBL).ZVE()).balanceOf(address(this));
        }
    }
```

However, this calculation doesn't account for the case when `seniorSupp` is 0. As a result, the calculation will revert due to division by 0, causing users to be unable to mint new `zSTT` inside the contract.

The `seniorSupp` value becoming 0 can occur under several scenarios:

- When the total defaults exceed the total supply of JTT and STT.
- When none of the users participate in the ITO to deposit on the senior tranche, causing the total STT supply to become 0.
- When a griefer buys all STT tokens (especially in the early stages) and burns all the tokens to DoS the `ZivoeTranches` contract.


## Impact

When this happens, users cannot mint new `zSTT` because the calculation of the reward will revert due to division by 0. And, users cannot mint new `zJTT` because the `isJuniorOpen` check will prevent the operation (due to `seniorSupp` is 0).

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeTranches.sol#L306
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeTranches.sol#L283
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeTranches.sol#L236-L262
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeTranches.sol#L203-L229

## Tool used

Manual Review

## Recommendation

When `seniorSupp`, consider to set `avgRatio` to `lowerRatioIncentiveBIPS`.

```diff
    function rewardZVESeniorDeposit(uint256 deposit) public view returns (uint256 reward) {

        (uint256 seniorSupp, uint256 juniorSupp) = IZivoeGlobals_ZivoeTranches(GBL).adjustedSupplies();

        uint256 avgRate;    // The avg ZVE per stablecoin deposit reward, used for reward calculation.

+      uint256 avgRatio;
+      if (seniorSupp == 0) {
+          avgRatio =  lowerRatioIncentiveBIPS;        
+     } else {
        uint256 diffRate = maxZVEPerJTTMint - minZVEPerJTTMint;
        uint256 startRatio = juniorSupp * BIPS / seniorSupp;
        uint256 finalRatio = juniorSupp * BIPS / (seniorSupp + deposit);
-        uint256 avgRatio = (startRatio + finalRatio) / 2;
+        avgRatio = (startRatio + finalRatio) / 2;
+    }

        if (avgRatio <= lowerRatioIncentiveBIPS) {
            avgRate = minZVEPerJTTMint;
        } else if (avgRatio >= upperRatioIncentiveBIPS) {
            avgRate = maxZVEPerJTTMint;
        } else {
            avgRate = minZVEPerJTTMint + diffRate * (avgRatio - lowerRatioIncentiveBIPS) / (upperRatioIncentiveBIPS - lowerRatioIncentiveBIPS);
        }

        reward = avgRate * deposit / 1 ether;

        // Reduce if ZVE balance < reward.
        if (IERC20(IZivoeGlobals_ZivoeTranches(GBL).ZVE()).balanceOf(address(this)) < reward) {
            reward = IERC20(IZivoeGlobals_ZivoeTranches(GBL).ZVE()).balanceOf(address(this));
        }
    }
```
