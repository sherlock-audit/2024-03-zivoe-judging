Expert Inky Bobcat

medium

# If `seniorSupp` reaches zero, all future deposits to `ZivoeTranches` will be blocked.

## Summary
If `seniorSupp` reaches zero, all future deposits to `ZivoeTranches` will be blocked. 

## Vulnerability Detail
When depositing into tranches using `ZivoeTranches.depositJunior()` or `ZivoeTranches.depositSenior()`, incentive rewards are calculated by the `rewardZVEJuniorDeposit` or `rewardZVESeniorDeposit` functions, respectively. 

```solidity
    function rewardZVEJuniorDeposit(uint256 deposit) public view returns (uint256 reward) {


        (uint256 seniorSupp, uint256 juniorSupp) = IZivoeGlobals_ZivoeTranches(GBL).adjustedSupplies();


        uint256 avgRate;    // The avg ZVE per stablecoin deposit reward, used for reward calculation.


        uint256 diffRate = maxZVEPerJTTMint - minZVEPerJTTMint;


        uint256 startRatio = juniorSupp * BIPS / seniorSupp;
        uint256 finalRatio = (juniorSupp + deposit) * BIPS / seniorSupp;
        uint256 avgRatio = (startRatio + finalRatio) / 2;


        if (avgRatio <= lowerRatioIncentiveBIPS) {
            avgRate = maxZVEPerJTTMint;
        } else if (avgRatio >= upperRatioIncentiveBIPS) {
            avgRate = minZVEPerJTTMint;
        } else {
            avgRate = maxZVEPerJTTMint - diffRate * (avgRatio - lowerRatioIncentiveBIPS) / (upperRatioIncentiveBIPS - lowerRatioIncentiveBIPS);
        }


        reward = avgRate * deposit / 1 ether;


        // Reduce if ZVE balance < reward.
        if (IERC20(IZivoeGlobals_ZivoeTranches(GBL).ZVE()).balanceOf(address(this)) < reward) {
            reward = IERC20(IZivoeGlobals_ZivoeTranches(GBL).ZVE()).balanceOf(address(this));
        }
    }
```

If `seniorSupp` ever reaches zero, either due to the burning of `zSTT` tokens or excessive defaults resulting in the adjusted senior supply being zero, all calls to `rewardZVEJuniorDeposit` and `rewardZVESeniorDeposit` will revert due to division by zero.

Though improbable, this scenario can occur, and if it does, it will block all future deposits to tranches (junior or senior). This is because `depositSenior()` and `depositJunior()` functions internally call `rewardZVEJuniorDeposit` and `rewardZVESeniorDeposit`, respectively, both of which will revert under this scenario as explained earlier. Consequently, the contract will remain trapped in this state, preventing any further deposits to tranches.

As observed from the deployment procedure in the testing suite, the only two minters of senior tranche tokens (`zSTT`) are `ZivoeITO` and `ZivoeTranches`, and the ownership of `zsTT` is renounced. Therefore, there is no way to add new minters. Since `ZivoeITO` cannot mint after the ITO ends, if the above scenario occurs due to defaults or an attack by a malicious actor (intentionally burning tokens to cause this), there will be no way to fix this, and the `ZivoeTranches` contract will be permanently disabled.
## Impact
In summary, if `seniorSupp` ever reaches zero, it will render it impossible to deposit funds into either of the tranches,  impacting the integrity of the protocol.
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeTranches.sol#L203-L262

## Tool used
Manual Review

## Recommendation
Consider checking if `seniorSupp` is zero before doing the remaining `avgRatio` calculations, as shown below.

```diff
diff --git a/ZivoeTranches.sol b/ZivoeTranches.mod.sol
index 0657523..2217d0f 100644
--- a/ZivoeTranches.sol
+++ b/ZivoeTranches.mod.sol
@@ -208,16 +208,20 @@ contract ZivoeTranches is ZivoeLocker, ReentrancyGuard {
 
         uint256 diffRate = maxZVEPerJTTMint - minZVEPerJTTMint;
 
-        uint256 startRatio = juniorSupp * BIPS / seniorSupp;
-        uint256 finalRatio = (juniorSupp + deposit) * BIPS / seniorSupp;
-        uint256 avgRatio = (startRatio + finalRatio) / 2;
-
-        if (avgRatio <= lowerRatioIncentiveBIPS) {
-            avgRate = maxZVEPerJTTMint;
-        } else if (avgRatio >= upperRatioIncentiveBIPS) {
+        if (seniorSupp == 0) {
             avgRate = minZVEPerJTTMint;
         } else {
-            avgRate = maxZVEPerJTTMint - diffRate * (avgRatio - lowerRatioIncentiveBIPS) / (upperRatioIncentiveBIPS - lowerRatioIncentiveBIPS);

+            uint256 startRatio = juniorSupp * BIPS / seniorSupp;
+            uint256 finalRatio = (juniorSupp + deposit) * BIPS / seniorSupp;
+            uint256 avgRatio = (startRatio + finalRatio) / 2;
+
+            if (avgRatio <= lowerRatioIncentiveBIPS) {
+                avgRate = maxZVEPerJTTMint;
+            } else if (avgRatio >= upperRatioIncentiveBIPS) {
+                avgRate = minZVEPerJTTMint;
+            } else {
+                avgRate = maxZVEPerJTTMint - diffRate * (avgRatio - lowerRatioIncentiveBIPS) / (upperRatioIncentiveBIPS - lowerRatioIncent
iveBIPS);
+            }
         }
 
         reward = avgRate * deposit / 1 ether;
@@ -241,16 +245,20 @@ contract ZivoeTranches is ZivoeLocker, ReentrancyGuard {
 
         uint256 diffRate = maxZVEPerJTTMint - minZVEPerJTTMint;
 
-        uint256 startRatio = juniorSupp * BIPS / seniorSupp;
-        uint256 finalRatio = juniorSupp * BIPS / (seniorSupp + deposit);
-        uint256 avgRatio = (startRatio + finalRatio) / 2;
-
-        if (avgRatio <= lowerRatioIncentiveBIPS) {
-            avgRate = minZVEPerJTTMint;
-        } else if (avgRatio >= upperRatioIncentiveBIPS) {
+        if (seniorSupp == 0) {
             avgRate = maxZVEPerJTTMint;
         } else {
-            avgRate = minZVEPerJTTMint + diffRate * (avgRatio - lowerRatioIncentiveBIPS) / (upperRatioIncentiveBIPS - lowerRatioIncentiveBIPS);
m
+            uint256 startRatio = juniorSupp * BIPS / seniorSupp;
+            uint256 finalRatio = juniorSupp * BIPS / (seniorSupp + deposit);
+            uint256 avgRatio = (startRatio + finalRatio) / 2;
+
+            if (avgRatio <= lowerRatioIncentiveBIPS) {
+                avgRate = minZVEPerJTTMint;
+            } else if (avgRatio >= upperRatioIncentiveBIPS) {
+                avgRate = maxZVEPerJTTMint;
+            } else {
+                avgRate = minZVEPerJTTMint + diffRate * (avgRatio - lowerRatioIncentiveBIPS) / (upperRatioIncentiveBIPS - lowerRatioIncent
iveBIPS);
+            }
         }
 
         reward = avgRate * deposit / 1 ether;
```