Rough Plum Tapir

medium

# `epochDiscountSenior` is not updated correctly which may cause loss of redeemed amount and potentially DoS

## Summary
`epochDiscountSenior` state variable is not updated correctly in [`tickEpoch`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCR/OCR_Modular.sol#L310) function, which may cause `ZSTT` users to get less redeemed amount when calling `processRequest`, and may even cause DoS in some edge cases.

## Vulnerability Detail
Users can redeem their junior and senoir tranche tokens in the `OCR_modular` contract, by first creating a request, and then after locking period, users can process the request to get redeemed stable coins at the cost of burning `zSTT` and `zJTT` tokens. In `processRequest` function, discount amount is calculated in case of protocol defaults, and corresponded discounted amount of stable coins will be transfered to users.

```solidity
    function processRequest(uint256 id) external _tickEpoch nonReentrant {
        require(requests[id].amount > 0, "OCR_Modular::processRequest() requests[id].amount == 0");
        require(requests[id].unlocks <= epoch, "OCR_Modular::processRequest() requests[id].unlocks > epoch");

        requests[id].unlocks = epoch + 14 days;

        // Calculate the full amount of redemptions allowed for current epoch.
        uint256 totalRedemptions = redemptionsAllowedSenior + redemptionsAllowedJunior;

        // Standardize the amount of stablecoins present (given totalRedemptions is in WEI precision).
        uint stablecoinBalance = IZivoeGlobals_OCR(GBL).standardize(
            IERC20(stablecoin).balanceOf(address(this)), 
            stablecoin
        );

        // Calculate portion of stablecoins present relative to totalRedemptions requests, in BIPS precision.
        uint256 portion = (stablecoinBalance * RAY / totalRedemptions) / 10**23;

        // Hardcap portion at 10,000 (BIPS) to prevent overflow.
        if (portion > BIPS) { portion = BIPS; }

        // Identify the amount of tokens burnable (redeemable) relative to allowed redemptions and stablecoins present.
        uint256 burnAmount = requests[id].amount * portion / BIPS;

        uint256 redeemAmount;

        if (requests[id].seniorElseJunior) {
            IERC20Burnable_OCR(IZivoeGlobals_OCR(GBL).zSTT()).burn(burnAmount);
            // Reduce redeemable amount by the defaulted portion relative to tranche.
            redeemAmount = burnAmount * (BIPS - epochDiscountSenior) / BIPS;
            redemptionsAllowedSenior -= requests[id].amount;
            requests[id].amount -= burnAmount;
            redemptionsQueuedSenior += requests[id].amount;
        }
        else {
            IERC20Burnable_OCR(IZivoeGlobals_OCR(GBL).zJTT()).burn(burnAmount);
            // Reduce redeemable amount by the defaulted portion relative to tranche.
            redeemAmount = burnAmount * (BIPS - epochDiscountJunior) / BIPS;
            redemptionsAllowedJunior -= requests[id].amount;
            requests[id].amount -= burnAmount;
            redemptionsQueuedJunior += requests[id].amount;
        }

        if (IERC20Metadata(stablecoin).decimals() < 18) {
            redeemAmount /= 10 ** (18 - IERC20Metadata(stablecoin).decimals());
        }

        require(redeemAmount > 0, "OCR_Modular::processRequest() redeemAmount == 0");
        
        IERC20(stablecoin).safeTransfer(requests[id].account, redeemAmount * (BIPS - redemptionsFeeBIPS) / BIPS);
        IERC20(stablecoin).safeTransfer(IZivoeGlobals_OCR(GBL).DAO(), redeemAmount * redemptionsFeeBIPS / BIPS);
        emit RequestProcessed(id, requests[id].account, burnAmount, redeemAmount, requests[id].seniorElseJunior);
    }
```

In all of functions related to requests, there is this `_tickEpoch` modifier, which essentially executes `tickEpoch` function before the logic of each function. In this `tickEpoch` function, key variables are updated:

```solidity
    function tickEpoch() public {
        while (block.timestamp >= epoch + 14 days) { 
            epoch += 14 days;
            redemptionsAllowedJunior += redemptionsQueuedJunior;
            redemptionsAllowedSenior += redemptionsQueuedSenior;
            redemptionsQueuedJunior = 0;
            redemptionsQueuedSenior = 0;
            uint256 totalDefaults = IZivoeGlobals_OCR(GBL).defaults();
            uint256 zSTTSupply = IERC20(IZivoeGlobals_OCR(GBL).zSTT()).totalSupply();
            uint256 zJTTSupply = IERC20(IZivoeGlobals_OCR(GBL).zJTT()).totalSupply();
            if (totalDefaults > zJTTSupply) {
                epochDiscountJunior = BIPS;
                totalDefaults -= zJTTSupply;
                // @note how likely when totalDefaults > zSTTSupply?
                epochDiscountSenior = (totalDefaults * RAY / zSTTSupply) / 10**23;
            }
            else {
                epochDiscountJunior = (totalDefaults * RAY / zJTTSupply) / 10**23;
            }
            emit EpochTicked(
                epoch, 
                redemptionsAllowedJunior, 
                redemptionsAllowedSenior, 
                epochDiscountJunior, 
                epochDiscountSenior
            );
        }
    }
``` 

An epoch is updated every 14 days, and in each update, pending redemption amounts are added to allowed amounts, and discount amount is also re-calculated based on the current default value in the protocol.

Now, let's talk about how `zJTT` and `zSTT` work. Since the junior token holders are expected to face more risks while having more returns, and the opposite for the senior token holders. So when there are defaults in the protocol, junior holders will be affected and have less amount of stable coins redeemed. When the default is at a rather unsafe value, junior token holders will get nothing redeemed, and senior token holders are also affected, and their redemption will be discounted, too.

The problem arises when the defaults are solved and the protocol is healthy again, in `tickEpoch` function, we see, when default is less than the total supply of junior tokens, `epochDiscountSenior` is not touched at all. Normally, this is okay as the protocol operates in a healthy state and only junior token discount needs to be taken into account, and `epochDiscountSenior` will be 0 as the default value. In the case of the protocol experiencing some heavy defaults, and just recovering from that, `epochDiscountSenior` is set to the value which is when the protocol has more default than junior tokens. Whereas the reality is, there are more junior tokens than default, if there is anything to be deducted, it would be junior tokens. Since `epochDiscountSenior` is never updated after the situiation `totalDefault > zJTTSupply`, senior token holders will always get less redeemed until `totalDefault > zJTTSupply` happens again.

In another edge case, when `totalDefaults > zJTTSupply` and `totalDefaults > zSTTSupply`, this makes `(totalDefaults * RAY / zSTTSupply) / 10**23;` to have a greater value than the `BIPS` value, which is `10000`. Later in `processRequest`, 

```solidity
redeemAmount = burnAmount * (BIPS - epochDiscountSenior) / BIPS;
```

will always revert due to underflow issue. When there are no defaults, and with the old value of `epochDiscountSenior`, senior token holders will never be able to redeem stable coins in `OCR_Modular` due to the lack of updates in `tickEpoch`.

## Impact
As explained in the previous section, senior holders can get less redeemed stable coins when the protocol underperforms and recovers from having more defaults than junior tokens. 

## Code Snippet
```solidity
            uint256 totalDefaults = IZivoeGlobals_OCR(GBL).defaults();
            uint256 zSTTSupply = IERC20(IZivoeGlobals_OCR(GBL).zSTT()).totalSupply();
            uint256 zJTTSupply = IERC20(IZivoeGlobals_OCR(GBL).zJTT()).totalSupply();
            if (totalDefaults > zJTTSupply) {
                epochDiscountJunior = BIPS;
                totalDefaults -= zJTTSupply;
                epochDiscountSenior = (totalDefaults * RAY / zSTTSupply) / 10**23;
            }
            else {
                epochDiscountJunior = (totalDefaults * RAY / zJTTSupply) / 10**23;
            }
```


## Tool used

Manual Review

## Recommendation
In the case of `totalDefault <= zJJSupply`, always set `epochDiscountSenior ` to zero.
