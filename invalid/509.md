Great Metal Ram

medium

# User could pay more `lateFee` than intended when miss several loan payments

## Summary

When user miss several loan payments and then pay the loan by calling `makePayment`, user can pay more `lateFee` than intended due incorrect timing usage for calculating `lateFee`.

## Vulnerability Detail

From the documentation, it is stated that In some cases, the grace period may be longer than the payment interval, and the borrower may miss several loan payments before a loan enters default. In such an event, the borrower must resolve each missed payment.

When the borrower resolves each payment by calling `makePayment`, or when it is done by the underwriter on behalf of the borrower by calling `processPayment`, it will calculate the `principalOwed`, `interestOwed`, and `lateFee` by calling `amountOwed`.

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L575-L602
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L636-L677

```solidity
    function makePayment(uint256 id) external nonReentrant {
        require(loans[id].state == LoanState.Active, "OCC_Modular::makePayment() loans[id].state != LoanState.Active");

>>>     (uint256 principalOwed, uint256 interestOwed, uint256 lateFee,) = amountOwed(id);

        emit PaymentMade(
            id, _msgSender(), principalOwed + interestOwed + lateFee, principalOwed,
            interestOwed, lateFee, loans[id].paymentDueBy + loans[id].paymentInterval
        );

        // Transfer interest + lateFee to YDL if in same format, otherwise keep here for 1INCH forwarding.
        if (stablecoin == IZivoeYDL_OCC(IZivoeGlobals_OCC(GBL).YDL()).distributedAsset()) {
            IERC20(stablecoin).safeTransferFrom(_msgSender(), IZivoeGlobals_OCC(GBL).YDL(), interestOwed + lateFee);
        }
        else {
            IERC20(stablecoin).safeTransferFrom(_msgSender(), OCT_YDL, interestOwed + lateFee);
        }
        if (principalOwed > 0) { IERC20(stablecoin).safeTransferFrom(_msgSender(), owner(), principalOwed); }

        if (loans[id].paymentsRemaining == 1) {
            loans[id].state = LoanState.Repaid;
            loans[id].paymentDueBy = 0;
        }
        else { loans[id].paymentDueBy += loans[id].paymentInterval; }

        loans[id].principalOwed -= principalOwed;
        loans[id].paymentsRemaining -= 1;
    }
```

Inside `amountOwed`, when calculating `lateFee`, it will depend on time passed from current `block.timestamp` to `paymentDueBy`.

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L440-L457

```solidity
    function amountOwed(uint256 id) public view returns (
        uint256 principal, uint256 interest, uint256 lateFee, uint256 total
    ) {
        // 0 == Bullet.
        if (loans[id].paymentSchedule == 0) {
            if (loans[id].paymentsRemaining == 1) { principal = loans[id].principalOwed; }
        }
        // 1 == Amortization (only two options, use else here).
        else { principal = loans[id].principalOwed / loans[id].paymentsRemaining; }

        // Add late fee if past loans[id].paymentDueBy.
        if (block.timestamp > loans[id].paymentDueBy && loans[id].state == LoanState.Active) {
>>>         lateFee = loans[id].principalOwed * (block.timestamp - loans[id].paymentDueBy) *
                loans[id].APRLateFee / (86400 * 365 * BIPS);
        }
        interest = loans[id].principalOwed * loans[id].paymentInterval * loans[id].APR / (86400 * 365 * BIPS);
        total = principal + interest + lateFee;
    }
```

However, this doesn't consider scenarios when borrowers miss several loan payments. When borrowers pay multiple loan installments by calling `makePayment` for each payment, the aggregate `lateFee` will be much larger due to the incorrect timing usage for calculating `lateFee`.

Consider a scenario where a borrower misses 2 loan payments, and currently, 5 days have passed since the latest missed loan payment `paymentDueBy`, but still within the grace period, and the configured `paymentInterval` is 7 days.

When making the first loan payment, the `lateFee` will use the current time (`block.timestamp`) - `loans[id].paymentDueBy`, resulting in 5 days + 7 days = 12 days.

When making the second loan payment, the `lateFee` will also use the current time (`block.timestamp`) - `loans[id].paymentDueBy`, resulting in 5 days.

So, the total `lateFee` will be deducted for 12 days + 5 days = 17 days, while borrowers only miss 7 days + 5 days = 12 days.

## Impact

This will result in borrower paying `lateFee` more than it should be.

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L575-L602
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L636-L677
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L440-L457

## Tool used

Manual Review

## Recommendation

Consider to implement another version of `amountOwed` that will capped the `lateFee` until the next `paymentDueBy` when `paymentsRemaining` is not equal to 1. **NOTE** : this custom version `amountOwed` should only be used when calculating fees for `makePayment` and `processPayment`.

```diff
    function amountOwed(uint256 id) public view returns (
        uint256 principal, uint256 interest, uint256 lateFee, uint256 total
    ) {
        // 0 == Bullet.
        if (loans[id].paymentSchedule == 0) {
            if (loans[id].paymentsRemaining == 1) { principal = loans[id].principalOwed; }
        }
        // 1 == Amortization (only two options, use else here).
        else { principal = loans[id].principalOwed / loans[id].paymentsRemaining; }

        // Add late fee if past loans[id].paymentDueBy.
        if (block.timestamp > loans[id].paymentDueBy && loans[id].state == LoanState.Active) {
+        if (loans[id].paymentsRemaining == 1) {
           lateFee = loans[id].principalOwed * (block.timestamp - loans[id].paymentDueBy) *
                loans[id].APRLateFee / (86400 * 365 * BIPS);
+       } else {
+           lateFee = loans[id].principalOwed * (min(block.timestamp, (loans[id].paymentDueBy + loans[id].paymentInterval)) - loans[id].paymentDueBy) * 
+            loans[id].APRLateFee / (86400 * 365 * BIPS);
+        }
        }
        interest = loans[id].principalOwed * loans[id].paymentInterval * loans[id].APR / (86400 * 365 * BIPS);
        total = principal + interest + lateFee;
    }
```
