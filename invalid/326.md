Clumsy Cobalt Lion

medium

# The `junor:senior` ratio can be violated because of defaults

## Summary
[ZivoeTranches.isJuniorOpen()](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeTranches.sol#L192C1-L196C6) will return `true` when there are defaults in the system even if the minted junior tokens are already at their max allowed ratio. 

## Vulnerability Detail
If [ZivoeTranches.isJuniorOpen()](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeTranches.sol#L192C1-L196C6) returns true, a junior deposit can be made, otherwise the deposit transaction is reverted.

Let's suppose the junior:senior max ratio is set to 45% and all the allowed junior tokens are already minted. A big default comes into the system. Now `isJuniorOpen` will be returning true until the adjusted junior supply returns back to 45% of the adjusted senior supply. Now, the defaults are repaid and the junior tokens have become a lot more than the senior ones, completely screwing the intended use of junior:senior ratio.

There are assumptions everywhere left in the code comments that the junior supply will always be at most `maxTrancheRatioBIPS` of the senior supply, however this is not true.
```solidity
    /// @dev This ratio represents the maximum size allowed for junior tranche, relative to senior tranche.
    ///      A value of 2,000 represent 20%, thus junior tranche at maximum can be 20% the size of senior tranche.
    uint256 public maxTrancheRatioBIPS = 4500;
```

## Impact

## Code Snippet
```solidity
    function isJuniorOpen(uint256 amount, address asset) public view returns (bool open) {
        uint256 convertedAmount = IZivoeGlobals_ZivoeTranches(GBL).standardize(amount, asset);
        (uint256 seniorSupp, uint256 juniorSupp) = IZivoeGlobals_ZivoeTranches(GBL).adjustedSupplies();
        return convertedAmount + juniorSupp <= seniorSupp * maxTrancheRatioBIPS / BIPS;
    }
```

```solidity
    function markDefault(uint256 id) external isUnderwriter {
        require(loans[id].state == LoanState.Active, "OCC_Modular::markDefault() loans[id].state != LoanState.Active");
        require( 
            loans[id].paymentDueBy + loans[id].gracePeriod < block.timestamp, 
            "OCC_Modular::markDefault() loans[id].paymentDueBy + loans[id].gracePeriod >= block.timestamp"
        );
        
        emit DefaultMarked(id, loans[id].principalOwed);
        loans[id].state = LoanState.Defaulted;
        IZivoeGlobals_OCC(GBL).increaseDefaults(
            IZivoeGlobals_OCC(GBL).standardize(loans[id].principalOwed, stablecoin)
        );
    }
```

```solidity
    function resolveDefault(uint256 id, uint256 amount) external nonReentrant {
        require(
            loans[id].state == LoanState.Defaulted, 
            "OCC_Modular::resolveDefaut() loans[id].state != LoanState.Defaulted"
        );

        uint256 paymentAmount;

        if (amount >= loans[id].principalOwed) {
            paymentAmount = loans[id].principalOwed;
            loans[id].principalOwed = 0;
            loans[id].state = LoanState.Resolved;
        }
        else {
            paymentAmount = amount;
            loans[id].principalOwed -= paymentAmount;
        }

        emit DefaultResolved(id, paymentAmount, _msgSender(), loans[id].state == LoanState.Resolved);

        IERC20(stablecoin).safeTransferFrom(_msgSender(), owner(), paymentAmount);
        IZivoeGlobals_OCC(GBL).decreaseDefaults(IZivoeGlobals_OCC(GBL).standardize(paymentAmount, stablecoin));
    }
    
```

## Tool used

Manual Review

## Recommendation
Use the raw totalSupply of the tokens in `isJuniorOpen`.
