Glorious Midnight Snake

medium

# `OCL_ZVE#pullFromLockerPartial()` could fail due to underflow

## Summary
[`OCL_ZVE#pullFromLockerPartial()`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L253-L284) could fail due to underflow in the calculation `basis -= preBasis - postBasis`

## Vulnerability Detail
[`OCL_ZVE#pullFromLockerPartial()`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L253-L284)  is used to withdraw specified asset from `OCL_ZVE` to its owner `DAO`
If `asset` is the lp token perviously minted by calling `OCL_ZVE#pushToLockerMulti()`, it will be burnt for `$ZVE` and `pairAsset` and both of them will be sent to `DAO`
Once redeemed, `basis` will be updated:
```solidity
279:            basis -= preBasis - postBasis;
```
However, `basis` might be less than  `preBasis - postBasis` due to the high fluctuation of the `$ZVE` price, resulting in an underflow during the calculation and and causing `pullFromLockerPartial()` to revert.

Copy below codes into [`Test_OCL_ZVE.sol`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-testing/src/TESTS_Lockers/Test_OCL_ZVE.sol) and run `forge test --rpc-url RPC-URL --match-test test_OCL_ZVE_SUSHI_pullFromLockerPartial_fail_underflow`:
```solidity
    function test_OCL_ZVE_SUSHI_pullFromLockerPartial_fail_underflow() public {

        mint("DAI", address(this), 100e18);
        ZVE.approve(OCL_ZVE_SUSHI_DAI.router(), 100e18);
        IERC20(DAI).approve(OCL_ZVE_SUSHI_DAI.router(), 100e18);
        //@audit-info Add liquidity for DAI-ZVE pool 
        IUniswapV2Router01(OCL_ZVE_SUSHI_DAI.router()).addLiquidity(DAI, address(ZVE), 100e18, 100e18, 100e18, 100e18, address(this), block.timestamp);

        address[] memory assets = new address[](2);
        assets[0] = DAI;
        assets[1] = address(ZVE);
        uint256[] memory amounts = new uint256[](2);
        amounts[0] = amounts[1] = 10e18;
        //@audit-info DAO add liquidity for DAI-ZVE pool 
        assert(god.try_pushMulti(address(DAO), address(OCL_ZVE_SUSHI_DAI), assets, amounts, new bytes[](2)));
        //@audit-info swap DAI for ZVE
        buyZVE_Sushi(1e18, DAI);
        address lpToken = ISushiFactory(OCL_ZVE_SUSHI_DAI.factory()).getPair(DAI, address(ZVE));
        uint256 lpAmount = IERC20(lpToken).balanceOf(address(OCL_ZVE_SUSHI_DAI));
        vm.prank(address(DAO));
        vm.expectRevert(stdError.arithmeticError);
        //@audit-info pullFromLockerPartial fail due to overflow/underflow
        OCL_ZVE_SUSHI_DAI.pullFromLockerPartial(lpToken, lpAmount, abi.encode(uint(0), uint(0)));
    }
```
## Impact
`OCL_ZVE#pullFromLockerPartial()` could fail due to underflow

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L279
## Tool used

Manual Review

## Recommendation
Update `basis` directly to `postBasis` when `basis` is less than `preBasis - postBasis`:
```diff
    function pullFromLockerPartial(
        address asset, uint256 amount, bytes calldata data
    ) external override onlyOwner nonReentrant {
        address ZVE = IZivoeGlobals_OCL_ZVE(GBL).ZVE();
        address pair = IFactory_OCL_ZVE(factory).getPair(pairAsset, ZVE);
        
        (uint amountAMin, uint amountBMin) = abi.decode(data, (uint, uint));

        // "pair" represents the liquidity pool token (minted, burned).
        // "pairAsset" represents the stablecoin paired against $ZVE.
        if (asset == pair) {
            (uint256 preBasis,) = fetchBasis();
            IERC20(pair).safeIncreaseAllowance(router, amount);

            // Router removeLiquidity() endpoint.
            (uint256 claimedPairAsset, uint256 claimedZVE) = IRouter_OCL_ZVE(router).removeLiquidity(
                pairAsset, ZVE, amount, 
                amountAMin, amountBMin, address(this), block.timestamp + 14 days
            );
            emit LiquidityTokensBurned(amount, claimedZVE, claimedPairAsset);
            assert(IERC20(pair).allowance(address(this), router) == 0);
            
            IERC20(pairAsset).safeTransfer(owner(), IERC20(pairAsset).balanceOf(address(this)));
            IERC20(ZVE).safeTransfer(owner(), IERC20(ZVE).balanceOf(address(this)));
            (uint256 postBasis,) = fetchBasis();
            require(postBasis < preBasis, "OCL_ZVE::pullFromLockerPartial() postBasis >= preBasis");
-           basis -= preBasis - postBasis;
+           basis = basis > preBasis - postBasis ?  basis - (preBasis - postBasis) : postBasis;
        }
        else {
            IERC20(asset).safeTransfer(owner(), amount);
        }
    }
```