Feisty Taupe Locust

high

# The borrowers can pay the interest twice by calling the callLoan function when they repay their full debt.

## Summary
The `borrower` needs to make `periodic payments`.
During each `payment interval`, they're required to pay the `interest` once.
However, they can pay the `interest` again in the `callLoan` function.
## Vulnerability Detail
Suppose a `borrower` needs to pay off his `debt` in `5 steps`.
He has made the first `payment` and now is the second `payment interval`.
During this `interval`, he made a `payment` for this `period`.
```solidity
function makePayment(uint256 id) external nonReentrant {
    (uint256 principalOwed, uint256 interestOwed, uint256 lateFee,) = amountOwed(id);  // @audit, interest for this payment interval
    if (stablecoin == IZivoeYDL_OCC(IZivoeGlobals_OCC(GBL).YDL()).distributedAsset()) {
        IERC20(stablecoin).safeTransferFrom(_msgSender(), IZivoeGlobals_OCC(GBL).YDL(), interestOwed + lateFee);
    }
    else {
        IERC20(stablecoin).safeTransferFrom(_msgSender(), OCT_YDL, interestOwed + lateFee);
    }
    if (principalOwed > 0) { IERC20(stablecoin).safeTransferFrom(_msgSender(), owner(), principalOwed); }

    if (loans[id].paymentsRemaining == 1) {
        loans[id].state = LoanState.Repaid;
        loans[id].paymentDueBy = 0;
    }
    else { loans[id].paymentDueBy += loans[id].paymentInterval; }  // @audit, move to the next payment interval

    loans[id].principalOwed -= principalOwed;
    loans[id].paymentsRemaining -= 1;
}
```
Before that `interval` ended, the `borrower` decided to fully repaid his `loan` using the `callLoan` function.
The problem is that the `interest` for this `period` is applied again, even though the `borrower` already paid it.
```solidity
function callLoan(uint256 id) external nonReentrant {
    uint256 principalOwed = loans[id].principalOwed;
    (, uint256 interestOwed, uint256 lateFee,) = amountOwed(id);  // @audit, calculate interest again for already paid interval

    if (stablecoin == IZivoeYDL_OCC(IZivoeGlobals_OCC(GBL).YDL()).distributedAsset()) {
        IERC20(stablecoin).safeTransferFrom(_msgSender(), IZivoeGlobals_OCC(GBL).YDL(), interestOwed + lateFee); // @audit, pay interest again
    }
    else {
        IERC20(stablecoin).safeTransferFrom(_msgSender(), OCT_YDL, interestOwed + lateFee);
    }

    IERC20(stablecoin).safeTransferFrom(_msgSender(), owner(), principalOwed);
}
```
## Impact
This becomes a problem, especially if the `payment interval` is long, as the `interest` for a single `payment interval` can result in significant `funds loss` for the `borrower`.
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L578-L592
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L500-L512
## Tool used

Manual Review

## Recommendation
We need to verify if the `borrower` has already paid the `interest` for the current `period` within the `callLoan` function.