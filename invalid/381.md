Feisty Taupe Locust

medium

# The late fee can be applied multiple times to the borrower in the OCC_Modular

## Summary
When the `borrower` misses a `payment`, the `late fee` is applied.
However, this `late fee` can be applied multiple times to the `borrower`.
## Vulnerability Detail
There is a `payment interval` within which `borrowers` are required to make `payments`.
```solidity
function createOffer(
    address borrower,
    uint256 borrowAmount,
    uint256 APR,
    uint256 APRLateFee,
    uint256 term,
    uint256 paymentInterval,  // @audit, here
    uint256 gracePeriod,
    int8 paymentSchedule
) isUnderwriter external {
    require(term > 0, "OCC_Modular::createOffer() term == 0");
    require(
        paymentInterval == 86400 * 7 || paymentInterval == 86400 * 14 || paymentInterval == 86400 * 28 || 
        paymentInterval == 86400 * 91 || paymentInterval == 86400 * 364, 
        "OCC_Modular::createOffer() invalid paymentInterval value, try: 86400 * (7 || 14 || 28 || 91 || 364)"
    );
}
```
If a `borrower` misses a `payment`, a `late fee` is applied to the `outstanding debt`.
Once the user misses a `payment` and the `grace period` passes, that `loan` can be marked as `defaults`.
```solidity
function markDefault(uint256 id) external isUnderwriter {
    require(loans[id].state == LoanState.Active, "OCC_Modular::markDefault() loans[id].state != LoanState.Active");
    require( 
        loans[id].paymentDueBy + loans[id].gracePeriod < block.timestamp,   // @audit, here
        "OCC_Modular::markDefault() loans[id].paymentDueBy + loans[id].gracePeriod >= block.timestamp"
    );
    loans[id].state = LoanState.Defaulted;
}
```
`Borrowers` should aim to avoid such situations as much as possible.
There is no direct relationship between the `payment interval` and the `grace period`.

Let's consider a scenario where the `payment interval` is `7 days` and the `grace period` is `20 days`.
Suppose the `borrower` misses the `payment` for the period `[T- 7 days, T]` and current `paymentDueBy` is `T`.
The `borrower` attempts to pay the `debt` but encounters difficulties, leading to next missed `payment`.
Assume that current `block.timestamp` is `T + 11 days`.
In this case, the `borrower` misses `payments` twice for the periods `[T - 7 days, T]` and `[T, T + 7 days]`.
However, since the current time is still within the `grace period`, the `loan` remains `active`.

The `borrower` gathers some `funds` and attempts to make `payments` for both missed `intervals`.
For the first `payment`, he should pay the `late fee` for the period `[T, T + 11 days`].
```solidity
function amountOwed(uint256 id) public view returns (
    uint256 principal, uint256 interest, uint256 lateFee, uint256 total
) {
    if (block.timestamp > loans[id].paymentDueBy && loans[id].state == LoanState.Active) {
        lateFee = loans[id].principalOwed * (block.timestamp - loans[id].paymentDueBy) *  // @audit, here
            loans[id].APRLateFee / (86400 * 365 * BIPS);
    }
    interest = loans[id].principalOwed * loans[id].paymentInterval * loans[id].APR / (86400 * 365 * BIPS);
    total = principal + interest + lateFee;
}
```
After this `payment`, the `paymentDueBy` increases by `7 days`, becoming `T + 7 days`.
```solidity
function makePayment(uint256 id) external nonReentrant {
    (uint256 principalOwed, uint256 interestOwed, uint256 lateFee,) = amountOwed(id);
    if (stablecoin == IZivoeYDL_OCC(IZivoeGlobals_OCC(GBL).YDL()).distributedAsset()) {
        IERC20(stablecoin).safeTransferFrom(_msgSender(), IZivoeGlobals_OCC(GBL).YDL(), interestOwed + lateFee);
    }
    else {
        IERC20(stablecoin).safeTransferFrom(_msgSender(), OCT_YDL, interestOwed + lateFee);
    }
    if (principalOwed > 0) { IERC20(stablecoin).safeTransferFrom(_msgSender(), owner(), principalOwed); }

    if (loans[id].paymentsRemaining == 1) {
        loans[id].state = LoanState.Repaid;
        loans[id].paymentDueBy = 0;
    }
    else { loans[id].paymentDueBy += loans[id].paymentInterval; }  // @audit, here

    loans[id].principalOwed -= principalOwed;
    loans[id].paymentsRemaining -= 1;
}
```
For the second `payment` for the missed period `[T, T + 7 days]`, he should pay the `late fee` for the period `[T + 7 days, T + 11 days]` due to current timestamp is still larger than the current `paymentDueBy`.
As a result, the `borrower` ends up paying the `late fee` twice for the period `[T + 7 days, T + 11 days]`.
## Impact
This situation is unfavorable for `borrowers`.
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L551-L555
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L608-L611
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L451-L454
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L598
## Tool used

Manual Review

## Recommendation
We should take a snapshot of the last time the `late fee` was applied. 