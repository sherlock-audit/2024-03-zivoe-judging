Helpful Mercurial Finch

medium

# After the initial tranche offering, late ZVE airdrop claimers create an unfair vesting schedule

## Summary

At the end of initial tranche offering (ITO), the ITO participants can call [`claimAirdrop`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L203-L203) to start and create a vesting schedule. However, due to a hardcoded `daysToCliff` parameter, it breaks the core functionality of the contract that in principle it needs to create a fair vesting schedule for all ITO participants, potentially causing a relative financial loss for those who call [`claimAirdrop`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L203-L203) function late.

## Vulnerability Detail

At the end of the ITO period, `claimAirdrop` can be called:

```solidity
function claimAirdrop(address depositor) external returns (
        uint256 zSTTClaimed, uint256 zJTTClaimed, uint256 ZVEVested
    ) {
      require(end != 0, "ZivoeITO::claimAirdrop() end == 0");
      require(block.timestamp > end || migrated, "ZivoeITO::claimAirdrop() block.timestamp <= end && !migrated");
      require(!airdropClaimed[depositor], "ZivoeITO::claimAirdrop() airdropClaimed[depositor]");
      require(
          seniorCredits[depositor] > 0 || juniorCredits[depositor] > 0, 
          "ZivoeITO::claimAirdrop() seniorCredits[depositor] == 0 && juniorCredits[depositor] == 0"
      );

      airdropClaimed[depositor] = true;

      // Temporarily store credit values, decrease them to 0 immediately after.
      uint256 seniorCreditsOwned = seniorCredits[depositor];
      uint256 juniorCreditsOwned = juniorCredits[depositor];

      seniorCredits[depositor] = 0;
      juniorCredits[depositor] = 0;

      // Calculate proportion of $ZVE awarded based on $pZVE credits.
      uint256 upper = seniorCreditsOwned + juniorCreditsOwned;
      uint256 middle = IERC20(IZivoeGlobals_ITO(GBL).ZVE()).totalSupply() / 20;
      uint256 lower = IERC20(IZivoeGlobals_ITO(GBL).zSTT()).totalSupply() * 3 + (
          IERC20(IZivoeGlobals_ITO(GBL).zJTT()).totalSupply()
      );

      emit AirdropClaimed(depositor, seniorCreditsOwned / 3, juniorCreditsOwned, upper * middle / lower);

      IERC20(IZivoeGlobals_ITO(GBL).zJTT()).safeTransfer(depositor, juniorCreditsOwned);
      IERC20(IZivoeGlobals_ITO(GBL).zSTT()).safeTransfer(depositor, seniorCreditsOwned / 3);

      if (upper * middle / lower > 0) {
          ITO_IZivoeRewardsVesting(IZivoeGlobals_ITO(GBL).vestZVE()).createVestingSchedule(
              depositor, 0, 360, upper * middle / lower, false
          );
      }
      
      return (seniorCreditsOwned / 3, juniorCreditsOwned, upper * middle / lower);
  }
```

In the last few lines:

```solidity
          ITO_IZivoeRewardsVesting(IZivoeGlobals_ITO(GBL).vestZVE()).createVestingSchedule(
              // account
              depositor, 
              // daysToCliff
              0, 
             // daysToVest
             360, 
             // amountToVest
             upper * middle / lower, 
             //revokable
            false
          );
```

is called. `daysToCliff` is set to `0` and the amount is linearly vested every second, so the `depositor` can call [ZivoeRewardsVesting::withdraw()](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L501-L501) anytime as soon as the vesting schedule is created to claim however much they can claim.

The main problem is that `daysToVest` is hardcoded as `360`, which means the sooner you call `claimAirdrop`, the more you will be able to withdraw $ZVE relatively than others. That means for others who don't `claimAirdrop` late, the amount that they can claim is at a disadvantage than the ones who called `claimAirdrop` first.

Example: ITO ends on Apr 20 00:00 (start of the day of Apr 20)
- Bob has 10,000 ZVE to vest, and calls `claimAirdrop` precisely on Apr 20 00:00
- Alice has 10,000 ZVE to vest too, and calls `claimAirdrop` on Apr 25 00:00

For both of them, `vestingPerSecond = amountToVest / (daysToVest * 1 days) = (10000 * 10 ** 18) / (360 * 86400) = 321502057613168` = 0.000321502057613168 ZVE per second .

Both of them do not `withdraw` any ZVE until Apr 30 00:00. At Apr 30 00:00, both of them call `withdraw`. [`amountWithdrawable`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L280-L280) gives us the amount that can be withdrawn at any time:

```solidity
vestingScheduleOf[account].vestingPerSecond * (
                block.timestamp - vestingScheduleOf[account].start
            ) - vestingScheduleOf[account].totalWithdrawn
```

Therefore,
- Bob is able to claim `321502057613168 * (10 * 86400) = 277777777777777152000` = 277.7777... ZVE
- Alice is only able to claim `321502057613168 * (5 * 86400) = 138888888888888576000` = 138.888... ZVE

They want to convert ZVE into USDC. Let's say 1 ZVE = 1 USDC. Then Bob gets about 277.77 USDC and Alice gets about 138.88 USDC. Later on, the price of ZVE drops to 0.5 USDC. Bob then would have safely secured more of his assets at a better price, which is unfair because they are just the same ITO participants.

## Impact

Since the price of ZVE will be volatile, the safest financial strategy would be always to withdraw ZVE as much as possible at the earliest time (as with any staked assets in the world of crypto in general).

The faulty `claimAirdrop` function creates a linear vesting schedule with an artificial cliff period until the time `claimAirdrop` is called for a `depositor`, which breaks the main promise and core functionality of the protocol where it is specified that ["The ZVE airdrop is subject to a 1-year linear vesting schedule. This is done to encourage long-term participation in the protocol."](https://docs.zivoe.com/user-docs/liquidity-providers/initial-tranche-offering). If someone comes a month later and calls `claimAirdrop`, that person is actually creating a 1 year vesting schedule with <ins>a month of cliff</ins>, which gives him a relative financial disadvantage and risk.

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L236-L238

## Tool used

Manual Review

## Recommendation

Because `daysToVest` parameter is hardcoded, we can change that to reflect the time at which `claimAirdrop` is called:

```diff
+ uint daysFromEnd = (block.timestamp - end) / 86400;
+ // It's possible that an ITO participant comes after a year.
+ if (daysFromEnd > 360) daysFromEnd = 360;
ITO_IZivoeRewardsVesting(IZivoeGlobals_ITO(GBL).vestZVE()).createVestingSchedule(
-  depositor, 0, 360, upper * middle / lower, false
+  depositor, 0, 360 - daysFromEnd, upper * middle / lower, false
);
```

This way, all ITO participants will be equally treated for their vesting schedules no matter when they call `claimAirdrop` function.