Shiny Carob Raven

high

# Incorrect amount of allowance decrease in ZivoeDAO.push() and ZivoeDAO.pushMulti()

## Summary

The `push()` and `pushMulti()` function in the `ZivoeDAO` contract does not correctly set the locker's allowance to 0 after pushing the assets to the locker. If a locker does not spend all the USDT allowance during the pushToLocker()/pushTolockerMulti() call, subsequent USDT push to the locker will be DOSed.

## Vulnerability Detail

Some ERC20 tokens, such as USDT, require resetting the approval to 0 first before setting it to another value. This is likely the reason why the push() and pushMulti() functions have a comment stating:
>  // ZivoeDAO MUST ensure "locker" has 0 allowance before this function concludes.

However, the way these functions ensure 0 allowance is incorrect:

```solidity
   function push(address locker, address asset, uint256 amount, bytes calldata data) external onlyOwner nonReentrant {
        ...

        IERC20(asset).safeIncreaseAllowance(locker, amount);
        ILocker_DAO(locker).pushToLocker(asset, amount, data);
        // ZivoeDAO MUST ensure "locker" has 0 allowance before this function concludes.
        if (IERC20(asset).allowance(address(this), locker) > 0) { IERC20(asset).safeDecreaseAllowance(locker, 0); }
    }
```
```solidity
    function pushMulti(
        address locker, address[] calldata assets, uint256[] calldata amounts, bytes[] calldata data
    ) external onlyOwner nonReentrant {
        ...

        IERC20(assets[i]).safeIncreaseAllowance(locker, amounts[i]);
            emit Pushed(locker, assets[i], amounts[i], data[i]);
        }
        ILocker_DAO(locker).pushToLockerMulti(assets, amounts, data);
        for (uint256 i = 0; i < assets.length; i++) {
            // ZivoeDAO MUST ensure "locker" has 0 allowance for each ERC20 token before this function concludes.
            if (IERC20(assets[i]).allowance(address(this), locker) > 0) { IERC20(assets[i]).safeDecreaseAllowance(locker, 0); }
        }
    }
```

Instead of decrease the current allowance, the push() and pushMulti() functions only call `safeDecreaseAllowance(locker, 0);`, which decreases 0 allowance.

If the locker does not spend all the allowance increased in the `safeIncreaseAllowance` line for the USDT token, the next push of USDT tokens to this locker will fail as USDT require the allowance to be 0 before setting it to another value.


## Impact

If a locker does not spend all the allowance when the DAO pushes the USDT token to it, then subsequent pushes from DAO to the locker for USDT token are DOSed, the DAO cannot push USDT to the locker anymore.


## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeDAO.sol#L239-L247

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeDAO.sol#L282C1-L301C6



## Tool used

Manual Review

## Recommendation

Decrease the correct allowance before the push()/pushMulti() function concludes, e.g:
```diff
- if (IERC20(asset).allowance(address(this), locker) > 0) { IERC20(asset).safeDecreaseAllowance(locker, 0); }
+ if (IERC20(asset).allowance(address(this), locker) > 0) { IERC20(asset).safeDecreaseAllowance(locker, IERC20(asset).allowance(address(this), locker)); }
```


