Ancient Chartreuse Kangaroo

high

# The `allowance` of a locker to `ZivoeDAO` can remain non-zero after the `push()` of funds

## Summary
`ZivoeDAO` does not properly ensure the decrease of `allowance` of a locker which could lead to unauthorized malicious transactions on behalf of the DAO.

## Vulnerability Detail
In `ZivoeDAO` contract, DAO calls `push()` and `pushMulti()` functions so to allocate funds to each one of the lockers. First, it `safeIncreaseAllowance()` the amount of capital it wants to send and then it calls the `pushToLocker()` function of the particular locker. Then, wanting to ensure that the locker has 0 allowance once the funds are pushed, it checks if the allowance is greater than 0 and then calls `safeDecreaseAllowance()` with parameters the `locker` and `0`. However, this does not decrease the `allowance` **TO** 0, but rather decreases the `allowance` **BY** 0 basically meaning that the `allowance` will remain unchanged and non-zero.
Here you can see what happens in the `safeDecreaseAllowance()` function of `SafeERC20.sol` :
```solidity
    function safeDecreaseAllowance(IERC20 token, address spender, uint256 requestedDecrease) internal {
        unchecked {
            uint256 currentAllowance = token.allowance(address(this), spender);
            if (currentAllowance < requestedDecrease) {
                revert SafeERC20FailedDecreaseAllowance(spender, currentAllowance, requestedDecrease);
            }
            forceApprove(token, spender, currentAllowance - requestedDecrease);
        }
    }
```
[Link to code](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/11dc5e3809ebe07d5405fe524385cbe4f890a08b/contracts/token/ERC20/utils/SafeERC20.sol#L61)

## Impact
A malicious locker can exploit this vulnerability to maintain a non-zero `allowance` from the locker to `ZivoeDAO` even after the `push()` of funds. This could potentially allow the locker to accumulate a significant amount of `allowance` over multiple `push()` operations. Moreover, this vulnerability extends to multiple types of assets due to `pushMulti()` function, allowing the locker to gather allowances across various coins. This amplifies the potential damage as the locker can perform unauthorized transactions on behalf of `ZivoeDAO` across multiple asset types, and could even siphon off a diverse range of funds from it.

## Code Snippet
Here is the vulnerable code snippet in `push()` function : 
```solidity
    function push(address locker, address asset, uint256 amount, bytes calldata data) external onlyOwner nonReentrant {
        // ...

        // ZivoeDAO MUST ensure "locker" has 0 allowance before this function concludes.
@>        if (IERC20(asset).allowance(address(this), locker) > 0) { IERC20(asset).safeDecreaseAllowance(locker, 0); }
    }
```
[Link to code](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeDAO.sol#L246)

And here is the code snippet in `pushMulti()` function :
```solidity
    function pushMulti(
        address locker, address[] calldata assets, uint256[] calldata amounts, bytes[] calldata data
    ) external onlyOwner nonReentrant {
        // ...

        for (uint256 i = 0; i < assets.length; i++) {
            // ZivoeDAO MUST ensure "locker" has 0 allowance for each ERC20 token before this function concludes.
@>            if (IERC20(assets[i]).allowance(address(this), locker) > 0) { IERC20(assets[i]).safeDecreaseAllowance(locker, 0); }
        }
    }
```
[Link to code](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeDAO.sol#L299)


## Tool used
Manual Review

## Recommendation
Consider decreasing the allowance of the locker by the amount of the allowance that it has, not by 0 :
```diff
    if (IERC20(asset).allowance(address(this), locker) > 0) {
-        IERC20(asset).safeDecreaseAllowance(locker, 0); 
+        IERC20(asset).safeDecreaseAllowance(locker, IERC20(asset).allowance(address(this), locker));
    }
```