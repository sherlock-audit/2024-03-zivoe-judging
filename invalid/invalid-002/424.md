Spare Bamboo Dog

medium

# The `IERC20(asset).safeDecreaseAllowance(locker, 0)` did not have any effect

## Summary
The code `IERC20(asset).safeDecreaseAllowance(locker, 0);` within the `ZivoeDAO` contract did not produce any effect.

## Vulnerability Detail
In the `ZivoeDAO` contract, the `push` function is used to transfer an ERC20 token from `ZivoeDAO` to the locker, as shown below:
```solidity
function push(address locker, address asset, uint256 amount, bytes calldata data) external onlyOwner nonReentrant {
    require(IZivoeGlobals_DAO(GBL).isLocker(locker), "ZivoeDAO::push() !IZivoeGlobals_DAO(GBL).isLocker(locker)");
    require(ILocker_DAO(locker).canPush(), "ZivoeDAO::push() !ILocker_DAO(locker).canPush()");
    emit Pushed(locker, asset, amount, data);
    IERC20(asset).safeIncreaseAllowance(locker, amount);
    ILocker_DAO(locker).pushToLocker(asset, amount, data);
    // ZivoeDAO MUST ensure "locker" has 0 allowance before this function concludes.
    if (IERC20(asset).allowance(address(this), locker) > 0) { IERC20(asset).safeDecreaseAllowance(locker, 0); }
}
```
The final step `IERC20(asset).safeDecreaseAllowance(locker, 0);` is intended to ensure that the locker has 0 allowance before this function concludes when `IERC20(asset).allowance(address(this), locker) > 0`. However, according to the implementation of `safeDecreaseAllowance` in OpenZeppelin code:

```solidity
function safeDecreaseAllowance(IERC20 token, address spender, uint256 requestedDecrease) internal {
    unchecked {
        uint256 currentAllowance = token.allowance(address(this), spender);
        if (currentAllowance < requestedDecrease) {
            revert SafeERC20FailedDecreaseAllowance(spender, currentAllowance, requestedDecrease);
        }
        forceApprove(token, spender, currentAllowance - requestedDecrease);
    }
}
```
The `requestedDecrease` represents the amount by which the allowance is decreased. Setting it to 0 means that the allowance has not decreased at all. Thus, the code `if (IERC20(asset).allowance(address(this), locker) > 0) { IERC20(asset).safeDecreaseAllowance(locker, 0); }` does not have any effect.


## Impact
This could potentially allow `IERC20(asset).allowance(address(this), locker)` to remain greater than 0 after calling the `push` function, allowing the `locker` to continue using the asset in the `ZivoeDAO` contract.

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeDAO.sol#L239-L247

## Tool used

Manual Review

## Recommendation

It is recommended to replace it with `if (IERC20(asset).allowance(address(this), locker) > 0) { IERC20(asset).forceApprove(locker, 0); }`.
