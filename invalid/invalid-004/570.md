Clumsy Cobalt Lion

medium

# Insufficient validation in `ZivoeRewardsVesting.createVestingSchedule`

## Summary
[ZivoeRewardsVesting.createVestingSchedule()](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L381-L425) accepts `daysToVest` parameter in days. The function should validate a vesting schedule to be no longer than 1800 days. It fails to do so because it assumets `daysToVest` is in seconds.

## Vulnerability Detail
One of the require statements tries to enforce the vesting period to be no longer than 1800 days, assuming `daysToVest` is in seconds.
```solidity
        require(daysToVest <= 1800 days, "ZivoeRewardsVesting::createVestingSchedule() daysToVest > 1800 days");
```

However, `daysToVest` are to be passed as days, not seconds. We can see that from 2 different sources. The first is this line of code that converts `daysToVest` to actual seconds.

```solidity
        vestingScheduleOf[account].end = block.timestamp + daysToVest * 1 days;
```

The second is the [ITO](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L236C1-L238C15). It passes `360`, not `360 days`.
```solidity
            ITO_IZivoeRewardsVesting(IZivoeGlobals_ITO(GBL).vestZVE()).createVestingSchedule(
                depositor, 0, 360, upper * middle / lower, false
            );
```
## Impact
As a result, `daysToVest` can go up to the number of seconds there are in 1800 days, which is `155 520 000 days`. Practically, this means there is no upper limit for `daysToVest`. Such vesting schedules, combined with `revokable = false` will stay forever active. 

## Code Snippet
```solidity
    function createVestingSchedule(
        address account, 
        uint256 daysToCliff, 
        uint256 daysToVest, 
        uint256 amountToVest, 
        bool revokable
    ) external onlyZVLOrITO {
        require(
            !vestingScheduleSet[account], 
            "ZivoeRewardsVesting::createVestingSchedule() vestingScheduleSet[account]"
        );
        require(
            IERC20(vestingToken).balanceOf(address(this)) - vestingTokenAllocated >= amountToVest, 
            "ZivoeRewardsVesting::createVestingSchedule() amountToVest > vestingToken.balanceOf(address(this)) - vestingTokenAllocated"
        );
        require(daysToVest <= 1800 days, "ZivoeRewardsVesting::createVestingSchedule() daysToVest > 1800 days");
        require(daysToCliff <= daysToVest, "ZivoeRewardsVesting::createVestingSchedule() daysToCliff > daysToVest");
        require(
            IZivoeITO_ZivoeRewardsVesting(IZivoeGlobals_ZivoeRewardsVesting(GBL).ITO()).seniorCredits(account) == 0 &&
            IZivoeITO_ZivoeRewardsVesting(IZivoeGlobals_ZivoeRewardsVesting(GBL).ITO()).juniorCredits(account) == 0,
            "ZivoeRewardsVesting::createVestingSchedule() seniorCredits(_msgSender) > 0 || juniorCredits(_msgSender) > 0"
        );

        vestingScheduleSet[account] = true;
        vestingTokenAllocated += amountToVest;
        
        vestingScheduleOf[account].start = block.timestamp;
        vestingScheduleOf[account].cliff = block.timestamp + daysToCliff * 1 days;
        vestingScheduleOf[account].end = block.timestamp + daysToVest * 1 days;
        vestingScheduleOf[account].totalVesting = amountToVest;
        vestingScheduleOf[account].vestingPerSecond = amountToVest / (daysToVest * 1 days);
        vestingScheduleOf[account].revokable = revokable;
        
        emit VestingScheduleCreated(
            account, 
            vestingScheduleOf[account].start, 
            vestingScheduleOf[account].cliff, 
            vestingScheduleOf[account].end, 
            vestingScheduleOf[account].totalVesting, 
            vestingScheduleOf[account].vestingPerSecond, 
            vestingScheduleOf[account].revokable
        );

        _stake(amountToVest, account);
    }
```

## Tool used
Manual Review

## Recommendation
```diff
-        require(daysToVest <= 1800 days, "ZivoeRewardsVesting::createVestingSchedule() daysToVest > 1800 days");
+       require(daysToVest <= 1800, "ZivoeRewardsVesting::createVestingSchedule() daysToVest > 1800 days");
```