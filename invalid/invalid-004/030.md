Faithful Sky Wallaby

high

# ZivoeRewardsVesting incorrectly checks vesting period during vesting creation, allowing rewards redeemable 400,000 years in the future

## Summary

When calling `ZivoeRewardsVesting.createVestingSchedule()` the function checks that `daysToVest` and `daysToCliff` are both below `1800 days`, meaning that these parameters are expected to already be scaled to `days`. However those 2 variables are both later multiplied by `1 days` meaning that vesting schedules can have vesting lengths that will never realistically finish.

## Vulnerability Detail

[ZivoeRewardsVesting::createVestingSchedule](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L381-L425)
```solidity
    function createVestingSchedule(
        address account, 
        uint256 daysToCliff, 
        uint256 daysToVest, 
        uint256 amountToVest, 
        bool revokable
    ) external onlyZVLOrITO {
... SKIP!...
        require(daysToVest <= 1800 days, "ZivoeRewardsVesting::createVestingSchedule() daysToVest > 1800 days");
        require(daysToCliff <= daysToVest, "ZivoeRewardsVesting::createVestingSchedule() daysToCliff > daysToVest");
        require(
            IZivoeITO_ZivoeRewardsVesting(IZivoeGlobals_ZivoeRewardsVesting(GBL).ITO()).seniorCredits(account) == 0 &&
            IZivoeITO_ZivoeRewardsVesting(IZivoeGlobals_ZivoeRewardsVesting(GBL).ITO()).juniorCredits(account) == 0,
            "ZivoeRewardsVesting::createVestingSchedule() seniorCredits(_msgSender) > 0 || juniorCredits(_msgSender) > 0"
        );

        vestingScheduleSet[account] = true;
        vestingTokenAllocated += amountToVest;
        
        vestingScheduleOf[account].start = block.timestamp;
        vestingScheduleOf[account].cliff = block.timestamp + daysToCliff * 1 days;
        vestingScheduleOf[account].end = block.timestamp + daysToVest * 1 days;
        vestingScheduleOf[account].totalVesting = amountToVest;
        vestingScheduleOf[account].vestingPerSecond = amountToVest / (daysToVest * 1 days);
        vestingScheduleOf[account].revokable = revokable;
```
The first 2 require statements in `createVestingSchedule` ensures that `daysToVest` and `daysToCliff` is below or equal to `1800 days` which is equal to `155520000`.

Meaning that both values could be equal to that maximum of `155520000`.
However these values are later scaled up to `1 days`:

[ZivoeRewardsVesting.sol#L408-L409](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L408-L409)
```solidity
        vestingScheduleOf[account].cliff = block.timestamp + daysToCliff * 1 days;
        vestingScheduleOf[account].end = block.timestamp + daysToVest * 1 days;
```
Meaning a vesting schedule can be equal to `155520000 days`
This means that vesting schedules can be created that will never finish vesting (the vesting will unlock in over 400,000 years).

If `revokable` is `true` then `ZivoeRewardsVesting::revokeVestingSchedule` can be called to fix this, however if `revokable` was set to `false` then those tokens will be in the vesting period forever and unredeemable. 

## Impact

Due to the incorrect require check `daysToCliff` and `daysToVest` can be set up to a maximum of `155520000 days` in the future, meaning any staked rewards through `createVestingSchedule` will be locked forever and lost. 

## Code Snippet

[ZivoeRewardsVesting.sol#L381-L425](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L381-L425)
[ZivoeRewardsVesting.sol#L408-L409](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L408-L409)

## Tool used

Manual Review

## Recommendation

Remove the days multiplication of the upper bound of the `daysToVest` check:

```diff
-       require(daysToVest <= 1800 days, "ZivoeRewardsVesting::createVestingSchedule() daysToVest > 1800 days");
+      require(daysToVest <= 1800, "ZivoeRewardsVesting::createVestingSchedule() daysToVest > 1800 days");
```