Great Metal Ram

medium

# Stablecoin prices are assumed to be $1 using a 1:1 ratio, which could cause issues in the event of stablecoin depegging

## Summary

When users deposit to `ZivoeTranches` or participate in ITO on `ZivoeITO`, it always use 1:1 ratio for the whitelisted stablecoins, despite the current state/price of the stablecoins and could cause issues in the case of stabelcoin depegging.

## Vulnerability Detail

It can be observed that when users deposit stablecoin to `ZivoeTranches` by calling `rewardZVESeniorDeposit` or `rewardZVEJuniorDeposit`, it will assume 1:1 ratio despite the current price of the whitelisted stablecoins.

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeTranches.sol#L268-L289
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeTranches.sol#L295-L315

```solidity
    function depositSenior(uint256 amount, address asset) public notPaused nonReentrant {
        require(
            IZivoeGlobals_ZivoeTranches(GBL).stablecoinWhitelist(asset), 
            "ZivoeTranches::depositSenior() !IZivoeGlobals_ZivoeTranches(GBL).stablecoinWhitelist(asset)"
        );
        require(tranchesUnlocked, "ZivoeTranches::depositSenior() !tranchesUnlocked");

        address depositor = _msgSender();

        IERC20(asset).safeTransferFrom(depositor, IZivoeGlobals_ZivoeTranches(GBL).DAO(), amount);
        
>>>     uint256 convertedAmount = IZivoeGlobals_ZivoeTranches(GBL).standardize(amount, asset);

        uint256 incentives = rewardZVESeniorDeposit(convertedAmount);

        emit SeniorDeposit(depositor, asset, amount, incentives);

        // Ordering important, transfer ZVE rewards prior to minting zJTT() due to totalSupply() changes.
        IERC20(IZivoeGlobals_ZivoeTranches(GBL).ZVE()).safeTransfer(depositor, incentives);
>>>     IERC20Mintable_ZivoeTranches(IZivoeGlobals_ZivoeTranches(GBL).zSTT()).mint(depositor, convertedAmount);
    }
```

The same case is also applied when users participate in the ITO on `ZivoeITO` contract.

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeITO.sol#L248-L271
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeITO.sol#L277-L298

```solidity
    function depositSenior(uint256 amount, address asset) public {
        require(block.timestamp < end, "ZivoeITO::depositSenior() block.timestamp >= end");
        require(!migrated, "ZivoeITO::depositSenior() migrated");
        require(
            asset == stables[0] || asset == stables[1] || asset == stables[2] || asset == stables[3],
            "ZivoeITO::depositSenior() asset != stables[0-3]"
        );
        require(
            !ITO_IZivoeRewardsVesting(IZivoeGlobals_ITO(GBL).vestZVE()).vestingScheduleSet(_msgSender()),
            "ZivoeITO::depositSenior() ITO_IZivoeRewardsVesting(vestZVE).vestingScheduleSet(_msgSender())"
        );

        address caller = _msgSender();
>>>     uint256 standardizedAmount = IZivoeGlobals_ITO(GBL).standardize(amount, asset);

        seniorCredits[caller] += standardizedAmount * 3;

        emit SeniorDeposit(caller, asset, amount, standardizedAmount * 3, standardizedAmount);

        IERC20(asset).safeTransferFrom(caller, address(this), amount);
>>>     IERC20Mintable_ITO(IZivoeGlobals_ITO(GBL).zSTT()).mint(address(this), standardizedAmount);
    }
```

## Impact

This could cause issues in the case of stablecoin depegging. Consider a scenario where depegging occurs and one of the whitelisted stablecoins is now priced at $0.8 for a short amount of time. Users deposit to `ZivoeTranches` to mint JTT/STT at this price and then stake the JTT/STT to the `ZivoeRewards` contract to get yield rewards from the protocol.

Now, the protocol has to pay the yield to the users based on the assumed 1:1 ratio, resulting in users receiving bigger yield rewards while providing a lesser amount of liquidity.

Besides that, it will also open arbitrage opportunities that can cause instability in the JTT/STT price in the market.

Historical references : the USDC depegging, have caused a series of stablecoin depegging events, leading to FRAX reaching $0.88 [here](https://crypto.ro/en/news/usdc-depeg-triggers-domino-effect-on-other-stablecoins/)

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeTranches.sol#L268-L289
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeTranches.sol#L295-L315
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeITO.sol#L248-L271
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeITO.sol#L277-L298

## Tool used

Manual Review

## Recommendation

Consider utilizing a price oracle and implementing a safeguard to check if the current provided stablecoin price within the allowed price range for deposit. If not, revert the operation.
