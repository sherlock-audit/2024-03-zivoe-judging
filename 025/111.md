Rough Tiger Cricket

medium

# The create new vesting schedule feature is DOS for specific account

## Summary
After create vesting schedule for an account, admin or ITO contract can revoke that schedule. However, the revoke function doesn't modify the `vestingScheduleSet[account]` when revoke, so after revoke, admin can't create new vesting schedule for that account

## Vulnerability Detail
When admin or ITO create a vesting schedule for an user A, they call the `createVestingSchedule` function. This function check if that account already have a vesting schedule or not by require `vestingScheduleSet[account] == false`. After that, function set `vestingScheduleSet[account] == true`. The problem happen when admin or ITO want to revoke the vesting schedule for that account by using `revokeVestingSchedule` function. This function require `vestingScheduleSet[account] == true` to continue process. However, this function doesn't change the state of `vestingScheduleSet[account]` to `false` after revoke. So when admin or ITO try to create new vesting schedule for user A, require `vestingScheduleSet[account] == false` which is not satisfy lead to can't create new vesting schedule for user A.
Noted that this flow in not normal flow for user, cause a normal user will call `claimAirdrop` and create a vesting schedule which is not revokable, so this will be a medium.
## POC 
Paste this function into `zivoe-core-testing\src\TESTS_Core\Test_ZivoeRewardsVesting.sol` and run test
```solidity
function test_mytest_poc_revoke_without_modify_state(uint96 random) public {
        uint256 amount = uint256(random);

        // createVestingSchedule().
        assert(zvl.try_createVestingSchedule(
            address(vestZVE), 
            address(moe), 
            amount % 360 + 1, 
            (amount % 360 * 5 + 1),
            amount % 12_500_000 ether + 1, 
            true
        ));

        // revoke vesting schedule
        hevm.startPrank(address(zvl));
        vestZVE.revokeVestingSchedule(address(moe));
        hevm.stopPrank();

        // because vestingScheduleSet[account] is not set to false when revoke so admin can't create new vesting schedule for that account
        assert(zvl.try_createVestingSchedule(
            address(vestZVE), 
            address(moe), 
            amount % 360 + 1, 
            (amount % 360 * 5 + 1),
            amount % 12_500_000 ether + 1, 
            true
        ));
    }
```
## Impact
Admin can't create new vesting schedule for user

## Code Snippet
[ZivoeRewardsVesting:createVestingSchedule](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L388)
```solidity
    function createVestingSchedule(
        address account, 
        uint256 daysToCliff, 
        uint256 daysToVest, 
        uint256 amountToVest, 
        bool revokable
    ) external onlyZVLOrITO {
        require(
            !vestingScheduleSet[account], 
            "ZivoeRewardsVesting::createVestingSchedule() vestingScheduleSet[account]"
        );
        require(
            IERC20(vestingToken).balanceOf(address(this)) - vestingTokenAllocated >= amountToVest, 
            "ZivoeRewardsVesting::createVestingSchedule() amountToVest > vestingToken.balanceOf(address(this)) - vestingTokenAllocated"
        );
```

[ZivoeRewardsVesting:revokeVestingSchedule](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L430)
```solidity
    function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
        require(
            vestingScheduleSet[account], 
            "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleSet[account]"
        );
        require(
            vestingScheduleOf[account].revokable, 
            "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleOf[account].revokable"
        );
        
        uint256 amount = amountWithdrawable(account);
        uint256 vestingAmount = vestingScheduleOf[account].totalVesting;


        vestingTokenAllocated -= amount;


        vestingScheduleOf[account].totalWithdrawn += amount;
        vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
        vestingScheduleOf[account].cliff = block.timestamp - 1;
        vestingScheduleOf[account].end = block.timestamp;


        vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);


        _totalSupply = _totalSupply.sub(vestingAmount);
        _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
        _writeCheckpoint(_checkpoints[account], _subtract, amount);
        _balances[account] = 0;
        stakingToken.safeTransfer(account, amount);


        vestingScheduleOf[account].revokable = false;


        emit VestingScheduleRevoked(
            account, 
            vestingAmount - vestingScheduleOf[account].totalWithdrawn, 
            vestingScheduleOf[account].cliff, 
            vestingScheduleOf[account].end, 
            vestingScheduleOf[account].totalVesting, 
            false
        );
    }
```
## Tool used

Manual Review

## Recommendation
Set `vestingScheduleSet[account] = false` in `revokeVestingSchedule` function.