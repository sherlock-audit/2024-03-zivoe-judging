Round Hazelnut Tadpole

medium

# Unclaimable portion of deposited reward token amount can be locked in corresponding `ZivoeRewards` or `ZivoeRewardsVesting` contract

## Summary
In the `ZivoeRewards` and `ZivoeRewardsVesting` contracts, not all of the deposited reward token amount can be claimed due to staking timing. Because these contracts' `_getRewardAt` function can only transfer out the claimable part of the deposited reward token amount, and there is no other way to transfer out the unclaimable portion, such unclaimable deposited reward token amount is locked in the corresponding contract.

## Vulnerability Detail
In the `ZivoeRewards` contract, not all of the deposited amount of a reward token can be claimed.

For example, if no active staking exists before the reward token's amount is deposited and the next staking takes place after the reward token's `periodFinish`, all of such reward token's deposited amount cannot be claimed no matter how long such staker stakes. In this case, 
calling the `earned` function shown in the Code Snippet section when such staker claims rewards returns 0 since `lastTimeRewardApplicable(_rewardsToken).sub(rewardData[_rewardsToken].lastUpdateTime)` equals 0, causing such staker's `rewards[_msgSender()][_rewardsToken]` to remain 0 in her or his `ZivoeRewards._getRewardAt` function call shown in the Code Snippet section, and all of such reward token's deposited amount remains in the contract.

For another instance, if no active staking exists before the reward token's amount is deposited and the next staking takes place when half of the reward token's `rewardsDuration` just finishes, half of the reward token's deposited amount can be claimed when such staker stakes until such reward token's `periodFinish` is reached. In this case, calling the `earned` function when such staker claims rewards returns half of such reward token's deposited amount because `lastTimeRewardApplicable(_rewardsToken).sub(rewardData[_rewardsToken].lastUpdateTime)` equals half of such reward token's `rewardsDuration`, causing such staker's `rewards[_msgSender()][_rewardsToken]` to be half of such reward token's deposited amount in her or his the `ZivoeRewards._getRewardAt` function call, and the other half of such reward token's deposited amount remains in the contract.

Currently, the only way to transfer out the reward token's deposited amount is the `ZivoeRewards._getRewardAt` function but calling such function can only transfer out the claimable reward token amount, not the unclaimable part of the deposited reward token amount. Moreover, the same issue occurs in the `ZivoeRewardsVesting` contract.

## Impact
Because there is no guarantee that staking would occur before the reward token's amount is deposited or just after the deposition of the reward token amount in the same block, part of the reward token's deposited amount can be unclaimable. Since there is no way to transfer out the unclaimable portion of the deposited reward token amount, such unclaimable amount will be locked in the corresponding `ZivoeRewards` or `ZivoeRewardsVesting` contract and be lost.

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L300-L304
```solidity
    function earned(address account, address _rewardsToken) public view returns (uint256 amount) {
        return _balances[account].mul(
            rewardPerToken(_rewardsToken).sub(accountRewardPerTokenPaid[account][_rewardsToken])
        ).div(1e18).add(rewards[account][_rewardsToken]);
    }
```

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L316-L323
```solidity
    function rewardPerToken(address _rewardsToken) public view returns (uint256 amount) {
        if (_totalSupply == 0) { return rewardData[_rewardsToken].rewardPerTokenStored; }
        return rewardData[_rewardsToken].rewardPerTokenStored.add(
            lastTimeRewardApplicable(_rewardsToken).sub(
                rewardData[_rewardsToken].lastUpdateTime
            ).mul(rewardData[_rewardsToken].rewardRate).mul(1e18).div(_totalSupply)
        );
    }
```

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L309-L311
```solidity
    function lastTimeRewardApplicable(address _rewardsToken) public view returns (uint256 timestamp) {
        return Math.min(block.timestamp, rewardData[_rewardsToken].periodFinish);
    }
```

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L287-L295
```solidity
    function _getRewardAt(uint256 index) internal nonReentrant {
        address _rewardsToken = rewardTokens[index];
        uint256 reward = rewards[_msgSender()][_rewardsToken];
        if (reward > 0) {
            rewards[_msgSender()][_rewardsToken] = 0;
            IERC20(_rewardsToken).safeTransfer(_msgSender(), reward);
            emit RewardDistributed(_msgSender(), _rewardsToken, reward);
        }
    }
```

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L490-L498
```solidity
    function _getRewardAt(uint256 index) internal nonReentrant {
        address _rewardsToken = rewardTokens[index];
        uint256 reward = rewards[_msgSender()][_rewardsToken];
        if (reward > 0) {
            rewards[_msgSender()][_rewardsToken] = 0;
            IERC20(_rewardsToken).safeTransfer(_msgSender(), reward);
            emit RewardDistributed(_msgSender(), _rewardsToken, reward);
        }
    }
```

## Tool used
Manual Review

## Recommendation
The `ZivoeRewards` and `ZivoeRewardsVesting` contracts can be respectively updated to add a function to only allow the trusted admin or DAO of the protocol to transfer out the unclaimable portion of the deposited reward token amount.