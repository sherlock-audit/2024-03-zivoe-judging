Jumpy Yellow Kitten

high

# ITO depositors will lose part of their ZVE allocation, since the calculation is based on TotalSupply, which becomes dynamic the moment ITO ends and migrateDeposits is called.

## Summary

The protocol is planning to distribute 5% of total ZVE supply for the Initial Token Offering. Users that have made deposits during ITO can call `claimAirdrop` after ITO has ended to receive their tranche tokens and vest their share of the 5% of ZVE supply. 

However, the moment ITO has ended (or even before by the team), anyone can call `migrateDeposits` which unlocks the tranche tokens contracts allowing anyone to deposit junior and senior tranche tokens. 

This is a major problem since the amount of ZVE vested is based on the distribution of the user's JTT/STT credit tokens and the JTT/STT `totalSupply()`. The moment anyone deposits any amount in the tranche token contracts, the `totalSupply()` will increase and the ITO depositors will receive less ZVE then they should.    

## Vulnerability Detail

Assume Sam, Bob, Alice and Kevin are all the ITO depositors and each one of them deposit 100k senior tokens. 

STT totalSupply = 400k
ZVE to be distributed = 25M/20 = 1_250k
Each person should receive  1_250_000 / 4 = 312_500 ZVE

ITO ends. 

Sam calls `claimAirdrop`. The calculation is `100k * 5% ZVE Supply / 400k`  and he receives 312_500 ZVE. 

Then Sue, an investor who missed ITO, wants to participate. She calls `migrateDeposits` to unlock the tranches and deposits 200k STT. 
STT totalSupply = 600k

Bob calls `claimAirdrop`. The calculation is now `100k * 5% ZVE Supply / 600k` and he receives   208_333 ZVE. 

Then Whaley comes along and invest a massive 1.4m in Senior tokens. 
STT totalSupply = 2_000k

How much will Alice and Kevin each receive? `100k *5% ZVE Supply / 2000k`. They receive 62_500 ZVE

All 4 ITO investors should have received 312_500 ZVE.
How much did they actually receive?
Sam = 312_500
Bob  = 208_333
Alice = 62_500 
Kevin = 62_500

I've created a PoC which demonstrates the effects. 
Run with: `forge test --rpc-url MainnetApiKey --match-test testExpectRevert_ClaimAirdrop_LosesZVE_IfDepositAfterMigration -vv`
Add the below test to `zivoe-core-testing/src/TESTS_Core/Test_ZivoeITO.sol`

```solidity 
    import "../../lib/forge-std/src/console.sol";

    function testExpectRevert_ClaimAirdrop_LosesZVE_IfDepositAfterMigration() public {
        
        uint256 amount_senior = 100_000 * 1e18;

        zvl.try_commence(address(ITO));
        hevm.warp(ITO.end() - 30 days + 1 seconds);

        //sam deposits 100k in ITO. As the sole depositor, he should get 1.25M ZVE
        hevm.startPrank(address(sam));
        depositSenior(DAI, amount_senior);
        hevm.stopPrank();

        // Warp to end of ITO.
        hevm.warp(ITO.end() + 1 seconds);

        // sue is a whale that missed the ITO due to vacation. 
        // She loves ZIVOE and is eager to invest
        // She calls migrateDeposits the second ITO ends and deposits 500k DAI into the Senior Tranche  
        hevm.startPrank(address(sue));
        ITO.migrateDeposits();
        uint256 investment= 500_000 * 1e18;
        mint("DAI", address(sue), investment);
        sue.try_approveToken(address(DAI), address(ZVT), investment);
        ZVT.depositSenior(investment, DAI);
        // This increases the total supply of zSTT from 100k to 600k


        // A few hours later, Sam finishes work and decides to claim his Airdrop
        hevm.startPrank(address(sam));
        ITO.claimAirdrop(address(sam));
        // Unfortunately, the claimAirdrop function calculates the ZVE amount by dividing the credits over the totalSupply,
        // which has increased from 100k => 600k.  
        uint256 totalVestedSam;
        (,,,totalVestedSam,,,)= vestZVE.viewSchedule(address(sam));
        uint256 expectedZVE= ZVE.totalSupply()/20;

        console.log("sam should claim this much ZVE: %s",expectedZVE/1e18);
        console.log("sam has claimed this much ZVE: %s",totalVestedSam/1e18);
        console.log("sam has lost this much ZVE: %s", (expectedZVE - totalVestedSam)/1e18);
        vm.expectRevert();
        assertEq(totalVestedSam,expectedZVE);

        /* 
        Output Logs:

        [PASS] testExpectRevert_ClaimAirdrop_LosesZVE_IfDepositAfterMigration() (gas: 1128469)
        Logs:
            sam should claim this much ZVE: 1_250_000
            sam has claimed this much ZVE:    208_333
            sam has lost this much ZVE:     1_041_666
        */ 
    }

```
## Impact

During the actual ITO, there will hundreds if not thousands of participants who all invest to receive their share of ZVE. As a result of this finding, virtually all participants will lose a part of ZVE they are owed, with the loss becoming greater as time passes on. 

Since ZVE receives yield through the YDL, this represents a direct financial loss to investors. Alongside the reduced voting powers in Governance decisions.

Following Sherlock rules, this finding falls under section IV and satisfies both criteria:
> IV. How to identify a high issue:

> Definite loss of funds without (extensive) limitations of external conditions.
- The ZVE token receives yield from the YDL, so any loss of tokens means a direct loss of yield to the user. 
>  Inflicts serious non-material losses (doesn't include contract simply not working).
- The ZVE tokens also gives voting rights which is used to determine the direction of the DAO funds. A loss of tokens means a direct reduction of voting power in the protocol. 
  


## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeITO.sol#L203-L242
## Tool used

Manual Review + Foundry

## Recommendation

On calling `migrateDeposits`, the totalSupply of STT and JTT should be stored in state variables which then can be used to calculate the correct amount of ZVE tokens the depositors should receive. 
