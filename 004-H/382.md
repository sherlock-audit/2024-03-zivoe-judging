Great Metal Ram

high

# `ZivoeITO`'s `claimAirdrop` will be impacted by the new `zSTT` and `zJTT` minting. Users will receive less rewards than they should.

## Summary

When the ITO ends, `migrateDeposits` is called to conclude the ITO by transferring the stablecoins to the DAO, then `unlock` the `YDL` and `ZVT` (Zivoe Tranches) contract. Then users who participated in the ITO can call `claimAirdrop` to claim the `zJTT` and `zSTT`, as well as to receive extra vested `ZVE` rewards that depend on the amount of user's junior and senior credit, as well as the total supply of `zJTT` and `zSTT`. However, users can now participate in the `ZVT` (Zivoe Tranches) contract and mint new `zJTT` and `zSTT`, which will increase the total supply of `zJTT` and `zSTT`, and decreasing the extra vested rewards for users that participate in the ITO.

## Vulnerability Detail

When the ITO ends, `migrateDeposits` can be called to conclude the ITO and trigger `unlock` function inside `YDL` and `ZVT`.

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeITO.sol#L313-L335

```solidity
    function migrateDeposits() external {
        require(end != 0, "ZivoeITO::migrateDeposits() end == 0");
        if (_msgSender() != IZivoeGlobals_ITO(GBL).ZVL()) {
            require(block.timestamp > end, "ZivoeITO::migrateDeposits() block.timestamp <= end");
        }
        require(!migrated, "ZivoeITO::migrateDeposits() migrated");
        
        migrated = true;

        emit DepositsMigrated(
            IERC20(stables[0]).balanceOf(address(this)), 
            IERC20(stables[1]).balanceOf(address(this)), 
            IERC20(stables[2]).balanceOf(address(this)), 
            IERC20(stables[3]).balanceOf(address(this))
        );

        for (uint256 i = 0; i < stables.length; i++) {
            IERC20(stables[i]).safeTransfer(IZivoeGlobals_ITO(GBL).DAO(), IERC20(stables[i]).balanceOf(address(this)));
        }

        ITO_IZivoeYDL(IZivoeGlobals_ITO(GBL).YDL()).unlock();
>>>      ITO_IZivoeTranches(IZivoeGlobals_ITO(GBL).ZVT()).unlock();
    }
```

This will make `ZVT`'s `tranchesUnlocked` to true and now users can deposits stablecoins to `ZVT` and mint new `zJTT` and `zSTT` and increasing `zJTT` and `zSTT` total supply.

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeTranches.sol#L404-L410

```solidity
    function unlock() external {
        require(
            _msgSender() == IZivoeGlobals_ZivoeTranches(GBL).ITO(), 
            "ZivoeTranches::unlock() _msgSender() != IZivoeGlobals_ZivoeTranches(GBL).ITO()"
        );
        tranchesUnlocked = true;
    }
```

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeTranches.sol#L268-L289
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeTranches.sol#L295-L315

The issue is, when users that participate in the ITO want to claim the airdrop by calling `claimAirdrop` and calculate vested `ZVE` reward, it depends on `zJTT` and `zSTT` total supply.

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeITO.sol#L224-L228

```solidity
    function claimAirdrop(address depositor) external returns (
        uint256 zSTTClaimed, uint256 zJTTClaimed, uint256 ZVEVested
    ) {
        // ...

        // Calculate proportion of $ZVE awarded based on $pZVE credits.
        uint256 upper = seniorCreditsOwned + juniorCreditsOwned;
        uint256 middle = IERC20(IZivoeGlobals_ITO(GBL).ZVE()).totalSupply() / 20;
>>>     uint256 lower = IERC20(IZivoeGlobals_ITO(GBL).zSTT()).totalSupply() * 3 + (
            IERC20(IZivoeGlobals_ITO(GBL).zJTT()).totalSupply()
        );

        emit AirdropClaimed(depositor, seniorCreditsOwned / 3, juniorCreditsOwned, upper * middle / lower);

        IERC20(IZivoeGlobals_ITO(GBL).zJTT()).safeTransfer(depositor, juniorCreditsOwned);
        IERC20(IZivoeGlobals_ITO(GBL).zSTT()).safeTransfer(depositor, seniorCreditsOwned / 3);

        if (upper * middle / lower > 0) {
            ITO_IZivoeRewardsVesting(IZivoeGlobals_ITO(GBL).vestZVE()).createVestingSchedule(
                depositor, 0, 360, upper * middle / lower, false
            );
        }
        
        return (seniorCreditsOwned / 3, juniorCreditsOwned, upper * middle / lower);
    }
```

## Impact

Due to the increased total supply of `zJTT` and `zSTT`, all users who participated in the ITO will now receive fewer ZVE rewards than they should.

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeITO.sol#L224-L228

## Tool used

Manual Review

## Recommendation

When `migrateDeposits()` is called, consider to create snapshot of `zJTT` and `zSTT` total supply.

```diff
    function migrateDeposits() external {
        require(end != 0, "ZivoeITO::migrateDeposits() end == 0");
        if (_msgSender() != IZivoeGlobals_ITO(GBL).ZVL()) {
            require(block.timestamp > end, "ZivoeITO::migrateDeposits() block.timestamp <= end");
        }
        require(!migrated, "ZivoeITO::migrateDeposits() migrated");
        
        migrated = true;

+      zJTTSupplyAtEnd = IERC20(IZivoeGlobals_ITO(GBL).zJTT()).totalSupply();
+      zSTTSupplyAtEnd = IERC20(IZivoeGlobals_ITO(GBL).zSTT()).totalSupply();

        emit DepositsMigrated(
            IERC20(stables[0]).balanceOf(address(this)), 
            IERC20(stables[1]).balanceOf(address(this)), 
            IERC20(stables[2]).balanceOf(address(this)), 
            IERC20(stables[3]).balanceOf(address(this))
        );

        for (uint256 i = 0; i < stables.length; i++) {
            IERC20(stables[i]).safeTransfer(IZivoeGlobals_ITO(GBL).DAO(), IERC20(stables[i]).balanceOf(address(this)));
        }

        ITO_IZivoeYDL(IZivoeGlobals_ITO(GBL).YDL()).unlock();
        ITO_IZivoeTranches(IZivoeGlobals_ITO(GBL).ZVT()).unlock();
    }
```

Use those snapshot value, when calculating extra airdrop's reward, but now we have to restrict users to claim airdrop **ONLY** when `migrated` is true : 

```diff
    function claimAirdrop(address depositor) external returns (
        uint256 zSTTClaimed, uint256 zJTTClaimed, uint256 ZVEVested
    ) {
        require(end != 0, "ZivoeITO::claimAirdrop() end == 0");
-        require(block.timestamp > end || migrated, "ZivoeITO::claimAirdrop() block.timestamp <= end && !migrated");
+        require(migrated, "ZivoeITO::claimAirdrop()  !migrated");
        require(!airdropClaimed[depositor], "ZivoeITO::claimAirdrop() airdropClaimed[depositor]");
        require(
            seniorCredits[depositor] > 0 || juniorCredits[depositor] > 0, 
            "ZivoeITO::claimAirdrop() seniorCredits[depositor] == 0 && juniorCredits[depositor] == 0"
        );

        // ...

        // Calculate proportion of $ZVE awarded based on $pZVE credits.
        uint256 upper = seniorCreditsOwned + juniorCreditsOwned;
        uint256 middle = IERC20(IZivoeGlobals_ITO(GBL).ZVE()).totalSupply() / 20;
-       uint256 lower = IERC20(IZivoeGlobals_ITO(GBL).zSTT()).totalSupply() * 3 + (
-           IERC20(IZivoeGlobals_ITO(GBL).zJTT()).totalSupply()
-       );
+       uint256 lower = zSTTSupplyAtEnd * 3 + (
+           zJTTSupplyAtEnd
+       );

        emit AirdropClaimed(depositor, seniorCreditsOwned / 3, juniorCreditsOwned, upper * middle / lower);

        IERC20(IZivoeGlobals_ITO(GBL).zJTT()).safeTransfer(depositor, juniorCreditsOwned);
        IERC20(IZivoeGlobals_ITO(GBL).zSTT()).safeTransfer(depositor, seniorCreditsOwned / 3);

        if (upper * middle / lower > 0) {
            ITO_IZivoeRewardsVesting(IZivoeGlobals_ITO(GBL).vestZVE()).createVestingSchedule(
                depositor, 0, 360, upper * middle / lower, false
            );
        }
        
        return (seniorCreditsOwned / 3, juniorCreditsOwned, upper * middle / lower);
    }
```
