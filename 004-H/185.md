Glorious Midnight Snake

medium

# ITO liquidity provider might receive less $ZVE than expected due to incorrect calculation

## Summary
ITO liquidity provider might receive less $ZVE than expected due to incorrect calculation
## Vulnerability Detail
- Anyone can deposit whitelisted stable coin into [`ZivoeITO.sol`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeITO.sol) for `$zJTT` or `$zSTT` by calling [`ZivoeITO#depositSenior()`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeITO.sol#L277-L298), [`ZivoeITO#depositJunior()`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeITO.sol#L248-L271) or [`ZivoeITO#depositBoth()`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeITO.sol#L305-L309)
- Once the ITO period ends or it is migrated, the ITO liquidity providers can claim their `$zJTT`, `$zSTT` and `$ZVE` rewards by calling [`ZivoeITO#claimAirdrop()`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeITO.sol#L203-L242)
- The total amount of `$ZVE` reward for all ITO liquidity providers is 5% of total supply of `$ZVE`, any eligible liquidity provider can get their  pro-rota `$ZVE` reward based on their $pZVE credits.
- The $ZVE reward will be locked in `vestZVE` with a vesting period of 360 days.
```solidity
    function claimAirdrop(address depositor) external returns (
        uint256 zSTTClaimed, uint256 zJTTClaimed, uint256 ZVEVested
    ) {
        require(end != 0, "ZivoeITO::claimAirdrop() end == 0");
        require(block.timestamp > end || migrated, "ZivoeITO::claimAirdrop() block.timestamp <= end && !migrated");
        require(!airdropClaimed[depositor], "ZivoeITO::claimAirdrop() airdropClaimed[depositor]");
        require(
            seniorCredits[depositor] > 0 || juniorCredits[depositor] > 0, 
            "ZivoeITO::claimAirdrop() seniorCredits[depositor] == 0 && juniorCredits[depositor] == 0"
        );

        airdropClaimed[depositor] = true;

        // Temporarily store credit values, decrease them to 0 immediately after.
        uint256 seniorCreditsOwned = seniorCredits[depositor];
        uint256 juniorCreditsOwned = juniorCredits[depositor];

        seniorCredits[depositor] = 0;
        juniorCredits[depositor] = 0;

        // Calculate proportion of $ZVE awarded based on $pZVE credits.
        uint256 upper = seniorCreditsOwned + juniorCreditsOwned;
        uint256 middle = IERC20(IZivoeGlobals_ITO(GBL).ZVE()).totalSupply() / 20;
        uint256 lower = IERC20(IZivoeGlobals_ITO(GBL).zSTT()).totalSupply() * 3 + (
            IERC20(IZivoeGlobals_ITO(GBL).zJTT()).totalSupply()
        );

        emit AirdropClaimed(depositor, seniorCreditsOwned / 3, juniorCreditsOwned, upper * middle / lower);

        IERC20(IZivoeGlobals_ITO(GBL).zJTT()).safeTransfer(depositor, juniorCreditsOwned);
        IERC20(IZivoeGlobals_ITO(GBL).zSTT()).safeTransfer(depositor, seniorCreditsOwned / 3);

        if (upper * middle / lower > 0) {
@>          ITO_IZivoeRewardsVesting(IZivoeGlobals_ITO(GBL).vestZVE()).createVestingSchedule(
@>              depositor, 0, 360, upper * middle / lower, false
@>          );
        }
        
        return (seniorCreditsOwned / 3, juniorCreditsOwned, upper * middle / lower);
    }
```
As we can see, the `$ZVE` reward calculation is `upper * middle / lower` and all three variables are calculated as below:
```solidity
        uint256 upper = seniorCreditsOwned + juniorCreditsOwned;
        uint256 middle = IERC20(IZivoeGlobals_ITO(GBL).ZVE()).totalSupply() / 20;
        uint256 lower = IERC20(IZivoeGlobals_ITO(GBL).zSTT()).totalSupply() * 3 + (
            IERC20(IZivoeGlobals_ITO(GBL).zJTT()).totalSupply()
        );
```
However, the reward calculation could be vary if `middle` or `lower` is changed:
- If somehow a proportion of `$ZVE` is burnt, `middle` will decrease.(It could occur, but it's unlikely.)
- If one user deposits their stable coin into `ZVT` for `$zJTT`, `$zSTT`, `lower` will increase (It's likely to happen)

Once above scenarios happen, The amount of `$ZVE` that an ITO liquidity provider can claim will be diluted.

Copy below codes into [`Test_ZivoeITO.sol`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-testing/src/TESTS_Core/Test_ZivoeITO.sol) and run `forge test --rpc-url RPC-URL --match-test test_ZivoeITO_claimAirdrop_less_zve`:
```solidity
    function test_ZivoeITO_claimAirdrop_less_zve() public {
        zvl.try_commence(address(ITO));
        hevm.warp(ITO.end() - 30 days + 1 seconds);
        //@audit-info sam deposit 100 ether of DAI for senior tranche, 20 ether of DAI for junior tranche
        depositBoth(DAI, 20 ether, 100 ether);
        //@audit-info jim deposit 100 ether of DAI for senior tranche, 20 ether of DAI for junior tranche
        depositBoth(DAI, 100 ether);

        // Warp to end of ITO.
        hevm.warp(ITO.end() + 1 seconds);

        // Pre-state claimAirdrop (senior).
        uint256 _sam_SeniorCredits = ITO.seniorCredits(address(sam));
        uint256 _jim_SeniorCredits = ITO.seniorCredits(address(jim));
        uint256 _sam_JuniorCredits = ITO.juniorCredits(address(sam));
        uint256 _jim_JuniorCredits = ITO.juniorCredits(address(jim));
        ITO.migrateDeposits();
        assert(ZVT.tranchesUnlocked());
        assert(!ITO.airdropClaimed(address(sam)));
        assert(!ITO.airdropClaimed(address(jim)));
        //@audit-info Sam's senior credit is the same as Jim's senior credit.
        assert(_sam_SeniorCredits == _jim_SeniorCredits);
        //@audit-info Sam's junior credit is the same as Jim's junior credit.
        assert(_sam_JuniorCredits == _jim_JuniorCredits);
        
        (uint256 _zSTT_Claimed_SAM, uint256 _zJTT_Claimed_SAM, uint256 _ZVE_Vested_SAM) = sam.claimAirdrop(address(ITO), address(sam));
        //@audit-info sam deposit 1 ether of DAI into ZVT for more senior tranche
        mint("DAI", address(sam), 1 ether);
        assert(sam.try_approveToken(DAI, address(ZVT), 1 ether));
        assert(sam.try_depositSeniorTranches(address(ZVT), 1 ether, DAI));
        (uint256 _zSTT_Claimed_JIM, uint256 _zJTT_Claimed_JIM, uint256 _ZVE_Vested_JIM) = jim.claimAirdrop(address(ITO), address(jim));

        assert(ITO.airdropClaimed(address(sam)));
        assert(ITO.airdropClaimed(address(jim)));
        assert(_zSTT_Claimed_SAM == _zSTT_Claimed_JIM);
        assert(_zJTT_Claimed_SAM == _zJTT_Claimed_JIM);
        //@audit-info $ZVE Jim claimed is less than Sam's 
        assert(_ZVE_Vested_SAM > _ZVE_Vested_JIM);
    }
```
We can notice that the amount of `$ZVE` Jim claimed is less than Sam's, even though they deposited the same amounts of DAI for both senior and junior tranches.
## Impact
ITO liquidity provider might receive less $ZVE than expected due to incorrect calculation
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeITO.sol#L226-L228
## Tool used

Manual Review

## Recommendation
It's reasonable to snapshot the total supplies of `$zJTT`, `$zSTT` and `$ZVE` when `migrateDeposits()` is called and use the snapshotted values for `$ZVE` reward calculation in `claimAirdrop()`