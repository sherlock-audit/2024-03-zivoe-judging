Alert Orange Antelope

medium

# Post-ITO Total Supply (Tranche tokens) Changes Cause Inequitable $ZVE Vesting in ZivoeITO Airdrop

## Summary

The ZivoeITO smart contract is designed to handle the distribution of rewards on the conclusion of an ITO. The `claimAirdrop()` function is responsible for calculating and distributing $ZVE tokens to participants based on their contributions or participation during the ITO.

## Vulnerability Detail

Once the ITO is concluded it unlocks the ZivoeTranches contract which allows new deposits through deposit functions which will mint new Tranche tokens. 

Since the ITO is over, users now can call `claimAirdrop()` function to claim thier rewards through vesting. 
The calculation for the vested $ZVE tokens is dependent on the total supply of the tranche tokens ($zSTT and $zJTT) at the time of claiming. 

Specifically, the formula used is:

> ZVEVested = (seniorCredits + juniorCredits) * (ZVE.totalSupply / 20) / (zSTT.totalSupply * 3 + zJTT.totalSupply)

The issue lies in the dynamic nature of the `lower` denominator `(zSTT.totalSupply * 3 + zJTT.totalSupply)`, which can increase if new tranche tokens are minted post-ITO but before a user claims their airdrop. This post-ITO minting results in a larger denominator (as totalSupply being a global variable), thereby decreasing the quotient and the amount of $ZVE vested for users who claim later.

## Impact

- This issue makes potential unfair distribution of $ZVE tokens, where users claiming their airdrop later may receive fewer tokens than earlier users who claimed, despite having the same participation in the ITO. This undermines the integrity of the ITO and can lead to user dissatisfaction. 
- This discrepancy arises purely from the timing of the claim and not from the proportionate contribution of each participant

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeITO.sol#L203

<details>

```solidity
  function claimAirdrop(address depositor) external returns (
        uint256 zSTTClaimed, uint256 zJTTClaimed, uint256 ZVEVested
    ) {
        require(end != 0, "ZivoeITO::claimAirdrop() end == 0");
        require(block.timestamp > end || migrated, "ZivoeITO::claimAirdrop() block.timestamp <= end && !migrated");
        require(!airdropClaimed[depositor], "ZivoeITO::claimAirdrop() airdropClaimed[depositor]");
        require(
            seniorCredits[depositor] > 0 || juniorCredits[depositor] > 0, 
            "ZivoeITO::claimAirdrop() seniorCredits[depositor] == 0 && juniorCredits[depositor] == 0"
        );

        airdropClaimed[depositor] = true;

        // Temporarily store credit values, decrease them to 0 immediately after.
        uint256 seniorCreditsOwned = seniorCredits[depositor];
        uint256 juniorCreditsOwned = juniorCredits[depositor];

        seniorCredits[depositor] = 0;
        juniorCredits[depositor] = 0;

        // Calculate proportion of $ZVE awarded based on $pZVE credits.
        uint256 upper = seniorCreditsOwned + juniorCreditsOwned;
        uint256 middle = IERC20(IZivoeGlobals_ITO(GBL).ZVE()).totalSupply() / 20;

>         uint256 lower = IERC20(IZivoeGlobals_ITO(GBL).zSTT()).totalSupply() * 3 + (
>             IERC20(IZivoeGlobals_ITO(GBL).zJTT()).totalSupply()
>         );

        emit AirdropClaimed(depositor, seniorCreditsOwned / 3, juniorCreditsOwned, upper * middle / lower);

        IERC20(IZivoeGlobals_ITO(GBL).zJTT()).safeTransfer(depositor, juniorCreditsOwned);
        IERC20(IZivoeGlobals_ITO(GBL).zSTT()).safeTransfer(depositor, seniorCreditsOwned / 3);

        if (upper * middle / lower > 0) {
            ITO_IZivoeRewardsVesting(IZivoeGlobals_ITO(GBL).vestZVE()).createVestingSchedule(
                depositor, 0, 360, upper * middle / lower, false
            );
        }
        
        return (seniorCreditsOwned / 3, juniorCreditsOwned, upper * middle / lower);
    }
```

</details>

## PoC

let's consider two users, Alice and Bob, who each have 10,000 senior credits and 5,000 junior credits from participating in the ITO. The ITO concludes with the following total supplies:

    zSTT.totalSupply = 1,000,000
    zJTT.totalSupply = 500,000

Alice claims her airdrop immediately, and her vested $ZVE tokens are calculated using the above supplies. Bob, however, delays his claim. In the meantime, new tranche tokens are minted, increasing the total supplies to:

    zSTT.totalSupply = 1,500,000
    zJTT.totalSupply = 750,000

When Bob finally claims his airdrop, the total supplies used in the calculation have increased, resulting in a lower amount of vested $ZVE tokens for him compared to Alice, despite their equal participation in the ITO.
This demonstrates how the timing of the claim can affect the reward due to changes in the total supply of tranche tokens.

## Tool used

Manual Review

## Recommendation

This issue could be mitigated by few ways but imo the best would be to Implement a snapshot of the total supplies of tranche tokens at the exact moment the ITO concludes. This snapshot should be used as a fixed reference for all reward calculations during the claimAirdrop process.
