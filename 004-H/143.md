Ancient Chartreuse Kangaroo

high

# Airdrop amounts diminish over time due to continuous increase in `zSTT` and `zJTT` tokens total supply

## Summary
The `ZivoeITO.sol` contract's airdrop distribution mechanism unfairly diminishes over time due to the continuous increase in the total supply of `zSTT` and `zJTT` tokens. This is caused by the `claimAirdrop()` function's calculation method, which doesn't account for the ongoing minting of `zSTT` and `zJTT` tokens in the `ZivoeTranches.sol` contract.

## Vulnerability Detail
The `claimAirdrop()` function in `ZivoeITO.sol` contract calculates the airdrop amount based on the ratio of `(seniorCreditsOwned + juniorCreditsOwned) * ERC20(IZivoeGlobals_ITO(GBL).ZVE()).totalSupply() / 20` to `ERC20(IZivoeGlobals_ITO(GBL).zSTT()).totalSupply() * 3 + ERC20(IZivoeGlobals_ITO(GBL).zJTT()).totalSupply()`. However, new `zSTT` and `zJTT` tokens are constantly minted in `ZivoeTranches.sol` contract, which increases the total supply of these tokens and decreases the airdrop amount over time. To illustrate this serious issue, add this test in `TEST_ZivoeITO.sol` and follow the logic reading the comments :

```solidity
    function test_ZivoeITO_claimAirdrop_Unfair() public {
        // Both Sam ans Sue will depositJunior 100 USDC and depositSenior 1000 USDC.
        uint256 amountJunior = 100000000; // 100 USDC
        uint256 amountSenior = 1000000000; // 1000 USDC
        address asset = USDC;

        // Starting the ITO.
        zvl.try_commence(address(ITO));
        hevm.warp(ITO.end() - 30 days + 1 seconds);

        // Sam deposits 1100 USDC totally in the ITO.
        mint("USDC", address(sam), amountJunior + amountSenior);
        assert(sam.try_approveToken(asset, address(ITO), amountJunior + amountSenior));
        assert(sam.try_depositBoth(address(ITO), amountSenior, asset, amountJunior, asset));

        // Sue deposits 1100 USDC totally in the ITO.
        mint("USDC", address(sue), amountJunior + amountSenior);
        assert(sue.try_approveToken(asset, address(ITO), amountJunior + amountSenior));
        assert(sue.try_depositBoth(address(ITO), amountSenior, asset, amountJunior, asset));
        
        // Time is passing and ITO ends.
        hevm.warp(ITO.end() + 1 seconds);
        ITO.migrateDeposits();

        // Sue claims her airdrop and vests her earned $ZVE.
        (,, uint256 zveVestedSue) = sue.claimAirdrop(address(ITO), address(sue));

        // Meanwhile, tranches are unlocked and sal investor deposits 100k DAI to senior tranche.
        mint("DAI", address(sal), 100000 ether);
        assert(sal.try_approveToken(address(DAI), address(ZVT), 100000 ether));
        assert(sal.try_depositSeniorTranches(address(ZVT), 100000 ether, DAI));

        // Sam claims his airdrop after some days and vests her earned $ZVE.
        (,, uint256 zveVestedSam) = sam.claimAirdrop(address(ITO), address(sam));

        // Sue and Sam invested the same money (1100 USDC) in the ITO, but Sue got a lot more $ZVE vested than Sam.
        assert(zveVestedSue > zveVestedSam);
    }
```

## Impact
As a result of this vulnerability, users who claim airdrops will receive less and less over time as deposits in `ZivoeTranches.sol` are made and new `zJTT` and `zSTT` will be minted. This will lead to an unfair distribution of the airdrop amount and in some cases, users may not receive any airdrop at all because supply of `zSTT` and `zJTT` tokens are constantly increasing and can become bigger than `(seniorCreditsOwned + juniorCreditsOwned) * ERC20(IZivoeGlobals_ITO(GBL).ZVE()).totalSupply() / 20`. In this way the `upper * middle / lower` ratio will be less than 1 and the airdrop amount will be 0.

## Code Snippet
Here is the vulnerable code snippet in `ZivoeITO.sol` contract:
```solidity
    function claimAirdrop(address depositor) external returns (
        uint256 zSTTClaimed, uint256 zJTTClaimed, uint256 ZVEVested
    ) {
        // ...

        // Calculate proportion of $ZVE awarded based on $pZVE credits.
        uint256 upper = seniorCreditsOwned + juniorCreditsOwned;
        uint256 middle = IERC20(IZivoeGlobals_ITO(GBL).ZVE()).totalSupply() / 20;
@>        uint256 lower = IERC20(IZivoeGlobals_ITO(GBL).zSTT()).totalSupply() * 3 + (
            IERC20(IZivoeGlobals_ITO(GBL).zJTT()).totalSupply()
        );

        // ... 
    }
```
[Link to code](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L226)

From these points on `ZivoeTranches.sol` contract, we can see that the total supply of $zSTT and $zJTT tokens are constantly increasing:
```solidity
    function depositJunior(uint256 amount, address asset) public notPaused nonReentrant {
        // ...
@>        IERC20Mintable_ZivoeTranches(IZivoeGlobals_ZivoeTranches(GBL).zJTT()).mint(depositor, convertedAmount);
    }
```
[Link to code](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeTranches.sol#L288)

```solidity
    function depositSenior(uint256 amount, address asset) public notPaused nonReentrant {
        // ...
@>        IERC20Mintable_ZivoeTranches(IZivoeGlobals_ZivoeTranches(GBL).zSTT()).mint(depositor, convertedAmount);
    }
```
[Link to code](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeTranches.sol#L314)

## Tool used
Manual Review

## Recommendation
Consider using two state variables named `totalSupplySenior` and `totalSupplyJunior` to keep track of the total supply of both trance tokens and use them to calculate the airdrop amount. This way, the airdrop amount will be fair and not decrease over time.
First, add them in the contract state:
```diff
+ uint256 public totalSupplySenior; /// @dev Total supply of Zivoe Senior Tranche ($zSTT) tokens during ITO.
+ uint256 public totalSupplyJunior;  /// @dev Total supply of Zivoe Junior Tranche ($zJTT) tokens during ITO.
```
Then, make sure you update them when users deposit in `depostiJunior()` and `depositSenior()` functions :
```diff
    function depositJunior(uint256 amount, address asset) public {
        // ...

+        totalSupplyJunior += standardizedAmount;
        juniorCredits[caller] += standardizedAmount;

        // ...
    }
```
```diff
    function depositSenior(uint256 amount, address asset) public {
        // ...
        
+        totalSupplySenior += standardizedAmount;
        seniorCredits[caller] += standardizedAmount;

        // ...
    }
```
Finally, update the airdrop calculation to use the new state variables in `claimAirdrop()` function :
```diff
    function claimAirdrop(address depositor) external returns (
        uint256 zSTTClaimed, uint256 zJTTClaimed, uint256 ZVEVested
    ) {
        // ...

-        uint256 lower = IERC20(IZivoeGlobals_ITO(GBL).zSTT()).totalSupply() * 3 + (
-            IERC20(IZivoeGlobals_ITO(GBL).zJTT()).totalSupply()
-        );
+        uint256 lower = totalSupplySenior * 3 + totalSupplyJunior;

        // ...
    }
```