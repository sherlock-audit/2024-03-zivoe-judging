Fierce Crepe Sloth

medium

# Rewards in the different ZivoeRewards implementations can be diluted by a malicious user

## Summary
The ``ZivoeRewards.sol`` and the ``ZivoeRewardsVesting.sol`` are forked versions of the Synthetix rewards distribution contract, with slight modifications supporting different assets simultaneously as rewards, allowing everybody to call the [depositRewards()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243) function with no requirement on the minimum amount deposited, and requiring the reward for a new period to be deposited every time the [depositRewards()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243) function is called. This allows a malicious actor to dilute the rewards users are supposed to receive. Given the fact that the **Zivoe protocol** is a lending protocol. One of its core functions is to properly incentivize users to lend their assets to the **Zivoe protocol** so it can later utilize them, this happens by distributing rewards to users (usually advertised as APY). Usually the protocol will distribute rewards to the different ``ZivoeRewards.sol`` implementations (stJTT, stSTT, stZVE) and the ``ZivoeRewardsVesting.sol`` on a 30 day basis. If there are no previous reward deposits the rewardRate will be calculated in the [depositRewards()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243) function in the following way:

```solidity
        rewardData[_rewardsToken].rewardRate = reward.div(rewardData[_rewardsToken].rewardsDuration);
```
Let's take a reward deposit of 30_000 USDC tokens.
*reward = 30_000e6*
*rewardsDuration = 30 days = 2_592_000 seconds*
*rewardRate = 30_000e6 / 2_592_000 ≈ 11_574*

10 days pass and a malicious actor calls the [depositRewards()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243) function with the reward parameter = 0. The function will calculate the rewardRate in the following way:

```solidity
        uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
        uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
        rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
```

*remaining = 1_728_000*
*leftover = 11_574  \* 1_728_000 = 19_999_872_000* 
*rewardRate = 19_999_872_000 / 2_592_000 ≈ 7_716*

For the following 20 days users that have staked will accrue a lot less rewards, than what they should, this is effectively stealing rewards for them, because if they want to have the chance to receive the full amount of rewards they were supposed to receive, they will have to continue staking for the next periods. However if there are new stakers in the new period the rewards will be further diluted as the new stakers should receive rewards proportional to their stake percentage as well. 

Lets say 20 days have passed, and the protocol makes a new deposit of **30_000e6 tokens**
*remaining = 864_000*
*leftover = 7_716  \* 864_000 = 6_666_624_000 ≈ 6_666e6* 
*rewardRate = (30_000e6 + 6_666e6) / 2_592_000 ≈ 14_146*

As can be seen from the above calculations, the ``rewardRate`` will be higher, and stakers that are staking during this period will receive more rewards than they should, effectively stealing from the previous stakers. 

Lets say 10 more days pass and a malicious actor calls the [depositRewards()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243) function with the reward parameter = 0. 

*remaining = 1_728_000*
*leftover = 14_146  \* 1_728_000 = 24_444_288_000* 
*rewardRate = 24_444_288_000 / 2_592_000 ≈ 9_430*

A malicious actor can dilute the rewards again, as we can see the ``rewardRate`` is less than what it is supposed to be for a reward deposit of **30_000e6** (should be **11_574** as per the first calculations). A malicious actor can dilute rewards until there is a period where a lot more rewards will be distributed, and make a big deposit for that period, effectively stealing rewards. Once the ``rewardRate`` gets normalized (for example he increases the ``rewardRate`` up until a certain period, then stakes in that reward period, and receives much bigger rewards, than he is supposed to) he can start over, and keep diluting rewards forever. 

## Vulnerability Detail
[Gist](https://gist.github.com/AtanasDimulski/e2edba2c03e4dd1325b9e73c8fd58ddb)

After following the steps in the above linked [gist](https://gist.github.com/AtanasDimulski/e2edba2c03e4dd1325b9e73c8fd58ddb) add the following test to the ``AuditorTests.t.sol`` file: 

```solidity
    function test_DiluteRewards() public {
        vm.startPrank(ZVL);
        uint256 duration = 30 days;
        stZVE.addReward(address(mockUSDC), duration);
        vm.stopPrank();

        vm.startPrank(simulateYLD);
        mockUSDC.mint(simulateYLD, 30_000e6); // this represent 30_000 USDC tokens to be distributed in the next 30 days
        mockUSDC.approve(address(stZVE), type(uint256).max);
        stZVE.depositReward(address(mockUSDC), 30_000e6);
        vm.stopPrank();

        vm.startPrank(attacker);
        console2.log("Initial rewards per second per token: ", getRewardRate());
        /// @notice 10 days has passed
        skip(864_000);
        console2.log("Rewards per token per second after 10 days have passed: ", getRewardRate());
        stZVE.depositReward(address(mockUSDC), 0);
        console2.log("Rewards per token per second after 10 days have passed, and rewards have been diluted: ", getRewardRate());
        vm.stopPrank();

        vm.startPrank(simulateYLD);
        skip(1_728_000);
        mockUSDC.mint(simulateYLD, 30_000e6); // this represent 30_000 USDC tokens to be distributed in the next 30 days
        stZVE.depositReward(address(mockUSDC), 30_000e6);
        vm.stopPrank();

        vm.startPrank(attacker);
        /// @notice above we skip 20 days, this represents the new reward period for the system (a whole reward period being 30 days)
        console2.log("Rewards per second per token after 20 more days have passed and a new deposit have been made: ", getRewardRate());
        /// @notice 10 days has passed
        skip(864_000);
        console2.log("Rewards per token per second after 10 more days have passed: ", getRewardRate());
        stZVE.depositReward(address(mockUSDC), 0);
        console2.log("Rewards per token per second after more 10 days have passed, and rewards have been diluted: ", getRewardRate());
        vm.stopPrank();
    }
```

```solidity
Logs:
  Initial rewards per second per token:  11574
  Rewards per token per second after 10 days have passed:  11574
  Rewards per token per second after 10 days have passed, and rewards have been diluted:  7716
  Rewards per second per token after 20 more days have passed and a new deposit have been made:  14146
  Rewards per token per second after 10 more days have passed:  14146
  Rewards per token per second after more 10 days have passed, and rewards have been diluted:  9430
```

To run the test use: ``forge test -vvv --mt test_DiluteRewards``
## Impact
A malicious actor can dilute user rewards. Users won't receive the rewards they should on time, and if they want to they will have to continue staking, as rewards keep getting pushed into the next cycle. New user may stake tokens in the new period effectively stealing rewards from the previous users (we are not talking about dust amounts here). A malicious actor can dilute the rewards for several rounds, until a lot of rewards are to be distributed in a singe period, then make a big stake effectively stealing rewards. Most of the stakers would be decentivized to keep their stake until this point, as they have never received the full rewards for a period up until this point. 

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243

## Tool used
Manual Review & Foundry

## Recommendation
Implement an access control allowing only the ``ZivoeYDL.sol``, the ``ZVL`` multisig or the ``ZivoeDAO`` to call the [depositReward()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243) function.