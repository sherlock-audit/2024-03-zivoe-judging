Curved Taffy Trout

high

# Permisionless `depositReward` allows substantial grief of `rewardRate`

## Summary
Upon calling [`depositReward`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L242) in `ZivoeRewards` and `ZivoeRewardsVesting` leftover rewards to distribute are divided by the entire distribution duration, decreasing the rate substantially.

## Vulnerability Detail
In both rewards contracts, the `depositReward` method lacks access control and input validation. Anyone can call the method with 0 or dust amount. This harms calculations in the following snippet: 
   
```solidity
            uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
            uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
            rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration); // divides deposit + leftover rewards on the entire reward duration
```
   
Imagine the following distribution scenario:  
1000 rewardTokens   
rewardsDuration = 10 periods  
rewardRate = 100 tokens per period  
5 periods pass, 500 rewardTokens are distributed, 500 are leftover  
depositReward is called with 0 (or dust amount)  
remaining = 5 periods  
leftover = 500 tokens  
rewardRate = (0 + 500) / 10 periods = 50 tokens per period  
rewardRate decreased 2-fold 

Any arbitrary call to this function disrupts rewardRate, regardless if caller were malicious or honest user. The more periods have passed, the greater the impact.
## Impact
Broken core functionality, partial DoS 
## Code Snippet

```solidity
    function depositReward(address _rewardsToken, uint256 reward) external updateReward(address(0)) nonReentrant {
        IERC20(_rewardsToken).safeTransferFrom(_msgSender(), address(this), reward);


        // Update vesting accounting for reward (if existing rewards being distributed, increase proportionally).
        if (block.timestamp >= rewardData[_rewardsToken].periodFinish) {
            rewardData[_rewardsToken].rewardRate = reward.div(rewardData[_rewardsToken].rewardsDuration);
        } else {
            uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
            uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
            rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
        }


        rewardData[_rewardsToken].lastUpdateTime = block.timestamp;
        rewardData[_rewardsToken].periodFinish = block.timestamp.add(rewardData[_rewardsToken].rewardsDuration);
        emit RewardDeposited(_rewardsToken, reward, _msgSender());
    }
```
## Tool used

Manual Review

## Recommendation
Impose input validation and access control.