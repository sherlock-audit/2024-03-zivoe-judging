Fierce Crepe Sloth

high

# If the reward token in ZivoeRewards and ZivoeRewardsVesting is a token with less than 18 decimals, rewards may get stuck in the contract

## Summary
The ``ZivoeRewards.sol`` and ``ZivoeRewardsVesting.sol`` contracts allow for multiple tokens to be used as rewards simultaneously. The ``ZivoeRewards.sol``  contract is a fork of the Synthetix rewards distribution contract, with slight modifications allowing for multiple tokens with different decimals to be used as rewards, requiring the new reward to be deposited when calling the [depositReward()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243) function , and allowing everybody to call the [depositReward()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243) function with no minimum amount required. However tokens with less than 18 decimals can result in rewards being locked in the contract, and stakers not receiving their rewards.  Rewards will be distributed for a period of 30 days in most cases as the main source of rewards will come from the ``ZivoeYDL.sol`` contract. The amounts in the following example are chosen to better illustrate the severity of the attack, they are not a specific edge case. Now lets take the USDC token as the rewards token. We have a total stake of **5_000_000e18 tokens**, no matter in which ``ZivoeRewards.sol`` instance (it can be the junior, senior or the zve staking rewards instances). We have a reward of **50_000e6 tokens**  that have to be distributed towards the stakers. If there are no previous rewards for the specific token deposited the ``rewardRate`` will be calculated by the [depositReward()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243) function in the following way:

```solidity
  rewardData[_rewardsToken].rewardRate = reward.div(rewardData[_rewardsToken].rewardsDuration);
```

*30 days = 2592000 seconds*
*50_000e6 / 2592000 = 19290*

The [rewardPerToken()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L196-L203) function updates the ``rewardPerTokenStored`` which is a global variable, as well as the ``lastUpdateTime`` meaning that if one user stakes or withdraws this values will be updated, and later used in the reward calculations for other users. 
```solidity
lastTimeRewardApplicable(_rewardsToken).sub(rewardData[_rewardsToken].lastUpdateTime)
```
is the duration between someone staking, withdrawing his stake or withdrawing his rewards. 
The [rewardPerToken()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L196-L203) function calculates rewards in the following way:

```solidity
   function rewardPerToken(address _rewardsToken) public view returns (uint256 amount) {
        if (_totalSupply == 0) { return rewardData[_rewardsToken].rewardPerTokenStored; }
        return rewardData[_rewardsToken].rewardPerTokenStored.add(
            lastTimeRewardApplicable(_rewardsToken).sub(
                rewardData[_rewardsToken].lastUpdateTime
            ).mul(rewardData[_rewardsToken].rewardRate).mul(1e18).div(_totalSupply)
        );
    }
```
Let **X** be the time interval between user actions and **Y**  be the ``rewardPerTokenStored``, we get the following equation:

*Y + (X \* 19_290 \* 1e18 / 5_000_000e18)*
If **Y = 0** and **X <= 250** the result will always be **0**. **250 seconds** is **4 minutes and 10 seconds**. So if an attacker calls the [stake()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L253-L262) function every **4 minutes** and stakes **1 WEI**, and starts doing this once the rewards are deposited, no rewards will accrue. Resulting in stakers not receiving any rewards, and tokens being stuck in the contract, as there is no mechanism to withdraw them, and once some time has passed the rewards for that time can't be added to a next reward period. Lets say **10 days** which is **864_000 seconds** has passed **16_666_560_000 â‰ˆ 16_666e6 tokens** will be locked forever due to the way rewards are calculated if the current period hasn't finished:

```solidity
     uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
     uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
     rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
```
A malicious user can further dilute the ``rewardRate`` as the [depositReward()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243) function can be called by anyone with a reward parameter = 0, but this is an issue of it own. Lets say 10 days have passed, 

*remaining = 1_728_000 seconds*
*leftover = 1_728_000 \* 19_290 = 33_333_120_000*
*rewardRate = 33_333_120_000 / 2_592_000 = 12_860*

As we can see the ``rewardRate`` becomes a lot less, if rewards are diluted, this way the malicious user can call the [stake()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L253-L262) function less often and still achieve the same result. Or if the reward deposited is too big, and a malicious actor can't make the amount added to the ``rewardPerTokenStored`` to be equal to 0, no matter the time interval, lowering the ``rewardRate`` will help him achieve that. Due to the following line in the [depositReward()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243) function:
```solidity
      IERC20(_rewardsToken).safeTransferFrom(_msgSender(), address(this), reward);
```
rewards, that were not properly distributed can't be used in new reward periods.
## Vulnerability Detail
[Gist](https://gist.github.com/AtanasDimulski/e2edba2c03e4dd1325b9e73c8fd58ddb)

After following the steps in the above linked [gist](https://gist.github.com/AtanasDimulski/e2edba2c03e4dd1325b9e73c8fd58ddb) add the following test to ``AuditorTests.t.sol``:

```solidity
    function test_RewardsWithLessDecimalsWillBeLost() public {
        vm.startPrank(ZVL);
        zivoeToken.transfer(alice, 5_000_000e18); // this will represent the total stake in the stZVE contract
        zivoeToken.transfer(attacker, 1e18);
        uint256 duration = 30 days;
        stZVE.addReward(address(mockUSDC), duration);
        vm.stopPrank();

        vm.startPrank(alice);
        zivoeToken.approve(address(stZVE), type(uint256).max);
        stZVE.stake(5_000_000e18);
        vm.stopPrank();

        vm.startPrank(simulateYLD);
        mockUSDC.mint(simulateYLD, 50_000e6); // this represent 50_000 USDC tokens to be distributed in 30 days
        mockUSDC.approve(address(stZVE), type(uint256).max);
        stZVE.depositReward(address(mockUSDC), 50_000e6);
        vm.stopPrank();

        vm.startPrank(attacker);
        zivoeToken.approve(address(stZVE), type(uint256).max);
        console2.log("Initial block timestamp: ", block.timestamp);
        skip(249);
        stZVE.stake(1);
        console2.log("The amount that alice have earned after ", block.timestamp, stZVE.earned(alice, address(mockUSDC)));
        skip(249);
        stZVE.stake(1);
        console2.log("The amount that alice have earned after: ", block.timestamp, stZVE.earned(alice, address(mockUSDC))); // after 498 seconds has passed reawrds should have been 5000000
        skip(249);
        stZVE.stake(1);
        console2.log("The amount that alice have earned after: ", block.timestamp, stZVE.earned(alice, address(mockUSDC))); // after 498 seconds has passed reawrds should have been 10000000
        vm.stopPrank();
    }
```

```solidity
Logs:
  Initial block timestamp:  1
  The amount that alice have earned after  250 0
  The amount that alice have earned after:  499 0
  The amount that alice have earned after:  748 0
```

To run the test use: ``forge test -vvv --mt test_RewardsWithLessDecimalsWillBeLost``

## Impact
Users that have staked their tokens won't receive rewards, and the reward tokens will be stuck in the contract, as there  is no mechanism to withdraw them, or include them in a new reward distribution period. 

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L196-L203

## Tool used
Manual Review & Foundry

## Recommendation
Consider a way to convert tokens with decimals less than 18 to an amount with 18 decimals