Round Hazelnut Tadpole

medium

# `ZivoeRewards.depositReward` and `ZivoeRewardsVesting.depositReward` functions can be called with 0 as `reward` input

## Summary
After the `ZivoeRewards.depositReward` or `ZivoeRewardsVesting.depositReward` function is called to deposit an amount of the corresponding reward token for distribution, subsequent calls of such function with 0 as the `reward` input for the same reward token would extend such reward token's `periodFinish` and reduce the claimable portion of such reward token's deposited amount at a given point of time without changing the part of the deposited reward token amount that is eligible to be claimed. In this case, Stakers can find their claimable portions much lower than expected at a given time point, requiring longer staking periods in order to receive the same reward token amounts.

## Vulnerability Detail
In the `ZivoeRewards` contract, when the `depositReward` function in the Code Snippet section is called, the corresponding reward token's `periodFinish` is set to `block.timestamp.add(rewardData[_rewardsToken].rewardsDuration)`. Assuming that there is only one staker, if such staker staked before such reward token amount is deposited, she or he can claim all of such deposited reward token amount when she or he stakes until such `periodFinish` is reached. However, after such deposition of the reward token, anyone can call the `depositReward` function with 0 as the `reward` input for the same reward token, which would execute `rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration)` and `rewardData[_rewardsToken].periodFinish = block.timestamp.add(rewardData[_rewardsToken].rewardsDuration)`. In this case, the corresponding reward token's `periodFinish` is extended to be beyond its original `periodFinish`, and its `rewardRate` becomes less than its original `rewardRate` without changing the part of such reward token's deposited amount that is eligible to be claimed. Thus, the previously mentioned staker can no longer claim all of such reward token's deposited amount when staking until such reward token's original `periodFinish` and must stake until such reward token's new `periodFinish` in order to claim all of such deposited amount. This action of calling the `depositReward` function with 0 as the `reward` input for the same reward token can be repeated, which will extend the corresponding reward token's `periodFinish` and decrease the claimable portion of such reward token's deposited amount at a given point of time for many times. Moreover, the same issue occurs in the `ZivoeRewardsVesting` contract.
## Impact
Although the staker expects to be able to claim certain portion of the reward token's deposited amount at a given time point, her or his actual claimable portion of such reward token's deposited amount can be much lower due to the `depositReward` function being called many times with 0 as the `reward` input for the same reward token. Hence, the staker is forced to stake much longer than expected if she or he wants to claim the same portion of such reward token's deposited amount.

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243
```solidity
    function depositReward(address _rewardsToken, uint256 reward) external updateReward(address(0)) nonReentrant {
        IERC20(_rewardsToken).safeTransferFrom(_msgSender(), address(this), reward);

        // Update vesting accounting for reward (if existing rewards being distributed, increase proportionally).
        if (block.timestamp >= rewardData[_rewardsToken].periodFinish) {
            rewardData[_rewardsToken].rewardRate = reward.div(rewardData[_rewardsToken].rewardsDuration);
        } else {
            uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
            uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
            rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
        }

        rewardData[_rewardsToken].lastUpdateTime = block.timestamp;
        rewardData[_rewardsToken].periodFinish = block.timestamp.add(rewardData[_rewardsToken].rewardsDuration);
        emit RewardDeposited(_rewardsToken, reward, _msgSender());
    }
```

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L352-L367
```solidity
    function depositReward(address _rewardsToken, uint256 reward) external updateReward(address(0)) nonReentrant {
        IERC20(_rewardsToken).safeTransferFrom(_msgSender(), address(this), reward);

        // Update vesting accounting for reward (if existing rewards being distributed, increase proportionally).
        if (block.timestamp >= rewardData[_rewardsToken].periodFinish) {
            rewardData[_rewardsToken].rewardRate = reward.div(rewardData[_rewardsToken].rewardsDuration);
        } else {
            uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
            uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
            rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
        }

        rewardData[_rewardsToken].lastUpdateTime = block.timestamp;
        rewardData[_rewardsToken].periodFinish = block.timestamp.add(rewardData[_rewardsToken].rewardsDuration);
        emit RewardDeposited(_rewardsToken, reward, _msgSender());
    }
```

## Tool used
Manual Review

## Recommendation
The `ZivoeRewards.depositReward` and `ZivoeRewardsVesting.depositReward` functions can be respectively updated to revert if the `reward` input value is lower than a reasonable positive amount.