Faithful Sky Wallaby

medium

# ZivoeRewards::depositReward will erroneously revert when trying to add rewards to not yet added tokens

## Summary

If `ZivoeRewards::depositReward()` and `ZivoeRewardsVesting::depositReward()` is called to add rewards for a token, then the function will erroneously revert if the token that's having rewards deposited hasn't already been added by calling `ZivoeRewards::addReward`. This occurs due to a divide by 0, causing any calling transaction to erroneously revert.

## Vulnerability Detail

When `ZivoeRewards::depositReward()` is called there are no checks to ensure that the `_rewardsToken` is already part of `rewardTokens`. This will lead to a divide by 0 revert.

[ZivoeRewards::depositReward()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L237)
```solidity
    function depositReward(address _rewardsToken, uint256 reward) external updateReward(address(0)) nonReentrant {
        IERC20(_rewardsToken).safeTransferFrom(_msgSender(), address(this), reward);

        // Update vesting accounting for reward (if existing rewards being distributed, increase proportionally).
        if (block.timestamp >= rewardData[_rewardsToken].periodFinish) {
            rewardData[_rewardsToken].rewardRate = reward.div(rewardData[_rewardsToken].rewardsDuration);
        } else {
            uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
            uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
            rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
        }
    }
```
When calculating `rewardData[_rewardsToken].rewardRate` the `reward` or `leftover` rewards are divided by `rewardData[_rewardsToken].rewardsDuration` which will be 0 if the token has not been add as a reward:

[ZivoeRewards::addReward](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L208-L223)
```solidity
    function addReward(address _rewardsToken, uint256 _rewardsDuration) external {
        require(
            _msgSender() == IZivoeGlobals_ZivoeRewards(GBL).ZVL(), 
            "_msgSender() != IZivoeGlobals_ZivoeRewards(GBL).ZVL()")
        ;
        require(_rewardsDuration > 0, "ZivoeRewards::addReward() _rewardsDuration == 0");
        require(
            rewardData[_rewardsToken].rewardsDuration == 0, 
            "ZivoeRewards::addReward() rewardData[_rewardsToken].rewardsDuration != 0"
        );
        require(rewardTokens.length < 10, "ZivoeRewards::addReward() rewardTokens.length >= 10");

        rewardTokens.push(_rewardsToken);
        rewardData[_rewardsToken].rewardsDuration = _rewardsDuration;
        emit RewardAdded(_rewardsToken);
    }
```
`addRewards` sets `rewardData[_rewardsToken].rewardsDuration = _rewardsDuration;` allowing the divisions in `depositReward` to successfully be completed. However in the outlined scenario, `rewardData[_rewardsToken].rewardsDuration = 0` will lead to a division by 0, causing the transation to erroneously revert.

## Impact

`ZivoeRewards::depositReward` and `ZivoeRewardsVesting::depositReward()` will erroneously revert due to a divide by 0 error when trying to add rewards for a` _rewardsToken` that has not already been initialized by calling `addReward`. `depositReward` is called by `ZivoeYDL` and `OCE_ZVE` which could all erroneously revert.

## Code Snippet

[ZivoeRewards::depositReward](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L237)
[ZivoeRewards::addReward](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L208-L223)

## Tool used

Manual Review

## Recommendation

Check that the `_rewardsToken` has already been initialized by adding this 0 check:

```diff
    function depositReward(address _rewardsToken, uint256 reward) external updateReward(address(0)) nonReentrant {
+      require(rewardData[_rewardsToken].rewardsDuration > 0, "ZivoeRewards::depositReward() rewardData[_rewardsToken].rewardsDuration == 0")
        IERC20(_rewardsToken).safeTransferFrom(_msgSender(), address(this), reward);

        // Update vesting accounting for reward (if existing rewards being distributed, increase proportionally).
        if (block.timestamp >= rewardData[_rewardsToken].periodFinish) {
            rewardData[_rewardsToken].rewardRate = reward.div(rewardData[_rewardsToken].rewardsDuration);
        } else {
            uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
            uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
            rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
        }
    }
```

