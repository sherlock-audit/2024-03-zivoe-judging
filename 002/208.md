Curved Marmalade Cobra

medium

# In `ZivoeRewards::rewardPerToken()`, in periods where `totalSupply = 0` rewards will be forever lost

## Summary
`ZivoeRewards::rewardPerToken()` correctly skips `rewardPerTokenStored` updates if `totalSupply == 0`. However, it does not discount the non emitted rewards from the period. 

## Vulnerability Detail
`ZivoeRewards` allows staking tokens in return for several rewards in the form of different tokens. Whenever a user interacts with the smart contract, it updates the global `rewardPerTokenStored` variable by multiplying `rewardRate` with `lastTimeRewardApplicable() - rewardData[_rewardsToken].lastUpdateTime` divided by `totalSupply` and if the user is not `address(0)`, its rewards by multiplying the balance with `rewardPerTokenStored - accountRewardPerTokenPaid[account][token]`.
Rewards are added via `ZivoeRewards::depositReward()`, which sets a `rewardRate` based on the `reward` amount plus any leftover from previous emissions. The leftover is calculated based on the time left to reach `rewardData[_rewardsToken].periodFinish` times the previous reward rate, `rewardData[_rewardsToken].rewardRate`.
Note that the fact that `totalSupply` may be `0` within any period is never dealt with, and as rewards have a fixed `periodFinish` and `rewardRate`, and when scheduling a new reward period, it adds the leftover only based on the time left times the `rewardRate`, rewards may be forever lost.

Add the following test to `Test_ZivoeRewards.sol`:
```solidity
function test_POC_LostRewardTokens_DueTo0TotalSupply() public {
    uint256 deposit = 100 ether;
    depositReward_DAI(address(stZVE), deposit);

    hevm.warp(block.timestamp + 8 hours);

    assertEq(IZivoeRewards(address(stZVE)).earned(address(sam), DAI), 0);

    assert(sam.try_approveToken(address(ZVE), address(stZVE), IERC20(address(ZVE)).balanceOf(address(sam))));
    sam.try_stake(address(stZVE), IERC20(address(ZVE)).balanceOf(address(sam)));
    
    hevm.warp(block.timestamp + 30 days);
    
    uint256 rewardsEarned = IZivoeRewards(address(stZVE)).earned(address(sam), DAI);
    assertEq(rewardsEarned, 98888888888888255000); // less than 100e18, rest is forever lost
}
```
As can be seen, `8 hours` passed until the first staker, losing this rewards forever.

## Impact
Forever lost rewards. The amount depends on the amount of time `totalSupply` was `0`, which is not easy to predict, but is likely to happen in the beginning of every new staking period, as stakers have less incentive to be staking without pending rewards.

## Code Snippet
[ZivoeRewards::rewardPerToken()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L123):
```solidity
function rewardPerToken(address _rewardsToken) public view returns (uint256 amount) {
    if (_totalSupply == 0) { return rewardData[_rewardsToken].rewardPerTokenStored; }
    return rewardData[_rewardsToken].rewardPerTokenStored.add(
        lastTimeRewardApplicable(_rewardsToken).sub(
            rewardData[_rewardsToken].lastUpdateTime
        ).mul(rewardData[_rewardsToken].rewardRate).mul(1e18).div(_totalSupply)
    );
}
```

## Tool used
Vscode

Foundry

Manual Review

## Recommendation

When `totalSupply == 0`, capture the rewards that were not emitted and add them later to leftover.
```solidity
mapping(address => uint256) public nonEmittedRewards;
...
modifier updateReward(address account) {
    for (uint256 i; i < rewardTokens.length; i++) {
        ...
        if (totalSupply == 0)
            nonEmittedRewards[token] += rewardData[token].rewardRate * (lastTimeRewardApplicable(token) - rewardData[token].lastUpdateTime);
        rewardData[token].lastUpdateTime = lastTimeRewardApplicable(token);
        ...
    }
    _;
}
...

function depositReward(address _rewardsToken, uint256 reward) external updateReward(address(0)) nonReentrant {
    ...
    uint256 nonEmittedRewards = nonEmittedRewards[_rewardsToken];
    nonEmittedRewards[_rewardsToken] = 0;
    if (block.timestamp >= rewardData[_rewardsToken].periodFinish) {
        rewardData[_rewardsToken].rewardRate = (reward + nonEmittedRewards).div(rewardData[_rewardsToken].rewardsDuration);
    } else {
        ...
        uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate) + nonEmittedRewards; 
        ...
    }
    ...
}
```
