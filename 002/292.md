Best Lava Dragon

medium

# Airdrop rewards for ITO pariticipants will be diluted.

## Summary
The Initial Tranche Offering is a liquidity bootstrapping event and will be the first opportunity for users to deposit stablecoins and receive protocol tokens. To incentivize participation, 5% of the ZVE token supply will be airdropped to users that participate in this event. Currently, there exists a flaw in `claimAirdrop()` function of the ZivoeITO, which will result in airdrop rewards being diluted.
## Vulnerability Detail
When depositing stablecoins into the ITO, users are awarded with 3 pZVE for each stablecoin deposited into the senior tranche and 1 pZVE for each stablecoin deposited into the junior tranche. 
[ZivoeITO.sol#L265](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L265)
```solidity
juniorCredits[caller] += standardizedAmount;
```
[ZivoeITO.sol#L292](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L292)
```solidity
seniorCredits[caller] += standardizedAmount * 3;
```

The amount of ZVE airdrop rewards a user is eligible for is calculated in the `claimAirdrop()` function based on their proportional ownership of pZVE.
[ZivoeITO.sol#L223-L239](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L223-L239)
```solidity
// Calculate proportion of $ZVE awarded based on $pZVE credits.
uint256 upper = seniorCreditsOwned + juniorCreditsOwned;
uint256 middle = IERC20(IZivoeGlobals_ITO(GBL).ZVE()).totalSupply() / 20;
uint256 lower = IERC20(IZivoeGlobals_ITO(GBL).zSTT()).totalSupply() * 3 + (
    IERC20(IZivoeGlobals_ITO(GBL).zJTT()).totalSupply()
);

if (upper * middle / lower > 0) {
    ITO_IZivoeRewardsVesting(IZivoeGlobals_ITO(GBL).vestZVE()).createVestingSchedule(
        depositor, 0, 360, upper * middle / lower, false
    );
}
```
`upper` is the amount of pZVE held by a user. 
`middle` is the 5% of the ZVE token supply and `lower` should be the total supply of pZVE.

To allow claiming of the airdrop `migrateDeposits()` must be called on the ITO. 
The problem is that after `migrateDeposits()` is called, ZivoeTranches contract becomes unlocked to accept direct deposits into both tranches, which will subsequently increase total supplies of both zJTT and zSTT, so they won't represent the total supply of pZVE anymore. Because of that the `upper * middle / lower` formula will underestimate the amount of ZVE each user is eligible for due to `lower` being inflated with direct deposits into tranches 
## Impact
The airdrop rewards will be diluted for users and part of the supply of the ZVE meant for the airdrop won't be distributed at all.

## Proof Of Concept
Please add this function to `Test_ZivoeITO.sol`.

```solidity
function test_ZivoeITO_dilute_airdop_rewards() public {
    // Start ITO
    zvl.try_commence(address(ITO));
    // Mint USDT to users
    mint("USDT", address(bob), 1000e6);
    mint("USDT", address(jim), 100e6);
    bob.try_approveToken(USDT, address(ITO), 1000e6);
    jim.try_approveToken(USDT, address(ITO), 100e6);
    // Users deposit into ITO
    // Now bob has 3000 senior credits
    vm.prank(address(bob));
    ITO.depositSenior(1000e6, address(USDT));
    assertEq(ITO.seniorCredits(address(bob)), 3000e18);
    // Jim has 100 junior credits
    vm.prank(address(jim));
    ITO.depositJunior(100e6, address(USDT));
    assertEq(ITO.juniorCredits(address(jim)), 100e18);
    // Total supply of pZVE = 3100
    uint256 snapshot = vm.snapshot();
    
    // 1st case: end the ITO and claimAirdrop immediately
    vm.warp(ITO.end() + 1 seconds);
    bob.try_migrateDeposits(address(ITO));
    ITO.claimAirdrop(address(bob));
    // upper = 3000
    // middle = 25_000_000 / 20 = 1_250_000
    // lower = 3100
    // totalVesting = 3000 * 1_250_000 / 3100 = 1_209_677
    // Bob is eligible for 1_209_677 ZVE Airdrop
    (, , , uint256 totalVesting, , , ) = vestZVE.vestingScheduleOf(
        address(bob)
    );
    assertEq((totalVesting / 1e18), 1209677);
    
    // Revert to before the end of the ITO
    vm.revertTo(snapshot);
    vm.warp(ITO.end() + 1 seconds);
    
    // 2nd case: end the ITO, but do not claim the airdrop yet
    bob.try_migrateDeposits(address(ITO));
    // Now because deposits were migrated, the tranches have been unlocked to accept deposits directly
    mint("USDT", address(sam), 1000e6);
    sam.try_approveToken(USDT, address(ZVT), 1000e6);
    // Sam deposits 1000 USDT into senior tranche directly
    vm.prank(address(sam));
    ZVT.depositSenior(1000e6, address(USDT));
    assertEq(zSTT.balanceOf(address(sam)), 1000e18);
    // Bob claims his airdrop, but receive 2x times less the amount of what he should've received
    ITO.claimAirdrop(address(bob));
    // upper = 3000
    // middle = 25_000_000 / 20 = 1_250_000
    // lower = 3100 + 3000 = 6100
    // totalVesting = 3000 * 1_250_000 / 6100 = 614_754
    (, , , uint256 totalVestingNew, , , ) = vestZVE.vestingScheduleOf(
        address(bob)
    );
    assertEq((totalVestingNew / 1e18), 614754);
    // Jim also claims his airdrop
    ITO.claimAirdrop(address(jim));
    (, , , uint256 totalVestingJim, , , ) = vestZVE.vestingScheduleOf(
        address(jim)
    );
    console2.log(
        "Amount of ZVE not airdropped:",
        1250000 - ((totalVestingNew / 1e18) + (totalVestingJim / 1e18))
    );
}
```
Console output:
```md
Amount of ZVE not airdropped: 614755
```
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L226-L228
## Tool used

Manual Review

## Recommendation
ZivoeITO should track the supply of pZVE internally on each deposit and distribute the airdrop based on that rather than totalSupply of zJTT and zSTT.