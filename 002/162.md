Glorious Midnight Snake

medium

# Anyone can prolong the yield distribution period to dilute the staker's reward

## Summary
Due to lack of access control on [`ZivoeRewards#depositReward()`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243) and [`ZivoeRewardsVesting#depositReward()`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L352-L367), anyone can call these functions with only spending `1 wei` to prolong the reward vesting period, resulting in `rewardRate` highly being diluted.

## Vulnerability Detail
- [`ZivoeYDL.sol`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeYDL.sol) is used to escrow yield in between distribution periods
- Once unlocked, the yield accumulated in ZivoeYDL can be distributed by calling [`ZivoeYDL#distributeYield()`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeYDL.sol#L213-L269) 60 days later, and then once every 30 days thereafter
- The yield distributed to `ZivoRewards` and `ZivoeRewardsVesting` will be linearly vested over 30 days.

However,  anyone can call `ZivoeRewards#depositReward()` or `ZivoeRewardsVesting#depositReward()` to update the distribution period, potentially diluting `rewardRate` and resulting in eligible users receiving less yield reward during a certain period:
- Suppose  that `ZivoeYDL#distributeYield()` is called and `3000 DAI` is distributed to `stZVE`(ZivoeRewards ($ZVE) contract). 
Up to a total of 100 DAI can be claimed each day (`rewardRate` is  `100 DAI / day`.
- A malicious user calls `stZVE#depositReward()` 3 days later with `1 WEI`. `rewardRate` will be updated to `2700 DAI / 30 days`, aka `90 DAI / day`
- The malicious user can repeat diluting `rewardRate` until next yield distribution.

The eligible user will receive less yield rewards due the `rewardRate` is highly diluted.

## Impact
Anyone can prolong the yield distribution period to dilute the staker's reward
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L352-L367
## Tool used

Manual Review

## Recommendation
Introduce access control on `ZivoeRewards#depositReward()` and `ZivoeRewardsVesting#depositReward()`:
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol:
```diff
interface IZivoeGlobals_ZivoeRewards {
    /// @notice Returns the address of the Zivoe Laboratory.
    function ZVL() external view returns (address);
+   function YDL() external view returns (address);
}
+interface IZivoeYDL_ZivoeRewards  {
+    function distributedAsset() external view returns (address asset);
+}

    function depositReward(address _rewardsToken, uint256 reward) external updateReward(address(0)) nonReentrant {
+       if (_rewardsToken == IZivoeYDL_ZivoeRewards(IZivoeGlobals_ZivoeRewards(GBL).YDL()).distributedAsset()) {
+           require(_msgSender() == IZivoeGlobals_ZivoeRewards(GBL).YDL(), "_msgSender() != IZivoeGlobals_ZivoeRewards(GBL).YDL()");
+       }
        IERC20(_rewardsToken).safeTransferFrom(_msgSender(), address(this), reward);

        // Update vesting accounting for reward (if existing rewards being distributed, increase proportionally).
        if (block.timestamp >= rewardData[_rewardsToken].periodFinish) {
            rewardData[_rewardsToken].rewardRate = reward.div(rewardData[_rewardsToken].rewardsDuration);
        } else {
            uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
            uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
            rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
        }

        rewardData[_rewardsToken].lastUpdateTime = block.timestamp;
        rewardData[_rewardsToken].periodFinish = block.timestamp.add(rewardData[_rewardsToken].rewardsDuration);
        emit RewardDeposited(_rewardsToken, reward, _msgSender());
    }
```

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol:
```diff
interface IZivoeGlobals_ZivoeRewardsVesting {
    /// @notice Returns the address of the ZivoeITO contract.
    function ITO() external view returns (address);

    /// @notice Returns the address of the ZivoeToken contract.
    function ZVE() external view returns (address);

    /// @notice Returns the address of the Zivoe Laboratory.
    function ZVL() external view returns (address);

+   function YDL() external view returns (address);
}

+interface IZivoeYDL_ZivoeRewardsVesting  {
+    function distributedAsset() external view returns (address asset);
+}

    function depositReward(address _rewardsToken, uint256 reward) external updateReward(address(0)) nonReentrant {
+       if (_rewardsToken == IZivoeYDL_ZivoeRewardsVesting(IZivoeGlobals_ZivoeRewardsVesting(GBL).YDL()).distributedAsset()) {
+           require(_msgSender() == IZivoeGlobals_ZivoeRewardsVesting(GBL).YDL(), "_msgSender() != IZivoeGlobals_ZivoeRewardsVesting(GBL).YDL()");
+       }
        IERC20(_rewardsToken).safeTransferFrom(_msgSender(), address(this), reward);

        // Update vesting accounting for reward (if existing rewards being distributed, increase proportionally).
        if (block.timestamp >= rewardData[_rewardsToken].periodFinish) {
            rewardData[_rewardsToken].rewardRate = reward.div(rewardData[_rewardsToken].rewardsDuration);
        } else {
            uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
            uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
            rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
        }

        rewardData[_rewardsToken].lastUpdateTime = block.timestamp;
        rewardData[_rewardsToken].periodFinish = block.timestamp.add(rewardData[_rewardsToken].rewardsDuration);
        emit RewardDeposited(_rewardsToken, reward, _msgSender());
    }
```