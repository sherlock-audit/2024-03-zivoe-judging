Curved Zinc Scallop

medium

# Users might receive fewer incentive rewards than expected when depositing into junior/senior tranches

## Summary

When users deposit into junior or senior tranches (using the `depositJunior` or `depositSenior` functions, respectively), they risk receiving fewer incentive rewards than expected due to front-running or transactions remaining in the mempool for too long.

## Vulnerability Detail

The issue described in this report concerns the case when users deposit into junior or senior tranches using the `depositJunior` or `depositSenior` functions. As the issue is similar in both cases, I will only explain it for the junior tranche, but the same will apply for the senior tranche.

When a user wants to deposit into the junior tranche, they will call the `depositJunior` function:

```solidity
function depositJunior(
    uint256 amount,
    address asset
) public notPaused nonReentrant {
    ...

    uint256 incentives = rewardZVEJuniorDeposit(convertedAmount);
    emit JuniorDeposit(depositor, asset, amount, incentives);

    // Ordering is important; transfer ZVE rewards prior to minting zJTT() due to totalSupply() changes.
    IERC20(IZivoeGlobals_ZivoeTranches(GBL).ZVE()).safeTransfer(
        depositor,
        incentives
    );
    IERC20Mintable_ZivoeTranches(IZivoeGlobals_ZivoeTranches(GBL).zJTT())
        .mint(depositor, convertedAmount);
}
```

The function will pull the asset token from the user and then mint them a corresponding amount of JTT tokens. The function will also calculate the ZVE incentive rewards that must be given to the user, and this is done by calling `rewardZVEJuniorDeposit`.

```solidity
function rewardZVEJuniorDeposit(
    uint256 deposit
) public view returns (uint256 reward) {
    (uint256 seniorSupp, uint256 juniorSupp) = IZivoeGlobals_ZivoeTranches(
        GBL
    ).adjustedSupplies();

    uint256 avgRate; // The avg ZVE per stablecoin deposit reward, used for reward calculation.

    uint256 diffRate = maxZVEPerJTTMint - minZVEPerJTTMint;

    uint256 startRatio = (juniorSupp * BIPS) / seniorSupp;
    uint256 finalRatio = ((juniorSupp + deposit) * BIPS) / seniorSupp;
    uint256 avgRatio = (startRatio + finalRatio) / 2;

    if (avgRatio <= lowerRatioIncentiveBIPS) {
        avgRate = maxZVEPerJTTMint;
    } else if (avgRatio >= upperRatioIncentiveBIPS) {
        avgRate = minZVEPerJTTMint;
    } else {
        avgRate =
            maxZVEPerJTTMint -
            (diffRate * (avgRatio - lowerRatioIncentiveBIPS)) /
            (upperRatioIncentiveBIPS - lowerRatioIncentiveBIPS);
    }

    reward = (avgRate * deposit) / 1 ether;

    // Reduce if ZVE balance < reward.
    if (
        IERC20(IZivoeGlobals_ZivoeTranches(GBL).ZVE()).balanceOf(
            address(this)
        ) < reward
    ) {
        reward = IERC20(IZivoeGlobals_ZivoeTranches(GBL).ZVE()).balanceOf(
            address(this)
        );
    }
}
```

As it can be seen, the function does calculate the amount of ZVE reward that must be sent to the user. However, by looking at the logic inside the function, we can see that there are two cases in which the user's ZVE reward will be less than what they expected:

- The obvious one is if the contract doesn't have enough ZVE tokens in its balance to cover the reward amount (last `if` statement in the function), in which case the reward will be capped (decreased) by the contract's ZVE balance.

- The second one is if the adjusted supplies of the junior and/or senior tranche change. In this case, the calculated `avgRatio` and thus `avgRate` will be different from what the user expected and could potentially result in lower ZVE rewards being sent.

Both cases mentioned above can occur if the user's deposit transaction gets front-run by other users or if it remains pending in the mempool for a long time. Due to the changes to the contract's ZVE balance or tranche supply that can happen while the transaction is pending, the user might receive fewer ZVE rewards than what they expected, resulting in a financial loss (the user will experience something similar to the slippage encountered in AMMs).

The same issue can occur when depositing into the senior tranche, as it also uses `rewardZVESeniorDeposit`, which has the same logic as in `rewardZVEJuniorDeposit`.

## Impact

Users might receive fewer ZVE rewards when depositing into junior or senior tranches with `depositJunior` or `depositSenior` functions, respectively.

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeTranches.sol#L283-L287

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeTranches.sol#L308-L313

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeTranches.sol#L205-L228

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeTranches.sol#L238-L261

## Tool used

Manual Review

## Recommendation

To address this issue, both the `depositJunior` and `depositSenior` functions should allow the user to set a minimum ZVE reward amount to be received upon depositing.