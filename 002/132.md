Helpful Mercurial Finch

high

# At the end of initial tranche offering, the amount of ZVE airdropped to a depositor will not reflect his proportional ownership of pZVE

## Summary

At the end of initial tranche offering (ITO), the amount of ZVE airdropped to an ITO participant very likely will not reflect his proportional ownership of pZVE.

## Vulnerability Detail

Zivoe offers an [initial tranche offering (ITO)](https://docs.zivoe.com/user-docs/liquidity-providers/initial-tranche-offering) as a way to incentivize participation in the protocol upon launch. 

The Zivoe team will call [`commence`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L339-L339) function to start the ITO, and the `end` of ITO will be marked as `block.timestamp + 30 days`:

```solidity
/// @notice Starts the ITO.
/// @dev    Only callable by ZVL.
function commence() external {
    require(end == 0, "ZivoeITO::commence() end !== 0");
    require(
        _msgSender() == IZivoeGlobals_ITO(GBL).ZVL(), 
        "ZivoeITO::commence() _msgSender() != IZivoeGlobals_ITO(GBL).ZVL()"
    );
    emit ITOCommenced(block.timestamp, block.timestamp + 30 days);
    end = block.timestamp + 30 days;
}
```

During ITO, the normal tranches in [`ZivoeTranches.sol` are locked by `tranchesUnlocked`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeTranches.sol#L83-L83) variable set as `false`. Liquidity providers are only allowed to interact with the tranches provided in [`ZivoeITO` contract](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L91-L91) in this period.

Liquidity providers will use [`depositJunior`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L248-L248), [`depositSenior`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L277-L277) or [`depositBoth`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L305-L305) to deposit their stablecoin into the tranches.

As they deposit stablecoin, their junior or senior credit will increase with [`juniorCredits[caller] += standardizedAmount;`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L265-L265) or [`seniorCredits[caller] += standardizedAmount * 3;`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L292-L292). These credits represent [what the documentation refers to as a "pZVE" (pre-ZVE token)](https://docs.zivoe.com/user-docs/liquidity-providers/initial-tranche-offering). They will be also minted [zSTT](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L297-L297) or [zJTT](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L270-L270) to represent their deposit.

As soon `block.timestamp > end` becomes true (the 30 days since the start of ITO elapses), the deposit functions will be no longer be available for use due to `require(block.timestamp < end, ...)` constraint. At this point, a permissionless function [`migrateDeposits`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L313-L313) can be called by anyone:

```solidity
/// @notice Migrate tokens to ZivoeDAO.
/// @dev    This function MUST only be callable after the ITO concludes (or earlier at ZVL discretion).
function migrateDeposits() external {
    require(end != 0, "ZivoeITO::migrateDeposits() end == 0");
    if (_msgSender() != IZivoeGlobals_ITO(GBL).ZVL()) {
        require(block.timestamp > end, "ZivoeITO::migrateDeposits() block.timestamp <= end");
    }
    require(!migrated, "ZivoeITO::migrateDeposits() migrated");
    
    migrated = true;

    emit DepositsMigrated(
        IERC20(stables[0]).balanceOf(address(this)), 
        IERC20(stables[1]).balanceOf(address(this)), 
        IERC20(stables[2]).balanceOf(address(this)), 
        IERC20(stables[3]).balanceOf(address(this))
    );

    for (uint256 i = 0; i < stables.length; i++) {
        IERC20(stables[i]).safeTransfer(IZivoeGlobals_ITO(GBL).DAO(), IERC20(stables[i]).balanceOf(address(this)));
    }

    ITO_IZivoeYDL(IZivoeGlobals_ITO(GBL).YDL()).unlock();
    ITO_IZivoeTranches(IZivoeGlobals_ITO(GBL).ZVT()).unlock();
}
```

Notably, `migrated` is now set as `true`, and [`ZivoeYDL` contract is `unlock`ed for use](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeYDL.sol#L321-L321), and [`ZivoeTranches` contract is also unlocked for use](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeTranches.sol#L403-L410) in the last two lines.

Then, the ITO participants can call [`claimAirdrop`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L203-L203) to create and start a vesting schedule of their allocated amount of ZVE:

```solidity
/// @notice Claim $zSTT, $zJTT, and begin a vesting schedule for $ZVE.
/// @dev    This function MUST only be callable after the ITO concludes.
/// @param  depositor   The address to claim for, generally _msgSender().
/// @return zSTTClaimed Amount of $zSTT airdropped.
/// @return zJTTClaimed Amount of $zJTT airdropped.
/// @return ZVEVested   Amount of $ZVE vested.
function claimAirdrop(address depositor) external returns (
    uint256 zSTTClaimed, uint256 zJTTClaimed, uint256 ZVEVested
) {
    require(end != 0, "ZivoeITO::claimAirdrop() end == 0");
    require(block.timestamp > end || migrated, "ZivoeITO::claimAirdrop() block.timestamp <= end && !migrated");
    require(!airdropClaimed[depositor], "ZivoeITO::claimAirdrop() airdropClaimed[depositor]");
    require(
        seniorCredits[depositor] > 0 || juniorCredits[depositor] > 0, 
        "ZivoeITO::claimAirdrop() seniorCredits[depositor] == 0 && juniorCredits[depositor] == 0"
    );

    airdropClaimed[depositor] = true;

    // Temporarily store credit values, decrease them to 0 immediately after.
    uint256 seniorCreditsOwned = seniorCredits[depositor];
    uint256 juniorCreditsOwned = juniorCredits[depositor];

    seniorCredits[depositor] = 0;
    juniorCredits[depositor] = 0;

    // Calculate proportion of $ZVE awarded based on $pZVE credits.
    uint256 upper = seniorCreditsOwned + juniorCreditsOwned;
    uint256 middle = IERC20(IZivoeGlobals_ITO(GBL).ZVE()).totalSupply() / 20;
    uint256 lower = IERC20(IZivoeGlobals_ITO(GBL).zSTT()).totalSupply() * 3 + (
        IERC20(IZivoeGlobals_ITO(GBL).zJTT()).totalSupply()
    );

    emit AirdropClaimed(depositor, seniorCreditsOwned / 3, juniorCreditsOwned, upper * middle / lower);

    IERC20(IZivoeGlobals_ITO(GBL).zJTT()).safeTransfer(depositor, juniorCreditsOwned);
    IERC20(IZivoeGlobals_ITO(GBL).zSTT()).safeTransfer(depositor, seniorCreditsOwned / 3);

    if (upper * middle / lower > 0) {
        ITO_IZivoeRewardsVesting(IZivoeGlobals_ITO(GBL).vestZVE()).createVestingSchedule(
            depositor, 0, 360, upper * middle / lower, false
        );
    }
    
    return (seniorCreditsOwned / 3, juniorCreditsOwned, upper * middle / lower);
}
```

Let us explain this function by giving a simple example.

During ITO, Bob deposits 1000 USDC into senior tranche, and 200 USDC into junior tranche. Other depositors in total deposited 9000 USDC into senior tranche, and 800 USDC into junior tranche.

Now, 30 days later, Bob's friend Alice, calls [`migrateDeposits`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L313-L313), so that the ITO can be finished and the normal tranches in the protocol are unlocked for use, and that Bob can [`claimAirdrop`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L203-L203).

Bob calls [`claimAirdrop`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L203-L203). The calculation is as follows:

```solidity
// standardized amount of 1000 USDC deposited * 3
seniorCreditsOwned = 3000 * 10 ** 18
// standardized amount of 200 USDC deposited
juniorCreditsOwned = 200 * 10 ** 18
upper = seniorCreditsOwned + juniorCreditsOwned = 
(3000 * 10 ** 18) + (200 * 10 ** 18) = 3200 * 10 ** 18
// 25000000 ether is the initial total supply of ZivoeToken. 5% is 1250000 ether.
middle = 25000000 ether / 20 = 1250000 ether
// total supply of zSTT * 3 + total supply of zJTT
lower = (10000 * 10 ** 18) * 3 + 1000 * 10 ** 18 = 31000 * 10 ** 18

upper * middle / lower = 
((3200 * 10 ** 18) * 1250000 ether) / (31000 * 10 ** 18)  =
3200 * 1250000000000000000000000 / 31000 = 129032258064516129032258
```

In this scenario, Bob rightfully vests 129032258064516129032258 ZVE as a parameter of [`createVestingSchedule`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L236-L238). 
The calculation is correct because Bob receives `129032258064516129032258 / 1250000000000000000000000 = 3200 / 31000` of total ZVE allocated for airdrop. So what can go wrong?

<ins>**The problem lies in the fact that the total supply of `ZVE`, `zSTT` and `zJTT` are not guaranteed to stay constant.**</ins>

Let's think of this scenario. Alice calls [`migrateDeposits`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L313-L313) as soon as `block.timestamp > end` thanks to the permisionlessness of the function, even before Bob calls [`claimAirdrop`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L203-L203), because she wants to start using tranches in `ZivoeTranches`.

Bob is having a lazy day, he will not call [`claimAirdrop`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L203-L203) anytime soon.

Now, the amount of airdrop that Bob will receive can now manipulated anytime. This is because we notice these lines in `claimAirdrop`:

```solidity
uint256 middle = IERC20(IZivoeGlobals_ITO(GBL).ZVE()).totalSupply() / 20;
uint256 lower = IERC20(IZivoeGlobals_ITO(GBL).zSTT()).totalSupply() * 3 + (
    IERC20(IZivoeGlobals_ITO(GBL).zJTT()).totalSupply()
);
```

Now that the tranches are open in [ZivoeTranches](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeTranches.sol#L61-L61) contract, anyone (including Alice) can go ahead and deposit some money into the tranches while Bob is having a lazy day. Then `IERC20(IZivoeGlobals_ITO(GBL).zSTT()).totalSupply()`  and `IERC20(IZivoeGlobals_ITO(GBL).zJTT()).totalSupply()` will no longer be the same as when they were right before [`migrateDeposits`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L313-L313) was called, because the total supply has now increased. 

For example, Charles, a rich man and a stranger to Bob and Alice, deposits 100,000 USDC into the senior tranche with an innocent intention using [`depositSenior` in `ZivoeTranches` contract](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeTranches.sol#L295-L295).

Then, Bob's yet-claimed airdrop amount will shrink immediately, because `IERC20(IZivoeGlobals_ITO(GBL).zJTT()).totalSupply()` increases. In such a case:

```solidity
// standardized amount of 1000 USDC Bob deposited * 3
seniorCreditsOwned = 3000 * 10 ** 18
// standardized amount of 200 USDC Bob deposited
juniorCreditsOwned = 200 * 10 ** 18
upper = seniorCreditsOwned + juniorCreditsOwned = 
(3000 * 10 ** 18) + (200 * 10 ** 18) = 3200 * 10 ** 18
// 25000000 ether is the initial total supply of ZivoeToken. 5% is 1250000 ether.
middle = 25000000 ether / 20 = 1250000 ether
// total supply of zSTT * 3 + total supply of zJTT
lower = (110000 * 10 ** 18) * 3 + 1000 * 10 ** 18 = 331000 * 10 ** 18

upper * middle / lower = 
((3200 * 10 ** 18) * 1250000 ether) / (331000 * 10 ** 18) = 
3200 * 1250000000000000000000000 / 331000 = 12084592145015105740181
```

This means when Bob calls [`claimAirdrop`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L203-L203) after Charles' deposit, he will only be able to claim a very much diluted amount of 12084592145015105740181 ZVE (12084.592145015105740181 accounting for decimal places) as compared to the correct amount of 129032258064516129032258 ZVE (129032.258064516129032258 accounting for decimal places)

Moreover, given that [`claimAirdrop`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L203-L203)  is a permisionless function that takes the `address` of a depositor, anyone with a malicious intent is able to call [`claimAirdrop`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L203-L203) with the `address` of Bob when they think that the total supply of zSTT and zJTT is large enough, so that Bob can receive the lowest airdrop amount possible.

In a similar way, if existing ZVE holders [`burn`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeToken.sol#L35-L35) their token for some reason, the airdrop amount for ITO participants will be exaggerated.

## Impact

- The protocol is designed such a way that it would incorrectly reduce amount of ZVE to be airdropped as it opens its tranches for use after ITO. This breaks the invariant that 5% of the total ZVE supply will be airdropped to participants based on their proportional ownership of pZVE.
- This causes loss of ZVE for all ITO participants. The loss amount depends on the amount deposited into tranches between the time that the ITO ends with [`migrateDeposits`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L313-L313) and the time the ITO participants claim (vest) their airdrops using [claimAirdrop](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L203-L203).
- This vulnerability is very easy to appear; everything just needs to work as the Zivoe team intended, then it will happen. No malicious actors even need to exist. Put simply, starting and ending the ITO and unlocking the normal senior and junior tranches will just cause the vulnerability to exist.

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L225-L228

## Tool used

Manual Review

## Recommendation

Since we know that [ZVE contract is starting with a hardcoded total supply of `25000000 ether`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeToken.sol#L24-L24), we can just write `1250000 ether` instead of `IERC20(IZivoeGlobals_ITO(GBL).ZVE()).totalSupply() / 20` which is vulnerable to changes if someone burns his ZVE token.

Also, instead of `uint256 lower = IERC20(IZivoeGlobals_ITO(GBL).zSTT()).totalSupply() * 3 + (IERC20(IZivoeGlobals_ITO(GBL).zJTT()).totalSupply())`, we introduce two novel variables called `totalSeniorCredits` and `totalJuniorCredits`. These two variables serve as a snapshot of total senior credits and total junior credits at the end of ITO period.

In [`claimAirdrop`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L203-L203):

```diff
- uint256 middle = IERC20(IZivoeGlobals_ITO(GBL).ZVE()).totalSupply() / 20;
- uint256 lower = IERC20(IZivoeGlobals_ITO(GBL).zSTT()).totalSupply() * 3 + (
-     IERC20(IZivoeGlobals_ITO(GBL).zJTT()).totalSupply()
- );
+ uint256 middle = 1250000 ether;
+ uint256 lower = totalSeniorCredits + totalJuniorCredits
```

Then in [`depositJunior`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L248-L248):

```diff
uint256 standardizedAmount = IZivoeGlobals_ITO(GBL).standardize(amount, asset);

juniorCredits[caller] += standardizedAmount;
+ totalJuniorCredits += standardizedAmount;
```

And similarly, in [`depositSenior`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L277-L277):

```diff
uint256 standardizedAmount = IZivoeGlobals_ITO(GBL).standardize(amount, asset);

seniorCredits[caller] += standardizedAmount * 3;
+ totalSeniorCredits += standardizedAmount * 3;
```

Such a fix will preserve the invariant that the accounting of total deposited amount during ITO will stay the same when `claimAirdrop` is called.