Quaint Brick Troll

medium

# Anyone can decrease the rewardRate and increase the periodFinish for all the reward tokens.

## Summary
The protocol implememts [```depositReward```](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L228C5-L243C6) function so that anyone/protocol itself can donate rewards to the stakers, however because ERC20 tokens doesn't revert on 0 amount transfers and the function doesn't implement a minimum amount to be donated, anyone can fake a donation by passing 0 as the argument to decrease the rewardRate and increase the ```rewardData[_rewardsToken].periodFinish``` duration while the rewarded amount stays the same.

## Vulnerability Detail
The ```rewardRate``` and ```periodFinish``` is updated when there is a deposit through the ```depositReward``` function. 
Suppose, initially if the ```rewardData[_rewardsToken].rewardsDuration``` was set to ```20 days``` and the ```rewardRate``` was called with 100 tokens as reward, 
which will set the ```rewardData[_rewardsToken].rewardRate``` to 5 tokens per day.

Imagine the current timestamp is the last date of march, then by design 100 tokens should be completely distributed at 20th of april unless more reward tokens are added i.e. ```periodFinish```= 20th of april.

After 5 days attacker calls ```depositReward``` with 0 as the reward, now to update the ```rewardRate``` this part of the function will be triggered;
```solidity
} else {
            uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
            uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
            rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
        }
```
```rewardData[_rewardsToken].rewardRate``` = 0(reward) + 75(leftover) / 20 days = 3.75 tokens per day. 
And 

```rewardData[_rewardsToken].periodFinish = block.timestamp.add(rewardData[_rewardsToken].rewardsDuration);``` 5th of april + 20 days = 25th of april.

Attacker successfully decreased the rate from 5 to 3.15 and increase the periodFinish to 25th of april which should have been completely distributed by 20th of april , without depositing any reward tokens. Which is a loss of time and delayed of reward for the stakers that can cause stakers to withdraw. 
Attacker can repeat this anytime he wants to furthur decrease the rate and increase the periodFinish time and also for every reward token which effects every staker in the contract.  


## Impact
In the ```ZivoeRewards``` and ```ZivoeRewardsVesting``` contract the periodFinish can be increased by anyone without even depositing any reward token( or dust amount for non-standard tokens which reverts on 0 amount transfers) through the ```depositReward``` function, which will cause the users to stake for a longer time for the same reward which breaks the core functionality of the reward mechanism implemented by the protocol. Which could also be considered a temporary DOS(how long is controllable by the attacker) because stakers are not able to withdraw their full reward which they should've been able to without the fake donation.

This can be done for all the reward tokens and can be done repetitively to decrease rate and extend the periodFinish furthur and furthur. 

## Code Snippet
```solidity
function depositReward(address _rewardsToken, uint256 reward) external updateReward(address(0)) nonReentrant {
        IERC20(_rewardsToken).safeTransferFrom(_msgSender(), address(this), reward);

        // Update vesting accounting for reward (if existing rewards being distributed, increase proportionally).
        if (block.timestamp >= rewardData[_rewardsToken].periodFinish) {
            rewardData[_rewardsToken].rewardRate = reward.div(rewardData[_rewardsToken].rewardsDuration);
        } else {
            uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
            uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
            rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
        }

        rewardData[_rewardsToken].lastUpdateTime = block.timestamp;
        rewardData[_rewardsToken].periodFinish = block.timestamp.add(rewardData[_rewardsToken].rewardsDuration);
        emit RewardDeposited(_rewardsToken, reward, _msgSender());
    }
```

## Tool used

Manual Review

## Recommendation
Either restrict the function so that only the permissioned addresses can call it, 
OR
implement a ```minimumReward``` that should be deposit whenever the function is called so that no one can do fake donations, declare ```minimumReward``` and add this in the first line of the ```depositReward``` function;
```diff

+      require(rewards >= minimumReward, "not enough rewards deposited!!!");
```
do not use ``` require(rewards > 0) ``` because attacker can still execute the same attack with dust deposits.

Note that this should be mitigated in both the ```ZivoeRewards``` and ```ZivoeRewardsVesting``` contracts.
