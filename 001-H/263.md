Bubbly Rouge Porcupine

medium

# `ZivoeRewards`  lacks  reward rate and balance check on `depositReward()`

## Summary

From [ZivoeRewards.depositReward()](https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewards.sol#L228)
This issue's root cause is the missing validation of reward rate. And it cannot be fixed by making the deposit reward action access control callable by only the YDL contract. So the proper fix is to add the check like synthetix https://github.com/Synthetixio/synthetix/blob/develop/contracts/StakingRewards.sol#L127.


`ZivoeRewards`  contract's [depositReward](https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewards.sol#L228) doesn't validate if the deposited reward amount is higher than the reward rate computed.
But Zivoe is missing this validation.


## Vulnerability Detail



https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243


```solidity
File: 2024-03-zivoe\zivoe-core-foundry\src\ZivoeRewards.sol


228:    function depositReward( address _rewardsToken, uint256 reward) external updateReward(address(0))  nonReentrant {
229:         IERC20(_rewardsToken).safeTransferFrom(_msgSender(), address(this), reward);
230:
231:         // Update vesting accounting for reward (if existing rewards being distributed, increase proportionally).
232:         if (block.timestamp >= rewardData[_rewardsToken].periodFinish) {
233:             rewardData[_rewardsToken].rewardRate = reward.div(rewardData[_rewardsToken].rewardsDuration);
234:         } else {
235:             uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
236:             uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
237:             rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
238:         }
239:
240:         rewardData[_rewardsToken].lastUpdateTime = block.timestamp;
241:         rewardData[_rewardsToken].periodFinish = block.timestamp.add(rewardData[_rewardsToken].rewardsDuration);
242:         emit RewardDeposited(_rewardsToken, reward, _msgSender());
243:     }


```


https://github.com/Synthetixio/synthetix/blob/54991e1fbaf62938362903ace31b1bc1e6c68828/contracts/StakingRewards.sol#L113C1-L132C6


```solidity


113:     function notifyRewardAmount(uint256 reward) external onlyRewardsDistribution updateReward(address(0)) {
114:         if (block.timestamp >= periodFinish) {
115:             rewardRate = reward.div(rewardsDuration);
116:         } else {
117:             uint256 remaining = periodFinish.sub(block.timestamp);
118:             uint256 leftover = remaining.mul(rewardRate);
119:             rewardRate = reward.add(leftover).div(rewardsDuration);
120:         }
121:
122:         // Ensure the provided reward amount is not more than the balance in the contract.
123:         // This keeps the reward rate in the right range, preventing overflows due to
124:         // very high values of rewardRate in the earned and rewardsPerToken functions;
125:         // Reward + leftover must be less than 2^256 / 10^18 to avoid overflow.
126:         uint balance = rewardsToken.balanceOf(address(this));
127:   >>>>  require(rewardRate <= balance.div(rewardsDuration), "Provided reward too high");
128:
129:         lastUpdateTime = block.timestamp;
130:         periodFinish = block.timestamp.add(rewardsDuration);
131:         emit RewardAdded(reward);
132:     }


```

Look at synthtix's notify reward action's validation  at https://github.com/Synthetixio/synthetix/blob/54991e1fbaf62938362903ace31b1bc1e6c68828/contracts/StakingRewards.sol#L127 and compare with zivoe.


Here there's no check if the input param `reward` amount is > 0 or not. Sometimes the reward deposit by [ZivoeYDL.distributeYield](https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeYDL.sol#L246) may deposit zero amounts if there's no yield directed towards certain group maybe stZVE, stJTT, stSTT. So, it will deposit zero tokens and it will just deflate the reward rate for further reward duration.
As the comment says, it `keeps the reward rate in the right range, preventing overflows due to very high values of rewardRate in the earned and rewardsPerToken functions`.

It is also a issue if very high rewards are deposited, causing overflow reverts when reward claim, stake and withdraw action becasue `rewardrate` is involved in these accounting.




## Impact


Overflow protection, due to high reward rates because they are used in other function's accounting.


If a reward is deposited with 0 as input, then reward rate will be deflated because the denominator dependent `rewardDuration` will keep on increasing, making the same reward amount to be distributed over longer periods of time, i.e., slow emission.


## Code Snippet


https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243


https://github.com/Synthetixio/synthetix/blob/54991e1fbaf62938362903ace31b1bc1e6c68828/contracts/StakingRewards.sol#L113C1-L132C6


## Tool used


Manual Review


## Recommendation


Recommend adding the check.

https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243


```diff
    function depositReward(address _rewardsToken, uint256 reward) external updateReward(address(0)) nonReentrant {
        IERC20(_rewardsToken).safeTransferFrom(_msgSender(), address(this), reward);


        // Update vesting accounting for reward (if existing rewards being distributed, increase proportionally).
        if (block.timestamp >= rewardData[_rewardsToken].periodFinish) {
            rewardData[_rewardsToken].rewardRate = reward.div(rewardData[_rewardsToken].rewardsDuration);
        } else {
            uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
            uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
            rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
        }


+       uint balance = IERC20(_rewardsToken).balanceOf(address(this));
+       require(rewardData[_rewardsToken].rewardRate <= balance.div(rewardData[_rewardsToken].rewardsDuration), "Provided reward too high");


        rewardData[_rewardsToken].lastUpdateTime = block.timestamp;
        rewardData[_rewardsToken].periodFinish = block.timestamp.add(rewardData[_rewardsToken].rewardsDuration);
        emit RewardDeposited(_rewardsToken, reward, _msgSender());
    }
```