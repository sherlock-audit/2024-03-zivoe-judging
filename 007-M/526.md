Great Metal Ram

high

# `forwardYield` of `OCL_ZVE` can be denied and skipped for another 30 days

## Summary

When `forwardYield` is called, it will fetch the current basis. If the current basis amount is greater than the last stored basis, it will forward the yield to the `YDL` or `OCT_YDL` contract. However, `fetchBasis` is easily manipulated, and attacker can temporarily manipulate the `pairAsset` balance inside the pool, causing the `forwardYield` yield to be skipped for another 30 days.

## Vulnerability Detail

It can be observed that when `forwardYield` is triggered, it will fetch  current basis and  If the current basis amount is greater than the last stored basis, it will trigger `_forwardYield` to process and forward the yield.

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L287-L305

```solidity
    function forwardYield() external {
        if (IZivoeGlobals_OCL_ZVE(GBL).isKeeper(_msgSender())) {
            require(
                block.timestamp > nextYieldDistribution - 12 hours, 
                "OCL_ZVE::forwardYield() block.timestamp <= nextYieldDistribution - 12 hours"
            );
        }
        else {
            require(
                block.timestamp > nextYieldDistribution, 
                "OCL_ZVE::forwardYield() block.timestamp <= nextYieldDistribution"
            );
        }

        (uint256 amount, uint256 lp) = fetchBasis();
        if (amount > basis) { _forwardYield(amount, lp); }
        (basis,) = fetchBasis();
        nextYieldDistribution += 30 days;
    }
```

However, the amount value returned by `fetchBasis` can be easily manipulated. An attacker can perform a flash swap to decrease the `IERC20(pairAsset).balanceOf(pool)` amount, trigger `forwardYield` via swap callback, and then return the required asset again to the pool.

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L336-L342

```solidity
    function fetchBasis() public view returns (uint256 amount, uint256 lp) {
        address pool = IFactory_OCL_ZVE(factory).getPair(pairAsset, IZivoeGlobals_OCL_ZVE(GBL).ZVE());
>>>     uint256 pairAssetBalance = IERC20(pairAsset).balanceOf(pool);
        uint256 poolTotalSupply = IERC20(pool).totalSupply();
        lp = IERC20(pool).balanceOf(address(this));
        amount = lp * pairAssetBalance / poolTotalSupply;
    }
```

## Impact

Attacker can deny `OCL_ZVE` to forward yield to the `YDL` or `OCT_YDL`, causing loss of yields for users.  

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L287-L305

## Tool used

Manual Review

## Recommendation

Consider to use pool reserve value instead of balance and not update `nextYieldDistribution` and current `basis` when the resulting `amount` is lower than last recorded `basis`.

```diff
    function forwardYield() external {
        if (IZivoeGlobals_OCL_ZVE(GBL).isKeeper(_msgSender())) {
            require(
                block.timestamp > nextYieldDistribution - 12 hours, 
                "OCL_ZVE::forwardYield() block.timestamp <= nextYieldDistribution - 12 hours"
            );
        }
        else {
            require(
                block.timestamp > nextYieldDistribution, 
                "OCL_ZVE::forwardYield() block.timestamp <= nextYieldDistribution"
            );
        }

        (uint256 amount, uint256 lp) = fetchBasis();
-        if (amount > basis) { _forwardYield(amount, lp); }
-        (basis,) = fetchBasis();
-        nextYieldDistribution += 30 days;
+        if (amount > basis) {
+            _forwardYield(amount, lp);
+             (basis,) = fetchBasis();
+            nextYieldDistribution += 30 days;
+        }

    }
```
