Wild Inky Hedgehog

medium

# Incorrect update of valid Field in Combine Struct Upon Loan Combination

## Summary

The `OCC_Modular::applyCombine`  #function incorrectly updates the `valid` field of the `Combine` struct in the `mapping combinations` by using `combineCounter` instead of the specified `id`.

## Vulnerability Detail

When a borrower attempts to apply an approved loan combination, the `applyCombine` function incorrectly updates the `valid` field of the `Combine` struct associated with a different id. Instead of using the provided `id` parameter to target the correct struct, it utilizes the `combineCounter` which is an incrementor for the `mapping combinations`, leading to unintended updates in the valid field of the `Combine` struct. This can result in improper handling of approved loan combinations, affecting the loan status and overall system behavior.

<details>

<summary>Code</summary>

```solidity
function applyCombine(uint256 id) external {
        require(combinations[id].valid, "OCC_Modular::applyCombine() !combinations[id].valid");
        require(
            block.timestamp < combinations[id].expires,
            "OCC_Modular::applyCombine() block.timestamp >= combinations[id].expires"
        );

        // @audit-info using combineCounter means that the Combine struct mapped to the passed id parameter never gets it's valid field updated to false
        combinations[combineCounter].valid = false;

        emit CombineApplied(
            _msgSender(),
            combinations[id].loans,
            combinations[id].term,
            combinations[id].paymentInterval,
            combinations[id].gracePeriod,
            combinations[id].paymentSchedule
        );

        uint256 notional;
        uint256 APR;

        for (uint256 i = 0; i < combinations[id].loans.length; i++) {
            uint256 loanID = combinations[id].loans[i];
            require(
                _msgSender() == loans[loanID].borrower,
                "OCC_Modular::applyCombine() _msgSender() != loans[loanID].borrower"
            );
            require(
                loans[loanID].state == LoanState.Active,
                "OCC_Modular::applyCombine() loans[loanID].state != LoanState.Active"
            );
            notional += loans[loanID].principalOwed;
            APR += loans[loanID].principalOwed * loans[loanID].APR;
            loans[loanID].principalOwed = 0;
            loans[loanID].paymentDueBy = 0;
            loans[loanID].paymentsRemaining = 0;
            loans[loanID].state = LoanState.Combined;
        }

        APR = APR / notional;

        uint256 term = combinations[id].term;
        uint256 APRLateFee = combinations[id].APRLateFee;
        uint256 paymentInterval = combinations[id].paymentInterval;
        uint256 gracePeriod = combinations[id].gracePeriod;
        int8 paymentSchedule = combinations[id].paymentSchedule;

        // "Friday" Payment Standardization, minimum 7-day lead-time
        // block.timestamp - block.timestamp % 7 days + 9 days + paymentInterval
        emit CombineLoanCreated(
            _msgSender(),  // borrower
            loanCounter,  // loanID
            notional,  // principalOwed
            APR,  // APR
            APRLateFee,  // APRLateFee
            block.timestamp - block.timestamp % 7 days + 9 days + paymentInterval,  // paymentDueBy
            term,  // term
            paymentInterval,  // paymentInterval
            gracePeriod,  // gracePeriod
            paymentSchedule  // paymentSchedule
        );
        loans[loanCounter] = Loan(
            _msgSender(), notional, APR, APRLateFee, block.timestamp - block.timestamp % 7 days + 9 days + paymentInterval,
            term, term, paymentInterval, block.timestamp - 1 days, gracePeriod, paymentSchedule, LoanState.Active
        );
        loanCounter += 1;
    }
```

</details>

## Impact

The vulnerability results in the `combinations[id].valid` field always being `true`, allowing borrowers to call the `applyCombine` function and activate additional loans without receiving the corresponding on-chain stablecoin. This situation leads to borrowers owing loans that they have not received, creating a discrepancy in loan management and potential financial liabilities.

**Proof of Concept:**

<details>

<summary>Code</summary>

- Create a `Findings.test.sol` file in your test directory for the Zivoe codebase and paste the contract code below:

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.17;

import {Test, console} from "forge-std/Test.sol";
// src
import {ZivoeDAO} from "../src/ZivoeDAO.sol";
import {ZivoeGlobals} from "../src/ZivoeGlobals.sol";
// src/lockers
import {OCC_Modular} from "../src/lockers/OCC/OCC_Modular.sol";
import {OCT_YDL} from "../src/lockers/OCT/OCT_YDL.sol";

contract Findings is Test {
    OCC_Modular DAI_OCC_Modular;
    ZivoeDAO DAO;
    ZivoeGlobals GBL;
    OCT_YDL YDL_Treasury;

    address constant DAI = 0x6B175474E89094C44Da98b954EedeAC495271d0F;

    address borrower = makeAddr("borrower");
    address underwriter = makeAddr("underwriter");
    address zivoeLabs = makeAddr("ZivoeLabs");

    function setUp() public {
        GBL = new ZivoeGlobals();
        DAO = new ZivoeDAO(address(GBL));
        YDL_Treasury = new OCT_YDL(address(DAO), address(GBL));

        // initialize ZivoeGlobals
        address[] memory globals = new address[](14);
        address[] memory stablecoins = new address[](3);

        globals[0] = address(DAO);
        globals[6] = address(YDL_Treasury);
        globals[10] = zivoeLabs;

        stablecoins[0] = DAI;
        GBL.initializeGlobals(globals, stablecoins);

        // lockers
        DAI_OCC_Modular = new OCC_Modular(
            address(DAO),
            address(DAI),
            address(GBL),
            underwriter,
            address(YDL_Treasury)
        );

        vm.prank(zivoeLabs);
        GBL.updateIsLocker(address(DAI_OCC_Modular), true);

        // deal ERC-20 with bal
        // deal(address(DAI), address(DAI_OCC_Modular), 100_000_000_000_000 ether);
    }

    function createLoanOffer(
        uint256 borrowAmount
    ) public returns (uint256 loanCounter) {
        // reusable code that let's underwriter create a loan offer and returns the id for the created loan
        loanCounter = DAI_OCC_Modular.loanCounter();
        vm.prank(underwriter);
        DAI_OCC_Modular.createOffer(
            borrower,
            borrowAmount,
            10,
            15,
            5,
            14 days,
            7 days,
            1
        );
    }

    function testApplyCombineForSpecificIDAlwaysFails() public {
        uint256[] memory loanIDs = new uint256[](3);
        vm.warp(30 days);
        // create & accept loan offers
        for (uint256 i = 0; i < loanIDs.length; i++) {
            // loan gets created by underwriter and loan id is returned
            loanIDs[i] = createLoanOffer((i + 1) * 1000 ether);

            // borrower accepts loan offer
            vm.prank(borrower);
            DAI_OCC_Modular.acceptOffer(loanIDs[i]);
        }

        // gets the current `uint256 combineCounter` that has not been mapped to any `struct Combine`
        uint256 combineCounterZero = DAI_OCC_Modular.combineCounter();

        // let's approve a combination
        vm.prank(underwriter);
        DAI_OCC_Modular.approveCombine(loanIDs, 15, 1, 28 days, 7 days, 1);

        vm.prank(borrower);
        DAI_OCC_Modular.applyCombine(combineCounterZero);

        // ----------------------

        // create & accept loan offers
        for (uint256 i = 0; i < loanIDs.length; i++) {
            loanIDs[i] = createLoanOffer((i + 1) * 1000 ether);
            vm.prank(borrower);
            DAI_OCC_Modular.acceptOffer(loanIDs[i]);
        }

        uint256 combineCounterOne = DAI_OCC_Modular.combineCounter();

        // let's make a combination exist
        vm.prank(underwriter);
        DAI_OCC_Modular.approveCombine(loanIDs, 15, 1, 28 days, 7 days, 1);

        // we won't apply the combination
        vm.prank(borrower);
        DAI_OCC_Modular.applyCombine(combineCounterOne);

        // ------------
        // combine with ID `combineCounterZero` should have valid = false but that is not the case
        (, , , , , , bool isCombineCounterZeroValid) = DAI_OCC_Modular
            .combinations(combineCounterZero);

        // combine with ID `combineCounterOne` should have valid = false but that is not the case
        (, , , , , , bool isCombineCounterOneValid) = DAI_OCC_Modular
            .combinations(combineCounterOne);

        // gets the current `uint256 combineCounter` that has not been mapped to any `struct Combine`
        uint256 combineCounterTwo = DAI_OCC_Modular.combineCounter();

        (, , , , , , bool isCombineCounterTwoValid) = DAI_OCC_Modular
            .combinations(combineCounterTwo);

        console.log("combineCounterZero => ", combineCounterZero);
        console.log("combineCounterOne => ", combineCounterOne);
        console.log("combineCounterTwo => ", combineCounterTwo);

        console.log("isCombineCounterZeroValid => ", isCombineCounterZeroValid);
        console.log("isCombineCounterOneValid => ", isCombineCounterOneValid);
        console.log("isCombineCounterTwoValid => ", isCombineCounterTwoValid);

        assertEq(isCombineCounterZeroValid, true);
        assertEq(isCombineCounterOneValid, true);
        assertEq(isCombineCounterTwoValid, false);
    }
}
```

</details>

- Make sure you have foundry and other contract dependencies installed.
- Run `forge test --mt testApplyCombineForSpecificIDAlwaysFails -vvvvv` in your terminal.

**RESULTS**

```js
Ran 1 test for test/Findings.test.sol:Findings
[PASS] testApplyCombineForSpecificIDAlwaysFails() (gas: 2254691)
Logs:
  combineCounterZero =>  0
  combineCounterOne =>  1
  combineCounterTwo =>  2
  isCombineCounterZeroValid =>  true
  isCombineCounterOneValid =>  true
  isCombineCounterTwoValid =>  false

// ...truncated
```

## Code Snippet

[Code Snippet](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L742-L749)

## Tool used

- Manual Review
- Foundry

## Recommendation

Refactor the `OCC_Modular::applyCombine` function accordingly:

```diff
    function applyCombine(uint256 id) external {
        require(combinations[id].valid, "OCC_Modular::applyCombine() !combinations[id].valid");
        ...
-       combinations[combineCounter].valid = false;
+       combinations[id].valid = false;
        ...
    }
```
