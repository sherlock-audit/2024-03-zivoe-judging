Sneaky Sand Barracuda

high

# _writeCheckpoint function is called with wrong amounts for a user if some tokens have already been withdrawn.

## Summary
_writeCheckpoint function is called with wrong amounts if some tokens have already been withdrawn which can cause user to left with some votes even though its balance has already been reduced to zero.

## Vulnerability Detail
Following is revokeVestingSchedule function
```solidity
function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
        require(
            vestingScheduleSet[account], 
            "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleSet[account]"
        );
        require(
            vestingScheduleOf[account].revokable, 
            "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleOf[account].revokable"
        );
        
        uint256 amount = amountWithdrawable(account);
        uint256 vestingAmount = vestingScheduleOf[account].totalVesting;

        vestingTokenAllocated -= amount;

        vestingScheduleOf[account].totalWithdrawn += amount;
        vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
        vestingScheduleOf[account].cliff = block.timestamp - 1;
        vestingScheduleOf[account].end = block.timestamp;

        vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);

        _totalSupply = _totalSupply.sub(vestingAmount);
        _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
        _writeCheckpoint(_checkpoints[account], _subtract, amount);
        _balances[account] = 0;
        stakingToken.safeTransfer(account, amount);

        vestingScheduleOf[account].revokable = false;

        emit VestingScheduleRevoked(
            account, 
            vestingAmount - vestingScheduleOf[account].totalWithdrawn, 
            vestingScheduleOf[account].cliff, 
            vestingScheduleOf[account].end, 
            vestingScheduleOf[account].totalVesting, 
            false
        );
    }
```
Issue lies in the following line 
    _writeCheckpoint(_checkpoints[account], _subtract, amount);
it only reduces the votes of the account by amount withdrawn. So if vesting has not ended then amount would be less than the total tokens vested by the user thus it causes less votes to be reduced even though in the its balance is reduced to zero in the next line thus causing lingering of votes/user left with votes even though it shouldn't be the case.
## Impact
Causes user to left some votes even though its balance is reduced to zero
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L453
## Tool used

Manual Review

## Recommendation
Make the following change in code in revokeVestingSchedule
```solidity
--- _writeCheckpoint(_checkpoints[account], _subtract, amount);
+++  _writeCheckpoint(_checkpoints[account], _subtract, vestingAmount - vestingScheduleOf[account].totalWithdrawn + amount);
```
        
      