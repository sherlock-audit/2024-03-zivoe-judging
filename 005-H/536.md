Decent Chiffon Wolf

high

# The `revokeVestingSchedule` function does not correctly track `_totalSupply`, `_totalSupplyCheckpoints` and `_checkpoints[account]`

## Summary

In [ZivoeRewardsVesting.revokeVestingSchedule()](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429), `_totalSupply` and `_totalSupplyCheckpoints` are updated by subtracting `vestingAmount`, and `_checkpoints[account]` is updated by subtracting `amount`.
In [ZivoeRewardsVesting.withdraw()](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L501), `_totalSupply`, `_totalSupplyCheckpoints` and `_checkpoints[account]` are updated by subtracting withdrawable `amount`.

Therefore, when `revokeVestingSchedule()` is called after withdrawal, `_totalSupply` and `_totalSupplyCheckpoints` are reduced by the total withdrawn amount twice. Additionally, `_checkpoints[account]` is not reduced by the canceled amount.

## Vulnerability Detail

In [ZivoeRwardsVesting._stake()](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L473) function, it tracks `amountToVest` using `_totalSupply`, `_totalSupplyCheckpoints` and `_checkpoints[account]`.

https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L473

```Solodity
File: zivoe-core-foundry\src\ZivoeRewardsVesting.sol
473:     function _stake(uint256 amount, address account) private nonReentrant updateReward(account) {
474:         require(amount > 0, "ZivoeRewardsVesting::_stake() amount == 0");
475: 
476:         _totalSupply = _totalSupply.add(amount);
477:         _writeCheckpoint(_totalSupplyCheckpoints, _add, amount);
478:         _writeCheckpoint(_checkpoints[account], _add, amount);
479:         _balances[account] = _balances[account].add(amount);
480:         emit Staked(account, amount);
481:     }
```

The `ZivoeRewardsVesting.withdraw()` function tracks the staker's withdrawn amount.
From [L508~L510](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L508~510) below, `_totalSupply`, `_totalSupplyCheckpoints` and `_checkpoints[account]` are updated by subtracting the withdrawn `amount`.

https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L501

```Solodity
File: zivoe-core-foundry\src\ZivoeRewardsVesting.sol
501:     function withdraw() public nonReentrant updateReward(_msgSender()) {
502:         uint256 amount = amountWithdrawable(_msgSender());
503:         require(amount > 0, "ZivoeRewardsVesting::withdraw() amountWithdrawable(_msgSender()) == 0");
504:         
505:         vestingScheduleOf[_msgSender()].totalWithdrawn += amount;
506:         vestingTokenAllocated -= amount;
507: 
508:         _totalSupply = _totalSupply.sub(amount);
509:         _writeCheckpoint(_totalSupplyCheckpoints, _subtract, amount);
510:         _writeCheckpoint(_checkpoints[_msgSender()], _subtract, amount);
511:         _balances[_msgSender()] = _balances[_msgSender()].sub(amount);
512:         stakingToken.safeTransfer(_msgSender(), amount);
513: 
514:         emit Withdrawn(_msgSender(), amount);
515:     }
```

When ZVL revokes vesting schedule, `_totalSupply` and `_totalSupplyCheckpoints` are updated by subtracting `vestingAmount` and `_checkpoints[account]` is updated by subtracting `amount` from L451~L453 below. Here, `vestingAmount` is the original `vestingScheduleOf[account].totalVesting` , which includes the amount already withdrawn by the staker.

https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429

```Solodity:
File: zivoe-core-foundry\src\ZivoeRewardsVesting.sol
429:     function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
             [...]
439:         uint256 amount = amountWithdrawable(account);
440:         uint256 vestingAmount = vestingScheduleOf[account].totalVesting;
441: 
442:         vestingTokenAllocated -= amount;
443: 
444:         vestingScheduleOf[account].totalWithdrawn += amount;
445:         vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
446:         vestingScheduleOf[account].cliff = block.timestamp - 1;
447:         vestingScheduleOf[account].end = block.timestamp;
448: 
449:         vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);
450: 
451:         _totalSupply = _totalSupply.sub(vestingAmount);
452:         _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
453:         _writeCheckpoint(_checkpoints[account], _subtract, amount);
             [...]
467:     }
```

Thus, `_totalSupply` and `_totalSupplyCheckpoints` are reduced by the withdrawn amount twice at L451~L452. `_checkpoints[account]` is reduced by the `amount` from L453. However, the reduction amount for `_totalSupply` and `_totalSupplyCheckpoints` should be the same as the reduction in
`_checkpoints[account]`. As a result, `_totalSupply` is calculated to be smaller than actual value. Consequently, `rewardPerToken()` function from L321 below returns greater value.

This means that stakers can receive more rewards than the actual rewards and they can also gain more voting power than they should have.

https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L316

```Solodity:
File: zivoe-core-foundry\src\ZivoeRewardsVesting.sol
316:     function rewardPerToken(address _rewardsToken) public view returns (uint256 amount) {
317:         if (_totalSupply == 0) { return rewardData[_rewardsToken].rewardPerTokenStored; }
318:         return rewardData[_rewardsToken].rewardPerTokenStored.add(
319:             lastTimeRewardApplicable(_rewardsToken).sub(
320:                 rewardData[_rewardsToken].lastUpdateTime
321:             ).mul(rewardData[_rewardsToken].rewardRate).mul(1e18).div(_totalSupply)
322:         );
323:     }
```

The decreased `_totalSupplyCheckpoints` makes the return value of the `quorum()` smaller than actual value from L73 below.

```Solodity:
File: zivoe-core-foundry\lib\openzeppelin-contracts\contracts\governance\extensions\GovernorVotesQuorumFraction.sol
72:     function quorum(uint256 blockNumber) public view virtual override returns (uint256) {
73:         return (token.getPastTotalSupply(blockNumber) * quorumNumerator(blockNumber)) / quorumDenominator();
74:     }
```

Decreasing `_checkpoints[account]` makes the return value of `IZivoeRewardsVesting_ZVG(IZivoeGlobals_ZVG(GBL).vestZVE()).getPastVotes()` smaller than actual value from L52 below.

```Solodity:
File: zivoe-core-foundry\src\libraries\ZivoeVotes.sol
50:     function getPastVotes(address account, uint256 blockNumber) public view virtual returns (uint256) {
51:         require(blockNumber < block.number, "ERC20Votes: block not yet mined");
52:         return _checkpointsLookup(_checkpoints[account], blockNumber);
53:     }
```

Thus, the governance voting is not conducted correctly.

Subtracting the withdrawn amount twice from L451~L452 below can lead to a DoS by underflow checking

```Solodity:
File: zivoe-core-foundry\src\ZivoeRewardsVesting.sol
429:     function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
            [...]
451:         _totalSupply = _totalSupply.sub(vestingAmount);
452:         _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
            [...]
467:     }
```

## Impact

- Incorrect tracking of `_totalSupply` allows stakers of `vestingToken` to receive more rewards than the actual rewards.
- Incorrect tracking of `_totalSupplyCheckpoints` causes the governor to miscalculate the `quorum()`, and incorrect tracking of `_checkpoints[account]` causes the governor to miscalculate `_getVotes()`. Thus, governance voting is not conducted correctly.
- Subtracting the withdrawn amount twice can lead to a Denial of Service (DoS) when calling the `revokeVestingSchedule()` function.

## Tool used

Manual Review

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L451~L453

## Recommendation

```diff
File: zivoe-core-foundry\src\ZivoeRewardsVesting.sol
function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
    [...]
-    _totalSupply = _totalSupply.sub(vestingAmount);
-    _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
-    _writeCheckpoint(_checkpoints[account], _subtract, amount);
+    _totalSupply = _totalSupply.sub(vestingAmount - vestingScheduleOf[account].totalWithdrawn + amount);
+    _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount - vestingScheduleOf[account].totalWithdrawn + amount);
+    _writeCheckpoint(_checkpoints[account], _subtract, vestingAmount - vestingScheduleOf[account].totalWithdrawn + amount);
    [...]
}
```
