Fierce Crepe Sloth

high

# Revoking user's vesting doesn't remove all of the user's votes

## Summary
The ``ZivoeGovernorV2.sol`` contract overrides the [_getVotes()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeGovernorV2.sol#L143-L152) function to the following: 
```solidity
    function _getVotes(
        address account,
        uint256 blockNumber,
        bytes memory /*params*/
    ) internal view virtual override(Governor, GovernorVotes) returns (uint256) {
        return token.getPastVotes(account, blockNumber) + 
            IZivoeRewardsVesting_ZVG(IZivoeGlobals_ZVG(GBL).vestZVE()).getPastVotes(account, blockNumber) +
            IZivoeRewards_ZVG(IZivoeGlobals_ZVG(GBL).stZVE()).getPastVotes(account, blockNumber);
    }
```

which means that when people are voting for new proposals their voting is comprised of the amount of ZVE tokens they hold, the amount they have staked in the ``ZivoeRewards.sol`` contract represented by ``$stZVE balance``, and their vested amount in ``ZivoeRewardsVesting.sol`` represented by ``$vestZVE``.  In the ``ZivoeRewardsVesting.sol`` contract a vesting schedule can be created either by the ``ZivoeITO.sol`` contract, based on user deposits, or directly by the ZVL address, which is a multisig wallet controlled by the Zivoe Laboratory by calling the [createVestingSchedule()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L381-L425) function. The ``ZivoeRewardsVesting.sol`` contract allows the ZVL wallet to revoke the vesting schedule of users, this might happen due to different reasons. For example a user is no longer contributing to the development of the protocol, his wallets gets compromised, etc. Revoking a vesting schedule is performed by calling the [revokeVestingSchedule()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429-L467) function. However the amount subtracted from ``_checkpoints[account]``  is incorrect:

```solidity
function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
        ...
        
        uint256 amount = amountWithdrawable(account);
        uint256 vestingAmount = vestingScheduleOf[account].totalVesting;

        vestingTokenAllocated -= amount;

        vestingScheduleOf[account].totalWithdrawn += amount;
        vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
        vestingScheduleOf[account].cliff = block.timestamp - 1;
        vestingScheduleOf[account].end = block.timestamp;

        vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);

        _totalSupply = _totalSupply.sub(vestingAmount);
        _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
        _writeCheckpoint(_checkpoints[account], _subtract, amount);
        _balances[account] = 0;
        stakingToken.safeTransfer(account, amount);

        vestingScheduleOf[account].revokable = false;
        ...
    }
```
As can be seen from the above code snippet ``amount`` is subtracted from the ``_checkpoints[account]`` of the user whose vesting is revoked. However ``amount`` represent the amount of tokens that a user can withdraw from his vesting schedule based on how much time has passed since his vesting schedule was created:

```solidity
    function amountWithdrawable(address account) public view returns (uint256 amount) {
        if (block.timestamp < vestingScheduleOf[account].cliff) { return 0; }
        if (
            block.timestamp >= vestingScheduleOf[account].cliff && 
            block.timestamp < vestingScheduleOf[account].end
        ) {
            return vestingScheduleOf[account].vestingPerSecond * (
                block.timestamp - vestingScheduleOf[account].start
            ) - vestingScheduleOf[account].totalWithdrawn;
        }
        else if (block.timestamp >= vestingScheduleOf[account].end) {
            return vestingScheduleOf[account].totalVesting - vestingScheduleOf[account].totalWithdrawn;
        }
        else { return 0; }
    }
```
A user who must not have any votes from the ``ZivoeRewardsVesting.sol`` contract will have them indefinitely, there is nothing that can be done to remove his votes. He can vote on all new proposals and the total votes that can be casted for a proposal will be more than all the votes that should exist. Depending on when a user vesting schedule is revoked, the consequences for the protocol will be severe, for example if a user got a vesting schedule for a year, but after a month the protocol team decides to revoke it, the user will be left with a big percentage of his initially allocated votes. 
NOTE: there is another problem with how ``_totalSupply`` and ``_totalSupplyCheckpoints`` are calculated, however this is a separate issue.
## Vulnerability Detail
[Gist](https://gist.github.com/AtanasDimulski/e2edba2c03e4dd1325b9e73c8fd58ddb)

After following the steps in the above linked [gist](https://gist.github.com/AtanasDimulski/e2edba2c03e4dd1325b9e73c8fd58ddb) add the following test to the ``AuditorTests.t.sol``:

```solidity
    function test_RevokedVestingDoesNotRemoveAllUserVotes() public {
        unlockYDLandVZVT();
        vm.startPrank(ZVL);
        uint256 amountToVest = 10_000e18;
        zivoeToken.transfer(address(zivoeRewardsVesting), amountToVest);
        zivoeRewardsVesting.createVestingSchedule(alice, 0, 360, amountToVest, true);
        console2.log("Alice initial votes: ", zivoeRewardsVesting.getVotes(alice));
        console2.log("Alice initial withdrawable amount: ", zivoeRewardsVesting.amountWithdrawable(alice));

        /// @notice assuming 1 ethereum block = 12 sec, 86400 / 12 = 7200 
        skip(86400);
        vm.roll(7200);
        console2.log("Alice withdrawable amount after 1 day has passed: ", zivoeRewardsVesting.amountWithdrawable(alice));
        zivoeRewardsVesting.revokeVestingSchedule(alice);

        /// @notice we skip one more day to better illustrate that alice still has votes
        skip(86400);
        vm.roll(7200);
        console2.log("Alice votes after her vesting has been revoked: ", zivoeRewardsVesting.getVotes(alice));
        vm.stopPrank();
    }
```

```solidity
Logs:
  Alice initial votes:  10000000000000000000000
  Alice initial withdrawable amount:  0
  Alice withdrawable amount after 1 day has passed:  27777777777777715200
  Alice votes after her vesting has been revoked:  9972222222222222284800
```

To run the test use: ``forge test -vvv --mt test_RevokedVestingDoesNotRemoveAllUserVotes``
## Impact
A user who must not have any votes from the ``ZivoeRewardsVesting.sol`` contract will have them indefinitely, there is nothing that can be done to remove his votes. He can vote on all new proposals and the total votes that can be casted for a proposal will be more than all the votes that should exist.

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429-L467
## Tool used

Manual Review & Foundry

## Recommendation
In the [revokeVestingSchedule()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429-L467) function consider adding a new variable ``vestingAmountUser`` which is equal to ``totalVesting`` and subtract the ``totalWihtdrawnAmount`` from it, right before the current withdrawable amount is added to the ``totalWithdrawn`` and subtract ``vestingAmountUser``  from the ``_checkpoints[account]``