Elegant Silver Scorpion

medium

# Incorrect logic causes inaccurate values for a weight/votes checkpoint

## Summary
Incorrect value for the `_writeCheckpoint()` function used in `ZivoeRewardsVesting::revokeVestingSchedule()` causes an inaccurate value for a weight/votes checkpoint

## Vulnerability Detail
This is the `ZivoeRewardsVesting::withdraw()` function:
```solidity
    function withdraw() public nonReentrant updateReward(_msgSender()) {
            uint256 amount = amountWithdrawable(_msgSender());
            require(amount > 0, "ZivoeRewardsVesting::withdraw() amountWithdrawable(_msgSender()) == 0");
            
            vestingScheduleOf[_msgSender()].totalWithdrawn += amount;
            vestingTokenAllocated -= amount;

            _totalSupply = _totalSupply.sub(amount);
            _writeCheckpoint(_totalSupplyCheckpoints, _subtract, amount);
            _writeCheckpoint(_checkpoints[_msgSender()], _subtract, amount);
            _balances[_msgSender()] = _balances[_msgSender()].sub(amount);
            stakingToken.safeTransfer(_msgSender(), amount);

            emit Withdrawn(_msgSender(), amount);
        }
```
Imagine a user withdraws all of his balance, then the `_balances` mapping for his address will be set to 0 as well as his checkpoint. That indicates the goal of the developers, they want to keep the `_balances` mapping for a particular user and his checkpoint in sync. The same goal can be noticed in the `ZivoeRewardsVesting::_stake()` function.

However, when a vesting schedule is revoked using the `ZivoeRewardsVesting::revokeVestingSchedule()` function, that same goal isn't achieved.
```solidity
function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
        require(
            vestingScheduleSet[account], 
            "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleSet[account]"
        );
        require(
            vestingScheduleOf[account].revokable, 
            "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleOf[account].revokable"
        );
        
        uint256 amount = amountWithdrawable(account);
        uint256 vestingAmount = vestingScheduleOf[account].totalVesting;

        vestingTokenAllocated -= amount;

        vestingScheduleOf[account].totalWithdrawn += amount;
        vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
        vestingScheduleOf[account].cliff = block.timestamp - 1;
        vestingScheduleOf[account].end = block.timestamp;

        vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);
        _totalSupply = _totalSupply.sub(vestingAmount);
        _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
        _writeCheckpoint(_checkpoints[account], _subtract, amount);
        _balances[account] = 0;
        stakingToken.safeTransfer(account, amount);

        vestingScheduleOf[account].revokable = false;

        emit VestingScheduleRevoked(
            account, 
            vestingAmount - vestingScheduleOf[account].totalWithdrawn, 
            vestingScheduleOf[account].cliff, 
            vestingScheduleOf[account].end, 
            vestingScheduleOf[account].totalVesting, 
            false
        );
    }
```
In this function, there is an issue. We can also see another goal of the developers - upon revocation of a vesting schedule, they are trying to revert as many of the state changes that occurred during the creation of a vesting schedule. For example, the `_totalSupply` is decreased by the total vesting amount for a particular vesting schedule (which would equal 0 if that's the only vesting schedule), the checkpoint for the total supply is also decreased by that same amount, the `_balances` mapping for him is decreased to 0.

The issue here is the fact that the checkpoint for the particular user is only decreased by the `amount` variable. This breaks 2 of the clear goals the developers had when developing this. First of all, the `_balances` mapping for the user is clearly not in check with the checkpoint as the `_balances` mapping for the user is set to 0 while the checkpoint would not be 0 unless the revocation of the vesting schedule didn't occur after the vesting period was over (highly unlikely). The checkpoint for the user now has a value that is more than 0 while his balance is 0 which is not expected as seen from the code in the `withdraw()` function. The second goal that's broken is the fact that this doesn't revert the state changes as much as possible as there is still a value for the checkpoint for the user.
## Impact
Incorrect logic causes inaccurate values for a weight/votes checkpoint
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429-L467
## Tool used

Manual Review

## Recommendation
```diff
+ _writeCheckpoint(_checkpoints[account], _subtract, _balances[account]);
- _writeCheckpoint(_checkpoints[account], _subtract, amount);
```
