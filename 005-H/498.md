Round Hazelnut Tadpole

medium

# Calling `revokeVestingSchedule` function does not reduce `_checkpoints[account]` at `block.number` to 0

## Summary
The `revokeVestingSchedule` function would be called before the `account` is fully vested, and such function call does not reduce `_checkpoints[account]` at `block.number` to 0 though it should. This inflates the number of votes owned by the `account`, and such `account` can use these extra votes, which it is not entitled to, to pass proposals that should not be passed if these extra votes are not used.

## Vulnerability Detail
When calling the `createVestingSchedule` function in the Code Snippet section to create vesting schedule for `account`, the `_stake` function in the Code Snippet section is called to increase `_checkpoints[account]` at `block.number` and `_balances[account]` to such `account`'s `totalVesting`. When the `revokeVestingSchedule` function in the Code Snippet section is called before the `account` is fully vested, such `account`'s withdrawable amount is less than its `totalVesting`. In this case, executing `_writeCheckpoint(_checkpoints[account], _subtract, amount)`, where `amount` is such `account`'s withdrawable amount, does not reduce `_checkpoints[account]` at `block.number` to 0 but executing `_balances[account] = 0` does reduce the corresponding `account`'s balance to 0. Hence, after calling the `createVestingSchedule` function, `_checkpoints[account]` at `block.number` can be higher than 0, which is the value that it should be.

## Impact
Because `_checkpoints[account]` at `block.number` is higher than what it should be, the number of votes for the corresponding `account` at the end of `blockNumber`, which depends on `IZivoeRewardsVesting_ZVG(IZivoeGlobals_ZVG(GBL).vestZVE()).getPastVotes(account, blockNumber)`, returned by the `_getVotes` function in the Code Snippet section is higher than what is should be. When such `account` owns the number of votes that is higher than entitled to, it can use these extra votes to pass proposals, which should not be passed if these extra votes are not used.

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L381-L425
```solidity
    function createVestingSchedule(
        address account, 
        uint256 daysToCliff, 
        uint256 daysToVest, 
        uint256 amountToVest, 
        bool revokable
    ) external onlyZVLOrITO {
        ...
        vestingScheduleOf[account].totalVesting = amountToVest;
        ...
        _stake(amountToVest, account);
    }
```

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L473-L481
```solidity
    function _stake(uint256 amount, address account) private nonReentrant updateReward(account) {
        require(amount > 0, "ZivoeRewardsVesting::_stake() amount == 0");

        _totalSupply = _totalSupply.add(amount);
        _writeCheckpoint(_totalSupplyCheckpoints, _add, amount);
        _writeCheckpoint(_checkpoints[account], _add, amount);
        _balances[account] = _balances[account].add(amount);
        emit Staked(account, amount);
    }
```

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429-L467
```solidity
    function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
        ...
        uint256 amount = amountWithdrawable(account);
        uint256 vestingAmount = vestingScheduleOf[account].totalVesting;

        vestingTokenAllocated -= amount;

        vestingScheduleOf[account].totalWithdrawn += amount;
        vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
        vestingScheduleOf[account].cliff = block.timestamp - 1;
        vestingScheduleOf[account].end = block.timestamp;

        vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);

        _totalSupply = _totalSupply.sub(vestingAmount);
        _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
        _writeCheckpoint(_checkpoints[account], _subtract, amount);
        _balances[account] = 0;
        stakingToken.safeTransfer(account, amount);

        vestingScheduleOf[account].revokable = false;
        ...
    }
```

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeGovernorV2.sol#L144-L152
```solidity
    function _getVotes(
        address account,
        uint256 blockNumber,
        bytes memory /*params*/
    ) internal view virtual override(Governor, GovernorVotes) returns (uint256) {
        return token.getPastVotes(account, blockNumber) + 
            IZivoeRewardsVesting_ZVG(IZivoeGlobals_ZVG(GBL).vestZVE()).getPastVotes(account, blockNumber) +
            IZivoeRewards_ZVG(IZivoeGlobals_ZVG(GBL).stZVE()).getPastVotes(account, blockNumber);
    }
```

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/libraries/ZivoeVotes.sol#L50-L53
```solidity
    function getPastVotes(address account, uint256 blockNumber) public view virtual returns (uint256) {
        require(blockNumber < block.number, "ERC20Votes: block not yet mined");
        return _checkpointsLookup(_checkpoints[account], blockNumber);
    }
```

## Tool used
Manual Review

## Recommendation
The `revokeVestingSchedule` function can be updated to execute `_writeCheckpoint(_checkpoints[account], _subtract, accountCheckPointReduction)`, where `accountCheckPointReduction` is the difference between `vestingScheduleOf[account].totalVesting` and `vestingScheduleOf[account].totalWithdrawn` before the `revokeVestingSchedule` function is called, instead of `_writeCheckpoint(_checkpoints[account], _subtract, amount)`.