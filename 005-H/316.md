Glorious Midnight Snake

high

# Incorrect subtraction in `ZivoeRewardsVesting#revokeVestingSchedule()`

## Summary
Incorrect values are used for subtraction in [`ZivoeRewardsVesting#revokeVestingSchedule()`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429-L467) 
## Vulnerability Detail
`ZVL` can set a revokable (`revokable = true`) vestingSchedule for an account via calling [`ZivoeRewardsVesting#createVestingSchedule()`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L381-L425).
`ZVL` can revoke the revokable vestingSchedule at any time via calling [`ZivoeRewardsVesting#revokeVestingSchedule()`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429-L467).
Once the vesting schedule is revoked, all unvested amounts should be removed from the vesting supply, and the specified account should no longer have any unvested $ZVE to obtain.

However, incorrect values are used for calculation, which could result in several consequence:
- `revokeVestingSchedule()` could revert due to underflow
- `ZivoeRewardsVesting` get less yield due to incorrect `_totalSupply`
- the revoked account can own unvested voting power even the unvested $ZVE is revoked

`vestingAmount` is subtracted from `_ totalSupply` and `_totalSupplyCheckpoints` in [L451-L452](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L451-L452):
```solidity
451:        _totalSupply = _totalSupply.sub(vestingAmount);
452:        _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
```
`_totalSupply` might be less than `vestingAmount`, resulting in `revokeVestingSchedule()` reverting.
If `revokeVestingSchedule()` doesn't revert, `_totalSupply` will be far less than expected.  Since `ZivoeRewardsVesting#totalSupply()` is used for yield distribution calculation in [`ZivoeYDL#distributeYield()`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeYDL.sol#L250-L255), the incorrect `_totalSupply` will cause `ZivoeRewardsVesting` receiving less yield.

`amount` is used for updating `_checkpoints[account]` in [L453](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L453):
```solidity
        _writeCheckpoint(_checkpoints[account], _subtract, amount);
```
The voting power of `account` could be non-zero even its balance in `ZivoeRewardsVesting` is 0

Copy below codes into [`Test_ZivoeRewardsVesting.sol`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-testing/src/TESTS_Core/Test_ZivoeRewardsVesting.sol) and run `forge test --rpc-url RPC-URL --match-test test_ZivoeRewardsVesting_revokeVestingSchedule_underflow`:
```solidity
    function test_ZivoeRewardsVesting_revokeVestingSchedule_underflow() public {
        //@audit-info create a revokable vesting schedule
        assert(zvl.try_createVestingSchedule(
            address(vestZVE), 
            address(moe), 
            0, 
            360,
            12_500_000 ether, 
            true //@audit-info revokable is true
        ));

        address moeAddr = address(moe);
        //@audit-info moe withdraws the available $ZVE
        hevm.warp(block.timestamp + 3 days);
        hevm.prank(moeAddr);
        vestZVE.withdraw();
        //@audit-info the vesting schedule can not be revoked due to underflow
        hevm.startPrank(address(zvl));
        vm.expectRevert(stdError.arithmeticError);
        vestZVE.revokeVestingSchedule(moeAddr);
        hevm.stopPrank();
    }
```
From the above we can see that `revokeVestingSchedule()` reverts due to underflow.

Copy below codes into [`Test_ZivoeRewardsVesting.sol`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-testing/src/TESTS_Core/Test_ZivoeRewardsVesting.sol) and run `forge test --rpc-url RPC-URL --match-test test_ZivoeRewardsVesting_revokeVestingSchedule_nonZeroVotingPower`:
```solidity
    function test_ZivoeRewardsVesting_revokeVestingSchedule_nonZeroVotingPower() public {
        //@audit-info create a revocable vesting schedule
        assert(zvl.try_createVestingSchedule(
            address(vestZVE), 
            address(moe), 
            0, 
            360,
            2_500_000 ether, 
            true //@audit-info revokable is true
        ));
        assert(zvl.try_createVestingSchedule(
            address(vestZVE), 
            address(pam), 
            0, 
            360,
            2_500_000 ether, 
            false //@audit-info revokable is false
        ));

        address moeAddr = address(moe);
        //@audit-info moe withdraws the available $ZVE
        hevm.warp(block.timestamp + 3 days);
        uint withdrawableAmount = vestZVE.amountWithdrawable(moeAddr);
        hevm.prank(moeAddr);
        vestZVE.withdraw();
        //@audit-info the vesting schedule is revoked
        hevm.startPrank(address(zvl));
        vestZVE.revokeVestingSchedule(moeAddr);
        hevm.stopPrank();
        uint votingPower = vestZVE.getVotes(moeAddr);
        uint balance = vestZVE.balanceOf(moeAddr);
        //@audit-info the voting power of moe in vestZVE is not zero
        assert(votingPower == 2_500_000 ether - withdrawableAmount);
        //@audit-info the balance of moe in vestZVE is 0
        assert(balance == 0);
    }
```
From the above we can see that moe still has voting power even their balance is zero.

## Impact
- `revokeVestingSchedule()` could revert due to underflow
- `ZivoeRewardsVesting` might receive far less yield due to incorrect `vestZVE#totalSupply()`
- There is no way to revoke unvested voting power even the unvested $ZVE is revoked.

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L451-L453
## Tool used

Manual Review

## Recommendation
Correct the subtractions in `ZivoeRewardsVesting#revokeVestingSchedule()`:
```diff
    function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
        require(
            vestingScheduleSet[account], 
            "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleSet[account]"
        );
        require(
            vestingScheduleOf[account].revokable, 
            "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleOf[account].revokable"
        );
        
        uint256 amount = amountWithdrawable(account);
        uint256 vestingAmount = vestingScheduleOf[account].totalVesting;

        vestingTokenAllocated -= amount;

        vestingScheduleOf[account].totalWithdrawn += amount;
        vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
        vestingScheduleOf[account].cliff = block.timestamp - 1;
        vestingScheduleOf[account].end = block.timestamp;

        vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);

-       _totalSupply = _totalSupply.sub(vestingAmount);
-       _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
-       _writeCheckpoint(_checkpoints[account], _subtract, amount);
+       _totalSupply = _totalSupply.sub(vestingAmount - vestingScheduleOf[account].totalWithdrawn);
+       _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount - vestingScheduleOf[account].totalWithdrawn);
+       _writeCheckpoint(_checkpoints[account], _subtract, vestingAmount - vestingScheduleOf[account].totalWithdrawn);
        _balances[account] = 0;
        stakingToken.safeTransfer(account, amount);

        vestingScheduleOf[account].revokable = false;

        emit VestingScheduleRevoked(
            account, 
            vestingAmount - vestingScheduleOf[account].totalWithdrawn, 
            vestingScheduleOf[account].cliff, 
            vestingScheduleOf[account].end, 
            vestingScheduleOf[account].totalVesting, 
            false
        );
    }
```