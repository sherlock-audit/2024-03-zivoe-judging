Ancient Chartreuse Kangaroo

high

# Incorrect Calculation of `_totalSupply` of `vestZVE`

## Summary
The `_totalSupply` of `vestZVE` are wrongly calculated due to double-subtracting upon revoking a `vestingSchedule` since it does not take into account potential previous withdrawals by the user.

## Vulnerability Detail
After the end of the "Initial Tranches Offering" in the `ZivoeITO.sol` contract, `vestingSchedules` are [created](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeITO.sol#L236) for the $ZVE that depositors have to claim. These `vestingSchedules` can be revoked and thus users take back their withdrawable `ZVE` . However, the `revokeVestingSchedule()` function in `ZivoeRewardsVesting.sol` subtracts the whole `vestingAmount` from the `_totalSupply` which represents the amount of `vestZVE` without taking into consideration previously withdrawals by the user which also decrease the `_totalSupply`. To replay the issue, add this test case in `Test_ZivoeRewardsVesting.sol` :

```solidity
    function test_ZivoeRewardsVesting_incorrectTotalSupplyCalculation() public {
        // Amount of ZVE that will be vested.
        uint256 amount = 5000 ether;

        // Create two vesting schedules for pam and moe.
        assert(zvl.try_createVestingSchedule(
            address(vestZVE), 
            address(moe), 
            0, 
            (amount % 360 * 5 + 1),
            amount, 
            true
        ));
        assert(zvl.try_createVestingSchedule(
            address(vestZVE), 
            address(pam), 
            0, 
            (amount % 360 * 5 + 1),
            amount, 
            true
        ));

        // The totalSupply of vestZVE is 5000 vestZVE + 5000 vestZVE.
        assertEq(vestZVE.totalSupply(), 10000 ether);

        // Moe withdraws some vestZVE after one day.
        hevm.warp(block.timestamp + 86400 seconds);
        assert(moe.try_withdraw(address(vestZVE)));

        // Then vesting schedule of moe is revoked.
        assert(zvl.try_revokeVestingSchedule(address(vestZVE), address(moe)));

        // But now totalSupply is not 5000 vestZVE (pam's balance). Is less.
        assertNotEq(vestZVE.totalSupply(), 5000 ether);
    }
```

## Impact
This vulnerability can lead to various serious issues such as :
1. Wrong calculations of rewards earned by users since they are [dependent](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L321) on the `totalSupply` of `vestZVE`.
2. Protocol has a false representation of the `vestZVE` supply.
3. Revoke of a `vestingSchedule` that has `totalWithdrawn` greater than the sum of all other `vestingSchedules` `totalVestings` could not be possible, as this could lead to an underflow in `_totalSupply`. To understand this scenario better, add this test case in `Test_ZivoeRewardsVesting.sol` :

```solidity
    function test_ZivoeRewardsVesting_vestingScheduleCanNotRevoke() public {
        uint256 amountMoe = 500000 ether; // 500,000 $ZVE to be vested by Moe
        uint256 amountPam = 10 ether; // 10 $ZVE to be vested by Pam

        // Create vesting shedule for moe and pam.
        assert(zvl.try_createVestingSchedule(
            address(vestZVE), 
            address(moe), 
            0, 
            360,
            amountMoe, 
            true
        ));
        assert(zvl.try_createVestingSchedule(
            address(vestZVE), 
            address(pam), 
            0, 
            360,
            amountPam, 
            true
        ));

        // Moe withdraws some vestZVE after ten days.
        hevm.warp(block.timestamp +  10 days);
        assert(moe.try_withdraw(address(vestZVE)));

        // VestingSchedule of moe can not be revoked since her withdrawals are much more than the vested amount of pam and the totalSupply of vestZVE will underflow.
        // This action will revert.
        assert(zvl.try_revokeVestingSchedule(address(vestZVE), address(moe)));
    }   
```

## Code Snippet
Here is the vulnerable code snippet in `withdraw()`from `ZivoeRewardsVesting.sol`:
```solidity
    function withdraw() public nonReentrant updateReward(_msgSender()) {
        uint256 amount = amountWithdrawable(_msgSender());
        // ...
        
        vestingScheduleOf[_msgSender()].totalWithdrawn += amount;
        vestingTokenAllocated -= amount;

@>        _totalSupply = _totalSupply.sub(amount);
        // ...
    }
```
[Link to Code](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L508)

And here, it is the double subtraction in `revokeVestingSchedule()` from `ZivoeRewardsVesting.sol`:
```solidity
    function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
        // ...

        uint256 vestingAmount = vestingScheduleOf[account].totalVesting;

        // ...

@>        _totalSupply = _totalSupply.sub(vestingAmount);

        // ...
    }
```
[Link to Code](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L451)

## Tool used
Manual Review, Foundry, VS Code

## Recommendation
Consider not subtracting the whole `vestingScheduleOf[account].totalVesting` value from `_totalSupply` and take account of `vestingScheduleOf[account].totalWithdrawn` too :
```diff
-    _totalSupply = _totalSupply.sub(vestingAmount);
+    _totalSupply = _totalSupply.sub(vestingAmount - vestingScheduleOf[account].totalWithdrawn);
```
