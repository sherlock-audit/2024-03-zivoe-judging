Helpful Mercurial Finch

medium

# Revoking a vesting schedule underrepresents `_totalSupply` of vestZVE contract, causing the earnings to be unfairly distributed.

## Summary

Zivoe Labs can create a vesting schedule ([`ZivoeRewardsVesting::createVestingSchedule`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L381-L381)) for its stakeholders such as employees, advisors, and early contributors.

Later, the Zivoe Laboratory can revoke a vesting schedule from a vestZVE token holder using [`ZivoeRewardsVesting::revokeVestingSchedule`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429-L429).

When it does that, the function incorrectly subtracts more `_totalSupply` than it needs to under a case in which a revoked vesting schedule owner has called [`ZivoeRewardsVesting::withdraw`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L501) before, turning a portion of his vestZVE into ZVE.

Consequently, it will inflate the value of rewards deposited to stZVE holders relative to vestZVE holders when it comes to distributing the yield by calling [`ZivoeYDL::distributeYield`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeYDL.sol#L213-L213). This comes at the loss of rewards for vestZVE holders, while it is an undeserved and incorrect inflation of rewards for stZVE holders.

FYI, I am submitting this issue although the vulnerability originates from the same line as other issues I've submitted because it says in ['the best practices' section of Sherlock docs](https://docs.sherlock.xyz/audits/judging/judging#x.-best-practices):
> Do not submit multiple issues in a single submission. Even if the 2 completely different issues occur on the same line, please make sure to separately submit them.

## Vulnerability Detail

Let's assume that Alice and Bob are the only employees at Zivoe and the only vestZVE holders of 100 vestZVE and 40 vestZVE respectively. Therefore, `_totalSupply` starts at `140`.

As time goes by, Alice has withdrawn 40 vestZVE using [ZivoeRewardsVesting::withdraw](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L501). When she calls that, `_totalSupply` is subtracted by 40, and becomes `100` as it should be.

In the meantime, Bob doesn't do anything.

Then, she resigns from her position, and Zivoe Labs calls [`ZivoeRewardsVesting::revokeVestingSchedule`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429-L429) to remove her vesting schedule entirely.

For the sake of simplicity, let us assume that the block timestamp at which she has withdrawn here vestZVE for the last time is the same as the block timestamp that Zivoe Labs calls [`ZivoeRewardsVesting::revokeVestingSchedule`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429-L429). Also assume that she withdrew first, and Zivoe Labs calls `revokeVestingSchedule` later in the same block. Note that these assumptions do not skew the result of the report.

```solidity
/// @notice Ends vesting schedule for a given account (if revokable).
/// @param  account The acount to revoke a vesting schedule for.
function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
    require(
        vestingScheduleSet[account], 
        "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleSet[account]"
    );
    require(
        vestingScheduleOf[account].revokable, 
        "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleOf[account].revokable"
    );
    
    // before any variable changes in the function, this is the initial state of the following variables:
    // _totalSupply = all users currently vested amount = (100 + 40 - 40) = 100
    // vestingScheduleOf[account].totalWithdrawn = 40 (60 remaining)

    // amount = 0 (assume that this is 0, it does not affect the result of the report if it were higher number)
    uint256 amount = amountWithdrawable(account);
    // vestingAmount = 100 (original vesting amount)
    uint256 vestingAmount = vestingScheduleOf[account].totalVesting;

    vestingTokenAllocated -= amount;

    vestingScheduleOf[account].totalWithdrawn += amount;
    vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
    vestingScheduleOf[account].cliff = block.timestamp - 1;
    vestingScheduleOf[account].end = block.timestamp;

    vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);

    // _totalSupply = 100 - 100 = 0, which is wrong, because it should be 40 (Bob still holds his 40 vestZVE, but _totalSupply says it's the total is 0!!)
    _totalSupply = _totalSupply.sub(vestingAmount);
    // Also wrong (covered in another issue)
    _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);        
    _writeCheckpoint(_checkpoints[account], _subtract, amount);
    _balances[account] = 0;
    stakingToken.safeTransfer(account, amount);

    vestingScheduleOf[account].revokable = false;

    emit VestingScheduleRevoked(
        account, 
        vestingAmount - vestingScheduleOf[account].totalWithdrawn, 
        vestingScheduleOf[account].cliff, 
        vestingScheduleOf[account].end, 
        vestingScheduleOf[account].totalVesting, 
        false
    );
}
```

The comments in the code block thoroughly walk you through each step. Here's a recap:
1. we start with `_totalSupply` of 100 because Alice has withdrawn 40 already
2. we start with `vestingAmount` of 100 because that is the original amount Alice was vested with
3. without these variables being modified in the middle of the function, we call `_totalSupply = _totalSupply.sub(vestingAmount)`. This results in `_totalSupply` becoming zero, because this code doesn't consider the previous withdrawal that Alice made.
4. `_totalSupply` is now underrepresented, because it should be 40 as Bob still holds his 40 vestZVE, but `_totalSupply` is 0.

Later on, in [`ZivoeYDL::distributeYield`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeYDL.sol#L250-L263), earnings are calculated and rewards/residuals are deposited to relevant contracts:

```solidity
uint256 splitBIPS = (
    IERC20(IZivoeGlobals_YDL(GBL).stZVE()).totalSupply() * BIPS
) / (
    IERC20(IZivoeGlobals_YDL(GBL).stZVE()).totalSupply() + 
    IERC20(IZivoeGlobals_YDL(GBL).vestZVE()).totalSupply()
);
uint stZVEAllocation = _protocol[i] * splitBIPS / BIPS;
uint vestZVEAllocation = _protocol[i] * (BIPS - splitBIPS) / BIPS;
IERC20(distributedAsset).safeIncreaseAllowance(IZivoeGlobals_YDL(GBL).stZVE(), stZVEAllocation);
IERC20(distributedAsset).safeIncreaseAllowance(IZivoeGlobals_YDL(GBL).vestZVE(),vestZVEAllocation);
IZivoeRewards_YDL(IZivoeGlobals_YDL(GBL).stZVE()).depositReward(distributedAsset, stZVEAllocation);
IZivoeRewards_YDL(IZivoeGlobals_YDL(GBL).vestZVE()).depositReward(distributedAsset, vestZVEAllocation);
```

The proportion to `depositReward` to stZVE and vestZVE contracts are determined by `splitBIPS`. 
- `splitBIPS / BIPS` is the proportion of the earnings that is deposited to `stZVE` contract.
- `(BIPS - splitBIPS) / BIPS` is the proportion of the earnings deposited to `vestZVE` contract. 
 
`splitBIPS` is determined by `IERC20(IZivoeGlobals_YDL(GBL).stZVE()).totalSupply()` and `IERC20(IZivoeGlobals_YDL(GBL).vestZVE()).totalSupply()`.
- If `IERC20(IZivoeGlobals_YDL(GBL).vestZVE()).totalSupply()` is relatively higher, `splitBIPS` will be relatively lower. This means proportionally more earnings are deposited to vestZVE contract and less earnings to stZVE contract.
- If `IERC20(IZivoeGlobals_YDL(GBL).vestZVE()).totalSupply()` is relatively lower, `splitBIPS` will be relatively higher. This means proportionally less earnings are deposited to vestZVE contract and more earnings to stZVE contract.
This should easily make sense, because the earnings should be forwarded to where there is more of staked/vested asset.

For example, if we have:
-  `IERC20(IZivoeGlobals_YDL(GBL).stZVE()).totalSupply() = 100`
-  `IERC20(IZivoeGlobals_YDL(GBL).vestZVE()).totalSupply() = 400`
Then, `splitBIPS` is 20%. This means: 
- 20% of `_protocol[i]` (a variable that represents protocol earnings) will be deposited to stZVE contract.
- 80% will be deposited to vestZVE contract.
This makes sense and this is all fair.

Now, assume that Charles is the only holder stZVE and has 100 stZVE. So Bob is expecting to receive `40/(100 + 40)` â‰ˆ 28.57% of the protocol earnings, which turns out to be 100 USDC.

But because `IERC20(IZivoeGlobals_YDL(GBL).vestZVE()).totalSupply()` is in fact underrepresented as `0`, 100% of the protocol earnings are forwarded to stZVE contract, inflating the rewards that Charles can get, and losing the rewards that Bob can get. In this extreme example, Bob can get zero rewards for his vestZVE holding.

The exact same thing happens at https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeYDL.sol#L289-L294 where residual is distributed. But since this is the same code and vulnerability anyway, we're not going to cover this one.

## Impact

The will inflate the value of rewards deposited to stZVE holders relative to vestZVE holders when it comes to distributing the yield by calling [`ZivoeYDL::distributeYield`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeYDL.sol#L213-L213). This comes at the loss of rewards for vestZVE holders, while it is an undeserved and incorrect inflation of rewards for stZVE holders. 

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L451-L451

## Tool used

Manual Review

## Recommendation

The code should correctly reflect the amount already withdrawn by the account whose vesting schedule is being revoked.