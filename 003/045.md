Elegant Silver Scorpion

high

# User funds scheduled for vesting can get locked due to incorrectly decrementing the total supply in `ZivoeRewardsVesting`

## Summary
In the `ZivoeRewardsVesting` contract, there are functionalities to create a vesting schedule, withdraw your funds from the vesting schedule and revoke the vesting schedule if the vesting schedule is set to be revokable. If a user withdraws his funds from the vesting schedule, the `totalSupply` will decrease. Then, if the vesting schedule is revoked, the `totalSupply` will decrease again. This causes a few possible issues. The least dangerous one is that `revokeVestingSchedule()` will revert if the `totalSupply` is less than the `vestingScheduleOf[account].totalVesting`. An issue that is a lot more dangerous is the fact that if another user tries to withdraw his funds after another user has withdrawn his and then his vesting schedule was revoked, the `withdraw()` function would just revert on him as long as the `totalSupply` is lower than the amount that is withdrawable by him.

## Vulnerability Detail
This is the `ZivoeRewardsVesting::withdraw()` function: 
```solidity
function withdraw() public nonReentrant updateReward(_msgSender()) {
        uint256 amount = amountWithdrawable(_msgSender());
        require(amount > 0, "ZivoeRewardsVesting::withdraw() amountWithdrawable(_msgSender()) == 0");
        
        vestingScheduleOf[_msgSender()].totalWithdrawn += amount;
        vestingTokenAllocated -= amount;

        _totalSupply = _totalSupply.sub(amount);
        _writeCheckpoint(_totalSupplyCheckpoints, _subtract, amount);
        _writeCheckpoint(_checkpoints[_msgSender()], _subtract, amount);
        _balances[_msgSender()] = _balances[_msgSender()].sub(amount);
        stakingToken.safeTransfer(_msgSender(), amount);

        emit Withdrawn(_msgSender(), amount);
    }
```
Whenever a user calls this function, it calculates the amount he is able to withdraw using the `ZivoeRewardsVesting::amountWithdrawable()` function. Then, `_totalSupply` will be decremented by that amount. Let's assume that this function was called after the whole vesting period is over for simplicity purposes (however, the vulnerability is still possible at any point during the vesting period). That means that the user withdrew all of the funds that were supposed to be vested to him.

Then, this is a small part of the `ZivoeRewardsVesting::revokeVestingSchedule()` function:
```solidity
        uint256 amount = amountWithdrawable(account);
        uint256 vestingAmount = vestingScheduleOf[account].totalVesting;

        vestingTokenAllocated -= amount;

        vestingScheduleOf[account].totalWithdrawn += amount;
        vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
        vestingScheduleOf[account].cliff = block.timestamp - 1;
        vestingScheduleOf[account].end = block.timestamp;

        vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);

        _totalSupply = _totalSupply.sub(vestingAmount);
```

The `amount` variable will be 0 as the user already withdrew his available funds. The `vestingAmount` however will be the total amount a user has to receive in the `vestingSchedule`. He already withdrew that using the `ZivoeRewardsVesting::withdraw()` function. There are some calculations which are not as dangerous and then, on the last line of the snippet I provided above, the `_totalSupply` is decremented by the `vestingAmount`. That means that the `_totalSupply` was decremented by the total amount a user received in the `vestingSchedule` twice, once in the  `ZivoeRewardsVesting::withdraw()` function and once here.

Then, whenever another user decides to withdraw his funds, the `_totalSupply` will be inaccurate and it could easily lead to a revert and the user's funds being locked in the contract until `_totalSupply` is increased again but then another user will not be able to withdraw his funds until `_totalSupply` is increased leading to a never ending cycle of user having their funds locked.
## Impact
Users can have their funds locked and not be able to withdraw.

## Proof Of Concept
Paste the following code in the `Test_ZivoeRewardsVesting.sol` file:
```solidity
function testFundsGetLocked() public {
        IERC20 stakingToken = vestZVE.stakingToken();

        vm.startPrank(address(zvl));
        vestZVE.createVestingSchedule(address(poe), 30, 90, 100 ether, true);
        vestZVE.createVestingSchedule(address(moe), 30, 90, 100 ether, true);
        vm.stopPrank();

        uint256 totalSupplyAfterVestingScheduleCreated = vestZVE.totalSupply();
        assertEq(200000000000000000000, totalSupplyAfterVestingScheduleCreated);

        vm.warp(block.timestamp + 90 * 1 days);

        uint256 userPoeStakingTokenBalanceBefore = stakingToken.balanceOf(address(poe));

        vm.prank(address(poe));
        vestZVE.withdraw();

        uint256 userPoeStakingTokenBalanceAfter = stakingToken.balanceOf(address(poe));
        uint256 totalSupplyAfterWithdraw = vestZVE.totalSupply();

        assertEq(userPoeStakingTokenBalanceBefore, 0);
        assertEq(userPoeStakingTokenBalanceAfter, 100000000000000000000);
        assertEq(totalSupplyAfterWithdraw, 100000000000000000000);

        vm.prank(address(zvl));
        vestZVE.revokeVestingSchedule(address(poe));

        uint256 totalSupplyAfterRevoke = vestZVE.totalSupply();
        assertEq(totalSupplyAfterRevoke, 0);

        vm.prank(address(moe));
        vm.expectRevert();
        vestZVE.withdraw();
    }
```


## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L501-L515
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L439-L451

## Tool used

Manual Review

## Recommendation
Have a variable `oldTotalWithdrawn` capturing the total withdrawn value before the update below:
```solidity
vestingScheduleOf[account].totalWithdrawn += amount;
```
Then, use it as following:
```diff
+ uint256 oldTotalWithdrawn = vestingScheduleOf[account].totalWithdrawn;
...
vestingScheduleOf[account].totalWithdrawn += amount;
...
- _totalSupply = _totalSupply.sub(vestingAmount);
+ _totalSupply = _totalSupply.sub(oldTotalWithdrawn);
```
The proposed recommendation is according to what I believe the developers had in mind coding this. In case I interpreted it wrong, the proposed recommendation might not suit them exactly however I believe it does.