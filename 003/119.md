Petite Velvet Duck

high

# totalSupply is incorrectly calculated during revokeVestingSchedule()

## Summary
If a user has already withdrawn some amount during the vesting period, then upon calling `revokeVestingSchedule()` the `_totalSupply` is decreased to a greater extent than it should be. This can even lead to a revert due to underflow and the user's vesting schedule can't be revoked.

## Vulnerability Detail
- Imagine a vesting schedule is created at timestamp `t` for Alice and the `amountToVest` she receives is `100`. Hence `vestingScheduleOf[alice].totalVesting = 100`.
- Assuming Alice to be the only user, we can see that `_totalSupply = 100`.
- These are the vesting params:
  - cliff = 10 days
  - total vesting duration = 40 days
- At `t + 10 days` when cliff is reached, she calls [withdraw()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L501) to withdraw 25 staking tokens which is her `amountWithdrawable()`.
  - Hence `_totalSupply = _totalSupply - 25 = 75` on [L508](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L508)
- ZVL or ITO calls [revokeVestingSchedule()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429) at `t + 30`.
  - `amountWithdrawable()` is called internally on [L439](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L439) to determine that `amount = 50` is what she can withdraw.
  - `vestingAmount` is set to equal `vestingScheduleOf[alice].totalVesting` or `100` on [L440](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L440).
  - On [L451](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L451) and L452, even though the `_totalSupply` which remains to be deducted is only `75`, the full `vestingAmount` of `100` is attempted to be deducted. This causes a revert due to underflow.
  - Had there been other users in the system, the revert wouldn't have happened. However the `_totalSupply` would have been decreased by `100` instead of `75` and the sum of the balances of all users would have been greater than the `_totalSupply`. The `_writeCheckpoint()` on L452 would have saved this reduced figure of `_totalSupply`.

## Impact
1. Breaks the invariant that the sum of balances of all users should be equal to `_totalSupply`.
2. As the following PoC shows, it can lead to situations where it's simply not possible to `revokeVestingSchedule()` as it reverts with a panic code (underflow error).
3. A reduced `_totalSupply` means that now the quorum fraction required will be calculated incorrectly. Each existing vote will have more proportional value (power) than before. See the [constructor](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeGovernorV2.sol#L62-L64) of `ZivoeGovernerV2.sol` which internally calls `GovernorVotesQuorumFraction(10)` where you can find [the comment](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/4e5b11919e91b18b6683b6f49a1b4fdede579969/contracts/governance/extensions/GovernorVotesQuorumFraction.sol#L24-L33):
```js
    /**
     * @dev Initialize quorum as a fraction of the token's total supply.
     *
     * The fraction is specified as `numerator / denominator`. By default the denominator is 100, so quorum is
     * specified as a percent: a numerator of 10 corresponds to quorum being 10% of total supply. The denominator can be
     * customized by overriding {quorumDenominator}.
     */
    constructor(uint256 quorumNumeratorValue) {
        _updateQuorumNumerator(quorumNumeratorValue);
    }
```

## Code Snippet
**Proof of Concept:** Add the following test inside `zivoe-core-testing/src/TESTS_Core/Test_ZivoeRewardsVesting.sol` and run via `forge test --rpc-url https://rpc.ankr.com/eth --mt test_t0x1c_bug_revokeVestingSchedule -vvvv` to see it revert with `panic: arithmetic underflow or overflow (0x11)`:
```js
    function test_t0x1c_bug_revokeVestingSchedule() public {
        uint256 t = block.timestamp;
        uint256 PRECISION_SAVER = 1 days;

        uint256 amount = 100 ether * PRECISION_SAVER; // multiplied with PRECISION_SAVER or `1 days` for ease of calculation, so that `vestingPerSecond` calculates as a non-rounded, exact whole number

        assert(zvl.try_createVestingSchedule(
            address(vestZVE), 
            address(moe), 
            10, 
            40,
            amount, 
            true
        ));

        // Pre-state.
        (
            uint256 start, 
            uint256 cliff, 
            uint256 end, 
            uint256 totalVesting, 
            uint256 totalWithdrawn, 
            uint256 vestingPerSecond,
        ) = vestZVE.viewSchedule(address(moe));

        assertEq(start, t);
        assertEq(cliff, t + 10 days);
        assertEq(end, t + 40 days);
        assertEq(totalVesting, amount);
        assertEq(totalWithdrawn, 0);
        assertEq(vestingPerSecond, amount / (40 days));
        assertEq(vestZVE.balanceOf(address(moe)), amount);
        assertEq(vestZVE.totalSupply(), amount);
        assertEq(ZVE.balanceOf(address(moe)), 0);

        hevm.warp(t + 10 days); // reached cliff (25% of vesting duration)

        uint256 amountWithdrawable = vestZVE.amountWithdrawable(address(moe));
        assertEq(amountWithdrawable, 25 ether * PRECISION_SAVER);  // 25% of `amount`
        assert(moe.try_withdraw(address(vestZVE)));
        assertEq(vestZVE.totalSupply(), 75 ether * PRECISION_SAVER); // 75% of `amount`
        assertEq(ZVE.balanceOf(address(moe)), 25 ether * PRECISION_SAVER);

        hevm.warp(t + 30 days); // reached 75% of vesting duration

        amountWithdrawable = vestZVE.amountWithdrawable(address(moe));
        assertEq(amountWithdrawable, 50 ether * PRECISION_SAVER);  // 50% of `amount` since 25% has already been withdrawn
        assert(zvl.try_revokeVestingSchedule(address(vestZVE), address(moe))); // @audit-issue : will revert here with `panic` due to underflow 
    }
```

<br>

Output:
```text

    ...
    ...
    ├─ [16906] Admin::try_revokeVestingSchedule(ZivoeRewardsVesting: [0x8227724C33C1748A42d1C1cD06e21AB8Deb6eB0A], Vester: [0x13aa49bAc059d709dd0a18D6bb63290076a702D7])
    │   ├─ [15275] ZivoeRewardsVesting::revokeVestingSchedule(Vester: [0x13aa49bAc059d709dd0a18D6bb63290076a702D7])
    │   │   ├─ [373] ZivoeGlobals::ZVL() [staticcall]
    │   │   │   └─ ← Admin: [0x2e234DAe75C793f67A35089C9d99245E1C58470b]
    │   │   └─ ← panic: arithmetic underflow or overflow (0x11)
    │   └─ ← false

```

## Tool used
Foundry

## Recommendation
The accounting can be corrected in the following manner:
```diff
    function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
        require(
            vestingScheduleSet[account], 
            "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleSet[account]"
        );
        require(
            vestingScheduleOf[account].revokable, 
            "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleOf[account].revokable"
        );
        
        uint256 amount = amountWithdrawable(account);
        uint256 vestingAmount = vestingScheduleOf[account].totalVesting;

        vestingTokenAllocated -= amount;
+       uint256 totalWithdrawnBeforeRevoke = vestingScheduleOf[account].totalWithdrawn;
        vestingScheduleOf[account].totalWithdrawn += amount;
        vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
        vestingScheduleOf[account].cliff = block.timestamp - 1;
        vestingScheduleOf[account].end = block.timestamp;

        vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);

-       _totalSupply = _totalSupply.sub(vestingAmount);
-       _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
+       _totalSupply = _totalSupply.sub(vestingAmount - totalWithdrawnBeforeRevoke);
+       _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount - totalWithdrawnBeforeRevoke);
        _writeCheckpoint(_checkpoints[account], _subtract, amount);
        _balances[account] = 0;
        stakingToken.safeTransfer(account, amount);

        vestingScheduleOf[account].revokable = false;

        emit VestingScheduleRevoked(
            account, 
            vestingAmount - vestingScheduleOf[account].totalWithdrawn, 
            vestingScheduleOf[account].cliff, 
            vestingScheduleOf[account].end, 
            vestingScheduleOf[account].totalVesting, 
            false
        );
    }
```