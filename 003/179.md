Helpful Mercurial Finch

medium

# Revoked vesting schedule owner still can `castVote` to affect voting result

## Summary
$vestZVE is one of the three tokens ($ZVE, $stZVE, $vestZVE) used for voting for governance proposals as per [`_getVotes` implementation in ZivoeGovernorV2](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeGovernorV2.sol#L144-L152). 

An `account`'s balance of $vestZVE is incremented by creating a vesting schedule ([`ZivoeRewardsVesting::createVestingSchedule`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L381-L381)). Later, the Zivoe Laboratory and `ZivoeITO` contract have an ability to revoke a vesting schedule from a $vestZVE token holder using [`ZivoeRewardsVesting::revokeVestingSchedule`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429-L429). 

It has been found that even after being revoked of a vesting schedule, an `account` still has an access to its vote originating from its past $vestZVE holding and can exercise its voting power to alter governance voting result.

## Vulnerability Detail

First, let us understand that any vesting schedules created by Zivoe protocol are linearly vested every second. This means that any $vestZVE holders can come anytime after the cliff period and claim a certain amount of vested $vestZVE.

At any point, if the Zivoe Laboratory needs to revoke a vesting schedule (for example, due to an employee quitting his job at Zivoe), it will call `revokeVestingSchedule`. 

The expected behavior of this function is that it:
- runs accounting of rewards for the `account` for the last time by calling `updateReward(account)`
- withdraws all withdrawable $ZVE and sends it to the user by calling `stakingToken.safeTransfer(account, amount);`
- modifies the storage variables related to the corresponding vesting schedule so that the `account` loses all of its future vested amount, and its ability to vote with $vestZVE (it can still vote with any withdrawn amount of ZVE).

However, the last step isn't carried out correctly by saving a wrong `checkpoint` of the `account`'s $vestZVE balance. Let's illustrate it with an example:

Alice and Bob are the employees at Zivoe Laboratory and have a vesting schedule respectively. Alice has a vested amount of 100 ZVE, and Bob has a vested amount of 60 ZVE. For the sake of simplicity, we assume that these two are the only vesting schedules that exist on Zivoe protocol.

Alice and Bob do not withdraw any vested ZVE for the time being.

After some time, Bob still hans't withdrawn any vested ZVE. Then, Alice decides to resign from Zivoe Labs, prompting Zivoe Labs to call `revokeVestingSchedule`. Alice is able to withdraw 10 ZVE because some time has passed upon revocation:

```solidity
/// @notice Ends vesting schedule for a given account (if revokable).
/// @param  account The acount to revoke a vesting schedule for.
function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
    require(
        vestingScheduleSet[account], 
        "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleSet[account]"
    );
    require(
        vestingScheduleOf[account].revokable, 
        "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleOf[account].revokable"
    );
    
    // before any variable changes in the function, this is the initial state of the following variables:
    // _totalSupply = all users vested amount = 160 (only 2 users = 100 + 60)
    // vestingScheduleOf[account].totalWithdrawn = 0 (100 remaining)

    // amount = 10
    uint256 amount = amountWithdrawable(account);
    // vestingAmount = 100
    uint256 vestingAmount = vestingScheduleOf[account].totalVesting;

    vestingTokenAllocated -= amount;

    // vestingScheduleOf[account].totalWithdrawn = 0 + 10 = 10
    vestingScheduleOf[account].totalWithdrawn += amount;
    // vestingScheduleOf[account].totalVesting = 10
    vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
    vestingScheduleOf[account].cliff = block.timestamp - 1;
    vestingScheduleOf[account].end = block.timestamp;

    vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);

    // _totalSupply = 160 - 100 = 60
    _totalSupply = _totalSupply.sub(vestingAmount);
    // The latest total supply checkpoint would have 60 votes
    _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);        
    // Wrong!! 
    _writeCheckpoint(_checkpoints[account], _subtract, amount);
    _balances[account] = 0;
    stakingToken.safeTransfer(account, amount);

    vestingScheduleOf[account].revokable = false;

    emit VestingScheduleRevoked(
        account, 
        vestingAmount - vestingScheduleOf[account].totalWithdrawn, 
        vestingScheduleOf[account].cliff, 
        vestingScheduleOf[account].end, 
        vestingScheduleOf[account].totalVesting, 
        false
    );
}
```

The problem with the code is the following line:

```solidity
    // Wrong!!
    _writeCheckpoint(_checkpoints[account], _subtract, amount);
```

The checkpoint is used to keep track of the history of Alice's balance of $vestZVE. When the vesting schedule is revoked, Alice no longer has any $vestZVE. However, this line of code allows Alice to keep 90 vestZVE has her votes, because `amount = 10` and she saved the latest checkpoint with 100 votes when her vesting schedule was created.

Using her 90 vestZVE left in the checkpoint, she can now [castVote](https://docs.openzeppelin.com/contracts/4.x/api/governance#IGovernor-castVote-uint256-uint8-) for a proposal, although she in fact has 0 vestZVE left, potentially affecting the result of the proposal.

## Impact

Revoked vesting schedule owner still can cast vote to affect governance voting result. The only action that needs to be taken by him, after being revoked by Zivoe Labs, is to `castVote`.

It is possible that a revoked vesting schedule owner had a lot of vestZVE because [30% of total supply of ZVE is allocated to team & advisors](https://docs.zivoe.com/user-docs/zivoe-token-zve). This suggests that someone who has nothing to do with the protocol anymore can alter the voting result significantly.

This breaks the core contract functionality of `ZivoeGovernorV2`.

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L453

## Tool used

Manual Review

## Recommendation

Revoking a vesting schedule should subtract the checkpoint by the total amount that is being removed from vesting.