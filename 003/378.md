Great Metal Ram

medium

# `revokeVestingSchedule` incorrectly update total supply and checkpoints

## Summary

Currently, `ZVL` can revoke the created vesting schedule by calling `revokeVestingSchedule`. However, this action will incorrectly update the total supply and checkpoints inside the Vesting contract, potentially leading to issues such as incorrect reward distribution and users failing to withdraw from the vesting contract.

## Vulnerability Detail

When `revokeVestingSchedule` is called, it can be observed that it will decrease the total supply and total supply checkpoints by the `vestingAmount` :

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429-L467

```solidity
    function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
        require(
            vestingScheduleSet[account], 
            "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleSet[account]"
        );
        require(
            vestingScheduleOf[account].revokable, 
            "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleOf[account].revokable"
        );
        
        uint256 amount = amountWithdrawable(account);
        uint256 vestingAmount = vestingScheduleOf[account].totalVesting;

        vestingTokenAllocated -= amount;

        vestingScheduleOf[account].totalWithdrawn += amount;
        vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
        vestingScheduleOf[account].cliff = block.timestamp - 1;
        vestingScheduleOf[account].end = block.timestamp;

        vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);

>>>     _totalSupply = _totalSupply.sub(vestingAmount);
>>>     _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
        _writeCheckpoint(_checkpoints[account], _subtract, amount);
        _balances[account] = 0;
        stakingToken.safeTransfer(account, amount);

        vestingScheduleOf[account].revokable = false;

        emit VestingScheduleRevoked(
            account, 
            vestingAmount - vestingScheduleOf[account].totalWithdrawn, 
            vestingScheduleOf[account].cliff, 
            vestingScheduleOf[account].end, 
            vestingScheduleOf[account].totalVesting, 
            false
        );
    }
```

This is incorrect because, if users have previously withdrawn the vested tokens by calling `withdraw`, the remaining tokens for users are actually `vestingAmount - vestingScheduleOf[account].totalWithdrawn`.

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L501-L515

```solidity
    /// @notice Withdraws the available amount of stakingToken from this contract.
    function withdraw() public nonReentrant updateReward(_msgSender()) {
        uint256 amount = amountWithdrawable(_msgSender());
        require(amount > 0, "ZivoeRewardsVesting::withdraw() amountWithdrawable(_msgSender()) == 0");
        
        vestingScheduleOf[_msgSender()].totalWithdrawn += amount;
        vestingTokenAllocated -= amount;

>>>     _totalSupply = _totalSupply.sub(amount);
>>>     _writeCheckpoint(_totalSupplyCheckpoints, _subtract, amount);
        _writeCheckpoint(_checkpoints[_msgSender()], _subtract, amount);
        _balances[_msgSender()] = _balances[_msgSender()].sub(amount);
        stakingToken.safeTransfer(_msgSender(), amount);

        emit Withdrawn(_msgSender(), amount);
    }
```

It can be observed that when `withdraw` is called, it will decrease total supply and total supply checkpoints by vested `amount`.

## Impact

This issue can impact reward distribution, as the total supply will be decreased more than it should be, causing `rewardPerTokenStored` to be incorrectly updated. Moreover, the more severe impact is that, due to the total supply being decreased more than it should be, some of the last users may be unable to withdraw from the vesting contract due to a revert when decreasing total supply and checkpoints.

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429-L467
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L501-L515

## Tool used

Manual Review

## Recommendation

Update the total supply and total supply checkpoints by decreasing them by `amount + (vestingAmount - vestingScheduleOf[account].totalWithdrawn)`.

```diff
    function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
        // ...

        vestingTokenAllocated -= amount;

        vestingScheduleOf[account].totalWithdrawn += amount;
        vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
        vestingScheduleOf[account].cliff = block.timestamp - 1;
        vestingScheduleOf[account].end = block.timestamp;

        vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);

-       _totalSupply = _totalSupply.sub(vestingAmount);
+       _totalSupply = _totalSupply.sub(amount + (vestingAmount - vestingScheduleOf[account].totalWithdrawn));
-       _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
+       _writeCheckpoint(_totalSupplyCheckpoints, _subtract, amount + (vestingAmount - vestingScheduleOf[account].totalWithdrawn));
        _writeCheckpoint(_checkpoints[account], _subtract, amount);
        _balances[account] = 0;
        stakingToken.safeTransfer(account, amount);
        
        // ...
    }
```
