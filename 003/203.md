Elegant Silver Scorpion

high

# Incorrect logic in the `ZivoeRewardsVesting.sol` contract can cause inaccuracies in the voting weight

## Summary
Incorrect logic in the `ZivoeRewardsVesting.sol` contract can cause inaccuracies in the voting weight. The `ZivoeRewardsVesting::revokeVestingSchedule()` uses the `vestingAmount` variable which is equal to the total vesting amount of a certain vesting schedule. However, that causes issues if the user has withdrawn money from his vesting schedule before the revocation of his vesting schedule.

## Vulnerability Detail
There is a functionality to revoke a vesting schedule of a particular user using `ZivoeRewardsVesting::revokeVestingSchedule()` if that vesting schedule is set to be revokable upon creation. It gives all of the funds that are withdrawable by the user at the moment of calling of that function and revokes the vesting disallowing the user to claim any other funds afterwards.
```solidity
function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
        require(
            vestingScheduleSet[account], 
            "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleSet[account]"
        );
        require(
            vestingScheduleOf[account].revokable, 
            "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleOf[account].revokable"
        );
        
        uint256 amount = amountWithdrawable(account);
        uint256 vestingAmount = vestingScheduleOf[account].totalVesting;

        vestingTokenAllocated -= amount;

        vestingScheduleOf[account].totalWithdrawn += amount;
        vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
        vestingScheduleOf[account].cliff = block.timestamp - 1;
        vestingScheduleOf[account].end = block.timestamp;

        vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);
        _totalSupply = _totalSupply.sub(vestingAmount);
        _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
        _writeCheckpoint(_checkpoints[account], _subtract, amount);
        _balances[account] = 0;
        stakingToken.safeTransfer(account, amount);

        vestingScheduleOf[account].revokable = false;

        emit VestingScheduleRevoked(
            account, 
            vestingAmount - vestingScheduleOf[account].totalWithdrawn, 
            vestingScheduleOf[account].cliff, 
            vestingScheduleOf[account].end, 
            vestingScheduleOf[account].totalVesting, 
            false
        );
    }
```
The issue arises on this line of the function:
```solidity
_writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
```
The function uses the `vestingAmount` variable to create the a new votes checkpoint.
The `vestingAmount` is equal to the total amount that can be vested by the user (imagine he had a vesting schedule with 1000 tokens to vest, he withdraws 500, the total amount that can be vested by that user would still be 1000).
```solidity
uint256 vestingAmount = vestingScheduleOf[account].totalVesting;
```
However, imagine the user withdrew some of his available to withdraw funds before the calling of that function using the `ZivoeRewardsVesting::withdraw()` function:
```solidity
function withdraw() public nonReentrant updateReward(_msgSender()) {
        uint256 amount = amountWithdrawable(_msgSender());
        require(amount > 0, "ZivoeRewardsVesting::withdraw() amountWithdrawable(_msgSender()) == 0");
        
        vestingScheduleOf[_msgSender()].totalWithdrawn += amount;
        vestingTokenAllocated -= amount;

        _totalSupply = _totalSupply.sub(amount);
        _writeCheckpoint(_totalSupplyCheckpoints, _subtract, amount);
        _writeCheckpoint(_checkpoints[_msgSender()], _subtract, amount);
        _balances[_msgSender()] = _balances[_msgSender()].sub(amount);
        stakingToken.safeTransfer(_msgSender(), amount);

        emit Withdrawn(_msgSender(), amount);
    }
```
For simplicity reasons, let's imagine that the time to vest passes and the user withdrew all of his funds and that his vesting schedule was revoked after it was over and he withdrew his funds. However, of course, the vulnerability can arise if he withdrew half of his funds and was revoked afterwards, the example given will be made like that for simplicity reasons as I mentioned.

1. User withdraws all of his funds (equal to the total vest amount) and there is a checkpoint created subtracting the amount he withdrew from the old weight/votes
2. His vesting schedule gets revoked and there is a new checkpoint created subtracting the total vest amount for him from the weight/votes

This causes issues as in this case, the weight decreases by the total vest amount twice. In a more realistic scenario where the revocation happens during the vesting period, the weight would firstly decrease by the amount withdrawn by the user (let's say 50% of his total vesting amount) and would then decrease by the total vesting amount causing serious inaccuracies with a very important part of the governance mechanism in the protocol. Of course, this could also unexpectedly revert if the amount being subtracted from the old weight is higher than the old weight itself.
## Impact
Serious inaccuracy with the votes/weight accounted in the protocol's state
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429-L467
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L501-L515
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/libraries/ZivoeVotes.sol#L110-L127
## Tool used

Manual Review

## Recommendation
Have a variable `oldTotalWithdrawn` capturing the total withdrawn value before the update below:
```solidity
vestingScheduleOf[account].totalWithdrawn += amount;
```
Then, use it as following:
```diff
+ uint256 oldTotalWithdrawn = vestingScheduleOf[account].totalWithdrawn;
...
vestingScheduleOf[account].totalWithdrawn += amount;
...
+ _writeCheckpoint(_totalSupplyCheckpoints, _subtract, oldTotalWithdrawn);
- _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
```
The proposed recommendation is according to what I believe the developers had in mind coding this. In case I interpreted it wrong, the proposed recommendation might not suit them exactly however I believe it does.