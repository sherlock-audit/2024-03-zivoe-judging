Best Lava Dragon

high

# ZVE tokens will get stuck in ZivoeRewardsVesting due to an underflow.

## Summary
User's ZVE tokens will get stuck in ZivoeRewardsVesting due to an underflow without a way to rescue them.
## Vulnerability Detail
When a vesting schedule is created for a user, his vesting ZVE tokens are automatically stake in order to earn additional rewards.
[ZivoeRewardsVesting.sol#L424](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L424)
```solidity
_stake(amountToVest, account);
```
When tokens are staked, the amount is added to `_totalSupply` and `_balances` variables.
[ZivoeRewardsVesting.sol#L473-L480](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L473-L480)
```solidity
function _stake(
    uint256 amount,
    address account
) private nonReentrant updateReward(account) {
    require(amount > 0, "ZivoeRewardsVesting::_stake() amount == 0");
    _totalSupply = _totalSupply.add(amount);
    ....
    _balances[account] = _balances[account].add(amount);
    emit Staked(account, amount);
}
```
If a user calls `withdraw()` to withdraw part of the ZVE that has already been vested, the amount withdrawn will be deducted from the `_totalSupply`.
[ZivoeRewardsVesting.sol#L501-L515](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L501-L515)
```solidity
function withdraw() public nonReentrant updateReward(_msgSender()) {
    uint256 amount = amountWithdrawable(_msgSender());
    require(
        amount > 0,
        "ZivoeRewardsVesting::withdraw() amountWithdrawable(_msgSender()) == 0"
    );
    vestingScheduleOf[_msgSender()].totalWithdrawn += amount;
    vestingTokenAllocated -= amount;
    _totalSupply = _totalSupply.sub(amount);
    ....
```
Now if after a user has withdrawn part of his ZVE, `revokeVestingSchedule()` is called, it will subtract `vestingScheduleOf[account].totalVesting` (which includes ZVE already withdrawn) from `_totalSupply`, essentially substracting the amount of withdrawn ZVE the second time.
[ZivoeRewardsVesting.sol#L429-L467](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429-L467)
```solidity
uint256 vestingAmount = vestingScheduleOf[account].totalVesting;
_totalSupply = _totalSupply.sub(vestingAmount);
```
This will lock in the contract part of the ZVE withdrawn via a call to `withdraw()`, since in the end `_totalSupply` will be less than all of the vesting ZVE by the amount withdrawn. Potentially the last user to withdraw his ZVE, won't be able to withdraw his full balance of ZVE at all.
## Impact
Locked funds in the contract.

## Proof Of Concept
Please add this test to Test_ZivoeRewardsVesting.sol.

```solidity
function test_ZivoeRewardsVesting_totalSupply_underflow() public {
    address userRevoked = makeAddr("userRevoked");
    address user = makeAddr("user");
    // Vesting schedules created for two users both vesting 100 ZVE
    vm.startPrank(address(zvl));
    vestZVE.createVestingSchedule(userRevoked, 0, 100, 100e18, true);
    vestZVE.createVestingSchedule(user, 0, 100, 100e18, true);
    vm.stopPrank();
    // 200 ZVE staked
    console2.log("Total supply staked:", vestZVE.totalSupply());
    // 50 days have passed
    vm.warp(block.timestamp + 50 days);
    // Around 50 ZVE is withdrawable for a user
    console2.log(
        "Amount withdrawable for a user:",
        vestZVE.amountWithdrawable(userRevoked)
    );
    // A user withdraws his 50 ZVE
    vm.prank(userRevoked);
    vestZVE.withdraw();
    // Now totalSupply becomes 150 ZVE, while totalVesting is not reduced for that user
    console2.log(
        "Total supply staked after a withdraw:",
        vestZVE.totalSupply()
    );
    // Get the totalVesting amount for a user
    (, , , uint256 totalVestingRevoked, , , ) = vestZVE.viewSchedule(
        userRevoked
    );
    assertEq(totalVestingRevoked, 100e18);
    // A user's vesting schedule is revoked and his totalVesting = 100 amount of ZVE is deducted from the totalSupply
    vm.prank(address(zvl));
    vestZVE.revokeVestingSchedule(userRevoked);
    // 50 ZVE staked
    console2.log(
        "Total supply staked after a revoked",
        vestZVE.totalSupply()
    );
    // Now a second user also has a totalVesting of 100 ZVE, while totalSupply is only 50 ZVE after a revoke
    (, , , uint256 totalVesting, , , ) = vestZVE.viewSchedule(user);
    console2.log("Total vesting for the last user:", totalVesting);
    assertEq(totalVesting, 100e18);
    // Last 50 days of the vesting schedule pass and a user tries to withdraw his 100 ZVE
    vm.warp(block.timestamp + 50 days);
    vm.prank(user);
    // His transaction reverts with an undeflow: 50 - 100
    vm.expectRevert(stdError.arithmeticError);
    vestZVE.withdraw();
}
```
Console output of the test:
```md
Logs:
  Total supply staked: 200000000000000000000
  
  Amount withdrawable for a user: 49999999999999680000
  
  Total supply staked after a withdraw: 150000000000000320000
  
  Total supply staked after a revoked 50000000000000320000
  Total vesting for the last user: 100000000000000000000
```
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L451-L452
## Tool used

Manual Review

## Recommendation
Change calculations of `_totalSupply` in `revokeVestingSchedule()` to following:
[ZivoeRewardsVesting.sol#L451-L452](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L451-L452)
```diff
- _totalSupply = _totalSupply.sub(vestingAmount);
+ _totalSupply = _totalSupply.sub(vestingAmount -  vestingScheduleOf[account].totalWithdrawn);

- _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
+ _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount - vestingScheduleOf[account].totalWithdrawn);
```

With that change the same test case outputs:
```md
Logs:
  Total supply staked: 200000000000000000000
  Amount withdrawable for a user: 49999999999999680000
  Total supply staked after a withdraw: 150000000000000320000
  Total supply staked after a revoked 100000000000000000000
  Total vesting for the last user: 100000000000000000000
```