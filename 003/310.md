Bubbly Rouge Porcupine

medium

# A revoked vesting recipient can still vote with checkpoints worth `vestingAmount - vested amount`

## Summary
In [ZivoeRewardsVesting.revokeVestingSchedule](https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429),  the balance is set to 0, meaning any voting rights have to be completely removed, and all the vesting allocation is also set to 0. But some checkpoints are not removed due to wrong accounting.


## Vulnerability Detail



https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429


https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L478



```solidity
File: 2024-03-zivoe\zivoe-core-foundry\src\ZivoeRewardsVesting.sol
442:     function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
443:         require(
444:             vestingScheduleSet[account],
445:             "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleSet[account]"
446:         );
447:         require(
448:             vestingScheduleOf[account].revokable,
449:             "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleOf[account].revokable"
450:         );
451:        
452: >>>     uint256 amount = amountWithdrawable(account);
453:         uint256 vestingAmount = vestingScheduleOf[account].totalVesting;
454:
455:         vestingTokenAllocated -= amount;
456:
457:         vestingScheduleOf[account].totalWithdrawn += amount;
458:         vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
459:         vestingScheduleOf[account].cliff = block.timestamp - 1;
460:         vestingScheduleOf[account].end = block.timestamp;
461:
462:         vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);
463:
465:         _totalSupply = _totalSupply.sub(vestingAmount);
466:         _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
467: >>>     _writeCheckpoint(_checkpoints[account], _subtract, amount);
468:         _balances[account] = 0;
469:         stakingToken.safeTransfer(account, amount);
471:
472:         vestingScheduleOf[account].revokable = false;
473:
474:         emit VestingScheduleRevoked(
475:             account,
476:             vestingAmount - vestingScheduleOf[account].totalWithdrawn,
477:             vestingScheduleOf[account].cliff,
478:             vestingScheduleOf[account].end,
479:             vestingScheduleOf[account].totalVesting,
480:             false
481:         );
482:     }


https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L478


488:     function _stake(uint256 amount, address account) private nonReentrant updateReward(account) {
489:         require(amount > 0, "ZivoeRewardsVesting::_stake() amount == 0");
490:
491:         _totalSupply = _totalSupply.add(amount);
492:         _writeCheckpoint(_totalSupplyCheckpoints, _add, amount);
493: >>>     _writeCheckpoint(_checkpoints[account], _add, amount);
494:         _balances[account] = _balances[account].add(amount);
495:         emit Staked(account, amount);
496:     }




```


**Attack / issue path:**


1. ZVL has allocated some vesting tokens by calling `createVestingSchedule` to an account with 1000 tokens for 2 months linear with a 7-day cliff.
2. Now, after 30 days, ZVL revokes the schedule by calling `revokeVestingSchedule(account)`, and in L452 above, amount = 500 ZVE tokens that are vested will be sent to the account. And the `_balances[account] = 0` made in L468.
3. But look at L467, `_checkpoints[account]` is made to subtract only `amount` which is 500 vested. But when `createVestingSchedule` is called to create a vesting schedule in L493, the amount = 1000. So now 500 tokens worth of checkpoints are still left out. And that account can be used to vote on governance.



## Impact
When revoking a vesting schedule, unvested allocation-worth checkpoints are not removed from the recipient, leading to lifetime free governance voting. The impact is higher depending on how soon a vesting schedule is revoked.


## Code Snippet


https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429


https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L478



## Tool used


Manual Review


## Recommendation



```diff
 function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {

        ***

        vestingTokenAllocated -= amount;

        vestingScheduleOf[account].totalWithdrawn += amount;
        vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
        vestingScheduleOf[account].cliff = block.timestamp - 1;
        vestingScheduleOf[account].end = block.timestamp;

        vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);

        _totalSupply = _totalSupply.sub(vestingAmount);
        _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
-       _writeCheckpoint(_checkpoints[account], _subtract, amount);
+       _writeCheckpoint(_checkpoints[account], _subtract, vestingAmount - vestingScheduleOf[account].totalWithdrawn);
        _balances[account] = 0;
        stakingToken.safeTransfer(account, amount);

        vestingScheduleOf[account].revokable = false;

        ***

    }
```

