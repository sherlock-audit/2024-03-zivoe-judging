Great Metal Ram

medium

# `lastDistribution` will be set to the wrong value when `ZivoeYDL.unlock()` is called

## Summary

When `ZivoeYDL.unlock()` is called, it will set `lastDistribution` to `block.timestamp + 30 days`. This means that `distributeYield` can only be called 60 days after `ZivoeYDL` is unlocked (`lastDistribution + daysBetweenDistributions * 86400`). However, when calculating the yield target for calculating tranches earnings when `distributeYield` is called for the first time, it will only consider `daysBetweenDistributions` (30 days), despite the 60 days that have already passed.

## Vulnerability Detail

When `ZivoeYDL.unlock()`, it will unlock the `YDL` contract and set `lastDistribution` to `block.timestamp + 30 days`.

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeYDL.sol#L328

```solidity
    function unlock() external {
        require(
            _msgSender() == IZivoeGlobals_YDL(GBL).ITO(), 
            "ZivoeYDL::unlock() _msgSender() != IZivoeGlobals_YDL(GBL).ITO()"
        );

        unlocked = true;
>>>     lastDistribution = block.timestamp + 30 days;

        // ...
    }
```

This means that `distributeYield` can only be called for the first time 60 days after `ZivoeYDL` is unlocked (`lastDistribution + daysBetweenDistributions * 86400`).

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeYDL.sol#L216

```solidity
    function distributeYield() external nonReentrant {
        require(unlocked, "ZivoeYDL::distributeYield() !unlocked"); 
        require(
>>>         block.timestamp >= lastDistribution + daysBetweenDistributions * 86400, 
            "ZivoeYDL::distributeYield() block.timestamp < lastDistribution + daysBetweenDistributions * 86400"
        );

        // Calculate protocol earnings.
        uint256 earnings = IERC20(distributedAsset).balanceOf(address(this));
        uint256 protocolEarnings = protocolEarningsRateBIPS * earnings / BIPS;
        uint256 postFeeYield = earnings.floorSub(protocolEarnings);

        // Update timeline.
        distributionCounter += 1;
        lastDistribution = block.timestamp;

        // Calculate yield distribution (trancheuse = "slicer" in French).
        (
>>>         uint256[] memory _protocol, uint256 _seniorTranche, uint256 _juniorTranche, uint256[] memory _residual
        ) = earningsTrancheuse(protocolEarnings, postFeeYield); 

        emit YieldDistributed(_protocol, _seniorTranche, _juniorTranche, _residual);
        
        // ...
    }
```

However, when calculating earning for tranches, it will only consider `daysBetweenDistributions`(30 days) when calculating yield target despite the 60 days that have already passed.

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeYDL.sol#L461

```solidity
    function earningsTrancheuse(uint256 yP, uint256 yD) public view returns (
        uint256[] memory protocol, uint256 senior, uint256 junior, uint256[] memory residual
    ) {
        protocol = new uint256[](protocolRecipients.recipients.length);
        residual = new uint256[](residualRecipients.recipients.length);
        
        // Accounting for protocol earnings.
        for (uint256 i = 0; i < protocolRecipients.recipients.length; i++) {
            protocol[i] = protocolRecipients.proportion[i] * yP / BIPS;
        }

        // Accounting for senior and junior earnings.
        uint256 _seniorProportion = MATH.seniorProportion(
            IZivoeGlobals_YDL(GBL).standardize(yD, distributedAsset),
>>>         MATH.yieldTarget(emaSTT, emaJTT, targetAPYBIPS, targetRatioBIPS, daysBetweenDistributions),
            emaSTT, emaJTT, targetAPYBIPS, targetRatioBIPS, daysBetweenDistributions
        );
        senior = (yD * _seniorProportion) / RAY;
        junior = (yD * MATH.juniorProportion(emaSTT, emaJTT, _seniorProportion, targetRatioBIPS)) / RAY;
        
        // Handle accounting for residual earnings.
        yD = yD.floorSub(senior + junior);
        for (uint256 i = 0; i < residualRecipients.recipients.length; i++) {
            residual[i] = residualRecipients.proportion[i] * yD / BIPS;
        }
    }
```

This will result in a lower `yieldTarget` than it should be, likely causing the senior proportion to be calculated using the `seniorProportionBase` calculation, which will also lead to incorrect proportion for junior and senior tranches.

## Impact

This will cause incorrect amount of yield distributed for users because the additional 30 days of yield, which should have also been accounted for when calculating the yield target, are not considered.

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeYDL.sol#L328
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeYDL.sol#L216

## Tool used

Manual Review

## Recommendation

When `ZivoeYDL.unlock()` is called, set `lastDistribution` to `block.timestamp` instead.
