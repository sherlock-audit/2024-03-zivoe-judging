Shiny Lipstick Puppy

medium

# DOS in function `OCL_ZVE.sol#pushToLockerMulti()`

## Summary
DOS occurs because the `OCL_ZVE.sol#pushToLockerMulti()` function checks whether the allocation for `router` is 0 after calling the `addLiquidity()` function.
## Vulnerability Detail
After calling the `addLiquidity()` function in the `OCL_ZVE.sol#pushToLockerMulti()` function, check whether the quota for `router` is 0.
```solidity
    function pushToLockerMulti(
        address[] calldata assets, uint256[] calldata amounts, bytes[] calldata data
    ) external override onlyOwner nonReentrant {
        SNIP...

        // Router addLiquidity() endpoint.
        uint balPairAsset = IERC20(pairAsset).balanceOf(address(this));
        uint balZVE = IERC20(ZVE).balanceOf(address(this));
194:    IERC20(pairAsset).safeIncreaseAllowance(router, balPairAsset);
195:    IERC20(ZVE).safeIncreaseAllowance(router, balZVE);

        // Prevent volatility of greater than 10% in pool relative to amounts present.
        (uint256 depositedPairAsset, uint256 depositedZVE, uint256 minted) = IRouter_OCL_ZVE(router).addLiquidity(
            pairAsset, 
            ZVE, 
            balPairAsset,
            balZVE, 
            (balPairAsset * 9) / 10,
            (balZVE * 9) / 10, 
            address(this), block.timestamp + 14 days
        );
        emit LiquidityTokensMinted(minted, depositedZVE, depositedPairAsset);
208:    assert(IERC20(pairAsset).allowance(address(this), router) == 0);
209:    assert(IERC20(ZVE).allowance(address(this), router) == 0);

        // Increase basis by difference.
        (uint256 postBasis,) = fetchBasis();
        require(postBasis > preBasis, "OCL_ZVE::pushToLockerMulti() postBasis <= preBasis");
        basis += postBasis - preBasis;
    }
```

On the other hand, the `addLiquidity()` function of Uniswap (Shushiswap) is as follows.
```solidity
    function addLiquidity(
        address tokenA,
        address tokenB,
        uint amountADesired,
        uint amountBDesired,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline
    ) external virtual override ensure(deadline) returns (uint amountA, uint amountB, uint liquidity) {
@@>     (amountA, amountB) = _addLiquidity(tokenA, tokenB, amountADesired, amountBDesired, amountAMin, amountBMin);
        address pair = UniswapV2Library.pairFor(factory, tokenA, tokenB);
@@>     TransferHelper.safeTransferFrom(tokenA, msg.sender, pair, amountA);
@@>     TransferHelper.safeTransferFrom(tokenB, msg.sender, pair, amountB);
        liquidity = IUniswapV2Pair(pair).mint(to);
    }

    function _addLiquidity(
        address tokenA,
        address tokenB,
        uint amountADesired,
        uint amountBDesired,
        uint amountAMin,
        uint amountBMin
    ) internal virtual returns (uint amountA, uint amountB) {
        // create the pair if it doesn't exist yet
        if (IUniswapV2Factory(factory).getPair(tokenA, tokenB) == address(0)) {
            IUniswapV2Factory(factory).createPair(tokenA, tokenB);
        }
        (uint reserveA, uint reserveB) = UniswapV2Library.getReserves(factory, tokenA, tokenB);
        if (reserveA == 0 && reserveB == 0) {
            (amountA, amountB) = (amountADesired, amountBDesired);
        } else {
@@>         uint amountBOptimal = UniswapV2Library.quote(amountADesired, reserveA, reserveB);
@@>         if (amountBOptimal <= amountBDesired) {
@@>             require(amountBOptimal >= amountBMin, 'UniswapV2Router: INSUFFICIENT_B_AMOUNT');
@@>             (amountA, amountB) = (amountADesired, amountBOptimal);
            } else {
                uint amountAOptimal = UniswapV2Library.quote(amountBDesired, reserveB, reserveA);
                assert(amountAOptimal <= amountADesired);
                require(amountAOptimal >= amountAMin, 'UniswapV2Router: INSUFFICIENT_A_AMOUNT');
                (amountA, amountB) = (amountAOptimal, amountBDesired);
            }
        }
    }
```
For simplicity, let's only look at the parts pointed out in the `_addLiquidity()` function.
As a result, the `amountA` and `amountB` actually transferred to the Uniswap pool are calculated as follows.
> amountA = amountADesired
> amountB = amountADesired * reserveB / reserveA
> Therefore, amountB <= amountBDesired. As a result, amountB <= balZVE.

At this time, `TransferHelper.safeTransferFrom(tokenB, msg.sender, pair, amountB)` is executed, `IERC20(ZVE).allowance(address(this), router)` is not 0.
Therefore, `pushToLockerMulti()` is reverted.
A previous audit report also mentioned a problem that caused a DOS in the slippage check because the actual transfer amount was smaller than the desired amount.
See A04 in https://github.com/runtimeverification/publications/blob/main/reports/smart-contracts/Zivoe_Locker_Contracts.pdf
## Impact
This call can always be reverted as the actual transfer amount may not be exactly the desired amount.
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L172-L215
## Tool used

Manual Review

## Recommendation
Modify the `OCL_ZVE.sol#pushToLockerMulti()` function as follows.
```solidity
    function pushToLockerMulti(
        address[] calldata assets, uint256[] calldata amounts, bytes[] calldata data
    ) external override onlyOwner nonReentrant {
        SNIP...

        // Prevent volatility of greater than 10% in pool relative to amounts present.
        (uint256 depositedPairAsset, uint256 depositedZVE, uint256 minted) = IRouter_OCL_ZVE(router).addLiquidity(
            pairAsset, 
            ZVE, 
            balPairAsset,
            balZVE, 
            (balPairAsset * 9) / 10,
            (balZVE * 9) / 10, 
            address(this), block.timestamp + 14 days
        );
        emit LiquidityTokensMinted(minted, depositedZVE, depositedPairAsset);
---     assert(IERC20(pairAsset).allowance(address(this), router) == 0);
---     assert(IERC20(ZVE).allowance(address(this), router) == 0);

+++     IERC20(pairAsset).safeTransfer(owner(), IERC20(pairAsset).balanceOf(address(this)));
+++     IERC20(ZVE).safeTransfer(owner(), IERC20(ZVE).balanceOf(address(this)));

        // Increase basis by difference.
        (uint256 postBasis,) = fetchBasis();
        require(postBasis > preBasis, "OCL_ZVE::pushToLockerMulti() postBasis <= preBasis");
        basis += postBasis - preBasis;
    }
```