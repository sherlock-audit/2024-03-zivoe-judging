Curved Zinc Scallop

medium

# `OCL_ZVE::_forwardYield` lacks slippage protection when removing liquidity

## Summary

When forwarding excess yield by calling `OCL_ZVE::forwardYield()`, the `OCL_ZVE::_forwardYield` function is invoked to remove liquidity from Uniswap/Sushiswap, but it doesn't implement slippage protection. This omission can result in a loss of funds if the transaction is front-run, stays pending in the mempool for too long, or if the pool is manipulated.

## Vulnerability Detail

To forward excess yield, the `OCL_ZVE::forwardYield()` function can be called by anyone after the `nextYieldDistribution` delay has passed. Under the hood, the internal `_forwardYield` function is responsible for removing liquidity from the AMM and transferring it to the appropriate recipients.

```solidity
function _forwardYield(uint256 amount, uint256 lp) private nonReentrant {
    address ZVE = IZivoeGlobals_OCL_ZVE(GBL).ZVE();
    uint256 lpBurnable = ((((amount - basis) * lp) / amount) *
        (BIPS - compoundingRateBIPS)) / BIPS;
    address pair = IFactory_OCL_ZVE(factory).getPair(pairAsset, ZVE);
    IERC20(pair).safeIncreaseAllowance(router, lpBurnable);
    (uint256 claimedPairAsset, uint256 claimedZVE) = IRouter_OCL_ZVE(router)
        .removeLiquidity(
            pairAsset,
            ZVE,
            lpBurnable,
            0, //@audit No slippage protection
            0, //@audit No slippage protection
            address(this),
            block.timestamp + 14 days // No real deadline set
        );
    emit LiquidityTokensBurned(lpBurnable, claimedZVE, claimedPairAsset);
    assert(IERC20(pair).allowance(address(this), router) == 0);
    uint balPairAsset = IERC20(pairAsset).balanceOf(address(this));
    emit YieldForwarded(pairAsset, balPairAsset);
    if (
        pairAsset !=
        IZivoeYDL_OCL_ZVE(IZivoeGlobals_OCL_ZVE(GBL).YDL())
            .distributedAsset()
    ) {
        IERC20(pairAsset).safeTransfer(OCT_YDL, balPairAsset);
    } else {
        IERC20(pairAsset).safeTransfer(
            IZivoeGlobals_OCL_ZVE(GBL).YDL(),
            balPairAsset
        );
    }
    IERC20(ZVE).safeTransfer(owner(), IERC20(ZVE).balanceOf(address(this)));
}
```

As shown, when the function calls `IRouter_OCL_ZVE::removeLiquidity`, both minimum received amount parameters are set to 0, allowing the function to accept zero amounts back from the pool in exchange for burning LP tokens (full slippage). Additionally, the deadline parameter is set equal to `block.timestamp + 14 days`, meaning no real deadline is imposed on the transaction at the time depend on the execution block timestamp which can be any block in the future.

Given these settings and the fact that any user can call `OCL_ZVE::forwardYield()` after the `nextYieldDistribution` delay has passed, there is a high risk that a malicious user could front-run the transaction, sandwich it, or manipulate the pool to collect tokens at the expense of the protocol. Alternatively, the forward transaction could remain pending in the mempool for an extended period (because of the useless deadline set), resulting in a loss of funds for the protocol.

## Impact

The lack of slippage protection in `OCL_ZVE::_forwardYield` when removing liquidity from Uniswap/Sushiswap can result in a loss of funds if the transaction is front-run, stays pending in the mempool for too long, or if the pool is manipulated.

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L316-L318

## Tool used

Manual Review

## Recommendation

To address this issue, the `OCL_ZVE` contract should implement slippage protection by including default slippage parameters (should be states set by the DAO) in `OCL_ZVE::_forwardYield` when removing liquidity.