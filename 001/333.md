Curved Marmalade Cobra

medium

# `OCL_ZVE::pushToLockerMulti()` assumes router will always pull all the balance, leading to DoS

## Summary

`OCL_ZVE::pushToLockerMulti()` increases the allowance of the `UniswapV2Router` or `SUSHI` router, executes `addLiquidity()` and asserts that the allowance is `0`, which should always revert and lead to DoS.

## Vulnerability Detail

`UniswapV2Router::addLiquidity()` [pulls](https://github.com/Uniswap/v2-periphery/blob/master/contracts/UniswapV2Router02.sol#L71-L74) the asset balances from `msg.sender` according to the calculations in [_addLiquidity()](https://github.com/Uniswap/v2-periphery/blob/master/contracts/UniswapV2Router02.sol#L33), not the provided amounts in the function call. These amounts act as maximum amounts, but the actual pulled tokens will be based on the current price. Thus, the contract `OCL_ZVE::pushToLockerMulti()` should be permanently DoSed, only possibly working for the first time it is called, as `Uniswap` automatically creates a pool with the price being the provided liquidity amounts. However, it should always fail after this, as it is near impossible to completely provide the correct ratios all the times (due to frontrunning, delays and so on).

A POC was carried out confirming the behaviour. Add the following test to `Test_OCL_ZVE.sol`.
```solidity
function test_POC_OCL_ZVE_SUSHI_pushToLockerMulti_revertsAfterFirstTime() public {
    address[] memory assets = new address[](2);
    uint256[] memory amounts = new uint256[](2);
    assets[0] = USDC;
    assets[1] = address(ZVE);
    amounts[0] = 5_000_010 * USD * 95 / 100;
    amounts[1] = 5_000_010 * USD;
    assert(god.try_pushMulti(address(DAO), address(OCL_ZVE_SUSHI_USDC), assets, amounts, new bytes[](2)));
    amounts[0] = 5_000_010 * USD;
    amounts[1] = 5_000_010 * USD;
    vm.expectRevert();
    vm.prank(address(god));
    DAO.pushMulti(address(OCL_ZVE_SUSHI_USDC), assets, amounts, new bytes[](2));
}
```

## Impact

DoS of `OCL_ZVE::pushToLockerMulti()`, which also makes most of the functionality of `OCL_ZVE` obsolete as it depends on it. Note that the funds can always be recovered by calling `OCL_ZVE::pullFromLocker()`.

## Code Snippet

[OCL_ZVE::pushToLockerMulti()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L172)
```solidity
function pushToLockerMulti(
    address[] calldata assets, uint256[] calldata amounts, bytes[] calldata data
) external override onlyOwner nonReentrant {
    ...
    // Router addLiquidity() endpoint.
    uint balPairAsset = IERC20(pairAsset).balanceOf(address(this));
    uint balZVE = IERC20(ZVE).balanceOf(address(this));
    IERC20(pairAsset).safeIncreaseAllowance(router, balPairAsset);
    IERC20(ZVE).safeIncreaseAllowance(router, balZVE);

    // Prevent volatility of greater than 10% in pool relative to amounts present.
    (uint256 depositedPairAsset, uint256 depositedZVE, uint256 minted) = IRouter_OCL_ZVE(router).addLiquidity(
        pairAsset, 
        ZVE, 
        balPairAsset,
        balZVE, 
        (balPairAsset * 9) / 10,
        (balZVE * 9) / 10, 
        address(this), block.timestamp + 14 days
    );
    emit LiquidityTokensMinted(minted, depositedZVE, depositedPairAsset);
    assert(IERC20(pairAsset).allowance(address(this), router) == 0);
    assert(IERC20(ZVE).allowance(address(this), router) == 0);
    ...
}
```

## Tool used

Manual Review

Vscode

Foundry

## Recommendation

The router checks the amounts with the provided `amountAMin` and `amountBMin`, thus the amounts are guaranteed to be correctly transferred up to these amounts. Thus, after calling the router `addLiquidity()`, the allowance should be set to 0 (not asserted).
