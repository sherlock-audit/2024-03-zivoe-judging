Clumsy Cobalt Lion

high

# AMM integrations do not work because of approval assertions

## Summary
[OCL_ZVE.pushToLockerMulti()](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L172-L215) assert that the Uniswap/Sushiswap router has spent all of the balance of the locker after liquidity addition/removal. However, this is impossible to be predicted because of market price volatility. Not only that, but an attacker can send 1 wei worth of stablecoin to the contract to DOS the function.

The same issue is present in [OCY_Convex_A](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCY/OCY_Convex_A.sol) and [OCY_Convex_C](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCY/OCY_Convex_C.sol)

## Vulnerability Detail
When pushing stablecoins from the DAO to the OCL Locker, the locker uses them to add liquidity to a Uniswap/Sushi pair. 

```solidity
        uint balPairAsset = IERC20(pairAsset).balanceOf(address(this));
        uint balZVE = IERC20(ZVE).balanceOf(address(this));
        IERC20(pairAsset).safeIncreaseAllowance(router, balPairAsset);
        IERC20(ZVE).safeIncreaseAllowance(router, balZVE);

        // Prevent volatility of greater than 10% in pool relative to amounts present.
        (uint256 depositedPairAsset, uint256 depositedZVE, uint256 minted) = IRouter_OCL_ZVE(router).addLiquidity(
            pairAsset, 
            ZVE, 
            balPairAsset,
            balZVE, 
            (balPairAsset * 9) / 10,
            (balZVE * 9) / 10, 
            address(this), block.timestamp + 14 days
        );
        emit LiquidityTokensMinted(minted, depositedZVE, depositedPairAsset);
        assert(IERC20(pairAsset).allowance(address(this), router) == 0);
        assert(IERC20(ZVE).allowance(address(this), router) == 0);
```
The code gives allowance to the router to spend its whole balance, then adds liquidity and finally `asserts` that the allowance of the both tokens is 0. If `addLiquidity` does not use up all tokens available to `OCL_LOCKER`, the transaction will revert. An attacker can take advantage of that and send 1 wei worth of `pairAsset` to the locker before pushing is done. Since `addLiquidity` will calculate the amount of `pairAsset` to be transferred based on the ratio the assets in the pool are currently at, this 1 wei of `pairAsset` will not be used. Therefore, the allowance after adding liquidity will not be 0 and the whole push operation will fail.

It's a permanent DOS because the push/pull functions of the locker are called by a timelock, meaning that the contract will not be able to push assets until the timelock makes a new transaction. However, this new transaction can be frontrun again.

This issue has one other impact, too. According to the above code, the locker should tolerate 10% volatility, but we see this is not the case because if the whole balance is not used up, the transaction reverts. Predicting the exact price before making a swap is almost impossible.

## Impact
1. DOS of `OCL_ZVE.pushToLockerMulti()`.
2. `OCL_ZVE` does not tolerate any price volatility.


## Code Snippet
PoC to add to [Test_OCL_ZVE](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-testing/src/TESTS_Lockers/Test_OCL_ZVE.sol)

```solidity
    function test_DOS_push() public {
        address[] memory assets = new address[](2);
        uint256[] memory amounts = new uint256[](2);

        assets[0] = DAI;
        assets[1] = address(ZVE);

        // Initiate the pool 1:1
        amounts[0] = 200 ether;
        amounts[1] = 200 ether;

        assert(
                god.try_pushMulti(
                    address(DAO),
                    address(OCL_ZVE_UNIV2_DAI),
                    assets,
                    amounts,
                    new bytes[](2)
                )
        );

        mint("DAI", address(0xb0b), 1e18);
        
        vm.prank(address(0xb0b));
        // It's enough to send just 1 wei worth of DAI to DOS the pushing
        IERC20(DAI).transfer(address(OCL_ZVE_UNIV2_DAI), 1);

        // pushing fails
        assertFalse(
                god.try_pushMulti(
                    address(DAO),
                    address(OCL_ZVE_UNIV2_DAI),
                    assets,
                    amounts,
                    new bytes[](2)
                )
        );
    }
```

## Tool used

Foundry

## Recommendation
Manually set the tokens' allowances to 0 after the liquidity addition/removal.