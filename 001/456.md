Great Metal Ram

medium

# Yield is not forwarded to `YDL` before the token is pulled from `OCL_ZVE`, causing a loss of yield for users.

## Summary

The DAO can trigger `pullFromLocker` or `pullFromLockerPartial` to pull the pair asset and ZVE from the `OCL_ZVE` locker. However, it doesn't forward the accrued yield to the `YDL` contract, resulting in a loss of yield for users.

## Vulnerability Detail

When `pullFromLocker` and `pullFromLockerPartial`, it will remove liquidity from Uniswap V2 or  Sushi pool and transfer the tokens to the DAO.

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L220-L247
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L253-L284

```solidity
    function pullFromLocker(address asset, bytes calldata data) external override onlyOwner nonReentrant {
        address ZVE = IZivoeGlobals_OCL_ZVE(GBL).ZVE();
        address pair = IFactory_OCL_ZVE(factory).getPair(pairAsset, ZVE);
        
        (uint amountAMin, uint amountBMin) = abi.decode(data, (uint, uint));

        // "pair" represents the liquidity pool token (minted, burned).
        // "pairAsset" represents the stablecoin paired against $ZVE.
        if (asset == pair) {
            uint256 preBalLPToken = IERC20(pair).balanceOf(address(this));
            IERC20(pair).safeIncreaseAllowance(router, preBalLPToken);

            // Router removeLiquidity() endpoint.
            (uint256 claimedPairAsset, uint256 claimedZVE) = IRouter_OCL_ZVE(router).removeLiquidity(
                pairAsset, ZVE, preBalLPToken, 
                amountAMin, amountBMin, address(this), block.timestamp + 14 days
            );
            emit LiquidityTokensBurned(preBalLPToken, claimedZVE, claimedPairAsset);
            assert(IERC20(pair).allowance(address(this), router) == 0);

            IERC20(pairAsset).safeTransfer(owner(), IERC20(pairAsset).balanceOf(address(this)));
            IERC20(ZVE).safeTransfer(owner(), IERC20(ZVE).balanceOf(address(this)));
            basis = 0;
        }
        else {
            IERC20(asset).safeTransfer(owner(), IERC20(asset).balanceOf(address(this)));
        }
    }
```

However, it doesn't consider the accrued yield (increased from the last recorded basis to the current basis) and transfers all the received tokens to the DAO.


## Impact

This will cause a loss of accrued yield for users that should have been forwarded to the `YDL` contract.

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L220-L247
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L253-L284

## Tool used

Manual Review

## Recommendation

Consider to forward yield if current basis is increased from the last recorded `basis`.

```diff
    function pullFromLocker(address asset, bytes calldata data) external override onlyOwner nonReentrant {
        // @audit - zve address can be changed?
        address ZVE = IZivoeGlobals_OCL_ZVE(GBL).ZVE();
        address pair = IFactory_OCL_ZVE(factory).getPair(pairAsset, ZVE);
        
        (uint amountAMin, uint amountBMin) = abi.decode(data, (uint, uint));

        // "pair" represents the liquidity pool token (minted, burned).
        // "pairAsset" represents the stablecoin paired against $ZVE.
        if (asset == pair) {
            uint256 preBalLPToken = IERC20(pair).balanceOf(address(this));
            IERC20(pair).safeIncreaseAllowance(router, preBalLPToken);

+          (uint256 amount, ) = fetchBasis();

            // Router removeLiquidity() endpoint.
            (uint256 claimedPairAsset, uint256 claimedZVE) = IRouter_OCL_ZVE(router).removeLiquidity(
                pairAsset, ZVE, preBalLPToken, 
                amountAMin, amountBMin, address(this), block.timestamp + 14 days 
            );
            emit LiquidityTokensBurned(preBalLPToken, claimedZVE, claimedPairAsset);
            assert(IERC20(pair).allowance(address(this), router) == 0);

+           if (amount > basis) {
+                if (pairAsset != IZivoeYDL_OCL_ZVE(IZivoeGlobals_OCL_ZVE(GBL).YDL()).distributedAsset()) {
+                    IERC20(pairAsset).safeTransfer(OCT_YDL, amount - basis);
+                }
+                else {
+                    IERC20(pairAsset).safeTransfer(IZivoeGlobals_OCL_ZVE(GBL).YDL(), amount - basis);
+                }               
+          }

            IERC20(pairAsset).safeTransfer(owner(), IERC20(pairAsset).balanceOf(address(this)));
            IERC20(ZVE).safeTransfer(owner(), IERC20(ZVE).balanceOf(address(this)));
            basis = 0;
        }
        else {
            IERC20(asset).safeTransfer(owner(), IERC20(asset).balanceOf(address(this)));
        }
    }
```

And implement similar implementation for `pullFromLockerPartial`.
