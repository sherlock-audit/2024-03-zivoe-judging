Feisty Taupe Locust

medium

# Partially pulling from OCL_ZVE can be reverted because of an incorrect basis calculation

## Summary
In `OCL_ZVE`, we track the amount of `pair asset` deposited into the `Uniswap V2 pool`.
However, we don't account for changes in the balance of the `pair asset` in the `pool` resulting from trading activity.
Consequently, partially pulling from `OCL_ZVE` can be reverted.
## Vulnerability Detail
Suppose the `pair asset` is `USDC` and `ZVE` token has the same price initially.

`OCL_ZVE` deposits `10,000` of both tokens into the `Uniswap V2 pool`, making the reserves `(10,000, 10,000)`.
Suppose `OCL_ZVE` is the sole `LP` in this `pool`.
The `basis` is obviously `10,000`.
```solidity
function pushToLockerMulti(
    address[] calldata assets, uint256[] calldata amounts, bytes[] calldata data
) external override onlyOwner nonReentrant {
    if (basis != 0) { (preBasis,) = fetchBasis(); }

    (uint256 depositedPairAsset, uint256 depositedZVE, uint256 minted) = IRouter_OCL_ZVE(router).addLiquidity(
        pairAsset, 
        ZVE, 
        balPairAsset,
        balZVE, 
        (balPairAsset * 9) / 10,
        (balZVE * 9) / 10, 
        address(this), block.timestamp + 14 days
    );
    emit LiquidityTokensMinted(minted, depositedZVE, depositedPairAsset);
  
    (uint256 postBasis,) = fetchBasis();
    require(postBasis > preBasis, "OCL_ZVE::pushToLockerMulti() postBasis <= preBasis");
    basis += postBasis - preBasis; // @audit, here
}
```

As the price of `ZVE` token increases, the `reserves` change to `(30,000, 3,333)` in the `pool`.

`OCL_ZVE` then attempts to partially pull out `50%` of it's liquidity.
The `preBasis` is `30,000` and `postBasis` is `15,000`.
Since the `basis` is less than `preBasis - postBasis`, the pulling will be reverted. (`10,000 < 30,000 - 15,000`)
```solidity
function pullFromLockerPartial(
    address asset, uint256 amount, bytes calldata data
) external override onlyOwner nonReentrant {
    if (asset == pair) {
        (uint256 preBasis,) = fetchBasis();  // @audit, preBasis = 30,000

        IERC20(pair).safeIncreaseAllowance(router, amount);

        (uint256 claimedPairAsset, uint256 claimedZVE) = IRouter_OCL_ZVE(router).removeLiquidity(
            pairAsset, ZVE, amount, 
            amountAMin, amountBMin, address(this), block.timestamp + 14 days
        );  // @audit, pull 50% of liquidity
        
        IERC20(pairAsset).safeTransfer(owner(), IERC20(pairAsset).balanceOf(address(this)));
        IERC20(ZVE).safeTransfer(owner(), IERC20(ZVE).balanceOf(address(this)));
        (uint256 postBasis,) = fetchBasis();  // @audit, postBasis = 15,000 
        require(postBasis < preBasis, "OCL_ZVE::pullFromLockerPartial() postBasis >= preBasis");
        basis -= preBasis - postBasis;  // @audit, 10,000 < 30,000 - 15,000
    }
    else {
        IERC20(asset).safeTransfer(owner(), amount);
    }
}
```

Of course, the `basis` is synchronized with the `pool` by calling the `forwardYield` function.
However, this function can only be called after the `nextYieldDistribution` time.
```solidity
function forwardYield() external {
    if (IZivoeGlobals_OCL_ZVE(GBL).isKeeper(_msgSender())) {
        require(
            block.timestamp > nextYieldDistribution - 12 hours, 
            "OCL_ZVE::forwardYield() block.timestamp <= nextYieldDistribution - 12 hours"
        );
    }
    else {
        require(
            block.timestamp > nextYieldDistribution, 
            "OCL_ZVE::forwardYield() block.timestamp <= nextYieldDistribution"
        );
    }

    (uint256 amount, uint256 lp) = fetchBasis();
    if (amount > basis) { _forwardYield(amount, lp); }
    (basis,) = fetchBasis(); // @audit, here
    nextYieldDistribution += 30 days;
}
```
## Impact

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L214
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L279
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L288-L299
## Tool used

Manual Review

## Recommendation
The `basis` can be reduced proportionally based on the amount of liquidity removed.