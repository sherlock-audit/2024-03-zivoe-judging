Furry Sepia Condor

medium

# `safeDecreaseAllowance` is used incorrectly in `push()`

## Summary
The `safeDecreaseAllowance` functionality is used incorrectly when the comments explicitly states there should be no leftover allowance
## Vulnerability Detail
Let's examine `ZivoeDAO.sol` contract's `push()` [function](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeDAO.sol#L239-L246) closely:

```solidity
    function push(address locker, address asset, uint256 amount, bytes calldata data) external onlyOwner nonReentrant {
        require(IZivoeGlobals_DAO(GBL).isLocker(locker), "ZivoeDAO::push() !IZivoeGlobals_DAO(GBL).isLocker(locker)");
        require(ILocker_DAO(locker).canPush(), "ZivoeDAO::push() !ILocker_DAO(locker).canPush()");
        emit Pushed(locker, asset, amount, data);
        IERC20(asset).safeIncreaseAllowance(locker, amount);
        ILocker_DAO(locker).pushToLocker(asset, amount, data);
@>      // ZivoeDAO MUST ensure "locker" has 0 allowance before this function concludes.
@>      if (IERC20(asset).allowance(address(this), locker) > 0) { IERC20(asset).safeDecreaseAllowance(locker, 0); } // @audit wrong usage
    }
```

The comment explicitly states that the DAO must ensure the locker has 0 allowance left before the function concludes. The issue is in the fact that by passing 0, it will attempt to [decrement the allowance](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/4e5b11919e91b18b6683b6f49a1b4fdede579969/contracts/token/ERC20/utils/SafeERC20.sol#L78) by value 0, instead of *setting* it to 0. This means that if there is any leftover allowance, this will achieve practically nothing and the function flow can conclude with leftover allowance.

```solidity
    function safeDecreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }
```
## Impact
Completely incorrect usage of `safeDecreaseAllowance` may conclude function flow with leftover allowance when it **MUST** not.
## Code Snippet
```solidity
    function push(address locker, address asset, uint256 amount, bytes calldata data) external onlyOwner nonReentrant {
        require(IZivoeGlobals_DAO(GBL).isLocker(locker), "ZivoeDAO::push() !IZivoeGlobals_DAO(GBL).isLocker(locker)");
        require(ILocker_DAO(locker).canPush(), "ZivoeDAO::push() !ILocker_DAO(locker).canPush()");
        emit Pushed(locker, asset, amount, data);
        IERC20(asset).safeIncreaseAllowance(locker, amount);
        ILocker_DAO(locker).pushToLocker(asset, amount, data);
@>      // ZivoeDAO MUST ensure "locker" has 0 allowance before this function concludes.
@>      if (IERC20(asset).allowance(address(this), locker) > 0) { IERC20(asset).safeDecreaseAllowance(locker, 0); } // @audit wrong usage
    }
```
## Tool used
Manual Review

## Recommendation
Decrement by actual amount needed instead of 0. Or use `safeApprove()` to reset allowance back to 0.