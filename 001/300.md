Cool Oily Seal

medium

# Attacker can DoS OCL_ZVE contract by calling OCL_ZVE::forwardYield before nextYieldDistribution is initialized

## Summary

`OCL_ZVE::forwardYield` can be called by an attacker before `nextYieldDistribution` is initialized to permanently DoS `OCL_ZVE`

## Vulnerability Detail

`OCL_ZVE::forwardYield` is an unpermissioned function used to forward yield in excess of the basis, and it sets `nextYieldDistribution += 30 days` at the end of the call:

[OCL_ZVE.sol#L286-L305](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L286-L305)
```solidity
    /// @notice This forwards yield in excess of the basis.
    function forwardYield() external {
        if (IZivoeGlobals_OCL_ZVE(GBL).isKeeper(_msgSender())) {
            require(
                block.timestamp > nextYieldDistribution - 12 hours, 
                "OCL_ZVE::forwardYield() block.timestamp <= nextYieldDistribution - 12 hours"
            );
        }
        else {
            require(
                block.timestamp > nextYieldDistribution, 
                "OCL_ZVE::forwardYield() block.timestamp <= nextYieldDistribution"
            );
        }

        (uint256 amount, uint256 lp) = fetchBasis();
        if (amount > basis) { _forwardYield(amount, lp); }
        (basis,) = fetchBasis();
>>      nextYieldDistribution += 30 days;
    }
```

`uint256 public nextYieldDistribution;` is a state variable which Determines next available forwardYield() call.
However this state variable isn't initialized at contract creation, it's only set when owner calls `OCL_ZVE::pushToLockerMulti`:

[OCL_ZVE.sol#L172-L186](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L172-L186)
```solidity
    function pushToLockerMulti(
        address[] calldata assets, uint256[] calldata amounts, bytes[] calldata data
    ) external override onlyOwner nonReentrant {
        address ZVE = IZivoeGlobals_OCL_ZVE(GBL).ZVE();
        require(
            assets[0] == pairAsset && assets[1] == ZVE,
            "OCL_ZVE::pushToLockerMulti() assets[0] != pairAsset || assets[1] != ZVE"
        );

        for (uint256 i = 0; i < 2; i++) {
            require(amounts[i] >= 10 * 10**6, "OCL_ZVE::pushToLockerMulti() amounts[i] < 10 * 10**6");
            IERC20(assets[i]).safeTransferFrom(owner(), address(this), amounts[i]);
        }

>>      if (nextYieldDistribution == 0) { nextYieldDistribution = block.timestamp + 30 days; }
```

Therefore an attacker can call (or frontrun) `OCL_ZVE::forwardYield` before the first call to `OCL_ZVE::pushToLockerMulti` to set `nextYieldDistribution += 30 days` and permanently DoS the `OCL_ZVE` contract.

## Impact

Permanent DoS of the `OCL_ZVE` contract

## Code Snippet

- https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L286-L305

## Tool used

Manual Review

## Recommendation

Ensure that `nextYieldDistribution` is initialized before adding `30 days`:

```diff
    /// @notice This forwards yield in excess of the basis.
    function forwardYield() external {
+      require(nextYieldDistribution != 0, "not started yet");
        if (IZivoeGlobals_OCL_ZVE(GBL).isKeeper(_msgSender())) {
            require(
                block.timestamp > nextYieldDistribution - 12 hours, 
                "OCL_ZVE::forwardYield() block.timestamp <= nextYieldDistribution - 12 hours"
            );
        }
        else {
            require(
                block.timestamp > nextYieldDistribution, 
                "OCL_ZVE::forwardYield() block.timestamp <= nextYieldDistribution"
            );
        }

        (uint256 amount, uint256 lp) = fetchBasis();
        if (amount > basis) { _forwardYield(amount, lp); }
        (basis,) = fetchBasis();
        nextYieldDistribution += 30 days;
    }
```