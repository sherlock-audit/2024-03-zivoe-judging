Faithful Sky Wallaby

medium

# ZivoeDAO incorrectly uses safeDecreaseAllowance, causing it to not change allowances and waste gas

## Summary

`ZivoeDAO` tries to set the allowance for ERC20 tokens to 0 after each transaction involving lockers, however the use of `safeDecreaseAllowance` is incorrect and doesn't decrease the allowance to 0. This wastes gas and leaves the allowances above 0, which goes against the intended functionality as stated in [code](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeDAO.sol#L245) comments.

## Vulnerability Detail

`ZivoeDAO::pushMulti` and `ZivoeDAO::push` increase the target lockers allowance to however much is needed for the transferFrom in the call, and once the transfer is finished the function try to set the allowances back to 0. 

[ZivoeDAO::push](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeDAO.sol#L239-L247)
```solidity
    function push(address locker, address asset, uint256 amount, bytes calldata data) external onlyOwner nonReentrant {
        require(IZivoeGlobals_DAO(GBL).isLocker(locker), "ZivoeDAO::push() !IZivoeGlobals_DAO(GBL).isLocker(locker)");
        require(ILocker_DAO(locker).canPush(), "ZivoeDAO::push() !ILocker_DAO(locker).canPush()");
        emit Pushed(locker, asset, amount, data);
        IERC20(asset).safeIncreaseAllowance(locker, amount);
        ILocker_DAO(locker).pushToLocker(asset, amount, data);
        // ZivoeDAO MUST ensure "locker" has 0 allowance before this function concludes.
        if (IERC20(asset).allowance(address(this), locker) > 0) { IERC20(asset).safeDecreaseAllowance(locker, 0); }
    }
```
The comments clearly state that is it very important to ensure that "locker has 0 allowance before this function concludes".

The OpenZeppelin library version that is being used to verify the behaviour is from the contest repo: 
[lib folder](https://github.com/sherlock-audit/2024-03-zivoe/tree/main/zivoe-core-foundry/lib)
[SafeERC20.sol](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/4e5b11919e91b18b6683b6f49a1b4fdede579969/contracts/token/ERC20/utils/SafeERC20.sol)

The allowance is checked to see if it is above 0, and if it is `safeDecreaseAllowance()` is called:
`IERC20(asset).safeDecreaseAllowance(locker, 0)`

[SafeERC20::safeDecreaseAllowance](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/4e5b11919e91b18b6683b6f49a1b4fdede579969/contracts/token/ERC20/utils/SafeERC20.sol#L70-L81)
```solidity
    function safeDecreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }
```
In the end the function calls `approve` where it overwrites the current allowance to 
`oldAllowance - value`

`ZivoeDAO::push` `requestedDecrease` is 0, resulting in an unchanged allowance and wasting gas.

The gas wastage is even worse in `pushMulti()` as there are multiple ERC20 tokens that this allowance decrease is applied to:

[ZivoeDAO::pushMulti](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeDAO.sol#L282-L301)
```solidity
    function pushMulti(
        address locker, address[] calldata assets, uint256[] calldata amounts, bytes[] calldata data
    ) external onlyOwner nonReentrant {
... SKIP!...
        for (uint256 i = 0; i < assets.length; i++) {
            IERC20(assets[i]).safeIncreaseAllowance(locker, amounts[i]);
            emit Pushed(locker, assets[i], amounts[i], data[i]);
        }
        ILocker_DAO(locker).pushToLockerMulti(assets, amounts, data);
        for (uint256 i = 0; i < assets.length; i++) {
            // ZivoeDAO MUST ensure "locker" has 0 allowance for each ERC20 token before this function concludes.
            if (IERC20(assets[i]).allowance(address(this), locker) > 0) { IERC20(assets[i]).safeDecreaseAllowance(locker, 0); }
        }
    }
```

In the end, the allowances for these ERC20 tokens are not decreased to 0 as intended, meaning any unspent allowances will remain after the function ends, whilst the function calls wasted unneeded gas, causing a small loss for the DAO each time these 2 functions are called.

## Impact

The allowances will not be reset to 0 as stated in the code comments, meaning non-zero allowances will remain after function end and additional gas will be wasted from the DAO on every call to the push functions.

## Code Snippet

[ZivoeDAO.sol#L245](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeDAO.sol#L245)
[ZivoeDAO.sol#L239-L247](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeDAO.sol#L239-L247)
[SafeERC20::safeDecreaseAllowance](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/4e5b11919e91b18b6683b6f49a1b4fdede579969/contracts/token/ERC20/utils/SafeERC20.sol#L70-L81)
[ZivoeDAO.sol#L282-L301](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeDAO.sol#L282-L301)

## Tool used

Manual Review

## Recommendation

Check the remaining allowance, if it is above 0 then reduce the allowance passing in that remaining allowance:

[push()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeDAO.sol#L246)
```diff
-       if (IERC20(asset).allowance(address(this), locker) > 0) { IERC20(asset).safeDecreaseAllowance(locker, 0); }
+      uint256 remainingAllowance = IERC20(asset).allowance(address(this), locker);
+      if (remainingAllowance > 0) { 
+           IERC20(asset).safeDecreaseAllowance(locker, remainingAllowance);
+     }
```

Also apply the same changes to [ZivoeDAO.pushMulti()](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeDAO.sol#L282C14-L301)