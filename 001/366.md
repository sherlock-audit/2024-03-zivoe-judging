Furry Sepia Condor

medium

# `safeDecreaseAllowance` is used incorrectly in `pushMulti()`

## Summary
The `safeDecreaseAllowance` functionality is used incorrectly when the comments explicitly states there should be no leftover allowance
## Vulnerability Detail
Let's examine `ZivoeDAO.sol` contract's `pushMulti()` [function](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeDAO.sol#L282-L301) closely:

```solidity
    function pushMulti(
        address locker, address[] calldata assets, uint256[] calldata amounts, bytes[] calldata data
    ) external onlyOwner nonReentrant {
        require(
            IZivoeGlobals_DAO(GBL).isLocker(locker), 
            "ZivoeDAO::pushMulti() !IZivoeGlobals_DAO(GBL).isLocker(locker)"
        );
        require(assets.length == amounts.length, "ZivoeDAO::pushMulti() assets.length != amounts.length");
        require(amounts.length == data.length, "ZivoeDAO::pushMulti() amounts.length != data.length");
        require(ILocker_DAO(locker).canPushMulti(), "ZivoeDAO::pushMulti() !ILocker_DAO(locker).canPushMulti()");
        for (uint256 i = 0; i < assets.length; i++) {
            IERC20(assets[i]).safeIncreaseAllowance(locker, amounts[i]);
            emit Pushed(locker, assets[i], amounts[i], data[i]);
        }
        ILocker_DAO(locker).pushToLockerMulti(assets, amounts, data);
        for (uint256 i = 0; i < assets.length; i++) {
@>          // ZivoeDAO MUST ensure "locker" has 0 allowance for each ERC20 token before this function concludes.
@>          if (IERC20(assets[i]).allowance(address(this), locker) > 0) { IERC20(assets[i]).safeDecreaseAllowance(locker, 0); } // @audit wrong usage
        }
    }
```

The comment explicitly states that the DAO must ensure the lockers have 0 allowance left before the function concludes. The issue is in the fact that by passing 0, it will attempt to [decrement the allowance](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/4e5b11919e91b18b6683b6f49a1b4fdede579969/contracts/token/ERC20/utils/SafeERC20.sol#L78) by value 0, instead of *setting* it to 0. This means that if there is any leftover allowance in any of the lockers, this will achieve practically nothing and the function flow can conclude with leftover allowance.

```solidity
    function safeDecreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }
```
## Impact
Completely incorrect usage of `safeDecreaseAllowance` may conclude function flow with leftover allowance when it **MUST** not.
## Code Snippet
```solidity
    function pushMulti(
        address locker, address[] calldata assets, uint256[] calldata amounts, bytes[] calldata data
    ) external onlyOwner nonReentrant {
        require(
            IZivoeGlobals_DAO(GBL).isLocker(locker), 
            "ZivoeDAO::pushMulti() !IZivoeGlobals_DAO(GBL).isLocker(locker)"
        );
        require(assets.length == amounts.length, "ZivoeDAO::pushMulti() assets.length != amounts.length");
        require(amounts.length == data.length, "ZivoeDAO::pushMulti() amounts.length != data.length");
        require(ILocker_DAO(locker).canPushMulti(), "ZivoeDAO::pushMulti() !ILocker_DAO(locker).canPushMulti()");
        for (uint256 i = 0; i < assets.length; i++) {
            IERC20(assets[i]).safeIncreaseAllowance(locker, amounts[i]);
            emit Pushed(locker, assets[i], amounts[i], data[i]);
        }
        ILocker_DAO(locker).pushToLockerMulti(assets, amounts, data);
        for (uint256 i = 0; i < assets.length; i++) {
@>          // ZivoeDAO MUST ensure "locker" has 0 allowance for each ERC20 token before this function concludes.
@>          if (IERC20(assets[i]).allowance(address(this), locker) > 0) { IERC20(assets[i]).safeDecreaseAllowance(locker, 0); } // @audit wrong usage
        }
    }
```
## Tool used
Manual Review

## Recommendation
Decrement by actual amount needed instead of 0. Or use `safeApprove()` to reset allowance back to 0.