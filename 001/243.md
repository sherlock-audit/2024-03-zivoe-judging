Shiny Lipstick Puppy

medium

# In the `ZivoeDAO.sol#push()` function, the allocation for `locker` is not initialized to 0.

## Summary
In the `ZivoeDAO.sol#push()` function, the token may be moved incorrectly because the allocation for `locker` is not initialized to 0.
## Vulnerability Detail
The `ZivoeDAO.sol#push()` function is as follows.
```solidity
    function push(address locker, address asset, uint256 amount, bytes calldata data) external onlyOwner nonReentrant {
        require(IZivoeGlobals_DAO(GBL).isLocker(locker), "ZivoeDAO::push() !IZivoeGlobals_DAO(GBL).isLocker(locker)");
        require(ILocker_DAO(locker).canPush(), "ZivoeDAO::push() !ILocker_DAO(locker).canPush()");
        emit Pushed(locker, asset, amount, data);
        IERC20(asset).safeIncreaseAllowance(locker, amount);
        ILocker_DAO(locker).pushToLocker(asset, amount, data);
        // ZivoeDAO MUST ensure "locker" has 0 allowance before this function concludes.
246:    if (IERC20(asset).allowance(address(this), locker) > 0) { IERC20(asset).safeDecreaseAllowance(locker, 0); }
    }
```
The comment of the `ZivoeDAO.sol#push()` function points out as follows.

>// ZivoeDAO MUST ensure "locker" has 0 allowance before this function concludes.

As a result, #L246 is performed to initialize the allocation for `locker` to 0.

On the other hand, let's look at the `safeDecreaseAllowance()` function of the `SafeERC20`.
```solidity
    function safeDecreaseAllowance(IERC20 token, address spender, uint256 requestedDecrease) internal {
        unchecked {
            uint256 currentAllowance = token.allowance(address(this), spender);
            if (currentAllowance < requestedDecrease) {
                revert SafeERC20FailedDecreaseAllowance(spender, currentAllowance, requestedDecrease);
            }
            forceApprove(token, spender, currentAllowance - requestedDecrease);
        }
    }
```

As you can see, `IERC20(asset).safeDecreaseAllowance(locker, 0)` preserves the locker allowance instead of initializing it to 0.
In Discord channel, @johnnyq agreed that if `safeIncreaseAllowance` is called but only part of it is sent during `pushToLocker` (e.g. 100,000 is acknowledged and only 80,000 is sent), there is a surplus allowance that needs to be reduced, and if the surplus allowance is not initialized to 0, the token may be incorrectly moved.

Therefore, this is unsafe.
## Impact
The token may be moved incorrectly because the allocation for `locker` is not initialized to 0.
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeDAO.sol#L239-L247
## Tool used

Manual Review

## Recommendation
Modify the `ZivoeDAO.sol#push()` function as follows.
```solidity
    function push(address locker, address asset, uint256 amount, bytes calldata data) external onlyOwner nonReentrant {
        require(IZivoeGlobals_DAO(GBL).isLocker(locker), "ZivoeDAO::push() !IZivoeGlobals_DAO(GBL).isLocker(locker)");
        require(ILocker_DAO(locker).canPush(), "ZivoeDAO::push() !ILocker_DAO(locker).canPush()");
        emit Pushed(locker, asset, amount, data);
        IERC20(asset).safeIncreaseAllowance(locker, amount);
        ILocker_DAO(locker).pushToLocker(asset, amount, data);
        // ZivoeDAO MUST ensure "locker" has 0 allowance before this function concludes.
---     if (IERC20(asset).allowance(address(this), locker) > 0) { IERC20(asset).safeDecreaseAllowance(locker, 0); }
+++     if (IERC20(asset).allowance(address(this), locker) > 0) { IERC20(asset).forceApprove(locker, 0); }
    }
```