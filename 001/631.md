Best Lava Dragon

medium

# OCL_ZVE uses 0% slippage tolerance when adding liquidity.

## Summary
The `pushToLockerMulti()` function of OCL_ZVE unexpectedly uses 0% slippage tolerance, which will result in almost all transactions reverting.
## Vulnerability Detail
When adding liquidity, OCL_ZVE locker increases allowance for the router by the balance of the contract. 
[OCL_ZVE.sol#L192-L195](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L192-L195)
```solidity
uint balPairAsset = IERC20(pairAsset).balanceOf(address(this));
uint balZVE = IERC20(ZVE).balanceOf(address(this));
IERC20(pairAsset).safeIncreaseAllowance(router, balPairAsset);
IERC20(ZVE).safeIncreaseAllowance(router, balZVE);
```
Later it calls `addLiquidity()` function on the router.
[OCL_ZVE.sol#L197-L206](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L197-L206)
```solidity
(uint256 depositedPairAsset, uint256 depositedZVE, uint256 minted) = IRouter_OCL_ZVE(
    router
).addLiquidity(
        pairAsset, // @audit-info tokenA
        ZVE, // @audit-info tokenB
        balPairAsset, // @audit-info amountA desired
        balZVE, // @audit-info amountB desired
        (balPairAsset * 9) / 10, // @audit-info amountAMin (90%)
        (balZVE * 9) / 10, // @audit-info amountBMin (90%)
        address(this), // @audit-info to
        block.timestamp + 14 days // @audit-info Deadline
    );
```
After the call to `addLiquidity()`, OCL_ZVE ensures that the whole allowance has been spent by the router, but in most cases that not going to happen, therefore transaction will fail.
[OCL_ZVE.sol#L208-L209](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L208-L209)
```solidity
 assert(IERC20(pairAsset).allowance(address(this), router) == 0);
 assert(IERC20(ZVE).allowance(address(this), router) == 0);
```

If were to look at the `addLiquidity()` function of the uniswap router, we would see that the actual amount transferred is not amountADesired and amountBdDesired, but the amount returned from `_addLiqudiity()`.
[UniswapV2Router02.sol#L61-L76](https://github.com/Uniswap/v2-periphery/blob/0335e8f7e1bd1e8d8329fd300aea2ef2f36dd19f/contracts/UniswapV2Router02.sol#L61-L76)
```solidity
(amountA, amountB) = _addLiquidity(tokenA, tokenB, amountADesired, amountBDesired, amountAMin, amountBMin);
address pair = UniswapV2Library.pairFor(factory, tokenA, tokenB);
TransferHelper.safeTransferFrom(tokenA, msg.sender, pair, amountA);
TransferHelper.safeTransferFrom(tokenB, msg.sender, pair, amountB);
```

[UniswapV2Router02.sol#L46-L59](https://github.com/Uniswap/v2-periphery/blob/0335e8f7e1bd1e8d8329fd300aea2ef2f36dd19f/contracts/UniswapV2Router02.sol#L46-L59)
```solidity
if (reserveA == 0 && reserveB == 0) {
            (amountA, amountB) = (amountADesired, amountBDesired);
        } else {
            uint amountBOptimal = UniswapV2Library.quote(amountADesired, reserveA, reserveB);
            if (amountBOptimal <= amountBDesired) {
                require(amountBOptimal >= amountBMin, 'UniswapV2Router: INSUFFICIENT_B_AMOUNT');
                (amountA, amountB) = (amountADesired, amountBOptimal);
            } else {
                uint amountAOptimal = UniswapV2Library.quote(amountBDesired, reserveB, reserveA);
                assert(amountAOptimal <= amountADesired);
                require(amountAOptimal >= amountAMin, 'UniswapV2Router: INSUFFICIENT_A_AMOUNT');
                (amountA, amountB) = (amountAOptimal, amountBDesired);
            }
        }
```
As we can see, the amountADesired and amountBDesired will only be transferred when pool is uninitalized, after that the transferred amount can be anything in the range of  `amountAMin <= amountA <= amountADesired` and  `amountBMin <= amountB <= amountBDesired`. 
While minAmount params are set to 90%, representing 10% slippage tolerance, the OCL_ZVE contract always expects router to transfer the entire balance, which would represent slippage tolerance of 0%. On the active market, such transactions will almost always revert, since having some slippage tolerance is required. 
## Impact
The `pushToLockerMulti()` function of OCL_ZVE cannot be used after pool initialization.
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L208-L209
## Tool used

Manual Review

## Recommendation
Instead of asserting allowance to be 0, check if it's greater than 0 after the call to `addLiquidity()` and decrease it to 0 if it's not.