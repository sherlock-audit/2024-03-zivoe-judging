Glorious Midnight Snake

medium

# Undistributed stable coin yield could be locked or distributed incorrectly when any one of parameters in `ZivoeYDL` is updated

## Summary
Undistributed yield rewards aren't distributed in advance when `ZivoeYDL#updateXXX()` is called, resulting in the undistributed yield being locked or distributed incorrectly later on.

## Vulnerability Detail
- Eligible users can borrow stable coin assets from Zivoe protocol. Any repaid interests and lateFee will be sent to `ZivoeYDL` or `OCT_YDL`
- The assets in `OCT_YDL` can be swapped to `distributedAsset` and sent to `ZivoeYDL` lately
- All `distributedAsset` yield can be distributed once every 30 days by calling [`ZivoeYDL#distributeYield()`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeYDL.sol#L213-L310).
- The distribution could be changed by updating parameters in `ZivoeYDL`:
  - `distributedAsset`: the stable coin asset to be distributed
  - `protocolEarningsRateBIPS`: The proportion of protocol earnings
  - `protocolRecipients`: the recipients who are eligible to receive protocol earnings
  - `residualRecipients`: the recipients who are eligible to receive residual earnings
  -  `targetAPYBIPS`: The target annualized yield for senior tranche.
  - `targetRatioBIPS`: The target ratio of junior to senior tranche.

However, a portion of the yield might not be distributed yet when any of above variables is updated, which could result in the undistributed yield being locked or distributed incorrectly later on.

Below POC can demonstrate that undistributed yield could be locked when `distributedAsset` is updated. For other parameters the POCs are almost same.
Copy below codes into [`Test_ZivoeYDL.sol`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-testing/src/TESTS_Core/Test_ZivoeYDL.sol) and run `forge test --rpc-url RPC-URL --match-test test_ZivoeYDL_distributeYield_perviousDistributionAssetLocked`:
```solidity
    function test_ZivoeYDL_distributeYield_perviousDistributionAssetLocked() public {
        // Simulating the ITO will "unlock" the YDL
        simulateITO_byTranche_optionalStake(1000 ether, true);
        
        hevm.startPrank(address(zvl));
        stSTT.addReward(USDC, 30 days);
        stJTT.addReward(USDC, 30 days);
        stZVE.addReward(USDC, 30 days);
        vestZVE.addReward(USDC, 30 days);
        hevm.stopPrank();

        //@audit-info make sure that `distributionAsset` is DAI
        assert(YDL.distributedAsset() == DAI);
        //@audit-info send 1000 ether DAI to YDL as yield  
        mint("DAI", address(YDL), 1000 ether);
        //@audit-info update distributionAsset from DAI to USDC
        vm.prank(ZivoeGlobals(YDL.GBL()).TLC());
        YDL.updateDistributedAsset(USDC);
        //@audit-info send 1000e6 USDC to YDL as yield
        mint("USDC", address(YDL), 1000e6);
        hevm.warp(YDL.lastDistribution() + YDL.daysBetweenDistributions() * 86400);
        //@audit-info call YDL.distributeYield()
        assert(bob.try_distributeYield(address(YDL)));
        //@audit-info DAI is locked
        assert(IERC20(DAI).balanceOf(address(YDL)) == 1000 ether);
        //@audit-info USDC is distributed
        assert(IERC20(USDC).balanceOf(address(YDL)) <= 1);
    }
```

## Impact
Eligible users might incur a loss on their yield reward.
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeYDL.sol#L356-L371
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeYDL.sol#L375-L386
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeYDL.sol#L392-L416
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeYDL.sol#L420-L424
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeYDL.sol#L428-L432
## Tool used

Manual Review

## Recommendation
Previous stable coin yield should be distributed immediately when any parameter in `ZivoeYDL` is updated:
```diff
    function distributeYield() external nonReentrant {
        require(unlocked, "ZivoeYDL::distributeYield() !unlocked"); 
        require(
            block.timestamp >= lastDistribution + daysBetweenDistributions * 86400, 
            "ZivoeYDL::distributeYield() block.timestamp < lastDistribution + daysBetweenDistributions * 86400"
        );
+       _distributeYield();
+   }
+   function _distributeYield() internal {
        // Calculate protocol earnings.
        uint256 earnings = IERC20(distributedAsset).balanceOf(address(this));
        uint256 protocolEarnings = protocolEarningsRateBIPS * earnings / BIPS;
        uint256 postFeeYield = earnings.floorSub(protocolEarnings);

        // Update timeline.
        distributionCounter += 1;
        lastDistribution = block.timestamp;

        // Calculate yield distribution (trancheuse = "slicer" in French).
        (
            uint256[] memory _protocol, uint256 _seniorTranche, uint256 _juniorTranche, uint256[] memory _residual
        ) = earningsTrancheuse(protocolEarnings, postFeeYield); 

        emit YieldDistributed(_protocol, _seniorTranche, _juniorTranche, _residual);
        
        // Update ema-based supply values.
        (uint256 aSTT, uint256 aJTT) = IZivoeGlobals_YDL(GBL).adjustedSupplies();
        emaSTT = MATH.ema(emaSTT, aSTT, retrospectiveDistributions.min(distributionCounter));
        emaJTT = MATH.ema(emaJTT, aJTT, retrospectiveDistributions.min(distributionCounter));

        // Distribute protocol earnings.
        for (uint256 i = 0; i < protocolRecipients.recipients.length; i++) {
            address _recipient = protocolRecipients.recipients[i];
            if (_recipient == IZivoeGlobals_YDL(GBL).stSTT() ||_recipient == IZivoeGlobals_YDL(GBL).stJTT()) {
                IERC20(distributedAsset).safeIncreaseAllowance(_recipient, _protocol[i]);
                IZivoeRewards_YDL(_recipient).depositReward(distributedAsset, _protocol[i]);
                emit YieldDistributedSingle(distributedAsset, _recipient, _protocol[i]);
            }
            else if (_recipient == IZivoeGlobals_YDL(GBL).stZVE()) {
                uint256 splitBIPS = (
                    IERC20(IZivoeGlobals_YDL(GBL).stZVE()).totalSupply() * BIPS
                ) / (
                    IERC20(IZivoeGlobals_YDL(GBL).stZVE()).totalSupply() + 
                    IERC20(IZivoeGlobals_YDL(GBL).vestZVE()).totalSupply()
                );
                uint stZVEAllocation = _protocol[i] * splitBIPS / BIPS;
                uint vestZVEAllocation = _protocol[i] * (BIPS - splitBIPS) / BIPS;
                IERC20(distributedAsset).safeIncreaseAllowance(IZivoeGlobals_YDL(GBL).stZVE(), stZVEAllocation);
                IERC20(distributedAsset).safeIncreaseAllowance(IZivoeGlobals_YDL(GBL).vestZVE(),vestZVEAllocation);
                IZivoeRewards_YDL(IZivoeGlobals_YDL(GBL).stZVE()).depositReward(distributedAsset, stZVEAllocation);
                IZivoeRewards_YDL(IZivoeGlobals_YDL(GBL).vestZVE()).depositReward(distributedAsset, vestZVEAllocation);
                emit YieldDistributedSingle(distributedAsset, IZivoeGlobals_YDL(GBL).stZVE(), stZVEAllocation);
                emit YieldDistributedSingle(distributedAsset, IZivoeGlobals_YDL(GBL).vestZVE(), vestZVEAllocation);
            }
            else {
                IERC20(distributedAsset).safeTransfer(_recipient, _protocol[i]);
                emit YieldDistributedSingle(distributedAsset, _recipient, _protocol[i]);
            }
        }

        // Distribute senior and junior tranche earnings.
        IERC20(distributedAsset).safeIncreaseAllowance(IZivoeGlobals_YDL(GBL).stSTT(), _seniorTranche);
        IERC20(distributedAsset).safeIncreaseAllowance(IZivoeGlobals_YDL(GBL).stJTT(), _juniorTranche);
        IZivoeRewards_YDL(IZivoeGlobals_YDL(GBL).stSTT()).depositReward(distributedAsset, _seniorTranche);
        IZivoeRewards_YDL(IZivoeGlobals_YDL(GBL).stJTT()).depositReward(distributedAsset, _juniorTranche);
        emit YieldDistributedSingle(distributedAsset, IZivoeGlobals_YDL(GBL).stSTT(), _seniorTranche);
        emit YieldDistributedSingle(distributedAsset, IZivoeGlobals_YDL(GBL).stJTT(), _juniorTranche);

        // Distribute residual earnings.
        for (uint256 i = 0; i < residualRecipients.recipients.length; i++) {
            if (_residual[i] > 0) {
                address _recipient = residualRecipients.recipients[i];
                if (_recipient == IZivoeGlobals_YDL(GBL).stSTT() ||_recipient == IZivoeGlobals_YDL(GBL).stJTT()) {
                    IERC20(distributedAsset).safeIncreaseAllowance(_recipient, _residual[i]);
                    IZivoeRewards_YDL(_recipient).depositReward(distributedAsset, _residual[i]);
                    emit YieldDistributedSingle(distributedAsset, _recipient, _protocol[i]);
                }
                else if (_recipient == IZivoeGlobals_YDL(GBL).stZVE()) {
                    uint256 splitBIPS = (
                        IERC20(IZivoeGlobals_YDL(GBL).stZVE()).totalSupply() * BIPS
                    ) / (
                        IERC20(IZivoeGlobals_YDL(GBL).stZVE()).totalSupply() + 
                        IERC20(IZivoeGlobals_YDL(GBL).vestZVE()).totalSupply()
                    );
                    uint stZVEAllocation = _residual[i] * splitBIPS / BIPS;
                    uint vestZVEAllocation = _residual[i] * (BIPS - splitBIPS) / BIPS;
                    IERC20(distributedAsset).safeIncreaseAllowance(IZivoeGlobals_YDL(GBL).stZVE(), stZVEAllocation);
                    IERC20(distributedAsset).safeIncreaseAllowance(IZivoeGlobals_YDL(GBL).vestZVE(), vestZVEAllocation);
                    IZivoeRewards_YDL(IZivoeGlobals_YDL(GBL).stZVE()).depositReward(distributedAsset, stZVEAllocation);
                    IZivoeRewards_YDL(IZivoeGlobals_YDL(GBL).vestZVE()).depositReward(distributedAsset, vestZVEAllocation);
                    emit YieldDistributedSingle(distributedAsset, IZivoeGlobals_YDL(GBL).stZVE(), stZVEAllocation);
                    emit YieldDistributedSingle(distributedAsset, IZivoeGlobals_YDL(GBL).vestZVE(), vestZVEAllocation);
                }
                else {
                    IERC20(distributedAsset).safeTransfer(_recipient, _residual[i]);
                    emit YieldDistributedSingle(distributedAsset, _recipient, _residual[i]);
                }
            }
        }
    }

    function updateDistributedAsset(address _distributedAsset) external nonReentrant {
        require(
            _distributedAsset != distributedAsset, 
            "ZivoeYDL::updateDistributedAsset() _distributedAsset == distributedAsset"
        );
        require(
            _msgSender() == IZivoeGlobals_YDL(GBL).TLC(), 
            "ZivoeYDL::updateDistributedAsset() _msgSender() != TLC()"
        );
        require(
            IZivoeGlobals_YDL(GBL).stablecoinWhitelist(_distributedAsset),
            "ZivoeYDL::updateDistributedAsset() !IZivoeGlobals_YDL(GBL).stablecoinWhitelist(_distributedAsset)"
        );
        emit UpdatedDistributedAsset(distributedAsset, _distributedAsset);
+       _distributeYield();
        distributedAsset = _distributedAsset;
    }

    function updateProtocolEarningsRateBIPS(uint256 _protocolEarningsRateBIPS) external {
        require(
            _msgSender() == IZivoeGlobals_YDL(GBL).TLC(), 
            "ZivoeYDL::updateProtocolEarningsRateBIPS() _msgSender() != TLC()"
        );
        require(
            _protocolEarningsRateBIPS <= 9000, 
            "ZivoeYDL::updateProtocolEarningsRateBIPS() _protocolEarningsRateBIPS > 9000"
        );
        emit UpdatedProtocolEarningsRateBIPS(protocolEarningsRateBIPS, _protocolEarningsRateBIPS);
+       _distributeYield();
        protocolEarningsRateBIPS = _protocolEarningsRateBIPS;
    }

    function updateRecipients(address[] memory recipients, uint256[] memory proportions, bool protocol) external {
        require(_msgSender() == IZivoeGlobals_YDL(GBL).TLC(), "ZivoeYDL::updateRecipients() _msgSender() != TLC()");
        require(
            recipients.length == proportions.length && recipients.length > 0, 
            "ZivoeYDL::updateRecipients() recipients.length != proportions.length || recipients.length == 0"
        );
        require(unlocked, "ZivoeYDL::updateRecipients() !unlocked");

        uint256 proportionTotal;
        for (uint256 i = 0; i < recipients.length; i++) {
            proportionTotal += proportions[i];
            require(proportions[i] > 0, "ZivoeYDL::updateRecipients() proportions[i] == 0");
            require(recipients[i] != address(0), "ZivoeYDL::updateRecipients() recipients[i] == address(0)");
        }

        require(proportionTotal == BIPS, "ZivoeYDL::updateRecipients() proportionTotal != BIPS (10,000)");
+       _distributeYield();
        if (protocol) {
            emit UpdatedProtocolRecipients(recipients, proportions);
            protocolRecipients = Recipients(recipients, proportions);
        }
        else {
            emit UpdatedResidualRecipients(recipients, proportions);
            residualRecipients = Recipients(recipients, proportions);
        }
    }

    function updateTargetAPYBIPS(uint256 _targetAPYBIPS) external {
        require(_msgSender() == IZivoeGlobals_YDL(GBL).TLC(), "ZivoeYDL::updateTargetAPYBIPS() _msgSender() != TLC()");
        emit UpdatedTargetAPYBIPS(targetAPYBIPS, _targetAPYBIPS);
+       _distributeYield();
        targetAPYBIPS = _targetAPYBIPS;
    }

    function updateTargetRatioBIPS(uint256 _targetRatioBIPS) external {
        require(_msgSender() == IZivoeGlobals_YDL(GBL).TLC(), "ZivoeYDL::updateTargetRatioBIPS() _msgSender() != TLC()");
        emit UpdatedTargetRatioBIPS(targetRatioBIPS, _targetRatioBIPS);
+       _distributeYield();
        targetRatioBIPS = _targetRatioBIPS;
    }
```