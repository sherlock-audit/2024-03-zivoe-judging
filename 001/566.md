Elegant Brunette Llama

medium

# ZivoeDAO incorrectly decreases allowance after pushing to the lockers

## Summary

The protocol is meant to work with the USDT token, which requires to always remove the prior allowance before setting a new one. This is known, however, it is wrongly implemented.

## Vulnerability Detail

If we look at `ZivoeDAO::push()` we can see that it pushes a certain amount of a chosen asset to the locker by calling `locker.pushToLocker()`.
```solidity
    function push(address locker, address asset, uint256 amount, bytes calldata data) external onlyOwner nonReentrant {
        require(IZivoeGlobals_DAO(GBL).isLocker(locker), "ZivoeDAO::push() !IZivoeGlobals_DAO(GBL).isLocker(locker)");
        require(ILocker_DAO(locker).canPush(), "ZivoeDAO::push() !ILocker_DAO(locker).canPush()");
        emit Pushed(locker, asset, amount, data);
        IERC20(asset).safeIncreaseAllowance(locker, amount);
        ILocker_DAO(locker).pushToLocker(asset, amount, data);
        // ZivoeDAO MUST ensure "locker" has 0 allowance before this function concludes.
        if (IERC20(asset).allowance(address(this), locker) > 0) { IERC20(asset).safeDecreaseAllowance(locker, 0); }
    }
``` 

After that call we need to make sure the allowance of the locker is set back to 0, becasue USDT requires it. But the call `IERC20(asset).safeDecreaseAllowance(locker, 0);` does nothing. It does not decrease the allowance to 0 as expected, but decreases the allowance BY 0, meaning the allowance stays the same after that call. Here we can see the `safeDecreaseAllowance` function used:
```solidity
function safeDecreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }
```

We can clearly see that the new allowance is calculated by subtracting the old one with the value provided. In our case the value provided is 0 meaning the allowance stays the same.

This is quite problematic because if the locker doesn't consume all of its allowance, the next call will revert if it involves USDT tokens. Remember USDT is a token that was explicitly mentioned by the protocol that they plan to work with.

## Impact

Leaves all of the USDT tokens stuck in `ZivoeDAO`.

## Code Snippet

https://github.com/Zivoe/zivoe-core-foundry/blob/master/src/ZivoeDAO.sol#L246
https://github.com/Zivoe/zivoe-core-foundry/blob/master/src/ZivoeDAO.sol#L299

## Tool used

Manual Review

## Recommendation

To resolve this issue the decreased amount should be equal to the current allowance of the locker instead of 0.

```diff
    function push(address locker, address asset, uint256 amount, bytes calldata data) external onlyOwner nonReentrant {
        require(IZivoeGlobals_DAO(GBL).isLocker(locker), "ZivoeDAO::push() !IZivoeGlobals_DAO(GBL).isLocker(locker)");
        require(ILocker_DAO(locker).canPush(), "ZivoeDAO::push() !ILocker_DAO(locker).canPush()");
        emit Pushed(locker, asset, amount, data);
        IERC20(asset).safeIncreaseAllowance(locker, amount);
        ILocker_DAO(locker).pushToLocker(asset, amount, data);
        // ZivoeDAO MUST ensure "locker" has 0 allowance before this function concludes.
-        if (IERC20(asset).allowance(address(this), locker) > 0) { IERC20(asset).safeDecreaseAllowance(locker, 0); }
+       if (IERC20(asset).allowance(address(this), locker) > 0) { IERC20(asset).safeDecreaseAllowance(locker, IERC20(asset).allowance(address(this), locker)); }
    }

    function pushMulti(
        address locker, address[] calldata assets, uint256[] calldata amounts, bytes[] calldata data
    ) external onlyOwner nonReentrant {
        require(
            IZivoeGlobals_DAO(GBL).isLocker(locker), 
            "ZivoeDAO::pushMulti() !IZivoeGlobals_DAO(GBL).isLocker(locker)"
        );
        require(assets.length == amounts.length, "ZivoeDAO::pushMulti() assets.length != amounts.length");
        require(amounts.length == data.length, "ZivoeDAO::pushMulti() amounts.length != data.length");
        require(ILocker_DAO(locker).canPushMulti(), "ZivoeDAO::pushMulti() !ILocker_DAO(locker).canPushMulti()");
        for (uint256 i = 0; i < assets.length; i++) {
            IERC20(assets[i]).safeIncreaseAllowance(locker, amounts[i]);
            emit Pushed(locker, assets[i], amounts[i], data[i]);
        }
        ILocker_DAO(locker).pushToLockerMulti(assets, amounts, data);
        for (uint256 i = 0; i < assets.length; i++) {
            // ZivoeDAO MUST ensure "locker" has 0 allowance for each ERC20 token before this function concludes.
-            if (IERC20(assets[i]).allowance(address(this), locker) > 0) { IERC20(assets[i]).safeDecreaseAllowance(locker, 0); }
+       if (IERC20(asset).allowance(address(this), locker) > 0) { IERC20(asset).safeDecreaseAllowance(locker, IERC20(asset).allowance(address(this), locker)); }
        }
    }
```