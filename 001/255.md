Bald Chocolate Hippo

medium

# `SafeDecreaseAllowance` will not set the allowance to 0 as expected.

## Summary

When using `safeDecreaseAllowance` in a ZivoeDAO to decrease the allowance to 0, the allowance will remain unchanged due to the way `safeDecreaseAllowance` works.
## Vulnerability Detail
`push` functions are used in `ZivoeDAO` to send the tokens to particular lockers. It tries to decrease the token allowance from DAO to locker 0 after the transfer is complete using the `safeDecreaseAllowance`

https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeDAO.sol#L246

```solidity
 function push(
        address locker,
        address asset,
        uint256 amount,
        bytes calldata data
    ) external onlyOwner nonReentrant {
        require(
            IZivoeGlobals_DAO(GBL).isLocker(locker),
            "ZivoeDAO::push() !IZivoeGlobals_DAO(GBL).isLocker(locker)"
        );
        require(
            ILocker_DAO(locker).canPush(),
            "ZivoeDAO::push() !ILocker_DAO(locker).canPush()"
        );
        emit Pushed(locker, asset, amount, data);
        IERC20(asset).safeIncreaseAllowance(locker, amount);
        ILocker_DAO(locker).pushToLocker(asset, amount, data);
        // ZivoeDAO MUST ensure "locker" has 0 allowance before this function concludes.

        if (IERC20(asset).allowance(address(this), locker) > 0) {
->>>      IERC20(asset).safeDecreaseAllowance(locker, 0);
        }
    }
```

The function `safeDecreaseAllowance` attempts to decrease the current allowance by the specified amount and sets the result as the new allowance. 

For instance, consider a scenario where the remaining allowance is 100, and `safeDecreaseAllowance` is called with a value of 0. In this case, the function tries to subtract 0 from 100, which results in 100 - 0 = 100. As a result, the new allowance remains the same.

https://github.com/OpenZeppelin/openzeppelin-contracts/blob/4e5b11919e91b18b6683b6f49a1b4fdede579969/contracts/token/ERC20/utils/SafeERC20.sol#L70C5-L81C6

```solidity
function safeDecreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
    ->>>    uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }
```
## Impact
1. The desired functionality of the system will break. 
2. The system uses USDT which has a two-step approval.  If the allowance is not decreased to 0 after the transaction, another `approve` call will fail resulting in the DOS of the system. 

https://github.com/d-xo/weird-erc20#approval-race-protections
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeDAO.sol#L246

https://github.com/OpenZeppelin/openzeppelin-contracts/blob/4e5b11919e91b18b6683b6f49a1b4fdede579969/contracts/token/ERC20/utils/SafeERC20.sol#L70C5-L81C6


## Tool used

Manual Review

## Recommendation
Instead of using `safeDecreaseAllowance` , Use the approve function to set the approval to 0 after the tx. 

```diff
 function push(
        address locker,
        address asset,
        uint256 amount,
        bytes calldata data
    ) external onlyOwner nonReentrant {
        require(
            IZivoeGlobals_DAO(GBL).isLocker(locker),
            "ZivoeDAO::push() !IZivoeGlobals_DAO(GBL).isLocker(locker)"
        );
        require(
            ILocker_DAO(locker).canPush(),
            "ZivoeDAO::push() !ILocker_DAO(locker).canPush()"
        );
        emit Pushed(locker, asset, amount, data);
        IERC20(asset).safeIncreaseAllowance(locker, amount);
        ILocker_DAO(locker).pushToLocker(asset, amount, data);
        // ZivoeDAO MUST ensure "locker" has 0 allowance before this function concludes.

        //@audit This functions fails for USDT
        if (IERC20(asset).allowance(address(this), locker) > 0) {
-            IERC20(asset).safeDecreaseAllowance(locker, 0);
+            IERC20(asset).approve(locker, 0);
        }
    }
```


