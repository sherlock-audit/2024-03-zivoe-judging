Quaint Brick Troll

high

# Mitigation in ZivoeDao to be compatible with USDT is implemented wrongly.

## Summary
Since USDT requires to approve 0 first to change the approval if there is any existing approvals, the contract tries to mitigate this by setting the allowance back to 0 if there is any after all the necessary transactions are done. This can be seen in both the last lines in [```push```](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeDAO.sol#L246C9-L246C116) and [```pushMulti```](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeDAO.sol#L299C13-L299C128) functions.

## Vulnerability Detail
If these last lines were not implemented, the contract would be able to push USDT the first time and revert next time if there are remaining approvals. The contract assumes to be mitigating this but because it is done incorrectly it will revert from the first time not allowing the contract to push tokens even at the first call.
If we look at the last line of the functions, it calls ```safeDecreaseAllowance(locker, 0)``` with 0 which is the same as decreasing 0 amount of allowance and approving the same remaining allowance.
We can see that ```safeDecreaseAllowance``` when call with 0 as value will try to approve the same oldAllowance:
```solidity
function safeDecreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }
```

## Impact
Not able to handle USDT incase there is leftover allowance which will revert the transaction because we approve the same amount before approving 0. 

## Code Snippet
```solidity
function push(address locker, address asset, uint256 amount, bytes calldata data) external onlyOwner nonReentrant {
        require(IZivoeGlobals_DAO(GBL).isLocker(locker), "ZivoeDAO::push() !IZivoeGlobals_DAO(GBL).isLocker(locker)");
        require(ILocker_DAO(locker).canPush(), "ZivoeDAO::push() !ILocker_DAO(locker).canPush()");
        emit Pushed(locker, asset, amount, data);
        IERC20(asset).safeIncreaseAllowance(locker, amount);
        ILocker_DAO(locker).pushToLocker(asset, amount, data);
        // ZivoeDAO MUST ensure "locker" has 0 allowance before this function concludes.
        if (IERC20(asset).allowance(address(this), locker) > 0) { IERC20(asset).safeDecreaseAllowance(locker, 0); }
    }
```

## Tool used

Manual Review

## Recommendation
change the last line in ```push``` to : 
```diff

-      if (IERC20(asset).allowance(address(this), locker) > 0) { IERC20(asset).safeDecreaseAllowance(locker, 0); }

+      if (IERC20(asset).allowance(address(this), locker) > 0) { IERC20(asset).safeDecreaseAllowance(locker, IERC20(asset).allowance(address(this), locker)); }

```

and ```pushMulti``` to :
```diff

-      if (IERC20(assets[i]).allowance(address(this), locker) > 0) { IERC20(assets[i]).safeDecreaseAllowance(locker, 0); }

+      if (IERC20(assets[i]).allowance(address(this), locker) > 0) { IERC20(assets[i]).safeDecreaseAllowance(locker, IERC20(assets[i]).allowance(address(this), locker)); }

```
