Petite Paisley Newt

medium

# Allowance can still be non-zero when it is expected to be zero

## Summary

According to [the inline docs](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeDAO.sol#L245), `asset` allowance from `DAO` to the `locker` must be 0 at the end of the call. However, this might not be true due to the incorrect argument being passed.

## Vulnerability Detail

During [the push call](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeDAO.sol#L239), the contract must first [increase allowance](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeDAO.sol#L243) to the locker before calling `pushToLocker`. After the call, it checks whether there is [still some allowance](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeDAO.sol#L246). As outlined above, the expectation is to have 0 allowance at the end of the call. If the allowance is greater than 0, the contract will [call](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeDAO.sol#L246) `IERC20(asset).safeDecreaseAllowance(locker, 0)`. This means that the allowance will be **decreased** by 0, not **set** to 0. Therefore, the allowance might not be 0 as expected.

The same mistake appears on [this line](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeDAO.sol#L299).

## Impact

An excess token allowance might remain despite the expectation of zero allowance.

## Code Snippet

In this test, we will simulate a locker that does not spend all the tokens supplied to the `push` call. We can see that the allowance is not 0 at the end, as expected.

```Solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.17;

import "../Utility/Utility.sol";

import "../../lib/zivoe-core-foundry/src/ZivoeLocker.sol";
import "../../lib/zivoe-core-foundry/lib/openzeppelin-contracts/contracts/token/ERC20/IERC20.sol";
import "../../lib/zivoe-core-foundry/lib/openzeppelin-contracts/contracts/token/ERC20/utils/SafeERC20.sol";

contract NewLocker is ZivoeLocker {
    using SafeERC20 for IERC20;

    constructor(address dao) {
        transferOwnership(dao);
    }

    function canPush() public pure override returns (bool) {
        return true;
    }

    // this locker will only pull 90% of the amount defined
    function pushToLocker(
        address asset,
        uint256 amount,
        bytes calldata
    ) external override onlyOwner {
        require(canPush(), "ZivoeLocker::pushToLocker() !canPush()");

        uint amountAdjusted = (amount * 90) / 100;

        IERC20(asset).safeTransferFrom(owner(), address(this), amountAdjusted);
    }
}

contract TestAllowance is Utility {
    NewLocker locker;

    function setUp() public {
        deployCore(false);

        locker = new NewLocker(address(DAO));

        zvl.try_updateIsLocker(address(GBL), address(locker), true);
    }

    function test_incorrectAllowance() public {
        uint amountDAI = 1e24;

        deal(DAI, address(DAO), amountDAI);

        // before the push the allowance is 0 as expected
        assertEq(IERC20(DAI).allowance(address(DAO), address(locker)), 0);

        // push to new locker
        hevm.prank(address(god));
        DAO.push(address(locker), DAI, amountDAI, "");

        // locker was not set to 0 as expected
        assertTrue(IERC20(DAI).allowance(address(DAO), address(locker)) > 0);
    }
}
```

## Tool used

Foundry, Manual review

## Recommendation

Change the parameter to the correct one.

```diff
        if (IERC20(asset).allowance(address(this), locker) > 0) {
-           IERC20(asset).safeDecreaseAllowance(locker, 0);
+           IERC20(asset).safeDecreaseAllowance(locker, IERC20(asset).allowance(address(this), locker));
        }
```
