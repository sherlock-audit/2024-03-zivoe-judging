Clumsy Cobalt Lion

medium

# `DAO.safeDecreaseAllowance` does not decrease the allowance

## Summary
After each push to a locker, the DAO must ensure that the allowance of the locker to spend the given token is set to 0. Otherwise, some tokens, like USDT, will revert on next push because they prohibit changing a non-zero allowance to a non-zero allowance. The DAO contract uses `safeDecreaseAllowance(0)` which decreases the allowance with 0 instead of setting it to 0.

## Vulnerability Detail
[ZivoeDAO.push()](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeDAO.sol#L246) and [ZivoeDAO.pushMulti()](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeDAO.sol#L299) must ensure that after pushing, the allowance of the locker to spend the pushed asset is 0. 

```solidity
IERC20(assets[i]).safeDecreaseAllowance(locker, 0);
```
The code assumes that `safeDescreaseAllowance` sets the allowance to 0, but it will actually decrease it by 0, which does nothing.
```solidity
    function safeDecreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }
```

As a result, the locker's allowance may stay a non-zero value after the push concludes. This will result in a revert for a subsequent pushes of the aforementioned stablecoins.

## Impact
Medium, since it requires the use of a weird stablecoin
## Code Snippet
```solidity
        // ZivoeDAO MUST ensure "locker" has 0 allowance before this function concludes.
        if (IERC20(asset).allowance(address(this), locker) > 0) { IERC20(asset).safeDecreaseAllowance(locker, 0); }
```
```solidity
        for (uint256 i = 0; i < assets.length; i++) {
            // ZivoeDAO MUST ensure "locker" has 0 allowance for each ERC20 token before this function concludes.
            if (IERC20(assets[i]).allowance(address(this), locker) > 0) { IERC20(assets[i]).safeDecreaseAllowance(locker, 0); }
        }
```
## Tool used

Manual Review

## Recommendation
```diff
        // ZivoeDAO MUST ensure "locker" has 0 allowance before this function concludes.
-        if (IERC20(asset).allowance(address(this), locker) > 0) { IERC20(asset).safeDecreaseAllowance(locker, 0); }
+      uint256 allowance = IERC20(asset).allowance(address(this), locker);
+      if (allowance > 0) { IERC20(asset).safeDecreaseAllowance(locker, allowance); }
```
```diff
        for (uint256 i = 0; i < assets.length; i++) {
            // ZivoeDAO MUST ensure "locker" has 0 allowance for each ERC20 token before this function concludes.
-           if (IERC20(assets[i]).allowance(address(this), locker) > 0) { IERC20(assets[i]).safeDecreaseAllowance(locker, 0); }
+           uint256 allowance = IERC20(assets[i]).allowance(address(this), locker);
+          if (allowance  > 0) { IERC20(assets[i]).safeDecreaseAllowance(locker, allowance ); }
        }
```
