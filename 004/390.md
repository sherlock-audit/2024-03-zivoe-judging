Best Lava Dragon

high

# A user can escape paying interest for some of the payment intervals.

## Summary
A user can escape paying interest for some of the payment intervals.
## Vulnerability Detail
The interest for a loan is calculated based on the payment interval of the loan. A loan that missed a payment, has up to a grace period to make a repayment before it enters a defaulted state. 
As per the [docs](https://docs.zivoe.com/user-docs/borrowers/how-do-defaults-work)
> In some cases, the grace period may be longer than the payment interval, and the borrower may miss several loan payments before a loan enters default. In such an event, the borrower must resolve each missed payment before late fees stop accruing. 

If a user has more than one unpaid intervals he can escape paying interest for some of them.

This can happen in two cases:
* By invoking `callLoan()` function to pay off the loan in full.
* By invoking `applyCombine()` function to combine multiple of his loans into one.

When a loan is called the interest and the principal to pay is calculated by `amountOwed()` function.
[OCC_Modular.sol#L500](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L500)
```solidity
(, uint256 interestOwed, uint256 lateFee, ) = amountOwed(id);
```
[OCC_Modular.sol#L455](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L455)
```solidity
interest =
    (loans[id].principalOwed *
        loans[id].paymentInterval *
        loans[id].APR) /
    (86400 * 365 * BIPS);
total = principal + interest + lateFee;
```
The problem is that `amountOwed()` does not account for a case where a loan has to make multiple payments, it will always return the interest just for the last payment interval. Therefore a user will only pay for one of the intervals before fully closing his loan.
[OCC_Modular.sol#L505-L518](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L505-L518)
```solidity
// Transfer interest to YDL if in same format, otherwise keep here for 1INCH forwarding.
if (
    stablecoin ==
    IZivoeYDL_OCC(IZivoeGlobals_OCC(GBL).YDL()).distributedAsset()
) {
    IERC20(stablecoin).safeTransferFrom(
        _msgSender(),
        IZivoeGlobals_OCC(GBL).YDL(),
        interestOwed + lateFee
    );
} else {
    IERC20(stablecoin).safeTransferFrom(
        _msgSender(),
        OCT_YDL,
        interestOwed + lateFee
    );
}
IERC20(stablecoin).safeTransferFrom(
    _msgSender(),
    owner(),
    principalOwed
);
loans[id].principalOwed = 0;
loans[id].paymentDueBy = 0;
loans[id].paymentsRemaining = 0;
loans[id].state = LoanState.Repaid;
```

The same issue can arise when a loan that has to make multiple payments is combined with another loan. In such case, the interest for unpaid intervals is omitted and not transferred to a newly combined loan.
## Impact
A user can escape paying the interest for some of the payment intervals.

## Proof Of Concept
Please add this function to Test_OCC_Modular.sol.
```solidity
function test_OCC_Modular_calling_loan_interest_lost() public {
    mint("DAI", address(tim), 1000e18);
    tim.try_approveToken(DAI, address(OCC_Modular_DAI), 1000e18);
    // uint256 borrowAmount = 100e18;
    uint256 APR = 5000;
    uint256 APRLateFee = 1000;
    uint256 term = 5;
    uint256 paymentInterval = 7 days;
    uint256 gracePeriod = 21 days;
    int8 paymentSchedule = 1;
    // Create loan
    vm.startPrank(address(roy));
    OCC_Modular_DAI.createOffer(
        address(tim),
        100e18 /* borrow amount */,
        APR,
        APRLateFee,
        term,
        paymentInterval,
        gracePeriod,
        paymentSchedule
    );
    vm.stopPrank();
    vm.prank(address(tim));
    OCC_Modular_DAI.acceptOffer(0);
    uint256 snapshot = vm.snapshot();
    // calculate the combined interest for 3 intervals
    vm.warp(block.timestamp + 20 days);
    (, uint256 interest, , ) = OCC_Modular_DAI.amountOwed(0);
    vm.prank(address(tim));
    OCC_Modular_DAI.makePayment(0);
    (, uint256 interestNew, , ) = OCC_Modular_DAI.amountOwed(0);
    vm.prank(address(tim));
    OCC_Modular_DAI.makePayment(0);
    (, uint256 interestNew1, , ) = OCC_Modular_DAI.amountOwed(0);
    vm.prank(address(tim));
    OCC_Modular_DAI.makePayment(0);
    uint256 combinedInterest = interest + interestNew + interestNew1;
    console2.log("Actual interest: ", combinedInterest);
    // Revert to state when loan is not paid before calling
    vm.revertTo(snapshot);
    uint256 balanceBefore = ERC20(DAI).balanceOf(address(tim));
    vm.prank(address(tim));
    OCC_Modular_DAI.callLoan(0);
    uint256 balanceAfter = ERC20(DAI).balanceOf(address(tim));
    console2.log(
        "Paid interest:   ",
        balanceBefore - 100e18 - balanceAfter
    );
}
```
The console output of the test:
```md
Logs:
  Actual interest:  2301369863013698628
  Paid interest:     958904109589041095
```
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L492-L529
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L742-L808
## Tool used

Manual Review

## Recommendation
In my opinion, the easiest solution that would not require changing the way interest is calculated is to not allow calling or combining a loan when `block.timestamp - paymentDueBy > 0`, since that would mean that the loan currently has a missing payment and is in a grace period.