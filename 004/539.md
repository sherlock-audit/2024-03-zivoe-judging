Decent Chiffon Wolf

medium

# `APRLate` must be greater than `APR` when `Loan.term = 1` in `OOC_Modular`.

## Summary

In Zivoe, loans carry a base interest rate, and an additional late fee interest rate that is applied when a payment is missed. When `loan.term = 1`, a borrower repays interest rate for `paymentInterval` and late fee interest rate for `gracePeriod`. In this scenario, if `APRLate` is set smaller than `APR`, the borrower repays less than the amount that he should pay in fact.

## Vulnerability Detail
When borrowers make a payment to the protocol using the [makePayment](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L575) function, they are required to pay the `prinicipalOwed`, the `interestOwed` and the `lateFee`, all of which are calculated at [L578](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L578).

```solidity
File: zivoe-core-foundry\src\lockers\OCC\OCC_Modular.sol
575:     function makePayment(uint256 id) external nonReentrant {
576:         require(loans[id].state == LoanState.Active, "OCC_Modular::makePayment() loans[id].state != LoanState.Active");
577: 
578:         (uint256 principalOwed, uint256 interestOwed, uint256 lateFee,) = amountOwed(id);
579: 
580:         emit PaymentMade(
581:             id, _msgSender(), principalOwed + interestOwed + lateFee, principalOwed,
582:             interestOwed, lateFee, loans[id].paymentDueBy + loans[id].paymentInterval
583:         );
584: 
585:         // Transfer interest + lateFee to YDL if in same format, otherwise keep here for 1INCH forwarding.
586:         if (stablecoin == IZivoeYDL_OCC(IZivoeGlobals_OCC(GBL).YDL()).distributedAsset()) {
587:             IERC20(stablecoin).safeTransferFrom(_msgSender(), IZivoeGlobals_OCC(GBL).YDL(), interestOwed + lateFee);
588:         }
589:         else {
590:             IERC20(stablecoin).safeTransferFrom(_msgSender(), OCT_YDL, interestOwed + lateFee);
591:         }
592:         if (principalOwed > 0) { IERC20(stablecoin).safeTransferFrom(_msgSender(), owner(), principalOwed); }
593: 
594:         if (loans[id].paymentsRemaining == 1) {
595:             loans[id].state = LoanState.Repaid;
596:             loans[id].paymentDueBy = 0;
597:         }
598:         else { loans[id].paymentDueBy += loans[id].paymentInterval; }
599: 
600:         loans[id].principalOwed -= principalOwed;
601:         loans[id].paymentsRemaining -= 1;
602:     }
```
The `prinicipalOwed`, `interestOwed`, and `lateFee` are calculated within the [amountOwed](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L440) function as follows:

```solidity
zivoe-core-foundry\src\lockers\OCC\OCC_Modular.sol
440: function amountOwed(uint256 id) public view returns (
441:         uint256 principal, uint256 interest, uint256 lateFee, uint256 total
442:     ) {
443:         // 0 == Bullet.
444:         if (loans[id].paymentSchedule == 0) {
445:             if (loans[id].paymentsRemaining == 1) { principal = loans[id].principalOwed; }
446:         }
447:         // 1 == Amortization (only two options, use else here).
448:         else { principal = loans[id].principalOwed / loans[id].paymentsRemaining; }
449: 
450:         // Add late fee if past loans[id].paymentDueBy.
451:         if (block.timestamp > loans[id].paymentDueBy && loans[id].state == LoanState.Active) {
452:             lateFee = loans[id].principalOwed * (block.timestamp - loans[id].paymentDueBy) * //@audit late fee
453:                 loans[id].APRLateFee / (86400 * 365 * BIPS);
454:         }
455:         interest = loans[id].principalOwed * loans[id].paymentInterval * loans[id].APR / (86400 * 365 * BIPS); //@audit interest of the interval
456:         total = principal + interest + lateFee;
457:     }
```
As shown in the previous code snippet, the `lateFee` is calculated based on the difference between the current timestamp and the `paymentDueBy` timestamp. Additionally, the `interest` is calculated for the duration of the `paymentInterval`.

Let's consider the following scenario:
- Alice has a bullet loan with a grace period of 10 days and a payment interval of 14 days. The loan term is 1 periods. `APR` of the loan is `10%`, `APRLate` of the loan is `5%`. 
- After 24 days, Alice is required to pay principal amount, the interest accrued over 14 days, and the late fees accumulated over 10 days.

However, in fact she should be required to pay interest accrued over 24 days because `term = 1`.  She doesn't pay interest accrued over 10 days with interest rate of `10 - 5 = 5%`. The action by Alice results in a financial loss to the protocol.

## Impact

When `loan.term = 1`, `APRLate > APR`, it causes a financial loss to the protocol. 

## Tool used

Manual Review

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L540-L570

## Recommendation

In the [OOC_Modular.createOffer](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L540) function, `APRLateFee` should be greater than `APR` when `term = 1`.

```diff
File: zivoe-core-foundry\src\lockers\OCC\OCC_Modular.sol

File: zivoe-core-foundry\src\lockers\OCC\OCC_Modular.sol
540:     function createOffer(
541:         address borrower,
542:         uint256 borrowAmount,
543:         uint256 APR,
544:         uint256 APRLateFee,
545:         uint256 term,
546:         uint256 paymentInterval,
547:         uint256 gracePeriod,
548:         int8 paymentSchedule
549:     ) isUnderwriter external {
550:         require(term > 0, "OCC_Modular::createOffer() term == 0");
551:         require(
552:             paymentInterval == 86400 * 7 || paymentInterval == 86400 * 14 || paymentInterval == 86400 * 28 || 
553:             paymentInterval == 86400 * 91 || paymentInterval == 86400 * 364, 
554:             "OCC_Modular::createOffer() invalid paymentInterval value, try: 86400 * (7 || 14 || 28 || 91 || 364)"
555:         );
556:         require(gracePeriod >= 7 days, "OCC_Modular::createOffer() gracePeriod < 7 days");
557:         require(paymentSchedule <= 1, "OCC_Modular::createOffer() paymentSchedule > 1");
+            if (term == 1) {
+                 require(
+                     APRLateFee >= APR,
+                     "OCC_Modular::acceptOffer() APRLateFee < APR"
+                 );
+              }
558: 
559:         emit OfferCreated(
560:             borrower, loanCounter, borrowAmount, APR, APRLateFee, term,
561:             paymentInterval, block.timestamp + 3 days, gracePeriod, paymentSchedule
562:         );
563: 
564:         loans[loanCounter] = Loan(
565:             borrower, borrowAmount, APR, APRLateFee, 0, term, term, paymentInterval, block.timestamp + 3 days,
566:             gracePeriod, paymentSchedule, LoanState.Offered
567:         );
568: 
569:         loanCounter += 1;
570:     }
```