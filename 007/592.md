Clumsy Cobalt Lion

medium

# Combining loans in `OCC` does not mark the combination as invalid

## Summary
If a combination is valid, [OCC_Modular.applyCombine()](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L749) should combine its loans into a new one and mark the combination an invalid. However, the function fails to set the invalid flag of the combination to false, because it uses wrong parameter.
## Vulnerability Detail
To apply a combination, it must be a valid one, i.e approved by an underwriter. The `valid` flag is used for that check and after it, this flag should be set to false. However, the code uses `combineCounter` instead of `id`. Because `combineCounter` points to the next combination to be created, the current loan's validity status will remain true.

```solidity
        require(combinations[id].valid, "OCC_Modular::applyCombine() !combinations[id].valid");
        require(
            block.timestamp < combinations[id].expires, 
            "OCC_Modular::applyCombine() block.timestamp >= combinations[id].expires"
        );

        combinations[combineCounter].valid = false;
```
## Impact
Combinations' `valid` flag stays `true` even after applying them.
## Code Snippet
```solidity
    function applyCombine(uint256 id) external {
        require(combinations[id].valid, "OCC_Modular::applyCombine() !combinations[id].valid");
        require(
            block.timestamp < combinations[id].expires, 
            "OCC_Modular::applyCombine() block.timestamp >= combinations[id].expires"
        );

        combinations[combineCounter].valid = false;

        emit CombineApplied(
            _msgSender(),
            combinations[id].loans, 
            combinations[id].term,
            combinations[id].paymentInterval, 
            combinations[id].gracePeriod,
            combinations[id].paymentSchedule
        );

        uint256 notional;
        uint256 APR;
        
        for (uint256 i = 0; i < combinations[id].loans.length; i++) {
            uint256 loanID = combinations[id].loans[i];
            require(
                _msgSender() == loans[loanID].borrower, 
                "OCC_Modular::applyCombine() _msgSender() != loans[loanID].borrower"
            );
            require(
                loans[loanID].state == LoanState.Active, 
                "OCC_Modular::applyCombine() loans[loanID].state != LoanState.Active"
            );
            notional += loans[loanID].principalOwed;
            APR += loans[loanID].principalOwed * loans[loanID].APR;
            loans[loanID].principalOwed = 0;
            loans[loanID].paymentDueBy = 0;
            loans[loanID].paymentsRemaining = 0;
            loans[loanID].state = LoanState.Combined;
        }

        APR = APR / notional;

        uint256 term = combinations[id].term;
        uint256 APRLateFee = combinations[id].APRLateFee;
        uint256 paymentInterval = combinations[id].paymentInterval;
        uint256 gracePeriod = combinations[id].gracePeriod;
        int8 paymentSchedule = combinations[id].paymentSchedule;
        
        // "Friday" Payment Standardization, minimum 7-day lead-time
        // block.timestamp - block.timestamp % 7 days + 9 days + paymentInterval
        emit CombineLoanCreated(
            _msgSender(),  // borrower
            loanCounter,  // loanID
            notional,  // principalOwed
            APR,  // APR
            APRLateFee,  // APRLateFee
            block.timestamp - block.timestamp % 7 days + 9 days + paymentInterval,  // paymentDueBy
            term,  // term
            paymentInterval,  // paymentInterval
            gracePeriod,  // gracePeriod
            paymentSchedule  // paymentSchedule
        );
        loans[loanCounter] = Loan(
            _msgSender(), notional, APR, APRLateFee, block.timestamp - block.timestamp % 7 days + 9 days + paymentInterval, 
            term, term, paymentInterval, block.timestamp - 1 days, gracePeriod, paymentSchedule, LoanState.Active
        );
        loanCounter += 1;
    }
```
## Tool used

Manual Review

## Recommendation
```diff
 -      combinations[combineCounter].valid = false;
+      combinations[id].valid = false;
```