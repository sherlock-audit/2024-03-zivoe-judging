Jovial Shamrock Skunk

medium

# Limitation set for vesting period can be bypassed.

## Summary
`createVestingSchedule` requires entire vesting period to be less than 1800 days , but this limitation can be bypassed.
## Vulnerability Detail
In `ZivoeRewardsVesting.sol`  createVestingSchedule  requires entire vesting period to be less than 1800 days.
`require(daysToVest <= 1800 days, "ZivoeRewardsVesting::createVestingSchedule() daysToVest > 1800 days");` but later in the code when `daysToVest` is set, it is multiplied by `1 days` . This arises following two situations
1. For above require statement to be followed the input should be in seconds format for eg `daysToVest = 10 days` , if input is sent in seconds then the entire vesting period(`daysToVest`),`daysToCliff` would become excessively large  which would make the contract seemingly unusable for that user.
2. If input is sent in Integer the require statement would be  bypassed and `daysToVest` with value greater than 1800 will also be accepted thus bypassing the limitation set by the contract.
## Impact
* For above require statement to be followed the input should be in seconds format for eg `daysToVest = 10 days` , if input is sent in seconds then the entire vesting period(`daysToVest`),`daysToCliff` would become excessively large  which would make the contract seemingly unusable for that user. . This transformation would render the contract practically unusable for the affected user, as the durations would extend far beyond reasonable timeframes.
* If input is sent in Integer the require statement would be  bypassed and `daysToVest` with value greater than 1800 will also be accepted thus bypassing the limitation set by the contract resulting in unreasonably long vesting periods beyond the intended limit.

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L396
```solidity
function createVestingSchedule(
        address account,
        uint256 daysToCliff,
        uint256 daysToVest,
        uint256 amountToVest,
        bool revokable
    ) external onlyZVLOrITO {
        require(
            !vestingScheduleSet[account],
            "ZivoeRewardsVesting::createVestingSchedule() vestingScheduleSet[account]"
        );
        require(
            IERC20(vestingToken).balanceOf(address(this)) - vestingTokenAllocated >= amountToVest,
            "ZivoeRewardsVesting::createVestingSchedule() amountToVest > vestingToken.balanceOf(address(this)) - vestingTokenAllocated"
        );
        require(daysToVest <= 1800 days, "ZivoeRewardsVesting::createVestingSchedule() daysToVest > 1800 days"); //@audit-issue checking daysToVest in days which is (1800 * 86400)
        require(daysToCliff <= daysToVest, "ZivoeRewardsVesting::createVestingSchedule() daysToCliff > daysToVest");
        require(
            IZivoeITO_ZivoeRewardsVesting(IZivoeGlobals_ZivoeRewardsVesting(GBL).ITO()).seniorCredits(account) == 0 &&
            IZivoeITO_ZivoeRewardsVesting(IZivoeGlobals_ZivoeRewardsVesting(GBL).ITO()).juniorCredits(account) == 0,
            "ZivoeRewardsVesting::createVestingSchedule() seniorCredits(_msgSender) > 0 || juniorCredits(_msgSender) > 0"
        );

        vestingScheduleSet[account] = true;
        vestingTokenAllocated += amountToVest; // global value

        vestingScheduleOf[account].start = block.timestamp;
        vestingScheduleOf[account].cliff = block.timestamp + daysToCliff * 1 days;
        vestingScheduleOf[account].end = block.timestamp + daysToVest * 1 days; // but here again that input is multiplied by 84600 , either `require(daysToVest <= 1800 days` will be bypass or vestingScheduleOf[account].end  will be too large value.
        vestingScheduleOf[account].totalVesting = amountToVest; //
        vestingScheduleOf[account].vestingPerSecond = amountToVest / (daysToVest * 1 days);
        vestingScheduleOf[account].revokable = revokable;

        emit VestingScheduleCreated(
            account,
            vestingScheduleOf[account].start,
            vestingScheduleOf[account].cliff,
            vestingScheduleOf[account].end,
            vestingScheduleOf[account].totalVesting,
            vestingScheduleOf[account].vestingPerSecond,
            vestingScheduleOf[account].revokable
        );
```
## Tool used
 
Manual Review

## Recommendation
```diff
function createVestingSchedule(
        address account,
        uint256 daysToCliff,
        uint256 daysToVest,
        uint256 amountToVest,
        bool revokable
    ) external onlyZVLOrITO {
        require(
            !vestingScheduleSet[account],
            "ZivoeRewardsVesting::createVestingSchedule() vestingScheduleSet[account]"
        );
        require(
            IERC20(vestingToken).balanceOf(address(this)) - vestingTokenAllocated >= amountToVest,
            "ZivoeRewardsVesting::createVestingSchedule() amountToVest > vestingToken.balanceOf(address(this)) - vestingTokenAllocated"
        );
-       require(daysToVest <= 1800 days, "ZivoeRewardsVesting::createVestingSchedule() daysToVest > 1800 days"); 
+      require(daysToVest <= 1800 , "ZivoeRewardsVesting::createVestingSchedule() daysToVest > 1800 days"); 
        require(daysToCliff <= daysToVest, "ZivoeRewardsVesting::createVestingSchedule() daysToCliff > daysToVest");
        require(
            IZivoeITO_ZivoeRewardsVesting(IZivoeGlobals_ZivoeRewardsVesting(GBL).ITO()).seniorCredits(account) == 0 &&
            IZivoeITO_ZivoeRewardsVesting(IZivoeGlobals_ZivoeRewardsVesting(GBL).ITO()).juniorCredits(account) == 0,
            "ZivoeRewardsVesting::createVestingSchedule() seniorCredits(_msgSender) > 0 || juniorCredits(_msgSender) > 0"
        );

     //...//
        );
```
