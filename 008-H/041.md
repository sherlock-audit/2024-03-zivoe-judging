Large Ceramic Swan

high

# Every time you call `ZivoeRewardsVesting::depositReward()` to deposit a reward, after the reward is vested, there will almost always be some dust left behind, and the amount will expand infinitely.

## Summary
Every time you call `ZivoeRewardsVesting::depositReward()` to deposit a reward, there will almost always be some dust left after the reward is vested. And it will not be used in subsequent calls, and will expand infinitely over time, locked in the contract.And when the `rewardData[_rewardsToken].periodFinish` time is exceeded and the user fails to receive the reward in time, call `ZivoeRewardsVesting::depositReward()` again to deposit the reward, then there will be more tokens remaining in the contract
## Vulnerability Detail
```js
    function depositReward(address _rewardsToken, uint256 reward) external updateReward(address(0)) nonReentrant {
        IERC20(_rewardsToken).safeTransferFrom(_msgSender(), address(this), reward);


        // Update vesting accounting for reward (if existing rewards being distributed, increase proportionally).
        if (block.timestamp >= rewardData[_rewardsToken].periodFinish) {
@>            rewardData[_rewardsToken].rewardRate = reward.div(rewardData[_rewardsToken].rewardsDuration);
        } else {
            uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
            uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
            rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
        }


        rewardData[_rewardsToken].lastUpdateTime = block.timestamp;
        rewardData[_rewardsToken].periodFinish = block.timestamp.add(rewardData[_rewardsToken].rewardsDuration);
        emit RewardDeposited(_rewardsToken, reward, _msgSender());
    }
```
### Poc
If it is the same reward amount, the same dust will be left every time
```js
    // Every time you call `ZivoeRewardsVesting::depositReward()` to deposit a reward, there will almost always be some dust left after the reward is vested.
    function testZivoeRewardsVestingDustLeftAfterTheRewardIsVested() public {
        // lpOne and lpTwo gets some vestZVE
        vm.prank(address(zvl));
        ITO.commence();
        uint256 amount = 1e18;
        depositSenior(lpOne,amount);
        depositSenior(lpTwo,amount);
        vm.warp(block.timestamp + ITO.end() + 1 seconds);
        ITO.claimAirdrop(lpOne);
        ITO.claimAirdrop(lpTwo);
        console.log("vestZVE.balanceOf(lpOne):",vestZVE.balanceOf(lpOne));
        console.log("vestZVE.balanceOf(lpTwo):",vestZVE.balanceOf(lpTwo));
        
        // zvl calls addReward() and depositReward()
        mint("WETH", address(zvl), amount);
        vm.startPrank(address(zvl));
        vestZVE.addReward(WETH,30 days);
        IERC20(WETH).approve(address(vestZVE),amount);
        vestZVE.depositReward(WETH,amount);    
        vm.stopPrank(); 
        // time to end
        vm.warp(block.timestamp + 40 days);
        // getRewards()
        vm.prank(lpOne);
        vestZVE.getRewards();
        vm.prank(lpTwo);
        vestZVE.getRewards();
        // assertEq(IERC20(WETH).balanceOf(lpOne) + IERC20(WETH).balanceOf(lpTwo) + IERC20(WETH).balanceOf(address(vestZVE)),amount);
        // first Residual WETH
        uint256 firstResidualWETH = IERC20(WETH).balanceOf(address(vestZVE));
        assertGt(firstResidualWETH,0);
        // zvl calls depositReward() again 
        mint("WETH", address(zvl), amount);
        vm.startPrank(address(zvl));
        IERC20(WETH).approve(address(vestZVE),amount);
        vestZVE.depositReward(WETH,amount);    
        vm.stopPrank(); 
        // time to end
        vm.warp(block.timestamp + 40 days);
        // getRewards()
        vm.prank(lpOne);
        vestZVE.getRewards();
        vm.prank(lpTwo);
        vestZVE.getRewards();
        // second Residual WETH
        uint256 secondResidualWETH = IERC20(WETH).balanceOf(address(vestZVE));
        console.log("first Residual WETH:",firstResidualWETH);
        console.log("second Residual WETH:",secondResidualWETH);
    }
    // [PASS] testZivoeRewardsVestingDustLeftAfterTheRewardIsVested() (gas: 1599445)
    // Logs:
    //     vestZVE.balanceOf(lpOne): 625000000000000000000000
    //     vestZVE.balanceOf(lpTwo): 625000000000000000000000
    //     first Residual WETH: 2500000
    //     second Residual WETH: 5000000
    function testZivoeRewardsVestingRewardsWereNotFullyClaimedAndNewRewardsWereAdded() public {
        uint256 amount = 1e18;
        // lpOne and lpTwo call stake with zSTT 
        deal(address(zSTT),lpOne,amount);
        vm.startPrank(lpOne);
        zSTT.approve(address(stSTT),amount);
        stSTT.stake(amount);
        vm.stopPrank();
        deal(address(zSTT),lpTwo,amount);
        vm.startPrank(lpTwo);
        zSTT.approve(address(stSTT),amount);
        stSTT.stake(amount);
        vm.stopPrank();
        // zvl calls addReward() and depositReward()
        mint("WETH", address(zvl), amount);
        vm.startPrank(address(zvl));
        stSTT.addReward(WETH,30 days);
        IERC20(WETH).approve(address(stSTT),amount);
        stSTT.depositReward(WETH,amount);    
        vm.stopPrank(); 
        // time to end
        vm.warp(block.timestamp + 40 days);
        // getRewards()
        vm.prank(lpOne);
        stSTT.getRewards();
        // vm.prank(lpTwo);
        // stSTT.getRewards();
        assertEq(IERC20(WETH).balanceOf(lpOne) + IERC20(WETH).balanceOf(lpTwo) + IERC20(WETH).balanceOf(address(stSTT)),amount);
        // first Residual WETH
        uint256 firstResidualWETH = IERC20(WETH).balanceOf(address(stSTT));
        assertGt(firstResidualWETH,0);
        // zvl calls depositReward() again 
        mint("WETH", address(zvl), amount);
        vm.startPrank(address(zvl));
        IERC20(WETH).approve(address(stSTT),amount);
        stSTT.depositReward(WETH,amount);    
        vm.stopPrank(); 
        // time to end
        vm.warp(block.timestamp + 40 days);
        // getRewards()
        vm.prank(lpOne);
        stSTT.getRewards();
        // vm.prank(lpTwo);
        // stSTT.getRewards();
        // second Residual WETH
        uint256 secondResidualWETH = IERC20(WETH).balanceOf(address(stSTT));
        console.log("first Residual WETH:",firstResidualWETH);
        console.log("second Residual WETH:",secondResidualWETH);
    }
    // [PASS] testZivoeRewardsVestingRewardsWereNotFullyClaimedAndNewRewardsWereAdded() (gas: 1022637)
    // Logs:
    //     first Residual WETH: 500000000001040000
    //     second Residual WETH: 1000000000002080000
```
## Impact
Every time you call `ZivoeRewardsVesting::depositReward()` to deposit a reward, there will almost always be some dust left after the reward is vested. And it will not be used in subsequent calls, and will expand infinitely over time.
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L352-L367
## Tool used

Manual Review

## Recommendation
Call `ZivoeRewardsVesting::depositReward()`, if `block.timestamp >= rewardData[_rewardsToken].periodFinish`, the sender has completed the transfer at this time, we can try to use the balance of the current contract as the reward amount
```diff
        // Update vesting accounting for reward (if existing rewards being distributed, increase proportionally).
        if (block.timestamp >= rewardData[_rewardsToken].periodFinish) {
+           reward = IERC20(_rewardsToken).balanceOf(address(this));
            rewardData[_rewardsToken].rewardRate = reward.div(rewardData[_rewardsToken].rewardsDuration);
        } else {
            uint256 remaining = rewardData[_rewardsToken].periodFinish.sub(block.timestamp);
            uint256 leftover = remaining.mul(rewardData[_rewardsToken].rewardRate);
            rewardData[_rewardsToken].rewardRate = reward.add(leftover).div(rewardData[_rewardsToken].rewardsDuration);
        }
```
test again, dust is exploited
```zsh
[PASS] testZivoeRewardsVestingDustLeftAfterTheRewardIsVested() (gas: 1600885)
Logs:
  vestZVE.balanceOf(lpOne): 625000000000000000000000
  vestZVE.balanceOf(lpTwo): 625000000000000000000000
  first Residual WETH: 2500000
  second Residual WETH: 2500000
```
