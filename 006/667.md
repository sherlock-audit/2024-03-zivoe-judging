Great Metal Ram

medium

# `OCY_Convex_A` and `OCY_Convex_C` `claimRewards` can be bricked by poison reward token

## Summary

When `convexRewards` contain malicious/poison extra reward tokens, `CRV` and `CVX` reward cannot be claimed.

## Vulnerability Detail

When `claimRewards` is called, it will trigger `convexRewards.getReward()` to claim all rewards, including extra rewards to the `OCY_Convex_A` / `OCY_Convex_C` contracts.

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCY/OCY_Convex_A.sol#L246-L266
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCY/OCY_Convex_C.sol#L200-L220

```solidity
    function claimRewards(bool extra) public nonReentrant {
        IBaseRewardPool_OCY_Convex_C(convexRewards).getReward();

        // Native Rewards (CRV, CVX)
        uint256 rewardsCRV = IERC20(CRV).balanceOf(address(this));
        uint256 rewardsCVX = IERC20(CVX).balanceOf(address(this));
        if (rewardsCRV > 0) { IERC20(CRV).safeTransfer(OCT_YDL, rewardsCRV); }
        if (rewardsCVX > 0) { IERC20(CVX).safeTransfer(OCT_YDL, rewardsCVX); }

        // Extra Rewards
        if (extra) {
            uint256 extraRewardsLength = IBaseRewardPool_OCY_Convex_C(convexRewards).extraRewardsLength();
            for (uint256 i = 0; i < extraRewardsLength; i++) {
                address rewardContract = IBaseRewardPool_OCY_Convex_C(convexRewards).extraRewards(i);
                uint256 rewardAmount = IBaseRewardPool_OCY_Convex_C(rewardContract).rewardToken().balanceOf(
                    address(this)
                );
                if (rewardAmount > 0) { IERC20(rewardContract).safeTransfer(OCT_YDL, rewardAmount); }
            }
        }
    }
```

Inside Convex Reward contract, `getReward` will claim all rewards including extra rewards.

```solidity
    function getReward(address _account, bool _claimExtras) public updateReward(_account) returns(bool){
        uint256 reward = earned(_account);
        if (reward > 0) {
            rewards[_account] = 0;
            rewardToken.safeTransfer(_account, reward);
            IDeposit(operator).rewardClaimed(pid, _account, reward);
            emit RewardPaid(_account, reward);
        }

        //also get rewards from linked rewards
        if(_claimExtras){
            for(uint i=0; i < extraRewards.length; i++){
 >>>            IRewards(extraRewards[i]).getReward(_account);
            }
        }
        return true;
    }

    function getReward() external returns(bool){
        getReward(msg.sender,true);
        return true;
    }
```

Since additional reward tokens are added by the gauge owner, if a particular token is bricked from transfer, then the call will always result in a revert.

Note : from the contest documentation, it is mentioned that protocol integration is RESTRICTED and not TRUSTED

>Are the admins of the protocols your contracts integrate with (if any) TRUSTED or RESTRICTED?

>RESTRICTED

## Impact

This will cause the `getReward` operations to always  prevent the protocol from receiving `CVX` and `CRV` rewards.

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCY/OCY_Convex_A.sol#L246-L266
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCY/OCY_Convex_C.sol#L200-L220

## Tool used

Manual Review

## Recommendation

Consider to call `convexRewards.getReward` providing contract address and the `extra` params.

```diff
    function claimRewards(bool extra) public nonReentrant {
-        IBaseRewardPool_OCY_Convex_C(convexRewards).getReward();
+        IBaseRewardPool_OCY_Convex_C(convexRewards).getReward(address(this), extra);

        // Native Rewards (CRV, CVX)
        uint256 rewardsCRV = IERC20(CRV).balanceOf(address(this));
        uint256 rewardsCVX = IERC20(CVX).balanceOf(address(this));
        if (rewardsCRV > 0) { IERC20(CRV).safeTransfer(OCT_YDL, rewardsCRV); }
        if (rewardsCVX > 0) { IERC20(CVX).safeTransfer(OCT_YDL, rewardsCVX); }

        // Extra Rewards
        if (extra) {
            uint256 extraRewardsLength = IBaseRewardPool_OCY_Convex_C(convexRewards).extraRewardsLength();
            for (uint256 i = 0; i < extraRewardsLength; i++) {
                address rewardContract = IBaseRewardPool_OCY_Convex_C(convexRewards).extraRewards(i);
                uint256 rewardAmount = IBaseRewardPool_OCY_Convex_C(rewardContract).rewardToken().balanceOf(
                    address(this)
                );
                if (rewardAmount > 0) { IERC20(rewardContract).safeTransfer(OCT_YDL, rewardAmount); }
            }
        }
    }
```
