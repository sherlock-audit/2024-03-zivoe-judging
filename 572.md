Great Metal Ram

medium

# `amountIn` check inside all `ZivoeSwapper`'s validation can be DoSed

## Summary

`ZivoeSwapper`'s `amountIn` check inside all validation functions can be easily DoSed due to strict equality check.

## Vulnerability Detail

Inside all validation functions on `ZivoeSwapper`, there is a strict amountIn check, it has to be equal to the amount decoded from `data`.

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/Utility/ZivoeSwapper.sol#L88

```solidity
    function handle_validation_12aa3caf(bytes calldata data, address assetIn, address assetOut, uint256 amountIn) internal view {
        (, SwapDescription memory _b,) = abi.decode(data[4:], (address, SwapDescription, bytes));
        require(address(_b.srcToken) == assetIn, "ZivoeSwapper::handle_validation_12aa3caf() address(_b.srcToken) != assetIn");
        require(address(_b.dstToken) == assetOut, "ZivoeSwapper::handle_validation_12aa3caf() address(_b.dstToken) != assetOut");
>>>     require(_b.amount == amountIn, "ZivoeSwapper::handle_validation_12aa3caf() _b.amount != amountIn");
        require(_b.dstReceiver == address(this), "ZivoeSwapper::handle_validation_12aa3caf() _b.dstReceiver != address(this)");
    }
```

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/Utility/ZivoeSwapper.sol#L97

```solidity
    function handle_validation_e449022e(bytes calldata data, address assetIn, address assetOut, uint256 amountIn) internal view {
        (uint256 _a,, uint256[] memory _c) = abi.decode(data[4:], (uint256, uint256, uint256[]));
>>>     require(_a == amountIn, "ZivoeSwapper::handle_validation_e449022e() _a != amountIn");
        // ...
    }
```

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/Utility/ZivoeSwapper.sol#L124

```solidity
    function handle_validation_0502b1c5(bytes calldata data, address assetIn, address assetOut, uint256 amountIn) internal view {
        (address _a, uint256 _b,, uint256[] memory _d) = abi.decode(data[4:], (address, uint256, uint256, uint256[]));
     require(_a == assetIn, "ZivoeSwapper::handle_validation_0502b1c5() _a != assetIn");
>>>     require(_b == amountIn, "ZivoeSwapper::handle_validation_0502b1c5() _b != amountIn");
       // ...
    }
```

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/Utility/ZivoeSwapper.sol#L158

```solidity
    function handle_validation_3eca9c0a(bytes calldata data, address assetIn, address assetOut, uint256 amountIn) internal pure {
        (OrderRFQ memory _a,,) = abi.decode(data[4:], (OrderRFQ, bytes, uint256));
        require(address(_a.takerAsset) == assetIn, "ZivoeSwapper::handle_validation_3eca9c0a() address(_a.takerAsset) != assetIn");
        require(address(_a.makerAsset) == assetOut, "ZivoeSwapper::handle_validation_3eca9c0a() address(_a.makerAsset) != assetOut");
>>>     require(_a.takingAmount == amountIn, "ZivoeSwapper::handle_validation_3eca9c0a() _a.takingAmount != amountIn");
    }
```

Inside `OCT_DAO` and `OCT_YDL`, where `ZivoeSwapper` is used to convert/swap assets, it will use `IERC20(asset).balanceOf(address(this))` as the amount that will be provided as `amountIn`.

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCT/OCT_DAO.sol#L87-L99
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCT/OCT_YDL.sol#L97-L110

```solidity
    function convertAndForward(address asset, bytes calldata data) external nonReentrant {
        require(
            IZivoeGlobals_OCT_YDL(GBL).isKeeper(_msgSender()),
            "OCT_YDL::convertAndForward !isKeeper(_msgSender())"
        );
        address distributedAsset = IZivoeYDL_OCT_YDL(IZivoeGlobals_OCT_YDL(GBL).YDL()).distributedAsset();
>>>     uint256 amountFrom = IERC20(asset).balanceOf(address(this));
        IERC20(asset).safeIncreaseAllowance(router1INCH_V5, amountFrom);
>>>     convertAsset(asset, distributedAsset, amountFrom, data);
        assert(IERC20(asset).allowance(address(this), router1INCH_V5) == 0);
        uint balDistributedAsset = IERC20(distributedAsset).balanceOf(address(this));
        emit AssetConvertedForwarded(asset, distributedAsset, amountFrom, balDistributedAsset);
        IERC20(distributedAsset).safeTransfer(IZivoeGlobals_OCT_YDL(GBL).YDL(), balDistributedAsset);
    }
```

## Impact

Attacker can front-ran the `OCT_DAO` and  `OCT_YDL` `convertAndForward` operation by donating dust amount of `asset` and causing the operation to revert due to strict equality check.

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/Utility/ZivoeSwapper.sol#L88
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/Utility/ZivoeSwapper.sol#L97
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/Utility/ZivoeSwapper.sol#L124
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/Utility/ZivoeSwapper.sol#L158

## Tool used

Manual Review

## Recommendation

Consider to modify the equality check to the following : 

```diff
// ...
-  require(_b.amount == amountIn)
+  require(_b.amount <= amountIn)
```
