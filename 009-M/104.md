Ancient Chartreuse Kangaroo

medium

# Malicious Default Borrowers Can Decrease $ZVE Rewards for Tranche Depositors by Front-running Deposits

## Summary
The `adjustedSupplies` variable, which represents the supplies of the `junior` and `senior` tranches considering defaults in `OCC_Modular.sol`, is crucial for calculating the $ZVE rewards for tranche depositors. However, this variable can be manipulated by malicious borrowers who front-run depositors.

## Vulnerability Detail
When an investor deposits into a tranche, they earn $ZVE token incentives based on the type of tranche (`senior` or `junior`) and the ratio of junior to senior supplies, which must be within a specific range (`lowerRatioIncentiveBIPS` and `upperRatioIncentiveBIPS`). The supplies of these two tranches are influenced by loan defaults in `OCC_Modular`. [When a loan defaults](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L615), the `defaults` global variable increases by the `principalOwed` of the loan, resulting in a decrease in the `zJTT` adjusted supply. A malicious borrower can then resolve the loan by calling the `resolveDefault()` function exactly before a deposit is made, which decreases the `defaults` and increases the `zJTT` adjusted supply. This gives the malicious borrower the opportunity to manipulate the `zJTT` adjusted supply and the `ratio` of `junior` to `senior` supplies, thereby decreasing the $ZVE rewards for the tranche depositors.

## Impact
Malicious borrowers can deliberately front-run deposits, resolve their defaulted loans, and manipulate the `adjustedSupplies` to significantly reduce the $ZVE rewards earned by honest tranche depositors. Î¤his can lead to a substantial loss of incentives for investors and potentially discouraging participation in the protocol.

## Code Snippet
Here is the vulnerable code snippet in `OCC_Modular.sol`:

```solidity
function resolveDefault(uint256 id, uint256 amount) external nonReentrant {
        require(
            loans[id].state == LoanState.Defaulted, 
            "OCC_Modular::resolveDefaut() loans[id].state != LoanState.Defaulted"
        );

        uint256 paymentAmount;

        if (amount >= loans[id].principalOwed) {
            paymentAmount = loans[id].principalOwed;
            loans[id].principalOwed = 0;
            loans[id].state = LoanState.Resolved;
        }
        else {
            paymentAmount = amount;
            loans[id].principalOwed -= paymentAmount;
        }

        emit DefaultResolved(id, paymentAmount, _msgSender(), loans[id].state == LoanState.Resolved);

        IERC20(stablecoin).safeTransferFrom(_msgSender(), owner(), paymentAmount);
        IZivoeGlobals_OCC(GBL).decreaseDefaults(IZivoeGlobals_OCC(GBL).standardize(paymentAmount, stablecoin));
    }
```
[Link to code](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCC/OCC_Modular.sol#L682C5-L704C6)

And here is the reward calculation of $ZVE that is directly related to front-runned `adjustedSupplies` in `ZivoeTranches.sol`:

```solidity
function rewardZVEJuniorDeposit(uint256 deposit) public view returns (uint256 reward) {

@>        (uint256 seniorSupp, uint256 juniorSupp) = ZivoeGlobals_ZivoeTranches(GBL).adjustedSupplies();

        uint256 avgRate;    // The avg ZVE per stablecoin deposit reward, used for reward calculation.

        uint256 diffRate = maxZVEPerJTTMint - minZVEPerJTTMint;

        uint256 startRatio = juniorSupp * BIPS / seniorSupp;
        uint256 finalRatio = (juniorSupp + deposit) * BIPS / seniorSupp;
        uint256 avgRatio = (startRatio + finalRatio) / 2;

        if (avgRatio <= lowerRatioIncentiveBIPS) {
            avgRate = maxZVEPerJTTMint;
        } else if (avgRatio >= upperRatioIncentiveBIPS) {
            avgRate = minZVEPerJTTMint;
        } else {
            avgRate = maxZVEPerJTTMint - diffRate * (avgRatio - lowerRatioIncentiveBIPS) / (upperRatioIncentiveBIPS - lowerRatioIncentiveBIPS);
        }

        reward = avgRate * deposit / 1 ether;

        // Reduce if ZVE balance < reward.
        if (IERC20(IZivoeGlobals_ZivoeTranches(GBL).ZVE()).balanceOf(address(this)) < reward) {
            reward = IERC20(IZivoeGlobals_ZivoeTranches(GBL).ZVE()).balanceOf(address(this));
        }
    }
```
[Link to code](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeTranches.sol#L203)

## Tool used
Manual Review

## Recommendation
We recommend to implement a time-weighted average of the `adjustedSupplies` in order to prevent the malicious borrowers from manipulating the `junior` to `senior` supplies ratio and therefore the $ZVE rewards of the depositors of the tranches.
