Helpful Mercurial Finch

high

# `epochDiscountSenior` does not reset to 0 after `totalDefaults` can be fully absorbed by the junior tranche in the next epoch, causing a loss in senior tranche redemptions

## Summary

[`epochDiscountSenior`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCR/OCR_Modular.sol#L62-L62) can fluctuate throughout different epochs depending on the global value of `defaults` set by the Zivoe team on a regular basis to reflect the off-chain defaults. It represents the value of defaulted loan in BIPS (Basis points).

It is a normal behavior of the protocol that [a senior tranche liquidity provider would want to redeem](https://docs.zivoe.com/user-docs/liquidity-providers/redemptions) his/her tranche token (`zSTT`), but may not be able to fully redeem it due to defaults causing a discount. This can happen if there are sufficient defaults that cannot be covered by the notional value of junior tranche, further discounting the value of senior tranche. When this is the case, `epochDiscountSenior > 0`.

Later on, it is possible that [the defaults are resolved](https://docs.zivoe.com/user-docs/liquidity-providers/how-do-defaults-work#resolving-defaults). In such a case, global `defaults` value will be decreased, and the NAV of the affected tranches should be restored. When the value of `defaults` can be sufficiently covered by the notional value of the junior tranche,  `epochDiscountSenior` must be set to `0` again because the senior tranche is fully unaffected by the `defaults`, thanks to the junior tranche.

However, <ins>it has been found that `epochDiscountSenior` does not correctly reset to `0` after the junior tranche can back all of the `defaults` by itself. Consequently, senior tranche depositors lose a certain amount of zSTT that can be redeemed back to stablecoin.</ins>

## Vulnerability Detail

Zivoe maintains two variables: [`epochDiscountJunior` and `epochDiscountSenior`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCR/OCR_Modular.sol#L62-L63).

Both are in BIPS (Basis points) terms and represent the discount rate for the redemption of tranche token.

Tranche token is a representation of a liquidity provider's deposit into either junior or senior tranche. According to the type of tranche utilized, the liquidity provider receives `zJTT` (Zivoe junior tranche token) or `zSTT` (Zivoe senior tranche token).

A liquidity provider can always file a request for redemption of the token to convert it back to his initial stablecoin deposit by calling [`createRequest`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCR/OCR_Modular.sol#L215-L215).

Then, he will wait for the next epoch to start (each epoch is 14 days long) to be able to process his redemption request because redemption requests can only be processed in subsequent epochs.

Each epoch is updated by [`tickEpoch` function](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCR/OCR_Modular.sol#L310-L336):

```solidity
/// @notice Ticks the epoch.
    function tickEpoch() public {
        while (block.timestamp >= epoch + 14 days) { 
            epoch += 14 days;
            redemptionsAllowedJunior += redemptionsQueuedJunior;
            redemptionsAllowedSenior += redemptionsQueuedSenior;
            redemptionsQueuedJunior = 0;
            redemptionsQueuedSenior = 0;
            uint256 totalDefaults = IZivoeGlobals_OCR(GBL).defaults();
            uint256 zSTTSupply = IERC20(IZivoeGlobals_OCR(GBL).zSTT()).totalSupply();
            uint256 zJTTSupply = IERC20(IZivoeGlobals_OCR(GBL).zJTT()).totalSupply();
            if (totalDefaults > zJTTSupply) {
                epochDiscountJunior = BIPS;
                totalDefaults -= zJTTSupply;
                epochDiscountSenior = (totalDefaults * RAY / zSTTSupply) / 10**23;
            }
            else {
                epochDiscountJunior = (totalDefaults * RAY / zJTTSupply) / 10**23;
            }
            emit EpochTicked(
                epoch, 
                redemptionsAllowedJunior, 
                redemptionsAllowedSenior, 
                epochDiscountJunior, 
                epochDiscountSenior
            );
        }
    }
```

And it is made sure that this function is called before any mutative actions in this `OCR_Modular` (on-chain redemption) contract, most importantly including `createRequest`, `destroyRequest`, and `processRequest` by using a modifier [`_tickEpoch`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCR/OCR_Modular.sol#L155-L159):

The problem lies in `tickEpoch` function. It is meant to handle every epoch, which means it is vital to reset any variables relevant to each epoch. However, inspecting the code tells us that when `totalDefaults > zJTTSupply`, `epochDiscountSenior` is updated to reflect the discount in redemption requests from the senior tranche, but when `totalDefaults <= zJTTSupply` in any later epochs, `epochDiscountSenior` is not reset to `0` to reflect the fact that there is no discount in redemption requests from the senior tranche anymore.

To further prove this, we can write a simple unit test. Head over to https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-testing/src/TESTS_Lockers/Test_OCR_Modular.sol, and at the end of the test contract `Test_OCR_Modular`, we add one more function called `function test_OCR_resetsEpochDiscountSenior_state() public`:

```solidity
function test_OCR_resetsEpochDiscountSenior_state() public {
        // startingSupplySTT = 10_000_000 ether;
        // startingSupplyJTT = 2_000_000 ether;

        // We're starting with defaults = 3_000_000 ether. That means that the 
        // junior tranche has already fully absorbed the defaults, and the senior
        // tranche is being discounted by the remaining defaults too.
        uint256 defaults = 3_000_000 ether;

        // Make sure we have valid testing parameters
        assert(defaults <= startingSupplySTT + startingSupplyJTT);

        // Increase defaults in system
        assert(god.try_increaseDefaults(address(OCG_Defaults_Test), defaults));
        assertEq(GBL.defaults(), defaults);

        // Pre-state.
        assertEq(OCR_DAI.redemptionsAllowedSenior(), 0);
        assertEq(OCR_DAI.redemptionsAllowedJunior(), 0);
        assertEq(OCR_DAI.redemptionsQueuedSenior(), 0);
        assertEq(OCR_DAI.redemptionsQueuedJunior(), 0);
        assertEq(OCR_DAI.epochDiscountSenior(), 0);
        assertEq(OCR_DAI.epochDiscountJunior(), 0);
        assertEq(OCR_DAI.epoch(), block.timestamp);

        uint preEpoch = block.timestamp;

        // Warp to epoch start, tickEpoch()
        hevm.warp(OCR_DAI.epoch() + 15 days);
        OCR_DAI.tickEpoch();
        assertEq(OCR_DAI.redemptionsAllowedSenior(), 0);
        assertEq(OCR_DAI.redemptionsAllowedJunior(), 0);
        assertEq(OCR_DAI.redemptionsQueuedSenior(), 0);
        assertEq(OCR_DAI.redemptionsQueuedJunior(), 0);
        // Since we know the amount of defaults, we can calculate the expected discount.
        // The junior tranche fully absorbs the defaults, so the senior tranche is fully discounted at 100%
        assertEq(OCR_DAI.epochDiscountJunior(), BIPS);
        // On the other hand, the senior tranche is discounted by the remaining defaults, which is 
        // defaults - startingSupplyJTT = 3_000_000 ether - 2_000_000 ether = 1_000_000 ether.
        // 1_000_000 ether equals 10% of the total supply of STT which is 10_000_000 ether, 
        // so the senior tranche is discounted by 10%. In BIPS terms, that's 1000.
        assertEq(OCR_DAI.epochDiscountSenior(), 1000);
        assertEq(OCR_DAI.epoch(), preEpoch + 14 days);

        // Defaults are all resolved now in the next epoch
        assert(god.try_decreaseDefaults(address(OCG_Defaults_Test), defaults));
        assertEq(GBL.defaults(), 0);
        // Warp to the next epoch
        hevm.warp(OCR_DAI.epoch() + 15 days);
        OCR_DAI.tickEpoch();
        assertEq(OCR_DAI.redemptionsAllowedSenior(), 0);
        assertEq(OCR_DAI.redemptionsAllowedJunior(), 0);
        assertEq(OCR_DAI.redemptionsQueuedSenior(), 0);
        assertEq(OCR_DAI.redemptionsQueuedJunior(), 0);
        assertEq(OCR_DAI.epoch(), preEpoch + 28 days);
        // epochDiscountJunior correctly resets to 0, now that the defaults are all repaid
        assertEq(OCR_DAI.epochDiscountJunior(), 0);
        // Fails because epochDiscountSenior is not properly reset in the new epochs 
        // where default < startingSupplyJTT. epochDiscountSenior remains unchanged at 1000,
        // which is a bug.
        assertEq(OCR_DAI.epochDiscountSenior(), 0);
    }
```

The test can be run with this command:

```console
forge test --rpc-url https://mainnet.infura.io/v3/f0d515d2debb4c3eb3ce6ba2b07cd0d3 --match-contract Test_OCR_Modular --match-test test_OCR_resetsEpochDiscountSenior_state -vv
```

Then, it will fail at the last assertion, proving that `epochDiscountSenior` fails to reset to 0:

![Screenshot 2024-04-18 at 19 08 24](https://github.com/sherlock-audit/2024-03-zivoe-9oelm/assets/22465806/4451e5be-7500-4968-becf-9e0f1e98a1e0)

The unit test covers a very simple scenario. The variables are set as follows:
```solidity
uint256 zSTT = 10_000_000 ether;
uint256 zJTT = 2_000_000 ether;
uint256 defaults = 3_000_000 ether;
```

In this scenario, `defaults - zJTT = 3_000_000 ether - 2_000_000 ether = 1_000_000 ether` cannot be covered by the notional value of junior tranche. Therefore, the remaining `1_000_000 ether` burden is on the senior tranche. That is why after the first `tickEpoch`, `assertEq(OCR_DAI.epochDiscountSenior(), 1000)` is true, to represent 10% discount in the senior tranche in BIPS.

However, after the default is resolved by `god.try_decreaseDefaults(address(OCG_Defaults_Test), defaults)`, `epochDiscountSenior` fails to reset to `0` to reflect being free of defaults at all, while `epochDiscountJunior` correctly resets to `0`; It keeps staying at `1000`, which is the vulnerability.

Based on the current code, `epochDiscountSenior` will never get to `0` once it increases. This is because at `epochDiscountSenior = (totalDefaults * RAY / zSTTSupply) / 10**23`, `epochDiscountSenior` is always bigger than `0` due to the constraint `totalDefaults > zJTTSupply`. In other words, once the senior tranche is discounted by the defaults, the discount rate will never be restored back to `0` even if the defaults resolve to the point where the junior tranche can back the defaults alone. 

## Impact
With an incorrect discount rate on the senior tranche redemptions:
- The discount incorrectly lowers the amount of the senior tranche token that can be redeemed back to stablecoin even when it should not be affected by defaults. 
- The senior tranche liquidity providers can experience even higher discount rate than the junior tranche ones as demonstrated in the test. This loses the advantage of being a senior tranche liquidity provider.
- Once there is a discount on the senior tranche redemptions and then later the defaults are resolved so that junior tranche alone itself can cover the defaults, there still will be a discount on the senior tranche redemptions. Therefore, the incorrect discount is semi-permanent as long as the defaults stay under the notional value of the junior tranche after the incorrect discount calculation.
- This does not require an intervention of an attacker. The only requirement is that the value of defaults exceeds the notional value of junior tranche at least once and the defaults get resolved later, allowing the junior tranche to cover the defaults on its own again.

Consequently, when a senior tranche liquidity provider wants to call [`processRequest`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCR/OCR_Modular.sol#L255-L255) and `totalDefaults <= zJTTSupply`, he will receive less `redeemAmount` because [`redeemAmount` is inversely proportional to `epochDiscountSenior`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCR/OCR_Modular.sol#L283-L284) and `epochDiscountSenior` will be an incorrect non-zero number:

```solidity
            // Reduce redeemable amount by the defaulted portion relative to tranche.
            redeemAmount = burnAmount * (BIPS - epochDiscountSenior) / BIPS;
```

For example, with the wrong discount rate of 10% in the unit test provided, a senior liquidity provider who wants to redeem 10,000 zSTT is only able to redeem 9,000 zSTT even if there are no defaults, given there are no other redemption requests and [at least a liquid capital of 9,000 USDC is ready for redemption](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCR/OCR_Modular.sol#L265-L268).

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCR/OCR_Modular.sol#L310-L336

## Tool used

Manual Review

## Recommendation

The fix should add one more of code in the `else` branch, which is:

```diff
if (totalDefaults > zJTTSupply) {
      epochDiscountJunior = BIPS;
      totalDefaults -= zJTTSupply;
      epochDiscountSenior = (totalDefaults * RAY / zSTTSupply) / 10**23;
}
else {
    epochDiscountJunior = (totalDefaults * RAY / zJTTSupply) / 10**23;
+   epochDiscountSenior = 0;
}
```

This will simply allow `epochDiscountSenior` to reset to zero again when the senior tranche is fully unaffected by `totalDefaults`.