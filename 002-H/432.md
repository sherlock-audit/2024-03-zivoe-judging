Mini Rouge Snail

high

# vesters `ZVE` tokens will be locked in the `ZiveoVestingRewards.sol` without a means of withdrawal if a vester's schedule is revoked

## Summary
During withdrawal, when [`withdraw()`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L501) is called the  [`_totalSupply`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L508) and [`_totalSupplyCheckpoints`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L509) are reduced by the vesterâ€™s  [`amountWithdrawable()`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L502) but the `vestingScheduleOf[account].totalVesting` of the user is not decreased/updated. 


However, when revoking a vester schedule [`revokeVestingSchedule()`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429), the [`_totalSupplyCheckpoints`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L452), and the [`_totalSupply`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L451) are reduced by the [`vestingScheduleOf[account].totalVesting`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L440) amount of the user being revoked to update the checkpoint. Meanwhile, the `vestingScheduleOf[account].totalVesting` contains the amount previous withdrawn by the user earlier.

This means if a vester has withdrawn after at  least one cliff before their vesting is revoked, the amount of their vested funds withdrawn thus far is subtracted twice form the `_totalSupplyCheckpoints`, (first during their withdrawal and second when they are being revoked because the `vestingScheduleOf[account].totalVesting` is not reduced during the initial withdrawal but still accounts for the withdrawn funds). This breaks the accounting of the `_totalSupplyCheckpoints` and when others vesters attempt to withdraw at the end of the vesting period, their funds are stuck without any means to retrieve it due to an underflow in whe [`_writeCheckpoint(...)`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/libraries/ZivoeVotes.sol#L120) is called to update `_totalSupplyCheckpoints`.

## Vulnerability Detail
- `ITO` ends, funds are migrated and vesting starts
- `ZVL` adds a vester
- all vesters withdraw after at least one cliff
- `ZVL` removes the added vester
- Vesting end and some vesteres cannot `fullyWithdraw`

Add the test `test_ZivoeRewardsVesting_Tokens_Are_Stuck()` to the `Test_ZivoeRewardsVesting.sol` file and run `forge test --mt test_ZivoeRewardsVesting_Tokens_Are_Stuck --rpc-url https://rpc.ankr.com/eth -vv` or `forge test --mt test_ZivoeRewardsVesting_Tokens_Are_Stuck --rpc-url <YOUR RPC URL> -vv`

```solidity
function test_ZivoeRewardsVesting_Tokens_Are_Stuck() public {

        // sam, sue, sal, sid, jim, joe, jon, jen claim their airdrop and begin vesting
        (,, uint256 _ZVE_Vested_SAM) = sam.claimAirdrop(address(ITO), address(sam));
        (,, uint256 _ZVE_Vested_SUE) = sue.claimAirdrop(address(ITO), address(sue));
        (,, uint256 _ZVE_Vested_SAL) = sal.claimAirdrop(address(ITO), address(sal));
        (,, uint256 _ZVE_Vested_SID) = sid.claimAirdrop(address(ITO), address(sid));
        (,, uint256 _ZVE_Vested_JIM) = jim.claimAirdrop(address(ITO), address(jim));
        (,, uint256 _ZVE_Vested_JOE) = joe.claimAirdrop(address(ITO), address(joe));
        (,, uint256 _ZVE_Vested_JON) = jon.claimAirdrop(address(ITO), address(jon));
        (,, uint256 _ZVE_Vested_JEN) = jen.claimAirdrop(address(ITO), address(jen));

        assertEq(_ZVE_Vested_SAM, vestZVE.balanceOf(address(sam)));


        emit log_named_decimal_uint("Sam's original $ZVE vested", vestZVE.balanceOf(address(sam)), 18);
        
        // @audit ZVL vests for MOE
        emit log("Moe is vested");
        assert(zvl.try_createVestingSchedule(
            address(vestZVE), 
            address(moe), 
            0, 
            120, 
            _ZVE_Vested_SAM, 
            true
        ));

        emit log("First reward is deposited");

        uint256 deposit = uint256(1_000_000 ether);

        // deposit DAI
        depositReward_DAI(address(vestZVE), deposit);
        // 30 days up
        hevm.warp(block.timestamp + 30 days - 2);

        (, uint256 cliff,,,,,) = vestZVE.vestingScheduleOf(address(moe));

        // moe can claim
        assertGt(block.timestamp, cliff);

        emit log("First withdrawa; cycle");
        // some users withdraw fully (vest + reward) and others withdraw partially it doesn't matter
        assert(sam.try_fullWithdraw(address(vestZVE)));
        assert(sal.try_fullWithdraw(address(vestZVE)));
        assert(joe.try_fullWithdraw(address(vestZVE)));

        hevm.prank(address(jim));
        vestZVE.withdraw();
        hevm.prank(address(sue));
        vestZVE.withdraw();
        hevm.prank(address(sid));
        vestZVE.withdraw();
        hevm.prank(address(jon));
        vestZVE.withdraw();
        hevm.prank(address(jen));
        vestZVE.withdraw();

        emit log("First withdrawal; done");

        depositReward_DAI(address(vestZVE), deposit);
        // 129 days up
        hevm.warp(block.timestamp + 100 days - 2);
        
        // moe withdraws again
        assert(moe.try_fullWithdraw(address(vestZVE)));

        // moe's vesting is revoked by ZVL
        assert(zvl.try_revokeVestingSchedule(address(vestZVE), address(moe)));

        emit log("Second withdrawa; cycle");

        // some users withdraw fully (vest + reward) and others withdraw partially it doesn't matter
        assert(sam.try_fullWithdraw(address(vestZVE)));
        assert(sal.try_fullWithdraw(address(vestZVE)));
        assert(joe.try_fullWithdraw(address(vestZVE)));

        hevm.prank(address(jim));
        vestZVE.withdraw();
        hevm.prank(address(sue));
        vestZVE.withdraw();
        hevm.prank(address(sid));
        vestZVE.withdraw();
        hevm.prank(address(jon));
        vestZVE.withdraw();
        hevm.prank(address(jen));
        vestZVE.withdraw();



        depositReward_DAI(address(vestZVE), deposit);
        // 360 days up
        // vesting is complete
        hevm.warp(block.timestamp + 230 days + 10);

        emit log("Vesting Ended");

        assert(sal.try_fullWithdraw(address(vestZVE)));
        assert(joe.try_fullWithdraw(address(vestZVE)));
        assert(jim.try_fullWithdraw(address(vestZVE)));
        assert(sue.try_fullWithdraw(address(vestZVE)));
        assert(sid.try_fullWithdraw(address(vestZVE)));

        emit log("Sam, Jon and Jen cant withdraw");
        assert(!jon.try_fullWithdraw(address(vestZVE)));
        assert(!jen.try_fullWithdraw(address(vestZVE)));
        assert(!sam.try_fullWithdraw(address(vestZVE)));

        // funds are still stuck after 380 days
        hevm.warp(block.timestamp + 20 days + 10);
        assert(!jon.try_fullWithdraw(address(vestZVE)));
        assert(!jen.try_fullWithdraw(address(vestZVE)));
        assert(!sam.try_fullWithdraw(address(vestZVE)));

    }
```

## Impact
Vesters funds will be stuck permanently in the `ZVERewardsVesting.sol ` contract without any means of withdrawal leading to a loss of funds to the vesters

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L501

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L502

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L509-L510

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L439-L453




## Tool used

Foundry Test

## Recommendation
Modify the `ZivoeRewardsVesting::withdraw()` function as shown below:
```solidity
function withdraw() public nonReentrant updateReward(_msgSender()) {
        uint256 amount = amountWithdrawable(_msgSender());
        
        ...
        
+        vestingScheduleOf[_msgSender()].totalVesting -= amount;

        _totalSupply = _totalSupply.sub(amount);
        ...

        emit Withdrawn(_msgSender(), amount);
    }
```

Also modify the `revokeVestingSchedule(...)` as shown below
```solidity
    function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
        ...
        _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
-        _writeCheckpoint(_checkpoints[account], _subtract, amount);
+        _writeCheckpoint(_checkpoints[account], _subtract, vestingAmount);
        _balances[account] = 0;
        ...
    }
```
