Quaint Brick Troll

high

# Revoking a user can lead to miscalculations in rewards and DOS for the last users to withdraw.

## Summary
In the ```ZivoeVestingSchedule``` contract the ```onlyZVLOrITO``` has the privilege to revoke a user through the ```revokeVestingSchedule```. If a user who had previously withdrawn some amount(or full) gets revoked then the ```_totalSupply``` will be deducted incorrectly(more than it should) which will lead to wrong ```rewardPerToken``` calculation which leads to more rewards given to users than the contract has which leads to more rewards than expected given to early claimers and DOS the late claimers because if they call ```getRewards``` to try to claim rewards ```IERC20(_rewardsToken).safeTransfer(_msgSender(), reward);``` will revert because the contract doesn't hold enough balance.

## Vulnerability Detail
To understand this better lets see an example;
* Say, a vesting schedule(revokable) is created for Alice with 100 tokens as ```amountToVest```, then ```vestingScheduleOf[alice]totalVesting``` of alice = 100. Also for the sake of this example lets assume there is no other user, so ```_totalSupply``` = 100. 
* After sometime alice has withdrawable = 50 and calls ```withdraw``` function, then ```_totalSupply``` = 100 - 50 = 50, ```vestingScheduleOf[alice].totalWithdrawn``` = 50,
note that alice's ```vestingScheduleOf[alice]totalVesting``` remains 100. 
* Now alice gets revoked through ```revokeVestingSchedule```, lets see the flow of this function:
it declares a uint256 ```vestingAmount = vestingScheduleOf[account].totalVesting;``` so ```vestingAmount``` = 100. Also it tries to deduct the ```_totalSupply``` as it should:
```_totalSupply = _totalSupply.sub(vestingAmount);``` but this will revert as we see that ```_totalSupply``` = 50 because of previous withdraw and the ```vestingAmount``` = 100, so ```50 - 100 = -50``` which is not possible in solidity so it will revert leading to unable to revoke alice. 
* It is not practical that the only user of ```ZivoeRewardsVestion``` will be only one user(alice), so lets assume there are two other users(A and B) with 50 ```vestingScheduleOf[account].totalVesting``` each. Therefore ```_totalSupply = 50 + 50 + 100 = 200```, after alice withdraws it becomes ```200 - 50 = 150```. Now alice gets revoked, previously it reverts because ```50 - 100 = -50``` is not possible in solidity but now, ```150 - 100 = 50``` which is possible. Thus, ```_totalSupply``` = 50, when it should have been 100 because user A and B still has 50 each. This is possible because the function deducts ```_totalSupply = _totalSupply.sub(vestingAmount);``` where ```vestingAmount = vestingScheduleOf[account].totalVesting;``` = 100 always, and not accounting for previously withdrawn 50 tokens by alice. 
* Since user A and B still has 50 tokens each staked in the contract, but the accounted ```_totalSupply``` is only 50 when it should be 100. The rewards calculation when ```updateReward``` is triggered for both the users, ```rewards[account][token] = earned(account, token);``` will store more than it should because the ```rewardPerToken``` is calculated wrongly;
```solidity
lastTimeRewardApplicable(_rewardsToken).sub(
                rewardData[_rewardsToken].lastUpdateTime
            ).mul(rewardData[_rewardsToken].rewardRate).mul(1e18).div(_totalSupply)
```
```_totalSupply``` is much lesser than it is so it will calculate more than it should for every user. Thus, leading to user B's reward being stuck if user A claims reward first. 


The only way for the stakers to claim rewards is if the protocol directly transfer reward tokens(each of them, one token less will still revert all) to the contract. Or they could do it themselves but still there is a chance to be frontruned by another late claimer. Eitherway it is still a loss of funds for the protocol and the user. If the protocol/user decides to accept the loss then it is also a permanent DOS.

## Impact
Because the ```_totalSupply``` is deducted incorrectly(previously withdrawn tokens are not accounted) in the ```revokeVestingSchedule``` when revoking a user it will lead to more rewards accounted(i.e. ```rewardPerToken```) than the actual reward token balance and early reward claimers can claim more than they should and late claimers will get DOS since there is not enough reward tokens in the contract.

Since ```_totalSupply``` is used to determine reward amount for all types of reward tokens, one change in this will effect all the reward tokens.

## Code Snippet
```solidity
 function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
        require(
            vestingScheduleSet[account], 
            "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleSet[account]"
        );
        require(
            vestingScheduleOf[account].revokable, 
            "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleOf[account].revokable"
        );
        
        uint256 amount = amountWithdrawable(account);
        uint256 vestingAmount = vestingScheduleOf[account].totalVesting; 
        //@audit missing - vestingScheduleOf[account].totalWithdrawn; here 

        vestingTokenAllocated -= amount;

        vestingScheduleOf[account].totalWithdrawn += amount;
        vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
        vestingScheduleOf[account].cliff = block.timestamp - 1;
        vestingScheduleOf[account].end = block.timestamp;

        vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn); 

        _totalSupply = _totalSupply.sub(vestingAmount);
```

## Tool used

Manual Review

## Recommendation
Change [this line](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L440C9-L440C73) in [```revokeVestingSchedule```] function to;
```diff

-       uint256 vestingAmount = vestingScheduleOf[account].totalVesting;

+       uint256 vestingAmount = vestingScheduleOf[account].totalVesting - vestingScheduleOf[account].totalWithdrawn;

``` 
