Fancy Burlap Mouse

high

# `totalSupply` is reduced by incorrect amount in revokeVestingSchedule function

## Summary

In revokeVestingSchedule function of `ZivoeRewardVesting` contract `totalSupply` and _totalSupplyCheckpoints are reduced by totalVesting amount instead of reducing it by revokedAmount.

## Vulnerability Detail

In vesting contract when revoking the vesting schedule of an investor instead of reducing `totalSupply` by remaining unvested amount  `totalSupply` is reduced by the total vestingAmount. This will cause problem if the investor already withdrawn some of his vested $ZVE before revoke.

```solidity
    function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {

        require(vestingScheduleSet[account]);
        require(vestingScheduleOf[account].revokable);
        
        uint256 amount = amountWithdrawable(account);
        uint256 vestingAmount = vestingScheduleOf[account].totalVesting;

        vestingTokenAllocated -= amount;

        vestingScheduleOf[account].totalWithdrawn += amount;
        vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
        vestingScheduleOf[account].cliff = block.timestamp - 1;
        vestingScheduleOf[account].end = block.timestamp;

        vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);

        // @audit totalSupply and _totalSupplyCheckpoints are reduced by incorrect amount
        _totalSupply = _totalSupply.sub(vestingAmount); 
        _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
        
        _writeCheckpoint(_checkpoints[account], _subtract, amount);
        _balances[account] = 0;
        stakingToken.safeTransfer(account, amount);

        vestingScheduleOf[account].revokable = false;

    }
```
[ZivoeRewardsVesting.sol#L451-L452](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L451-L452)


As totalSupply is reduced by more amount than intended  `sum of balances != totalSupply `

Due to this $ZVE of users who withdraw their funds at the last will be locked in the contract due to an underflow when reducing the `totalSupply`

```solidity
    function withdraw() public nonReentrant updateReward(_msgSender()) {
        uint256 amount = amountWithdrawable(_msgSender());
        require(amount > 0, "ZivoeRewardsVesting::withdraw() amountWithdrawable(_msgSender()) == 0");
        
        vestingScheduleOf[_msgSender()].totalWithdrawn += amount;
        vestingTokenAllocated -= amount;

        // @audit Underflow will happen here
        _totalSupply = _totalSupply.sub(amount);
        _writeCheckpoint(_totalSupplyCheckpoints, _subtract, amount);
        _writeCheckpoint(_checkpoints[_msgSender()], _subtract, amount);
        _balances[_msgSender()] = _balances[_msgSender()].sub(amount);
        stakingToken.safeTransfer(_msgSender(), amount);

        emit Withdrawn(_msgSender(), amount);
    }
 ```
 
 [ZivoeRewardsVesting.sol#L508](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L508)

## Impact
Funds of users will be locked in the contract.
If $10M $ZVE of an investor is revoked $10M $ZVE of other users will be locked.

## Code Snippet
[ZivoeRewardsVesting.sol#L451-L452](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L451-L452)

 [ZivoeRewardsVesting.sol#L508](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L508)

## Tool used

Manual Review

## Recommendation

Instead of reducing `totalSupply`  by initial `vestedAmount` reduced it by revokedAmount
```diff
-        _totalSupply = _totalSupply.sub(vestingAmount); 
-        _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);

+        uint revokedAmount = vestingScheduleOf[account].totalVesting - vestingScheduleOf[account].totalWithdrawn;
+        _totalSupply = _totalSupply.sub(vestingAmount); 
+        _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
```
