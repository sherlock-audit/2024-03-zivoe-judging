Helpful Mercurial Finch

medium

# Revoking a vesting schedule underrepresents `_totalSupply` of vestZVE in a certain case, inflating `rewardPerToken` and causing a financial loss for the protocol.

## Summary
Zivoe Labs can create a vesting schedule ([`ZivoeRewardsVesting::createVestingSchedule`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L381-L381)) for its stakeholders such as employees, advisors, and early contributors. 

Later, the Zivoe Laboratory can revoke a vesting schedule from a vestZVE token holder using [`ZivoeRewardsVesting::revokeVestingSchedule`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429-L429).

When it does that, the function incorrectly subtracts more `_totalSupply` than it needs to under a case in which a revoked vesting schedule owner has called [`ZivoeRewardsVesting::withdraw`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L501) before, turning a portion of his vestZVE into ZVE.

Consequently, it will inflate the value of rewards paid to the vestZVE holders due to the usage of `_totalSupply` in [`ZivoeRewardsVesting::rewardPerToken`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L316-L316), making it a financial loss for the protocol.

## Vulnerability Detail

Let's illustrate it with an example.

Alice and Bob are the employees at Zivoe Laboratory and have a vesting schedule respectively. Alice has a vested amount of 100 ZVE, and Bob has a vested amount of 60 ZVE. For the sake of simplicity, we assume that these two are the only vesting schedules that exist on Zivoe protocol.

Then, Alice at some point in the future, calls `withdraw()` to withdraw his claimable portion of vested ZVE, which turns out to be 10 vestZVE. The state changes are denoted as comments in the code:

```solidity
/// @notice Withdraws the available amount of stakingToken from this contract.
function withdraw() public nonReentrant updateReward(_msgSender()) {
    // amount = 10 vestZVE
    uint256 amount = amountWithdrawable(_msgSender());
    require(amount > 0, "ZivoeRewardsVesting::withdraw() amountWithdrawable(_msgSender()) == 0");
    
    // vestingScheduleOf[_msgSender()].totalWithdrawn = 10 vestZVE
    vestingScheduleOf[_msgSender()].totalWithdrawn += amount;
    // vestingTokenAllocated = 160 - 10 = 150 vestZVE
    vestingTokenAllocated -= amount;
    
     // _totalSupply = 160 - 10 = 150 vestZVE
    _totalSupply = _totalSupply.sub(amount);
     // The latest checkpoint would have 150 vestZVE as votes
    _writeCheckpoint(_totalSupplyCheckpoints, _subtract, amount);
     // The latest checkpoint would have 90 vestZVE as votes
    _writeCheckpoint(_checkpoints[_msgSender()], _subtract, amount);
     // _balances[_msgSender()] = 100 - 10 = 90 vestZVE
    _balances[_msgSender()] = _balances[_msgSender()].sub(amount);
     // transfer 10 ZVE (not vestZVE) to Alice
    stakingToken.safeTransfer(_msgSender(), amount);

    emit Withdrawn(_msgSender(), amount);
}
```

After that, Alice resigns from Zivoe Labs, prompting Zivoe Labs to call `revokeVestingSchedule`. Alice is able to withdraw 10 ZVE because some time has passed at the time of revocation:

```solidity
/// @notice Ends vesting schedule for a given account (if revokable).
/// @param  account The acount to revoke a vesting schedule for.
function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
    require(
        vestingScheduleSet[account], 
        "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleSet[account]"
    );
    require(
        vestingScheduleOf[account].revokable, 
        "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleOf[account].revokable"
    );
    
    // before any variable changes in the function, this is the initial state of the following variables:
    // _totalSupply = all users vested amount = (100 - 10 + 60) = 90 + 60 = 150 
    // vestingScheduleOf[account].totalWithdrawn = 10 (90 remaining)

    // amount = 10
    uint256 amount = amountWithdrawable(account);
    // vestingAmount = 100
    uint256 vestingAmount = vestingScheduleOf[account].totalVesting;

    vestingTokenAllocated -= amount;

    // vestingScheduleOf[account].totalWithdrawn = 10 + 10 = 20
    vestingScheduleOf[account].totalWithdrawn += amount;
    // vestingScheduleOf[account].totalVesting = 20
    vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
    vestingScheduleOf[account].cliff = block.timestamp - 1;
    vestingScheduleOf[account].end = block.timestamp;

    vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);

    // _totalSupply = 150 - 100 = 50 (wrong!!)
    _totalSupply = _totalSupply.sub(vestingAmount);
    // The latest total supply checkpoint would have 50 votes, which is also wrong (covered in another issue)
    _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);        
    // Wrong!! (covered in another issue)
    _writeCheckpoint(_checkpoints[account], _subtract, amount);
    _balances[account] = 0;
    stakingToken.safeTransfer(account, amount);

    vestingScheduleOf[account].revokable = false;

    emit VestingScheduleRevoked(
        account, 
        vestingAmount - vestingScheduleOf[account].totalWithdrawn, 
        vestingScheduleOf[account].cliff, 
        vestingScheduleOf[account].end, 
        vestingScheduleOf[account].totalVesting, 
        false
    );
}
```

The problem lies in this line:
```solidity
    _totalSupply = _totalSupply.sub(vestingAmount);
```

`_totalSupply` already reflects Alice's previous withdrawal by already being subtracted by 10 ZVE, but `vestingAmount` is the full original amount that Alice started her vesting with. Therefore, the true total supply would have to be 60 ZVE which is the amount of vestZVE owned by Bob only, but it turns out to be 50 ZVE because it reflects her withdrawal twice.

Then, Zivoe Labs calls [`ZivoeRewardsVesting::addReward(DAI, 1 days)`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L328-L328) to add `DAI` as a reward for vestZVE owners, with 1 day of `rewardsDuration`. Then it calls [`ZivoeRewardsVesting::depositReward`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L352-L352) to deposit 1000 DAI for distribution.

After `1 days`, Bob now proceeds to claim his rewards for the first time by calling [`ZivoeRewardsVesting::getRewards()`](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L484). Before this function is called, `updateReward` is called:

```solidity
modifier updateReward(address account) {
    for (uint256 i; i < rewardTokens.length; i++) {
        address token = rewardTokens[i];
        rewardData[token].rewardPerTokenStored = rewardPerToken(token);
        rewardData[token].lastUpdateTime = lastTimeRewardApplicable(token);
        if (account != address(0)) {
            rewards[account][token] = earned(account, token);
            accountRewardPerTokenPaid[account][token] = rewardData[token].rewardPerTokenStored;
        }
    }
    _;
}
```

This function again calls `ZivoeRewardsVesting::rewardPerToken`:

```solidity
/// @param _rewardsToken The asset that's being distributed.
/// @return amount The cumulative amount of rewards distributed per LP token.
function rewardPerToken(address _rewardsToken) public view returns (uint256 amount) {
    if (_totalSupply == 0) { return rewardData[_rewardsToken].rewardPerTokenStored; }
    return rewardData[_rewardsToken].rewardPerTokenStored.add(
        lastTimeRewardApplicable(_rewardsToken).sub(
            rewardData[_rewardsToken].lastUpdateTime
        ).mul(rewardData[_rewardsToken].rewardRate).mul(1e18).div(_totalSupply)
    );
}
```

For the purpose of demonstration, let's say `rewardData[_rewardsToken].rewardRate` is `10 DAI / 1 days`. Then, the function will return: 

```solidity
uint temp_a = lastTimeRewardApplicable(_rewardsToken).sub(
            rewardData[_rewardsToken].lastUpdateTime
        ) = 1 days;
uint result = temp_a.mul(10 * 10**18 / 1 days).mul(1e18).div(50 * 10 ** 18) = (10 *10 ** 18).mul(1e18).div(50 * 10 ** 18) = 200000000000000000
```

200000000000000000 / 10**18 = 0.2 DAI. Then, `rewardData[token].rewardPerTokenStored = 0.2 DAI`.

Finally, calculate the amount returned by `ZivoeRewardsVesting::earned`:

```solidity
function earned(address account, address _rewardsToken) public view returns (uint256 amount) {
    return _balances[account].mul(
        rewardPerToken(_rewardsToken).sub(accountRewardPerTokenPaid[account][_rewardsToken])
    ).div(1e18).add(rewards[account][_rewardsToken]);
}
```

The returned value will be `((60 * 10 ** 18) * 200000000000000000) / 1000000000000000000 + 0 = 12000000000000000000` = 12 DAI. We have successfully showed that Bob in fact earns more than what's specified by `rewardRate`. 

If we had the correct `_totalSupply`, we would have:

`rewardData[token].rewardPerTokenStored = (10 * 10 ** 18).mul(1e18).div(60 * 10 ** 18) = 166666666666666666` =  0.16666666666666666 DAI. And `earned` will return `((60 * 10 ** 18) * 166666666666666666) / 1000000000000000000 + 0 = 9999999999999999960` â‰ˆ 10000000000000000000 = 10 DAI, which complies with the `rewardRate`.

## Impact

The protocol pays more reward than intended, which comes at the cost of losing money.

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L451

## Tool used

Manual Review

## Recommendation

```diff
-    _totalSupply = _totalSupply.sub(vestingAmount);
+    uint removedVestZVEAmount = vestingAmount - vestingScheduleOf[account].totalWithdrawn + amount;
+    _totalSupply = _totalSupply.sub(removedVestZVEAmount);
```

Such a fix will correctly reflect the amount that was already withdrawn. `vestingScheduleOf[account].totalWithdrawn + amount` looks odd, but this is because `vestingScheduleOf[account].totalWithdrawn += amount;` earlier in the function call.