Tangy Concrete Chimpanzee

high

# Incorrect accounting in `ZivoeRewardsVesting::revokeVestingSchedule()` will cause late user(s) to be denied the complete withdrawal of ZVE in `ZivoeRewardsVesting::withdraw()`

## Summary

## Vulnerability Detail

Given that ZivoeITO is a template contract for Initial Tranche Offering, even though it doesn't currently support revoking a vesting schedule, there could be a variation of this that causes the inability of late user(s) from withdrawing the ZVE locked in the Rewards Vesting contract from ITO because of incorrect accounting.

Users can withdraw some tokens during a vesting schedule by calling `ZivoeRewardsVesting::withdraw()`. Doing this during or after the cliffing period withdraws the amount of tokens proportionate to the time passed since the cliff period. **_Since there is a reduction in their balance, the _totalSupply, representing the total LP contribution, vestingTokenAllocated, checkpoints, and user's LP balance is reduced accordingly._**

```solidity
    function withdraw() public nonReentrant updateReward(_msgSender()) {
        uint256 amount = amountWithdrawable(_msgSender());
        require(amount > 0, "ZivoeRewardsVesting::withdraw() amountWithdrawable(_msgSender()) == 0");
        
        vestingScheduleOf[_msgSender()].totalWithdrawn += amount;
        vestingTokenAllocated -= amount;

        _totalSupply = _totalSupply.sub(amount);
        _writeCheckpoint(_totalSupplyCheckpoints, _subtract, amount);
        _writeCheckpoint(_checkpoints[_msgSender()], _subtract, amount);
        _balances[_msgSender()] = _balances[_msgSender()].sub(amount);
        stakingToken.safeTransfer(_msgSender(), amount);

        emit Withdrawn(_msgSender(), amount);
    }
```

When revoking is enabled, a user isn't bound to the vesting schedule anymore, their account is wiped, and ZVE is transferred to their account. However, due to incorrect accounting, if there are users who withdraw a part of their tokens after the cliff period and then their schedule was revoked by ZVL, they'll hinder other late users (users who withdraw their tokens last) from being able to withdraw the entirety of their entitlements because, with each revocation, [`ZivoeRewardsVestings::revokeVestingSchedule()`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429C1-L467C6) removes the entire vesting amount rather than the amount withdrawable from the LP _totalSupply and totalSupplyCheckpoints. At some point, this will cause an underflow.

```solidity
        function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {

        ..........................
        
        uint256 amount = amountWithdrawable(account);
        uint256 vestingAmount = vestingScheduleOf[account].totalVesting;

        vestingTokenAllocated -= amount;

        vestingScheduleOf[account].totalWithdrawn += amount;
        vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
        vestingScheduleOf[account].cliff = block.timestamp - 1;
        vestingScheduleOf[account].end = block.timestamp;

        vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);

        _totalSupply = _totalSupply.sub(vestingAmount);
        _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
        _writeCheckpoint(_checkpoints[account], _subtract, amount);
        _balances[account] = 0;
        stakingToken.safeTransfer(account, amount);

        vestingScheduleOf[account].revokable = false;

        ......................
    }
```

## Impact
The underflow of totalSupply and totalSupplyCheckpoints is causing the loss of a part or the entirety of the tokens to late users.

## Proof of Concept
```solidity
    function test_ZivoeRewardsVesting_withdraw_incorrectAccounting() public {
        //create a vesting schedule by 
        assert(zvl.try_createVestingSchedule(
            address(vestZVE), 
            address(pam), 
            0, 
            365,
            100_000 ether, 
            true
        ));

        assert(zvl.try_createVestingSchedule(
            address(vestZVE), 
            address(moe), 
            0, 
            365,
            100_000 ether, 
            true
        ));

        hevm.warp(block.timestamp + 15 days);

        uint unstake = vestZVE.amountWithdrawable(address(pam));
        emit log_named_uint("Pam's Amount withdrawable after cliffing (+15 days) is ", unstake);
        emit log_named_uint("LP Total Supply is ", vestZVE.totalSupply());

        // withdraw().
        hevm.expectEmit(true, false, false, true, address(vestZVE));
        emit Withdrawn(address(pam), unstake);
        assert(pam.try_withdraw(address(vestZVE)));

        emit log_named_uint("LP Total Supply (After Partial Withdrawal) is ", vestZVE.totalSupply());

        hevm.startPrank(address(zvl));
        vestZVE.revokeVestingSchedule(address(pam));

        //sam attempts to withdraw after vesting period ends
        hevm.warp(block.timestamp + 360 days);
        assert(moe.try_withdraw(address(vestZVE)));
    }
```

## Code Snippet

```solidity
        function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
        require(
            vestingScheduleSet[account], 
            "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleSet[account]"
        );
        require(
            vestingScheduleOf[account].revokable, 
            "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleOf[account].revokable"
        );
        
        uint256 amount = amountWithdrawable(account);
        uint256 vestingAmount = vestingScheduleOf[account].totalVesting;


        vestingTokenAllocated -= amount;


        vestingScheduleOf[account].totalWithdrawn += amount;
        vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
        vestingScheduleOf[account].cliff = block.timestamp - 1;
        vestingScheduleOf[account].end = block.timestamp;


        vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);


        _totalSupply = _totalSupply.sub(vestingAmount);
        _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
        _writeCheckpoint(_checkpoints[account], _subtract, amount);
        _balances[account] = 0;
        stakingToken.safeTransfer(account, amount);


        vestingScheduleOf[account].revokable = false;


        emit VestingScheduleRevoked(
            account, 
            vestingAmount - vestingScheduleOf[account].totalWithdrawn, 
            vestingScheduleOf[account].cliff, 
            vestingScheduleOf[account].end, 
            vestingScheduleOf[account].totalVesting, 
            false
        );
    }
```

## Tool used

Manual Review & Foundry Testing

## Recommendation
```diff
    function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
        require(
            vestingScheduleSet[account], 
            "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleSet[account]"
        );
        require(
            vestingScheduleOf[account].revokable, 
            "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleOf[account].revokable"
        );
        
        uint256 amount = amountWithdrawable(account);
        uint256 vestingAmount = vestingScheduleOf[account].totalVesting;


        vestingTokenAllocated -= amount;


        vestingScheduleOf[account].totalWithdrawn += amount;
        vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
        vestingScheduleOf[account].cliff = block.timestamp - 1;
        vestingScheduleOf[account].end = block.timestamp;


        vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);


-        _totalSupply = _totalSupply.sub(vestingAmount);
+        _totalSupply = _totalSupply.sub(amount);
-        _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
+        _writeCheckpoint(_totalSupplyCheckpoints, _subtract, amount);
        _writeCheckpoint(_checkpoints[account], _subtract, amount);
        _balances[account] = 0;
        stakingToken.safeTransfer(account, amount);


        vestingScheduleOf[account].revokable = false;


        emit VestingScheduleRevoked(
            account, 
            vestingAmount - vestingScheduleOf[account].totalWithdrawn, 
            vestingScheduleOf[account].cliff, 
            vestingScheduleOf[account].end, 
            vestingScheduleOf[account].totalVesting, 
            false
        );
    }
```