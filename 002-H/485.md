Round Hazelnut Tadpole

high

# `revokeVestingSchedule` function, when called after staking token withdrawal of the user whose vesting schedule is to be revoked, incorrectly reduces `_totalSupply` and `_totalSupplyCheckpoints` at `block.number` to values that are lower than what they should be

## Summary
The `revokeVestingSchedule` function, when called after the staking token withdrawal of the user whose vesting schedule is to be revoked, incorrectly reduces `_totalSupply` and `_totalSupplyCheckpoints` at `block.number` to values that are lower than what they should be. This issue allows users to claim more rewards than entitled to, resulting in losses of the rewards that should not be distributed for the `ZivoeRewardsVesting` contract and inaccurate total supply retrievals.

## Vulnerability Detail
The `revokeVestingSchedule` function shown in the Code Snippet section executes `_totalSupply = _totalSupply.sub(vestingAmount)` and `_writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount)`, where `vestingAmount` is `vestingScheduleOf[account].totalVesting`. However, when such `account` has called the `withdraw` function shown in the Code Snippet section before the `revokeVestingSchedule` function call, `_totalSupply` and `_totalSupplyCheckpoints` at `block.number` have been reduced by such `account`'s withdrawable amount at the moment of the `withdraw` function call already, and subtracting such `vestingAmount` from `_totalSupply` and `_totalSupplyCheckpoints` at `block.number` would be reducing too much in the `revokeVestingSchedule` function call, causing the `_totalSupply` and `_totalSupplyCheckpoints` at `block.number` to be less than what they should be.

The following example is presented to demonstrate the issue.
1. ZVL calls the `createVestingSchedule` function shown in the Code Snippet section with `amountToVest` being 10000e18 and `revokable` being true to set the vesting schedule for Alice. The relevant variables take the following values after this step.
	- `vestingTokenAllocated`: 10000e18
	- `vestingScheduleOf[Alice].totalVesting`: 10000e18
	- `_totalSupply`: 10000e18
	- `_totalSupplyCheckpoints` at `block.number`: 10000e18
2. ZVL calls the `createVestingSchedule` function with `amountToVest` also being 10000e18 for Bob. The relevant variables take the following values after this step.
	- `vestingTokenAllocated`: 20000e18
	- `vestingScheduleOf[Bob].totalVesting`: 10000e18
	- `_totalSupply`: 20000e18
	- `_totalSupplyCheckpoints` at `block.number`: 20000e18
	- `_balances[Bob]`: 10000e18
3. Alice calls the `withdraw` function when `amountWithdrawable(Alice)` returns 3000e18. The relevant variables take the following values after this step. Note that `_totalSupply` and `_totalSupplyCheckpoints` at `block.number` are reduced by Alice's withdrawable amount already.
	- `vestingScheduleOf[Alice].totalWithdrawn`: 3000e18
	- `vestingTokenAllocated`: 17000e18
	- `_totalSupply`: 17000e18
	- `_totalSupplyCheckpoints` at `block.number`: 17000e18
4. ZVL calls the `revokeVestingSchedule` function to end the vesting schedule for Alice, whose vesting schedule is revokable, when `amountWithdrawable(Alice)` returns 2000e18. The relevant variables take the following values after this step. Note that `_totalSupply` and `_totalSupplyCheckpoints` at `block.number` are incorrectly reduced to 7000e18, which is lower than 10000e18 that is what they should be and what `vestingTokenAllocated` is correctly reduced to.
	- `vestingScheduleOf[Alice].totalWithdrawn`: 5000e18
	- `vestingScheduleOf[Alice].totalVesting`: 5000e18
	- `vestingTokenAllocated`: 10000e18
	- `_totalSupply`: 17000e18 - 10000e18 = 7000e18
	- `_totalSupplyCheckpoints` at `block.number`: 17000e18 - 10000e18 = 7000e18

## Impact
Because `_totalSupply` is lower than what it should be, the `earned` function shown in the Code Snippet section would return `amount` that is higher than what it should be. Continuing with the previous example, Bob can claim a reward token amount that is higher than entitled to since the `earned` function for him would return the following reward amount. Allowing users to claim more rewards than entitled to means that the `ZivoeRewardsVesting` contract losses reward amounts that should not be distributed.

```solidity
Bob's reward amount of _rewardsToken
= _balances[Bob] * ((Bob's staking duration) * rewardData[_rewardsToken].rewardRate) / _totalSupply
= 10000e18 * ((Bob's staking duration) * rewardData[_rewardsToken].rewardRate) / 7000e18
= (10/7) * ((Bob's staking duration) * rewardData[_rewardsToken].rewardRate)
> (Bob's staking duration) * rewardData[_rewardsToken].rewardRate
```

Moreover, since `_totalSupplyCheckpoints` at `block.number` is lower than what it should be, calling the `getPastTotalSupply` function shown in the Code Snippet section can retrieve incorrectly lower total supply at the end of the relevant `blockNumber` as well.

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L381-L425
```solidity
    function createVestingSchedule(
        address account, 
        uint256 daysToCliff, 
        uint256 daysToVest, 
        uint256 amountToVest, 
        bool revokable
    ) external onlyZVLOrITO {
        ...
        vestingScheduleSet[account] = true;
        vestingTokenAllocated += amountToVest;
        
        vestingScheduleOf[account].start = block.timestamp;
        vestingScheduleOf[account].cliff = block.timestamp + daysToCliff * 1 days;
        vestingScheduleOf[account].end = block.timestamp + daysToVest * 1 days;
        vestingScheduleOf[account].totalVesting = amountToVest;
        vestingScheduleOf[account].vestingPerSecond = amountToVest / (daysToVest * 1 days);
        vestingScheduleOf[account].revokable = revokable;
        ...
        _stake(amountToVest, account);
    }
```

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L473-L481
```solidity
    function _stake(uint256 amount, address account) private nonReentrant updateReward(account) {
        ...
        _totalSupply = _totalSupply.add(amount);
        _writeCheckpoint(_totalSupplyCheckpoints, _add, amount);
        _writeCheckpoint(_checkpoints[account], _add, amount);
        _balances[account] = _balances[account].add(amount);
        ...
    }
```

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L501-L515
```solidity
    function withdraw() public nonReentrant updateReward(_msgSender()) {
        uint256 amount = amountWithdrawable(_msgSender());
        ...
        vestingScheduleOf[_msgSender()].totalWithdrawn += amount;
        vestingTokenAllocated -= amount;

        _totalSupply = _totalSupply.sub(amount);
        _writeCheckpoint(_totalSupplyCheckpoints, _subtract, amount);
        _writeCheckpoint(_checkpoints[_msgSender()], _subtract, amount);
        _balances[_msgSender()] = _balances[_msgSender()].sub(amount);
        stakingToken.safeTransfer(_msgSender(), amount);
        ...
    }
```

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429-L467
```solidity
    function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
        ...
        uint256 amount = amountWithdrawable(account);
        uint256 vestingAmount = vestingScheduleOf[account].totalVesting;

        vestingTokenAllocated -= amount;

        vestingScheduleOf[account].totalWithdrawn += amount;
        vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
        vestingScheduleOf[account].cliff = block.timestamp - 1;
        vestingScheduleOf[account].end = block.timestamp;

        vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);

        _totalSupply = _totalSupply.sub(vestingAmount);
        _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
        _writeCheckpoint(_checkpoints[account], _subtract, amount);
        _balances[account] = 0;
        stakingToken.safeTransfer(account, amount);

        vestingScheduleOf[account].revokable = false;
        ...
    }
```

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L300-L304
```solidity
    function earned(address account, address _rewardsToken) public view returns (uint256 amount) {
        return _balances[account].mul(
            rewardPerToken(_rewardsToken).sub(accountRewardPerTokenPaid[account][_rewardsToken])
        ).div(1e18).add(rewards[account][_rewardsToken]);
    }
```

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L316-L323
```solidity
    function rewardPerToken(address _rewardsToken) public view returns (uint256 amount) {
        if (_totalSupply == 0) { return rewardData[_rewardsToken].rewardPerTokenStored; }
        return rewardData[_rewardsToken].rewardPerTokenStored.add(
            lastTimeRewardApplicable(_rewardsToken).sub(
                rewardData[_rewardsToken].lastUpdateTime
            ).mul(rewardData[_rewardsToken].rewardRate).mul(1e18).div(_totalSupply)
        );
    }
```

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/libraries/ZivoeVotes.sol#L63-L66
```solidity
    function getPastTotalSupply(uint256 blockNumber) public view virtual returns (uint256) {
        require(blockNumber < block.number, "ERC20Votes: block not yet mined");
        return _checkpointsLookup(_totalSupplyCheckpoints, blockNumber);
    }
```

## Tool used
Manual Review

## Recommendation
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L439-L452 can be updated to the following code.

```solidity
        uint256 amount = amountWithdrawable(account);
        uint256 vestingAmount = vestingScheduleOf[account].totalVesting;

        uint256 totalSupplyReduction = vestingScheduleOf[account].totalVesting - vestingScheduleOf[account].totalWithdrawn;

        vestingTokenAllocated -= amount;

        vestingScheduleOf[account].totalWithdrawn += amount;
        vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
        vestingScheduleOf[account].cliff = block.timestamp - 1;
        vestingScheduleOf[account].end = block.timestamp;

        vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);

        _totalSupply = _totalSupply.sub(totalSupplyReduction);
        _writeCheckpoint(_totalSupplyCheckpoints, _subtract, totalSupplyReduction);
```