Bubbly Rouge Porcupine

medium

# wrong `_totalSupply` accounting  causing DOS on `ZivoeRewardsVesting.withdraw` and `ZivoeRewardsVesting.revokeVestingSchedule`

 




## Summary


Root cause: wrong `_totalSupply` accounting on  [ZivoeRewardsVesting.revokeVestingSchedule](https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L451)


impact :
    1. DOS on [ZivoeRewardsVesting.withdraw](https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewards.sol#L299) and [ZivoeRewardsVesting.revokeVestingSchedule](https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429) on few edge cases due to underflow revert.
    2. So either some vesting recipients cannot claim their vested tokens, and also a situation where [ZivoeRewardsVesting.revokeVestingSchedule](https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429) will revert if there are multiple revokes.


## Vulnerability Detail


https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L451


https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L501


```solidity
File: 2024-03-zivoe\zivoe-core-foundry\src\ZivoeRewardsVesting.sol
442:     function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
443:         require(
444:             vestingScheduleSet[account],
445:             "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleSet[account]"
446:         );
447:         require(
448:             vestingScheduleOf[account].revokable,
449:             "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleOf[account].revokable"
450:         );
451:        
452: >>>     uint256 amount = amountWithdrawable(account);
453:         uint256 vestingAmount = vestingScheduleOf[account].totalVesting;
454:
455:         vestingTokenAllocated -= amount;
456:
457:         vestingScheduleOf[account].totalWithdrawn += amount;
458:         vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
459:         vestingScheduleOf[account].cliff = block.timestamp - 1;
460:         vestingScheduleOf[account].end = block.timestamp;
461:
462:         vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);
463:
465: >>>     _totalSupply = _totalSupply.sub(vestingAmount);
466:         _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
467:         _writeCheckpoint(_checkpoints[account], _subtract, amount);
468:         _balances[account] = 0;
469:         stakingToken.safeTransfer(account, amount);
471:
472:         vestingScheduleOf[account].revokable = false;
473:
474:         emit VestingScheduleRevoked(
475:             account,
476:             vestingAmount - vestingScheduleOf[account].totalWithdrawn,
477:             vestingScheduleOf[account].cliff,
478:             vestingScheduleOf[account].end,
479:             vestingScheduleOf[account].totalVesting,
480:             false
481:         );
482:     }



518:     function withdraw() public nonReentrant updateReward(_msgSender()) {
519:         uint256 amount = amountWithdrawable(_msgSender());
520:         require(amount > 0, "ZivoeRewardsVesting::withdraw() amountWithdrawable(_msgSender()) == 0");
521:        
522:         vestingScheduleOf[_msgSender()].totalWithdrawn += amount;
523:         vestingTokenAllocated -= amount;
524:
525:  >>>    _totalSupply = _totalSupply.sub(amount);
526:         _writeCheckpoint(_totalSupplyCheckpoints, _subtract, amount);
527:         _writeCheckpoint(_checkpoints[_msgSender()], _subtract, amount);
528:         _balances[_msgSender()] = _balances[_msgSender()].sub(amount);
529:         stakingToken.safeTransfer(_msgSender(), amount);
530:
531:         emit Withdrawn(_msgSender(), amount);
532:     }



```


Attack / issue path :


1. There are 2 vesting schedules currently, which may be before or after ITO. And one is revokable, and the other is not. And both vest 1000 ZVE each for 1 month end at a 7-day cliff.
2. After 2 weeks, both recipients will withdraw their tokens, so 500 will be transferred each. And 500 more to vest for each. So there is still 1000 ZVE of total supply in the contract.
3. Right now, the ZVL tries to revoke the schedule of one guy, which the ZVL calls [ZivoeRewardsVesting.revokeVestingSchedule](https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429). And look at L465  above, the whole `vestingAmount = 1000` will be removed from total supply, setting it to zero for now.


4. Now, if the other unrevokable recipient will try to withdraw the other half 500 tokens at the end of month, and he will get reverted at L525 above, because  total supply = 0 - 500 will underflow revert.


This issue is just one edge case, but these edge cases are very likely to occur.
1. The vesting recipient frontruns the [ZivoeRewardsVesting.revokeVestingSchedule](https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429) and calls  [ZivoeRewardsVesting.withdraw](https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewards.sol#L299).
2.  before ITO,
3. and at the end of the most vesting schedules, or
4. Even at anytime, if multiple revoke actions happen, becasue the whole `vestingAmount` will be subtracted from total supply.



## Impact


Medium DOS impact and easy likelihood/occurence to carry the attack and triggering the impact. So giving it a medium.


    1. DOS on [ZivoeRewardsVesting.withdraw](https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewards.sol#L299) and [ZivoeRewardsVesting.revokeVestingSchedule](https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429) on few edge cases due to underflow revert.
    2. so either some vesting recipients cannot claim their vested tokens, and also a situation where [ZivoeRewardsVesting.revokeVestingSchedule](https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L429) will revert if there are multiple revokes.


## Code Snippet


https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L451


https://github.com/sherlock-audit/2024-03-zivoe/blob/01e00e6f27b58392a6fa0b82c84a46a783a0df3c/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L501


## Tool used


Manual Review


## Recommendation



```diff
 function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {

        ***

        vestingTokenAllocated -= amount;

        vestingScheduleOf[account].totalWithdrawn += amount;
        vestingScheduleOf[account].totalVesting = vestingScheduleOf[account].totalWithdrawn;
        vestingScheduleOf[account].cliff = block.timestamp - 1;
        vestingScheduleOf[account].end = block.timestamp;

        vestingTokenAllocated -= (vestingAmount - vestingScheduleOf[account].totalWithdrawn);

-       _totalSupply = _totalSupply.sub(vestingAmount);
+       _totalSupply = _totalSupply.sub(vestingAmount - vestingScheduleOf[account].totalWithdrawn);
        _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
        _writeCheckpoint(_checkpoints[account], _subtract, amount);
        _balances[account] = 0;
        stakingToken.safeTransfer(account, amount);

        vestingScheduleOf[account].revokable = false;

        ***

    }
```