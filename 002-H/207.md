Passive Rouge Peacock

medium

# ````_totalSupply```` and ````_totalSupplyCheckpoints```` are wrongly updated while ````revokeVestingSchedule()````

## Summary
In ````ZivoeRewardsVesting.revokeVestingSchedule()````, the ````_totalSupply```` and ````_totalSupplyCheckpoints```` are wrongly updated, which would cause users unable to withdraw their vested ````ZVE```` token.

## Vulnerability Detail
The issue arises on L440, the correct````vestingAmount ```` that  ````_totalSupply```` should be subtracted is the remaining vesting amount of ````totalVesting - totalWithdrawn````, as the ````totalWithdrawn```` amount has been previously subtracted on L508 of  ````withdraw()```` function.
```diff
File: zivoe-core-testing\lib\zivoe-core-foundry\src\ZivoeRewardsVesting.sol
429:     function revokeVestingSchedule(address account) external updateReward(account) onlyZVLOrITO nonReentrant {
...
-440:         uint256 vestingAmount = vestingScheduleOf[account].totalVesting;
+440:         uint256 vestingAmount = vestingScheduleOf[account].totalVesting - vestingScheduleOf[account].totalWithdrawn;
...
451:         _totalSupply = _totalSupply.sub(vestingAmount);
452:         _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
...
467:     }

File: zivoe-core-testing\lib\zivoe-core-foundry\src\ZivoeRewardsVesting.sol
501:     function withdraw() public nonReentrant updateReward(_msgSender()) {
502:         uint256 amount = amountWithdrawable(_msgSender());
...
508:         _totalSupply = _totalSupply.sub(amount);
509:         _writeCheckpoint(_totalSupplyCheckpoints, _subtract, amount);
510:         _writeCheckpoint(_checkpoints[_msgSender()], _subtract, amount);
...
515:     }

```

As the ````totalWithdrawn```` portion is subtracted twice, it would cause underflow revert on L508 while later users try to withdraw ````ZVE```` token.
The following PoC shows a case of the above impact:
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

import "../Utility/Utility.sol";
import "forge-std/console2.sol";

contract TestRewardVestingTotalSupplyBug is Utility {
    address private _alice;
    address private _bob;
    function setUp() public {
        _alice = makeAddr("alice");
        _bob = makeAddr("bob");
        deployCore(false);
    }

    function test() public {
        // 1. create vesting schedule for alice and bob
        assert(zvl.try_createVestingSchedule(
            address(vestZVE), 
            _alice, 
            30, 
            120,
            1_000_000 ether, 
            true
        ));
        assert(zvl.try_createVestingSchedule(
            address(vestZVE), 
            _bob, 
            30, 
            120,
            1_000_000 ether, 
            true
        ));
        assertEq(2_000_000 ether, vestZVE.totalSupply());
        
        // 2. make sure enough ZVE balance for withdrawal
        assertGt(ZVE.balanceOf(address(vestZVE)), 2_000_000 ether);
    
        // 3. 50% vested and alice withdraw
        vm.warp(block.timestamp + 60 days);
        assertApproxEqAbs(500_000 ether, vestZVE.amountWithdrawable(_alice), 1 ether);
        assertApproxEqAbs(500_000 ether, vestZVE.amountWithdrawable(_bob), 1 ether);
        vm.prank(_alice);
        vestZVE.withdraw();

        // 4. revoke alice
        zvl.try_revokeVestingSchedule(address(vestZVE), _alice);

        // 5. a bit more ZVE vested, now bob can't withdraw
        vm.warp(block.timestamp + 1 days);
        vm.startPrank(_bob);
        vm.expectRevert();
        vestZVE.withdraw();
        vm.stopPrank();
    }
}
```

And the test log:
```solidity
2024-03-zivoe\zivoe-core-testing> forge test --mc TestRewardVestingTotalSupplyBug -vv
[â °] Compiling...
No files changed, compilation skipped

Ran 1 test for src/TESTS_Core/Bug_RewardsVestingTotalSupply.t.sol:TestRewardVestingTotalSupplyBug
[PASS] test() (gas: 747184)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 7.67ms (1.26ms CPU time)

Ran 1 test suite in 38.46ms (7.67ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Impact
````_totalSupply```` and ````_totalSupplyCheckpoints```` are wrongly updated, which would cause users unable to withdraw their vested ````ZVE```` token.

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L451~L452

## Tool used

Manual Review

## Recommendation
See ````Vulnerability Detail```` section
