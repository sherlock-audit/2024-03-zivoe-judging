Curved Zinc Scallop

high

# `ZivoeRewardsVesting::revokeVestingSchedule` incorrectly decreases the total staking supply `_totalSupply`

## Summary

When a vesting schedule is revoked with `ZivoeRewardsVesting::revokeVestingSchedule`, the staking total supply `_totalSupply` amount is incorrectly decreased by the full vesting amount without accounting for previous user withdrawals, which will result in wrong `_totalSupply` accounting leading to some users being unable to withdraw their staked tokens (fund loss).

## Vulnerability Detail

The ZVL can decide to revoke the vesting schedule given to a certain user by calling the `ZivoeRewardsVesting::revokeVestingSchedule` function below:

```solidity
function revokeVestingSchedule(
    address account
) external updateReward(account) onlyZVLOrITO nonReentrant {
    require(
        vestingScheduleSet[account],
        "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleSet[account]"
    );
    require(
        vestingScheduleOf[account].revokable,
        "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleOf[account].revokable"
    );

    uint256 amount = amountWithdrawable(account);
    uint256 vestingAmount = vestingScheduleOf[account].totalVesting;

    vestingTokenAllocated -= amount;

    vestingScheduleOf[account].totalWithdrawn += amount;
    vestingScheduleOf[account].totalVesting = vestingScheduleOf[account]
        .totalWithdrawn;
    vestingScheduleOf[account].cliff = block.timestamp - 1;
    vestingScheduleOf[account].end = block.timestamp;

    vestingTokenAllocated -= (vestingAmount -
        vestingScheduleOf[account].totalWithdrawn);

    //@audit incorrectly decrement total vestingAmount
    _totalSupply = _totalSupply.sub(vestingAmount);
    _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
    _writeCheckpoint(_checkpoints[account], _subtract, amount);
    _balances[account] = 0;
    stakingToken.safeTransfer(account, amount);

    vestingScheduleOf[account].revokable = false;

    emit VestingScheduleRevoked(
        account,
        vestingAmount - vestingScheduleOf[account].totalWithdrawn,
        vestingScheduleOf[account].cliff,
        vestingScheduleOf[account].end,
        vestingScheduleOf[account].totalVesting,
        false
    );
}
```

The function does calculate the current withdrawable amount for the user and then it updates the `vestingScheduleOf[account]` struct data to prevent user from withdrawing in the future, next, it decrement the remaining vesting amount from the state `vestingTokenAllocated`, till here everything is fine.

The issue now is when decreasing the total staking supply represented by `_totalSupply`, the function does decrement the total `vestingAmount` amount which is equal to the initial vesting amount set when the vesting schedule was created, `vestingAmount` is the cached value of `vestingScheduleOf[account].totalVesting` before it was update:

```solidity
uint256 vestingAmount = vestingScheduleOf[account].totalVesting;
```

This amount is never update after the creation of the vesting schedule, and thus it will always represent the total vesting amount even if user withdraw some of the amount along the way.

Knowing that there is a scenario that can occur in which the `_totalSupply` amount get decreased more than intended resulting in some users being unable to withdraw in the future, and this scenario has relatively high likelihood of occurring, let's highlight it in the example below:

- For this scenario assumes that there are no changes in the total supply amount apart from the one related to Bob vesting schedule ( just for calculation simplicity, the issue is relevant in all cases).

- Bob was given a vesting schedule of a 10000 tokens for 1 year, and the total supply was equal to 100000 tokens (Bob balance is accounted for). So we have:

```solidity
_totalSupply = 100000;
vestingScheduleOf[Bob].totalVesting = 10000;
```

- After 3 months, Bob decides to withdraw some of his vesting tokens and in that period he's allowed to withdraw 2500 tokens (just for example, the number doesn't really matter for our scenario), so Bob call `ZivoeRewardsVesting::withdraw` function which updates the states as follows:

```solidity
_totalSupply = 100000 - 2500 = 97500;
vestingScheduleOf[Bob].totalWithdrawn = 0 + 2500 = 2500;
vestingScheduleOf[Bob].totalVesting = 10000; // not changed
```

We notice that `vestingScheduleOf[account].totalVesting` is not changed by the operation but `_totalSupply` is changed.

- A month after that (so 4 month has passed since vesting schedule creation) ZVL decides to revoke Bob vesting schedule and calls `ZivoeRewardsVesting::revokeVestingSchedule`, we suppose at that point Bob withdrawable amount was 1000 tokens, first thing the function does cache Bob total vesting amount in `vestingAmount` variable:

```solidity
uint256 vestingAmount = vestingScheduleOf[Bob].totalVesting = 10000;
```

Then the function does the following updates (only mention the relevant ones):

```solidity
vestingScheduleOf[Bob].totalWithdrawn = 2500 + 1000 = 3500;
vestingScheduleOf[Bob].totalVesting = 3500;
_totalSupply = _totalSupply.sub(vestingAmount) = 97500 - 10000 = 87500;
```

We notice that the total supply amount was decreased up to `87500` while normally it should only drops to `90000` because Bob was only given `10000` vesting tokens, but we see an additional drop of `2500` tokens because Bob did withdraw some of his vesting tokens before the vesting was revoked and those were not accounted for in the `revokeVestingSchedule` function logic.

- Now that the total supply accounting is wrong (less than intended), some users that will try to withdraw in the future might face a revert because of an underflow in `ZivoeRewardsVesting::withdraw` function as `_totalSupply` is less than what it should be which lead to an underflow in this [line](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L508).

So to summarize, the issue happens because `ZivoeRewardsVesting::revokeVestingSchedule` function neglect the user previously withdrawn vesting amounts when revoking him which result in some users facing financial loss as they will be unable to withdraw their staked tokens (`ZivoeRewardsVesting::withdraw` reverts due underflow when decrementing `_totalSupply`).

**NOTE: This issue has a relatively a high likelihood of occurring as there is no way to prevent users from withdraw part of their vesting tokens before getting revoked, and such cases each time the revoke function is called it will make the situation even worse meaning more users won't be able to withdraw later on**

## Impact

When a vesting schedule is revoke with `ZivoeRewardsVesting::revokeVestingSchedule`, the staking total supply `_totalSupply` amount is incorrectly decreased by the full vesting amount without counting previous withdrawals which will result in wrong `_totalSupply` accounting leading to some users being unable to withdraw their staked tokens.

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L440-L451

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/ZivoeRewardsVesting.sol#L508

## Tool used

Manual Review

## Recommendation

To address this issue, the `ZivoeRewardsVesting::revokeVestingSchedule` function must update `_totalSupply` correctly according to the previously withdrawn amount, the following changed should be introduced:

```diff
function revokeVestingSchedule(
    address account
) external updateReward(account) onlyZVLOrITO nonReentrant {
    require(
        vestingScheduleSet[account],
        "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleSet[account]"
    );
    require(
        vestingScheduleOf[account].revokable,
        "ZivoeRewardsVesting::revokeVestingSchedule() !vestingScheduleOf[account].revokable"
    );

    uint256 amount = amountWithdrawable(account);
    uint256 vestingAmount = vestingScheduleOf[account].totalVesting;
++  uint256 totalWithdrawn = vestingScheduleOf[account].totalWithdrawn;

    vestingTokenAllocated -= amount;

    vestingScheduleOf[account].totalWithdrawn += amount;
    vestingScheduleOf[account].totalVesting = vestingScheduleOf[account]
        .totalWithdrawn;
    vestingScheduleOf[account].cliff = block.timestamp - 1;
    vestingScheduleOf[account].end = block.timestamp;

    vestingTokenAllocated -= (vestingAmount -
        vestingScheduleOf[account].totalWithdrawn);

--  _totalSupply = _totalSupply.sub(vestingAmount);
++  _totalSupply = _totalSupply.sub(vestingAmount - totalWithdrawn);
    _writeCheckpoint(_totalSupplyCheckpoints, _subtract, vestingAmount);
    _writeCheckpoint(_checkpoints[account], _subtract, amount);
    _balances[account] = 0;
    stakingToken.safeTransfer(account, amount);

    vestingScheduleOf[account].revokable = false;

    emit VestingScheduleRevoked(
        account,
        vestingAmount - vestingScheduleOf[account].totalWithdrawn,
        vestingScheduleOf[account].cliff,
        vestingScheduleOf[account].end,
        vestingScheduleOf[account].totalVesting,
        false
    );
}
```