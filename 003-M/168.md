Sleepy Fuzzy Pig

high

# ```pushToLockerMulti``` in ```OCL_ZVE``` will be DOS in most cases because of asserting 0 allowance after adding liquidity

## Summary
```pushToLockerMulti``` in ```OCL_ZVE``` will be DOS in most cases because of asserting 0 allowance after adding liquidity.
## Vulnerability Detail
```OCL_ZVE``` manages liquidity provisioning for a Uniswap v2 or Sushi pool. 
In ```pushToLockerMulti```, liquidity is added by calling ```addLiquidity``` of ```router``` contract.
```addLiquidity``` in ```UniswapV2Router02``` is shown below:
```solidity
  function _addLiquidity(
        address tokenA,
        address tokenB,
        uint amountADesired,
        uint amountBDesired,
        uint amountAMin,
        uint amountBMin
    ) internal virtual returns (uint amountA, uint amountB) {
        ······
        (uint reserveA, uint reserveB) = UniswapV2Library.getReserves(factory, tokenA, tokenB);
        if (reserveA == 0 && reserveB == 0) {
            (amountA, amountB) = (amountADesired, amountBDesired);
        } else {
            uint amountBOptimal = UniswapV2Library.quote(amountADesired, reserveA, reserveB);
            if (amountBOptimal <= amountBDesired) {
                require(amountBOptimal >= amountBMin, 'UniswapV2Router: INSUFFICIENT_B_AMOUNT');
                (amountA, amountB) = (amountADesired, amountBOptimal);
            } else {
                uint amountAOptimal = UniswapV2Library.quote(amountBDesired, reserveB, reserveA);
                assert(amountAOptimal <= amountADesired);
                require(amountAOptimal >= amountAMin, 'UniswapV2Router: INSUFFICIENT_A_AMOUNT');
                (amountA, amountB) = (amountAOptimal, amountBDesired);
            }
        }
    }
    function addLiquidity(
        address tokenA,
        address tokenB,
        uint amountADesired,
        uint amountBDesired,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline
    ) external virtual override ensure(deadline) returns (uint amountA, uint amountB, uint liquidity) {
        (amountA, amountB) = _addLiquidity(tokenA, tokenB, amountADesired, amountBDesired, amountAMin, amountBMin);
        address pair = UniswapV2Library.pairFor(factory, tokenA, tokenB);
        TransferHelper.safeTransferFrom(tokenA, msg.sender, pair, amountA);
        TransferHelper.safeTransferFrom(tokenB, msg.sender, pair, amountB);
        liquidity = IUniswapV2Pair(pair).mint(to);
    }
```
The token amount transfered to the uniswap v2 pool must be:
$$\frac{amountA}{amountB}=\frac{reserveA}{reserveB}$$
```solidity
(uint256 depositedPairAsset, uint256 depositedZVE, uint256 minted) = IRouter_OCL_ZVE(router).addLiquidity(
            pairAsset,
            ZVE,
            balPairAsset,
            balZVE,
            (balPairAsset * 9) / 10,
            (balZVE * 9) / 10,
            address(this),
            block.timestamp + 14 days
        );
```
So in most cases the ```balPairAsset``` or ```balZVE``` will be larger than the corresponding values of amountA or amountB (the actual amount depositing to the pool).
Before adding liquidity:
```solidity
        uint256 balPairAsset = IERC20(pairAsset).balanceOf(address(this));
        uint256 balZVE = IERC20(ZVE).balanceOf(address(this));
        IERC20(pairAsset).safeIncreaseAllowance(router, balPairAsset);
        IERC20(ZVE).safeIncreaseAllowance(router, balZVE);
```
After adding liquidity:
```solidity
        assert(IERC20(pairAsset).allowance(address(this), router) == 0);
        assert(IERC20(ZVE).allowance(address(this), router) == 0);
```
The allowance won't be equal to 0 in most cases. The assert will revert. ```pushToLockerMulti``` in ```OCL_ZVE``` will be DOS.
## Impact
Can't add liquidity to the a Uniswap v2 or Sushi pool in most cases because an partial transfer has resulted in a non-zero allowance.
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L172-L209
https://github.com/Uniswap/v2-periphery/blob/0335e8f7e1bd1e8d8329fd300aea2ef2f36dd19f/contracts/UniswapV2Router02.sol#L33-L76
## Tool used
manual
## Recommendation
```diff
        (uint256 depositedPairAsset, uint256 depositedZVE, uint256 minted) = IRouter_OCL_ZVE(router).addLiquidity(
            pairAsset,
            ZVE,
            balPairAsset,
            balZVE,
            (balPairAsset * 9) / 10,
            (balZVE * 9) / 10,
            address(this),
            block.timestamp + 14 days
        );
          emit LiquidityTokensMinted(minted, depositedZVE, depositedPairAsset);
+         if (IERC20(pairAsset).balanceOf(address(this)) > 0) {
+            IERC20(pairAsset).safeTransfer(owner(), IERC20(pairAsset).balanceOf(address(this)));
+         }
+        if (IERC20(ZVE).balanceOf(address(this)) > 0) {
+            IERC20(ZVE).safeTransfer(owner(), IERC20(ZVE).balanceOf(address(this)));
+         }
         // no need to assert allowance
-        assert(IERC20(pairAsset).allowance(address(this), router) == 0);
-        assert(IERC20(ZVE).allowance(address(this), router) == 0);
```