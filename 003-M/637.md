Breezy White Poodle

medium

# `OCL_ZVE:pushToLockerMulti` will very likely to fail due to the assertion on the allowance

## Summary

When adding liquidity to UniswapV2Router or SushiV2 router, it is good to add reasonale range because it is almost not possible to predict the exact ratio of the assets, which the protocol set to have 10% of the wiggle room.

However, due to the assertion on the allowance, this flexibility is not realized. The `pushToLockerMulti` will only succeed when the ratio the DAO gives meet the pool's optimal ratio exactly, which is unlikely. (It is possible if it is the first deposit.)

As the result, the functionality of the `OCL_ZVE` is not really usable.

## Vulnerability Detail

The `OCL_ZVE` is to manage liquidity to the Sushiswap or Uniswap. As the first step, the liquidity should be added to the pool via router. The only way to do that is by `OCL_ZVE:pushToLockerMulti`.

In the `pushToLockerMulti`, the assets will be used by calling `addLiquidity`.

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L197-L209

The UniswapV2Router allows the user to give the minimum amount, because it is not really possible to predict the exact ratio between assets. (If it is the first deposit, it would not matter, though)

The `pushToMulti` sets the 90% of the balance as the minimum amount, which is reasonable.

However, it incorrectly expects that the entire allowance will be spent by asserting the allowance after to be zero.


Here is a code snippet of the UniswapV2Router. It shows that the amount to be transferred from will be updated to be the optimal value (if it is bigger than the specified min amount). And transfer only the updated amount. Thus, the actual transfer amount may be less than the inital approval, and if so, the allowance afterwards will not be zero.

If it should be the case, the entire transaction will fail.

```solidity
    function _addLiquidity(
        address tokenA,
        address tokenB,
        uint amountADesired,
        uint amountBDesired,
        uint amountAMin,
        uint amountBMin
    ) internal virtual returns (uint amountA, uint amountB) {
        // create the pair if it doesn't exist yet
        if (IUniswapV2Factory(factory).getPair(tokenA, tokenB) == address(0)) {
            IUniswapV2Factory(factory).createPair(tokenA, tokenB);
        }
        (uint reserveA, uint reserveB) = UniswapV2Library.getReserves(factory, tokenA, tokenB);
        if (reserveA == 0 && reserveB == 0) {
            (amountA, amountB) = (amountADesired, amountBDesired);
        } else {
            uint amountBOptimal = UniswapV2Library.quote(amountADesired, reserveA, reserveB);
            if (amountBOptimal <= amountBDesired) {
                require(amountBOptimal >= amountBMin, 'UniswapV2Router: INSUFFICIENT_B_AMOUNT');
                (amountA, amountB) = (amountADesired, amountBOptimal);
            } else {
                uint amountAOptimal = UniswapV2Library.quote(amountBDesired, reserveB, reserveA);
                assert(amountAOptimal <= amountADesired);
                require(amountAOptimal >= amountAMin, 'UniswapV2Router: INSUFFICIENT_A_AMOUNT');
                (amountA, amountB) = (amountAOptimal, amountBDesired);
            }
        }
    }
    function addLiquidity(
        address tokenA,
        address tokenB,
        uint amountADesired,
        uint amountBDesired,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline
    ) external virtual override ensure(deadline) returns (uint amountA, uint amountB, uint liquidity) {
        (amountA, amountB) = _addLiquidity(tokenA, tokenB, amountADesired, amountBDesired, amountAMin, amountBMin);
        address pair = UniswapV2Library.pairFor(factory, tokenA, tokenB);
        TransferHelper.safeTransferFrom(tokenA, msg.sender, pair, amountA);
        TransferHelper.safeTransferFrom(tokenB, msg.sender, pair, amountB);
        liquidity = IUniswapV2Pair(pair).mint(to);
    }
```

## Impact

The `OCL_ZVE:pushToLockerMulti` wil most likely to revert therefore rendering the entire contract useless.

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L197-L209

## Tool used

Manual Review

## Recommendation

Consider setting the allowance after the `addLiquidity` to zero, instead of asserting to be zero.

