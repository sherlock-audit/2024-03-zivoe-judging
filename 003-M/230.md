Glorious Midnight Snake

medium

# `OCL_ZVE#pushToLockerMulti()` is unlikely to succeed due to the failure of assertion

## Summary
The [allowance assertions](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L208-L209) are highly likely to fail, leading to [`OCL_ZVE#pushToLockerMulti()`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L172-L215) being reverted.
## Vulnerability Detail
Zivoe protocol can allocate their capital to `$ZVE/stablecoin` pools to earn yields/profits by calling [`OCL_ZVE#pushToLockerMulti()`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L172-L215).
`pushToLockerMulti()` tries to deposit all balances of `$ZVE` and `pairAsset` into the pool for lp tokens:
```solidity
        uint balPairAsset = IERC20(pairAsset).balanceOf(address(this));
        uint balZVE = IERC20(ZVE).balanceOf(address(this));
        IERC20(pairAsset).safeIncreaseAllowance(router, balPairAsset);
        IERC20(ZVE).safeIncreaseAllowance(router, balZVE);

        // Prevent volatility of greater than 10% in pool relative to amounts present.
        (uint256 depositedPairAsset, uint256 depositedZVE, uint256 minted) = IRouter_OCL_ZVE(router).addLiquidity(
            pairAsset, 
            ZVE, 
            balPairAsset,
            balZVE, 
            (balPairAsset * 9) / 10,
            (balZVE * 9) / 10, 
            address(this), block.timestamp + 14 days
        );
```
Once finished, it will check if the allowance is zero:
```solidity
        assert(IERC20(pairAsset).allowance(address(this), router) == 0);
        assert(IERC20(ZVE).allowance(address(this), router) == 0);
```

However, `router` is unlikely to spend all allowances unless it's first time to provide the liquidity for the `$ZVE/pariAsset` pool. Therefore at least one of allowances will be non-zero, resulting in the failure of assertion.
Copy below codes into [`Test_OCL_ZVE.sol`](https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-testing/src/TESTS_Lockers/Test_OCL_ZVE.sol) and run `forge test --rpc-url RPC-URL --match-test test_OCL_ZVE_SUSHI_pushToLockerMulti_assertion_fail`:
```solidity
    function test_OCL_ZVE_SUSHI_pushToLockerMulti_assertion_fail() public {
        address[] memory assets = new address[](2);
        assets[0] = DAI;
        assets[1] = address(ZVE);
        uint256[] memory amounts = new uint256[](2);
        amounts[0] = 1e8;
        amounts[1] = 1e8;
        //@audit-info deposit 1e8 DAI and 1e8 ZVE into $ZVE/DAI pool
        assert(god.try_pushMulti(address(DAO), address(OCL_ZVE_SUSHI_DAI), assets, amounts, new bytes[](2)));
        amounts[1] = 1e8+1;
        //@audit-info it will fail to deposit 1e8 DAI and 1e8+1 ZVE into $ZVE/DAI pool
        assert(god.try_pushMulti(address(DAO), address(OCL_ZVE_SUSHI_DAI), assets, amounts, new bytes[](2)) == false);
    }
```

## Impact
The Zivoe Protocol is unlikely to allocate their capital to $ZVE/stablecoin pools to earn any yields because the failure of `OCL_ZVE#pushToLockerMulti()`
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L208-L209
## Tool used

Manual Review

## Recommendation
Remove the assertions and set allowances to zero:
```diff
    function pushToLockerMulti(
        address[] calldata assets, uint256[] calldata amounts, bytes[] calldata data
    ) external override onlyOwner nonReentrant {
        ...
        emit LiquidityTokensMinted(minted, depositedZVE, depositedPairAsset);
-       assert(IERC20(pairAsset).allowance(address(this), router) == 0);
-       assert(IERC20(ZVE).allowance(address(this), router) == 0);
+      if (IERC20(pairAsset).allowance(address(this), router) != 0) {
+          IERC20(pairAsset).safeDecreaseAllowance(router, IERC20(pairAsset).allowance(address(this), router));
+      }
+      if (IERC20(ZVE).allowance(address(this), router) != 0) {
+          IERC20(ZVE).safeDecreaseAllowance(router, IERC20(ZVE).allowance(address(this), router));
+      }
        ...
    }
```