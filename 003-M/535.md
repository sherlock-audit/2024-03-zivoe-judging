Decent Chiffon Wolf

high

# The function `OCL_ZVE.pushToLockerMulti()` often results in a revert.

## Summary

The function [OCL_ZVE.pushToLockerMulti()](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L172-L215) is responsible for pulling capital from the DAO and adding liquidity into a `$ZVE/pairAsset` pool. However, it frequently results in a revert due to incorrect assertion of the allowance of `pairAsset` and `$ZVE`.  Since DAO is unable to add liquidity to any `pairAsset/$ZVE` pool, the token `$ZVE` is difficult to trade on decentralized exchanges (DEXs) like Uniswap V2 or Sushi. As a result, `$ZVE` lacks DEX liquidity, which in turn lowers its value.

## Vulnerability Detail

OCL_ZVE first allocates capital to a `$ZVE/pairAsset` pool of Uniswap V2 or Sushi. Then it removes capital from the pool and forwards yield every 30 days to the YDL with compounding mechanisms. But when ZivoeDAO allocates capital to a `$ZVE/pairAsset` pool, it frequently reverts.

#### Example
Assume OCL_ZVE wants to allocates 10 ** 8 of `$ZVE` and 10 ** 8 of `pairAsset` to the `$ZVE/pairAsset` pool.

It means: 
   - balPairAsset = 10 ** 8
   - balZVE = 10 ** 8

First, OCL_ZVE increase amount of `pairAsset` and `$ZVE` that router can take from to 10 ** 8. It was shown below.

```solidity
File: zivoe-core-foundry\src\lockers\OCL\OCL_ZVE.sol

192:    uint balPairAsset = IERC20(pairAsset).balanceOf(address(this));
193:    uint balZVE = IERC20(ZVE).balanceOf(address(this));
194:    IERC20(pairAsset).safeIncreaseAllowance(router, balPairAsset);
195:    IERC20(ZVE).safeIncreaseAllowance(router, balZVE);
```
Second, OCL_ZVE calls `IRouter_OCL_ZVE(router).addLiquidity()` to add liquidity into `pairAsset/$ZVE` pool. 
```solidity
File: zivoe-core-foundry\src\lockers\OCL\OCL_ZVE.sol

198:    (uint256 depositedPairAsset, uint256 depositedZVE, uint256 minted) = IRouter_OCL_ZVE(router).addLiquidity(
199:        pairAsset, 
200:        ZVE, 
201:        balPairAsset,
202:        balZVE, 
203:        (balPairAsset * 9) / 10,
204:        (balZVE * 9) / 10, 
205:        address(this), block.timestamp + 14 days
206:    );
207:    emit LiquidityTokensMinted(minted, depositedZVE, depositedPairAsset);
```
Here, the variables `depositedPairAsset` and `depositedZVE` are actual amounts of tokens that is deposited to the pool.

Let assume `reserve0` and `reserve1` of `pairAsset/$ZVE` pool are as following when OCL_ZVE calls `addLiquidity()`.
- reserve0 = 100 * 10 ** 20
- reserve1 = 99 * 10** 20

`depositedPairAsset`, `depositedZVE` is calculated as shown below.
- depositedPairAsset = balPairAsset = 10 ** 8
- depositedZVE = reserve1 * depositedPairAsset / reserve0 = (99 * 10** 20) * (10 ** 8) / (100 * 10 ** 20) = 99 * 10 ** 6

`pairAsset/$ZVE` pool takes 10 ** 8 of `pairAsset` and 99 * 10 **6 of `$ZVE` from OCL_ZVE.

https://github.com/Uniswap/v2-periphery/blob/0335e8f7e1bd1e8d8329fd300aea2ef2f36dd19f/contracts/UniswapV2Router02.sol#L61-L76
```solidity
File: UniswapV2Router02.sol

61:    function addLiquidity(
62:        address tokenA,
63:        address tokenB,
64:        uint amountADesired,
65:        uint amountBDesired,
66:        uint amountAMin,
67:        uint amountBMin,
68:        address to,
69:        uint deadline
70:    ) external virtual override ensure(deadline) returns (uint amountA, uint amountB, uint liquidity) {
71:        (amountA, amountB) = _addLiquidity(tokenA, tokenB, amountADesired, amountBDesired, amountAMin, amountBMin);
72:        address pair = UniswapV2Library.pairFor(factory, tokenA, tokenB);
73:        TransferHelper.safeTransferFrom(tokenA, msg.sender, pair, amountA); //@audit-info only transferfrom amountA
74:        TransferHelper.safeTransferFrom(tokenB, msg.sender, pair, amountB); //@audit-info only transferfrom amountB
75:        liquidity = IUniswapV2Pair(pair).mint(to);
76:     }
```

Finally , OCL_ZVE checks if the allowed amount of `pairAsset` and `$ZVE` that Uniswap 2 router can take from OCL_ZVE equals to 0.

However, as `pairAsset/$ZVE` pool only takes 99 * 10 ** 6 of `$ZVE` from OCL_ZVE, allowed amount of `$ZVE` is still 10 ** 6. Consequently, L209 reverts. 

```solidity
208:    assert(IERC20(pairAsset).allowance(address(this), router) == 0);
209:    assert(IERC20(ZVE).allowance(address(this), router) == 0);
```

Typically, when OCL_ZVE calls `addLiquidity()` one of two cases below often happens due to swap rate of Uniswap V2 or Sushi pool varies all the time.
- depositedPairAsset != balPairAsset
- depositedZVE != balZVE

Consequently, the function [OCL_ZVE.pushToLockerMulti()](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L172-L215) is frequently reverted.

## Impact

When DAO calls [OCL_ZVE.pushToLockerMulti()](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L172-L215), it is frequently reverted.   

Since DAO is unable to add liquidity to any `pairAsset/$ZVE` pool, the token `$ZVE` is difficult to trade on decentralized exchanges (DEXs) like Uniswap V2 or Sushi. As a result, `$ZVE` lacks DEX liquidity, which in turn lowers its value.

## Tool used

Manual Review

## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L208-L209

## Recommendation

```diff
File: zivoe-core-foundry\src\lockers\OCL\OCL_ZVE.sol
172:     function pushToLockerMulti(
173:         address[] calldata assets, uint256[] calldata amounts, bytes[] calldata data
174:     ) external override onlyOwner nonReentrant {
             [...]
191:         // Router addLiquidity() endpoint.
192:         uint balPairAsset = IERC20(pairAsset).balanceOf(address(this));
193:         uint balZVE = IERC20(ZVE).balanceOf(address(this));
194:         IERC20(pairAsset).safeIncreaseAllowance(router, balPairAsset);
195:         IERC20(ZVE).safeIncreaseAllowance(router, balZVE);
196: 
197:         // Prevent volatility of greater than 10% in pool relative to amounts present.
198:         (uint256 depositedPairAsset, uint256 depositedZVE, uint256 minted) = IRouter_OCL_ZVE(router).addLiquidity(
199:             pairAsset, 
200:             ZVE, 
201:             balPairAsset,
202:             balZVE, 
203:             (balPairAsset * 9) / 10,
204:             (balZVE * 9) / 10, 
205:             address(this), block.timestamp + 14 days
206:         );
207:         emit LiquidityTokensMinted(minted, depositedZVE, depositedPairAsset);
+            IERC20(pairAsset).safeDecreaseAllowance(router, balPairAsset-depositedPairAsset);
+            IERC20(ZVE).safeDecreaseAllowance(router, balZVE-depositedZVE);
208:         assert(IERC20(pairAsset).allowance(address(this), router) == 0); 
209:         assert(IERC20(ZVE).allowance(address(this), router) == 0); 
             [...]
215:    }
```