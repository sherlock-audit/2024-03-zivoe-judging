Shiny Carob Raven

high

# OCL_ZVE.pushToLockerMulti is vulnerable to Denial-of-Service (DOS) attacks due to its strict zero allowance checks

## Summary

Attackers can DOS the `OCL_ZVE.pushToLockerMulti` function by front-running it and sending tokens to the contract, which breaks the zero allowance assertions in the function.

## Vulnerability Detail

The `pushToLockerMulti()` function in the `OCL_ZVE` contract pulls capital from the DAO and adds liquidity into the $ZVE/pairAsset pool. It is called by the `ZivoeDAO.pushMulti()` function, which is executed through by Zivoe TimelockerController (ZivoeTLC)'s proposal. 

```solidity
    function pushToLockerMulti(
        address[] calldata assets, uint256[] calldata amounts, bytes[] calldata data
    ) external override onlyOwner nonReentrant {
        ...

        // Router addLiquidity() endpoint.
        uint balPairAsset = IERC20(pairAsset).balanceOf(address(this));
        uint balZVE = IERC20(ZVE).balanceOf(address(this));
        IERC20(pairAsset).safeIncreaseAllowance(router, balPairAsset);
        IERC20(ZVE).safeIncreaseAllowance(router, balZVE);

        // Prevent volatility of greater than 10% in pool relative to amounts present.
        (uint256 depositedPairAsset, uint256 depositedZVE, uint256 minted) = IRouter_OCL_ZVE(router).addLiquidity(
            pairAsset, 
            ZVE, 
            balPairAsset,
            balZVE, 
            (balPairAsset * 9) / 10,
            (balZVE * 9) / 10, 
            address(this), block.timestamp + 14 days
        );
        emit LiquidityTokensMinted(minted, depositedZVE, depositedPairAsset);
        assert(IERC20(pairAsset).allowance(address(this), router) == 0); // @audit: zero allowance check here
        assert(IERC20(ZVE).allowance(address(this), router) == 0);  // @audit: and zero allowance check here

        ...
    }
```

However,  it asserts that the approved allowances are all used during `addLiquidity` call (see the two assert lines). For these assertions to pass, the ratio between the passed-in `balZVE` and `balPairAsset` params in `addLiquidity` should match exactly the $ZVE/pairAsset reserve ratio in the pool.

Normally, the DAO members can compute the correct `amounts` based on the $ZVE/pairAsset ratio and propose, vote, execute a proposal to add liquidity via the OCL_ZVE locker. But if the $ZVE price in the pool changes (i.e. the $ZVE/pairAsset reserve ratio changes) when the proposal is actually executed, the `pushToLockerMulti` call fails due to its strict zero allowance checks.

Even if the $ZVE price stays the same, attackers can front-run the proposal execution and send $ZVE or pairAsset to the contract to manipulate the balZVE/balPairAsset ratio,  resulting in `addLiquidity` not spending all the allowance and causing the `pushToLockerMulti` call to fail.

Without the pushToLockerMulti() function to start with allocating liquidity to the pool, the OCL_ZVE can not be used to remove liquidity and forward yield. In other words, DOS attacks on the pushToLockerMulti() function makes the OCL_ZVE locker useless.


### POC
```diff

    function test_OCL_ZVE_UNIV2_pushToLockerMulti_state_subsequent(uint96 randomA, uint96 randomB) public {
        
        uint256 amountA = uint256(randomA) % (10_000_000 * USD) + 10 * USD;
        uint256 amountB = uint256(randomB) % (10_000_000 * USD) + 10 * USD;
-       uint256 modularity = randomA % 4;
+      uint256 modularity = 0;

        pushToLockerInitial_Uni(amountA, amountB, modularity);

        address[] memory assets = new address[](2);
        uint256[] memory amounts = new uint256[](2);

        assets[1] = address(ZVE);
        amounts[0] = amountA;
        amounts[1] = amountB;

        if (modularity == 0) {
            assets[0] = DAI;

            // Pre-state.
            (uint256 _preBasis, uint256 _preLPTokens) = OCL_ZVE_UNIV2_DAI.fetchBasis();
            
+          mint("DAI", address(bob), 1);
+          bob.try_transferToken(address(DAI), address(OCL_ZVE_SUSHI_DAI), 1);

            assert(god.try_pushMulti(address(DAO), address(OCL_ZVE_UNIV2_DAI), assets, amounts, new bytes[](2)));

            // Post-state.
            (uint256 _postBasis, uint256 _postLPTokens) = OCL_ZVE_UNIV2_DAI.fetchBasis();
            assertGt(_postLPTokens, _preLPTokens);
            assertGt(_postBasis, _preBasis);
            
        }

        ...
    }
```
To illustrate our point, we made some modifications to the [test_OCL_ZVE_UNIV2_pushToLockerMulti_state_subsequent](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-testing/src/TESTS_Lockers/Test_OCL_ZVE.sol#L1457-L1527)](https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-testing/src/TESTS_Lockers/Test_OCL_ZVE.sol#L1457-L1527) testcase.
Here we set `modularity` to 0 to only test the $ZVE/$DAI pool without loss of generosity. Then, we ask a BlackHat (Bob) to transfer 1 wei $DAI to the locker before the DAO calls `pushMulti`. With this modification, the testcase will fail, demonstrating that simply sending 1 wei token to the locker in advance can DOS the `OCL_ZVE.pushToLockerMulti` function.

## Impact
Attackers can easily DOS the `OCL_ZVE.pushToLockerMulti()` function by front-running and donating. Without the pushToLockerMulti() call to add liquidity, the `OCL_ZVE` contract can not proceed with liquidity provision and yield forwarding.

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L208-L209

## Tool used

Manual Review

## Recommendation
Instead of asserting the allowance is zero, manually setting it to zero..
```diff
    function pushToLockerMulti(
        address[] calldata assets, uint256[] calldata amounts, bytes[] calldata data
    ) external override onlyOwner nonReentrant {
        ...

        (uint256 depositedPairAsset, uint256 depositedZVE, uint256 minted) = IRouter_OCL_ZVE(router).addLiquidity(
            pairAsset, 
            ZVE, 
            balPairAsset,
            balZVE, 
            (balPairAsset * 9) / 10,
            (balZVE * 9) / 10, 
            address(this), block.timestamp + 14 days
        );
        emit LiquidityTokensMinted(minted, depositedZVE, depositedPairAsset);
-       assert(IERC20(pairAsset).allowance(address(this), router) == 0);
-       assert(IERC20(ZVE).allowance(address(this), router) == 0);
+       IERC20(pairAsset).approve(router, 0);
+       IERC20(ZVE).approve(router, 0);
       
       ...

    }
```