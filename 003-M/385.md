Feisty Taupe Locust

high

# The addition of liquidity to the Uniswap V2 pool can be reverted because the Uniswap V2 router doesn't use all allowed tokens.

## Summary

## Vulnerability Detail
When adding `liquidity` to the `Uniswap V2 pool`, the actual deposited amounts may differ from the input amounts.
To handle this, there are parameters indicating the minimum allowed deposited amounts.
The router will use the actual deposited amounts, which means it may not use all allowed amounts.
```solidity
function addLiquidity(
    address tokenA,
    address tokenB,
    uint amountADesired,  
    uint amountBDesired,
    uint amountAMin,
    uint amountBMin,
    address to,
    uint deadline
) external virtual override ensure(deadline) returns (uint amountA, uint amountB, uint liquidity) {
    (amountA, amountB) = _addLiquidity(tokenA, tokenB, amountADesired, amountBDesired, amountAMin, amountBMin);
    address pair = UniswapV2Library.pairFor(factory, tokenA, tokenB);
    TransferHelper.safeTransferFrom(tokenA, msg.sender, pair, amountA);  // @audit, here
    TransferHelper.safeTransferFrom(tokenB, msg.sender, pair, amountB);  // @audit, here
    liquidity = IUniswapV2Pair(pair).mint(to);
}
```
However, in the `OCL_ZVE`, we assume that the `Uniswap V2 router` will always use the allowed amounts.
```solidity
function pushToLockerMulti(
    address[] calldata assets, uint256[] calldata amounts, bytes[] calldata data
) external override onlyOwner nonReentrant {
    uint balPairAsset = IERC20(pairAsset).balanceOf(address(this));
    uint balZVE = IERC20(ZVE).balanceOf(address(this));
    IERC20(pairAsset).safeIncreaseAllowance(router, balPairAsset);  // @audit, here
    IERC20(ZVE).safeIncreaseAllowance(router, balZVE);  // @audit, here

    (uint256 depositedPairAsset, uint256 depositedZVE, uint256 minted) = IRouter_OCL_ZVE(router).addLiquidity(
        pairAsset, 
        ZVE, 
        balPairAsset,
        balZVE, 
        (balPairAsset * 9) / 10,
        (balZVE * 9) / 10, 
        address(this), block.timestamp + 14 days
    );
    assert(IERC20(pairAsset).allowance(address(this), router) == 0);  // @audit, here
    assert(IERC20(ZVE).allowance(address(this), router) == 0);  // @audit, here
}
```
As a result, the transaction will be reverted.

The reason the tests pass in the `Test_OCL_ZVE.sol` is that the `_addLiquidity` function returns the input parameters when it's the first deposit.
```solidity
function _addLiquidity(
    address tokenA,
    address tokenB,
    uint amountADesired,
    uint amountBDesired,
    uint amountAMin,
    uint amountBMin
) internal virtual returns (uint amountA, uint amountB) {
    (uint reserveA, uint reserveB) = UniswapV2Library.getReserves(factory, tokenA, tokenB);
    if (reserveA == 0 && reserveB == 0) {
        (amountA, amountB) = (amountADesired, amountBDesired);  // @audit, here
    } else {
        
    }
}
```
## Impact
We can not use `OCL_ZVE` `locker`.
## Code Snippet
https://github.com/sherlock-audit/2024-03-zivoe/blob/d4111645b19a1ad3ccc899bea073b6f19be04ccd/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L208-L209
## Tool used

Manual Review

## Recommendation
