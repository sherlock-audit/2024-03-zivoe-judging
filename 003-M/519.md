Great Metal Ram

high

# Push tokens to `OCL_ZVE` could revert most of the time due to allowance check

## Summary

When DAO decide to push tokens to `OCL_ZVE`, it will provide the tokens to the Uniswap V2 or Sushi pool to receive liquidity token. However, due to `pairAsset` and `ZVE` allowance check, the call could revert most of the time.

## Vulnerability Detail

When DAO push tokens to `OCL_ZVE` and triggering `pushToLockerMulti`, it will provide the tokens to the pool via router by calling `addLiquidity`, providing the needed params 

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L208-L209

```solidity
    function pushToLockerMulti(
        address[] calldata assets, uint256[] calldata amounts, bytes[] calldata data
    ) external override onlyOwner nonReentrant {
        address ZVE = IZivoeGlobals_OCL_ZVE(GBL).ZVE();
        require(
            assets[0] == pairAsset && assets[1] == ZVE,
            "OCL_ZVE::pushToLockerMulti() assets[0] != pairAsset || assets[1] != ZVE"
        );

        for (uint256 i = 0; i < 2; i++) {
            require(amounts[i] >= 10 * 10**6, "OCL_ZVE::pushToLockerMulti() amounts[i] < 10 * 10**6");
            IERC20(assets[i]).safeTransferFrom(owner(), address(this), amounts[i]);
        }

        if (nextYieldDistribution == 0) { nextYieldDistribution = block.timestamp + 30 days; }

        uint256 preBasis;
        if (basis != 0) { (preBasis,) = fetchBasis(); }

        // Router addLiquidity() endpoint.
        uint balPairAsset = IERC20(pairAsset).balanceOf(address(this));
        uint balZVE = IERC20(ZVE).balanceOf(address(this));
        IERC20(pairAsset).safeIncreaseAllowance(router, balPairAsset);
        IERC20(ZVE).safeIncreaseAllowance(router, balZVE);

        // Prevent volatility of greater than 10% in pool relative to amounts present.
>>>     (uint256 depositedPairAsset, uint256 depositedZVE, uint256 minted) = IRouter_OCL_ZVE(router).addLiquidity(
            pairAsset, 
            ZVE, 
            balPairAsset,
            balZVE, 
            (balPairAsset * 9) / 10,
            (balZVE * 9) / 10, 
            address(this), block.timestamp + 14 days
        );
        emit LiquidityTokensMinted(minted, depositedZVE, depositedPairAsset);
>>>     assert(IERC20(pairAsset).allowance(address(this), router) == 0);
>>>     assert(IERC20(ZVE).allowance(address(this), router) == 0);

        // Increase basis by difference.
        (uint256 postBasis,) = fetchBasis();
        require(postBasis > preBasis, "OCL_ZVE::pushToLockerMulti() postBasis <= preBasis");
        basis += postBasis - preBasis;
    }
```

However, it can be observed that after providing liquidity, it will check whether both the `pairAsset` and `ZVE` allowance to the router are 0, which will cause issues. Most of the time, the actual `pairAsset` and `ZVE` deposited could change due to a larger change in the true market price before the transaction is confirmed. 

docs reference : [here](https://docs.uniswap.org/contracts/v2/guides/smart-contract-integration/providing-liquidity)

## Impact

This will cause the allowance for either `pairAsset` or `ZVE` to the router to become non-zero and the call will revert.

## Code Snippet

https://github.com/sherlock-audit/2024-03-zivoe/blob/main/zivoe-core-foundry/src/lockers/OCL/OCL_ZVE.sol#L208-L209

## Tool used

Manual Review

## Recommendation

Instead of checking the allowance, better to set the allowance to the router to 0 by calling `safeApprove`.

```diff
    function pushToLockerMulti(
        address[] calldata assets, uint256[] calldata amounts, bytes[] calldata data
    ) external override onlyOwner nonReentrant {
        // ...

        // Prevent volatility of greater than 10% in pool relative to amounts present.
        (uint256 depositedPairAsset, uint256 depositedZVE, uint256 minted) = IRouter_OCL_ZVE(router).addLiquidity(
            pairAsset, 
            ZVE, 
            balPairAsset,
            balZVE, 
            (balPairAsset * 9) / 10,
            (balZVE * 9) / 10, 
            address(this), block.timestamp + 14 days
        );
        emit LiquidityTokensMinted(minted, depositedZVE, depositedPairAsset);
-       assert(IERC20(pairAsset).allowance(address(this), router) == 0);
-       assert(IERC20(ZVE).allowance(address(this), router) == 0);
+       IERC20(pairAsset).safeApprove(router, 0);
+       IERC20(ZVE).safeApprove(router, 0);

        // Increase basis by difference.
        (uint256 postBasis,) = fetchBasis();
        require(postBasis > preBasis, "OCL_ZVE::pushToLockerMulti() postBasis <= preBasis");
        basis += postBasis - preBasis;
    }
```
